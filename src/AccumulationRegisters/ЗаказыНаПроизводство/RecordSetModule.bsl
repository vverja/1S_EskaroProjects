Перем мПериод          Экспорт; // Период движений
Перем мТаблицаДвижений Экспорт; // Таблица движений

// Выполняет приход по регистру.
//
// Параметры:
//  Нет.
//
Процедура ВыполнитьПриход() Экспорт

	ОбщегоНазначения.ВыполнитьДвижениеПоРегистру(ЭтотОбъект, ВидДвиженияНакопления.Приход);

КонецПроцедуры // ВыполнитьПриход()

// Выполняет расход по регистру.
//
// Параметры:
//  Нет.
//
Процедура ВыполнитьРасход() Экспорт

	ОбщегоНазначения.ВыполнитьДвижениеПоРегистру(ЭтотОбъект, ВидДвиженияНакопления.Расход);

КонецПроцедуры // ВыполнитьРасход()

// Выполняет движения по регистру.
//
// Параметры:
//  Нет.
//
Процедура ВыполнитьДвижения() Экспорт

	Загрузить(мТаблицаДвижений);

КонецПроцедуры // ВыполнитьДвижения()

// Процедура контролирует остаток по данному регистру по переданному документу
// и его табличной части. В случае недостатка товаров выставляется флаг отказа и 
// выдается сообщение.
//
// Параметры:
//  ДокументОбъект - объект проводимого документа, 
//  Параметры      - структура, содержащая значения параметров необходимых для контроля остатков
//  Отказ          - флаг отказа в проведении,
//  Заголовок      - строка, заголовок сообщения об ошибке проведения.
//
Процедура КонтрольОстатков(ДокументОбъект, ИмяТабличнойЧасти, СтруктураШапкиДокумента, Отказ, Заголовок, РежимПроведения) Экспорт

	Если РежимПроведения <> РежимПроведенияДокумента.Оперативный Тогда
		Возврат;
	КонецЕсли;
	
	МетаданныеДокумента = ДокументОбъект.Метаданные();
	ИмяДокумента        = МетаданныеДокумента.Имя;
	
				 
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
    СтруктураПараметров = новый Структура("МетаданныеДокумента,ИмяДокумента,ИмяТабличнойЧасти,Ссылка, СтруктураШапкиДокумента",
		МетаданныеДокумента,ИмяДокумента,ИмяТабличнойЧасти,ДокументОбъект.Ссылка,СтруктураШапкиДокумента);
		
	СформироватьВременнуюТаблицуДокумента(МенеджерВременныхТаблиц,СтруктураПараметров);
	
	Если глЗначениеПеременной("ИспользоватьБлокировкуДанных")  Тогда
		
		СтруктураПараметровБлокировки = Новый Структура(
			"ТипТаблицы,ИмяТаблицы,ИсточникДанных,ИмяВременнойТаблицы"
			,"РегистрНакопления"
			,"ЗаказыНаПроизводство"
			,МенеджерВременныхТаблиц
			,"ВременнаяТаблицаДокумента");
			
		СтруктураИсточникаДанных = Новый Структура(
			"ЗаказНаПроизводство,Номенклатура,ХарактеристикаНоменклатуры"
			,"Заказ"
			,"Номенклатура"
			,"ХарактеристикаНоменклатуры");
			
		
		ОбщегоНазначения.УстановитьУправляемуюБлокировку(СтруктураПараметровБлокировки,,СтруктураИсточникаДанных, Отказ, Заголовок);
	КонецЕсли; 
	
	Запрос = Новый Запрос;
    Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;

	// Установим параметры запроса
	Запрос.УстановитьПараметр("ВидВоспроизводства", СтруктураШапкиДокумента.ВидВоспроизводства);

	Запрос.Текст = "
	|ВЫБРАТЬ // Запрос, контролирующий остатки на складах
	|	Док.Номенклатура 							 КАК Номенклатура,
	|	Док.Номенклатура.Представление               КАК НоменклатураПредставление,
	|	Док.Заказ      								 КАК Заказ,
	|	Док.Номенклатура.ЕдиницаХраненияОстатков.Представление     КАК ЕдиницаХраненияОстатковПредставление,"+
	?(СтруктураПараметров.ЕстьХарактеристика,"
	|	Док.ХарактеристикаНоменклатуры 				 КАК ХарактеристикаНоменклатуры,","")+"
	|	Сумма(Док.ДокументКоличество)      			 КАК ДокументКоличество, 
	|	Максимум(ЕСТЬNULL(Остатки.КоличествоОстаток, 0))  	 КАК ОстатокКоличество
	|ИЗ 
	|	ВременнаяТаблицаДокумента КАК Док
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрНакопления.ЗаказыНаПроизводство.Остатки(, 
	|		(Номенклатура,ХарактеристикаНоменклатуры,ЗаказНаПроизводство) В (ВЫБРАТЬ Номенклатура,ХарактеристикаНоменклатуры,Заказ ИЗ ВременнаяТаблицаДокумента) "
	+?(СтруктураШапкиДокумента.ВидВоспроизводства=неопределено,"","
	|   	И ВидВоспроизводства = &ВидВоспроизводства")+") КАК Остатки
	|ПО 
	|	Док.Номенклатура  = Остатки.Номенклатура
	|	И Док.Заказ = Остатки.ЗаказНаПроизводство "
	+ ?(СтруктураПараметров.ЕстьХарактеристика, "
	| И Док.ХарактеристикаНоменклатуры  = Остатки.ХарактеристикаНоменклатуры"
	,"") + "
	|
	|СГРУППИРОВАТЬ ПО
	|	Док.Номенклатура,
	|	Док.Заказ"
	+ ?(СтруктураПараметров.ЕстьХарактеристика, "
	|	, Док.ХарактеристикаНоменклатуры "
	,"") + "
	|	
	|ИМЕЮЩИЕ Максимум(ЕСТЬNULL(Остатки.КоличествоОстаток, 0)) < Сумма(Док.ДокументКоличество)
	|ДЛЯ ИЗМЕНЕНИЯ РегистрНакопления.ЗаказыНаПроизводство.Остатки // Блокирующие чтение таблицы остатков регистра для разрешения коллизий многопользовательской работы
	|";
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;

	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		СтрокаСообщения = "Остатка " + 
		УправлениеЗапасами.ПредставлениеНоменклатуры(Выборка.НоменклатураПредставление,
								  ?(СтруктураПараметров.ЕстьХарактеристика, Выборка.ХарактеристикаНоменклатуры, "")) +
		", заказанного по документу " + (Выборка.Заказ) + " недостаточно.";

		УправлениеЗапасами.ОшибкаНетОстатка(СтрокаСообщения, Выборка.ОстатокКоличество, Выборка.ДокументКоличество,
		Выборка.ЕдиницаХраненияОстатковПредставление, Отказ, Заголовок);
	КонецЦикла;

КонецПроцедуры // КонтрольОстатков()

Процедура СформироватьВременнуюТаблицуДокумента(МенеджерВременныхТаблиц, СтруктураПараметров)
	Перем СтруктураШапкиДокумента;
	
	СтруктураШапкиДокумента = СтруктураПараметров.СтруктураШапкиДокумента;
	Если СтруктураШапкиДокумента.Свойство("ИмяРеквизитаЗаказНаПроизводство") Тогда
		ИмяРеквизитаЗаказНаПроизводство = СтруктураШапкиДокумента.ИмяРеквизитаЗаказНаПроизводство;
	Иначе
		ИмяРеквизитаЗаказНаПроизводство = "ЗаказВыпуска";
	КонецЕсли;

	
	Если СтруктураПараметров.ИмяДокумента = "КомплектацияНоменклатуры" Тогда  //Комплектация номенклатуры
		ИмяРеквизитаЗаказНаПроизводство = "Ссылка.ЗаказВыпуска";

		Если СтруктураПараметров.ИмяТабличнойЧасти="" Тогда //сборка 
			ИмяТаблицы = СтруктураПараметров.ИмяДокумента;
			ЕстьХарактеристика = истина;
			ТекстКоличество = "Док.Количество * Док.Коэффициент / Док.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент";
		Иначе //Разборка
			ИмяТаблицы          = СтруктураПараметров.ИмяДокумента + "." + СокрЛП(СтруктураПараметров.ИмяТабличнойЧасти);
            ЕстьХарактеристика = истина;
            ТекстКоличество = "Док.Количество * Док.Коэффициент / Док.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент";
		КонецЕсли;
	Иначе
		
		МетаданныеТабЧастиДокумента = СтруктураПараметров.МетаданныеДокумента.ТабличныеЧасти.Найти(СтруктураПараметров.ИмяТабличнойЧасти);

		ИмяТаблицы          = СтруктураПараметров.ИмяДокумента + "." + СокрЛП(СтруктураПараметров.ИмяТабличнойЧасти);
		ЕстьХарактеристика  = МетаданныеТабЧастиДокумента.Реквизиты.Найти("ХарактеристикаНоменклатуры") <> Неопределено;
		
	
		Если МетаданныеТабЧастиДокумента.Реквизиты.Найти("Коэффициент") <> Неопределено
		   И МетаданныеТабЧастиДокумента.Реквизиты.Найти("ЕдиницаИзмерения") <> Неопределено Тогда
			ТекстКоличество = "Док.Количество * Док.Коэффициент / Док.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент";
		Иначе
			ТекстКоличество = "Док.Количество";
		КонецЕсли;
	КонецЕсли;	
	СтруктураПараметров.Вставить("ЕстьХарактеристика",ЕстьХарактеристика);
	СтруктураПараметров.Вставить("ИмяТаблицы",ИмяТаблицы);

	ЕстьКачество = СтруктураПараметров.ИмяДокумента = "ОтчетПроизводстваЗаСмену"
				 И СтруктураПараметров.ИмяТабличнойЧасти = "Продукция"
			     И МетаданныеТабЧастиДокумента.Реквизиты.Найти("Качество") <> Неопределено;

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ 
	|	Док.Номенклатура 							 КАК Номенклатура,
	|	%Заказ%      								 КАК Заказ,"
	+ ?(ЕстьХарактеристика, "
	|	Док.ХарактеристикаНоменклатуры"
	,"Значение(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)") + " КАК ХарактеристикаНоменклатуры,
	|	СУММА( " + ТекстКоличество + " )             КАК ДокументКоличество 
	|ПОМЕСТИТЬ ВременнаяТаблицаДокумента
	|ИЗ 
	|	Документ." + ИмяТаблицы + " КАК Док
    |ГДЕ
	|	Док.Ссылка  =  &ДокументСсылка
	|	И %Заказ% ССЫЛКА Документ.ЗаказНаПроизводство
	|	И %Заказ% <> &ПустойЗаказНаПроизводство "
	+ ?(ЕстьКачество, " И Док.Качество = &КачествоНовый ", "") + "
	|
	|СГРУППИРОВАТЬ ПО
	|
	|	Док.Номенклатура,
	|	%Заказ%"
	+ ?(ЕстьХарактеристика, "
	|	, Док.ХарактеристикаНоменклатуры "
	,"") + "
	|";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%Заказ%", "Док."+ИмяРеквизитаЗаказНаПроизводство);
    // Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка",  СтруктураПараметров.Ссылка);
	Запрос.УстановитьПараметр("ПустойЗаказНаПроизводство", Документы.ЗаказНаПроизводство.ПустаяСсылка());
	Запрос.УстановитьПараметр("КачествоНовый",             Справочники.Качество.Новый);
    Запрос.Выполнить();
КонецПроцедуры