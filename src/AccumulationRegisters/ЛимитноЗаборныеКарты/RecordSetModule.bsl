Перем мПериод Экспорт; 
Перем мТаблицаДвижений Экспорт;

Процедура ДобавитьДвижение() Экспорт
	
	мТаблицаДвижений.ЗаполнитьЗначения( мПериод, "Период");
	мТаблицаДвижений.ЗаполнитьЗначения( Истина,  "Активность");

	ОбщегоНазначения.ВыполнитьДвижениеПоРегистру(ЭтотОбъект);
	
КонецПроцедуры // ДобавитьДвижение()

// Процедура контролирует остаток лимита отпуска материалов по переданному документу
// и его табличной части. В случае недостатка лимита отпуска выставляется флаг отказа и 
// выдается сообщение.
//
// Параметры:
//  ДокументОбъект    - объект проводимого документа, 
//  ИмяТабличнойЧасти - строка, имя табличной части, которая проводится по регистру, 
//  СтруктураШапкиДокумента - структура, содержащая значения "через точку" ссылочных реквизитов по шапке документа,
//  Отказ             - флаг отказа в проведении,
//  Заголовок         - строка, заголовок сообщения об ошибке проведения.
//
Процедура КонтрольЛимитов(ДокументОбъект, ИмяТабличнойЧасти, СтруктураШапкиДокумента, Отказ, Заголовок) Экспорт
	
	МетаданныеДокумента = ДокументОбъект.Метаданные();
	ИмяДокумента        = МетаданныеДокумента.Имя;
	ИмяТаблицы          = ИмяДокумента + "." + СокрЛП(ИмяТабличнойЧасти);
	
	Если ИмяДокумента = "ОтчетПроизводстваЗаСмену" Тогда
		ТекстДокумент = "
			|ВЫБРАТЬ
			|	ПродукцияПолучатели.Номенклатура 				КАК Номенклатура,
			|	ПродукцияПолучатели.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
			|	ПродукцияПолучатели.Ссылка.Склад 				КАК Склад,
			|	ПродукцияПолучатели.ПодразделениеПолучатель 	КАК Подразделение,
			|	СУММА(ПродукцияПолучатели.Количество * ПродукцияПолучатели.Коэффициент /ПродукцияПолучатели.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент) КАК ДокументКоличество,
			|	СУММА(ПродукцияПолучатели.ОтпущеноСверхЛимита * ПродукцияПолучатели.Коэффициент /ПродукцияПолучатели.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент) КАК ОтпущеноСверхЛимита
			|ПОМЕСТИТЬ ВременнаяТаблицаДокумента
			|
			|ИЗ
			|	Документ.ОтчетПроизводстваЗаСмену.Продукция КАК ПродукцияПолучатели
			|ГДЕ
			|	ПродукцияПолучатели.Ссылка  =  &ДокументСсылка
			|	И ПродукцияПолучатели.ВидВыпуска = &ВидВыпуска
			|	И ПродукцияПолучатели.НаправлениеВыпуска = &НаправлениеВыпуска
			|СГРУППИРОВАТЬ ПО
			|	ПродукцияПолучатели.Номенклатура,
			|	ПродукцияПолучатели.ХарактеристикаНоменклатуры,
			|	ПродукцияПолучатели.Ссылка.Склад,
			|	ПродукцияПолучатели.ПодразделениеПолучатель
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ПродукцияНаправления.Номенклатура,
			|	ПродукцияНаправления.ХарактеристикаНоменклатуры,
			|	ПродукцияНаправления.Ссылка.Склад,
			|	НаправленияСписания.Подразделение,
			|	СУММА(	ВЫБОР КОГДА НаправленияСписания.КоэффициентИтого ЕСТЬ NULL
			|					ИЛИ НаправленияСписания.КоэффициентИтого = 0 ТОГДА
			|				0
			|			ИНАЧЕ
			|				ПродукцияНаправления.Количество * ПродукцияНаправления.Коэффициент /ПродукцияНаправления.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент *
			|				НаправленияСписания.Коэффициент / НаправленияСписания.КоэффициентИтого
			|			КОНЕЦ
			|	) КАК ДокументКоличество,
			|	СУММА(НаправленияСписания.ОтпущеноСверхЛимита) КАК ОтпущеноСверхЛимита
			|ИЗ
			|	Документ.ОтчетПроизводстваЗаСмену.Продукция КАК ПродукцияНаправления
			|
			|	ЛЕВОЕ СОЕДИНЕНИЕ (
			|		ВЫБРАТЬ
			|			НаправленияСписания.Подразделение КАК Подразделение,
			|			НаправленияСписания.Коэффициент	  КАК Коэффициент,
			|			НаправленияИтоги.КоэффициентИтого КАК КоэффициентИтого,
			|			НаправленияСписания.КлючСвязи	  КАК КлючСвязи,
			|			СУММА(НаправленияСписания.ОтпущеноСверхЛимита) КАК ОтпущеноСверхЛимита
			|		ИЗ
			|			Документ.ОтчетПроизводстваЗаСмену.НаправленияСписания КАК НаправленияСписания
			|		
			|			ЛЕВОЕ СОЕДИНЕНИЕ (
			|				ВЫБРАТЬ
			|					НаправленияСписанияИтоги.КлючСвязи	  		КАК КлючСвязи,
			|					СУММА(НаправленияСписанияИтоги.Коэффициент) КАК КоэффициентИтого
			|				ИЗ
			|					Документ.ОтчетПроизводстваЗаСмену.НаправленияСписания КАК НаправленияСписанияИтоги
			|				ГДЕ
			|					НаправленияСписанияИтоги.Ссылка  =  &ДокументСсылка
			|				СГРУППИРОВАТЬ ПО
			|					НаправленияСписанияИтоги.КлючСвязи
			|				) КАК НаправленияИтоги
			|			ПО
			|				НаправленияСписания.КлючСвязи = НаправленияИтоги.КлючСвязи
			|		ГДЕ
			|			НаправленияСписания.Ссылка  =  &ДокументСсылка
			|		СГРУППИРОВАТЬ ПО
			|			НаправленияСписания.Подразделение,
			|			НаправленияСписания.Коэффициент,
			|			НаправленияИтоги.КоэффициентИтого,
			|			НаправленияСписания.КлючСвязи
			|		) КАК НаправленияСписания
			|	ПО
			|		ПродукцияНаправления.КлючСвязи = НаправленияСписания.КлючСвязи
			|
			|ГДЕ
			|	ПродукцияНаправления.Ссылка  =  &ДокументСсылка
			|	И ПродукцияНаправления.ВидВыпуска = &ВидВыпуска
			|	И ПродукцияНаправления.НаправлениеВыпуска = &НаправлениеВыпускаСписок
			|СГРУППИРОВАТЬ ПО
			|	ПродукцияНаправления.Номенклатура,
			|	ПродукцияНаправления.ХарактеристикаНоменклатуры,
			|	ПродукцияНаправления.Ссылка.Склад,
			|	НаправленияСписания.Подразделение
			|";
			
	Иначе
		ТекстДокумент = "
			|	ВЫБРАТЬ
			|		Док.Номенклатура 				КАК Номенклатура,
			|		Док.ХарактеристикаНоменклатуры 	КАК ХарактеристикаНоменклатуры,
			|		Док.Склад 						КАК Склад,
			|		Док.Ссылка.Подразделение		КАК Подразделение,
			|		СУММА(Док.Количество * Док.Коэффициент /Док.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент) КАК ДокументКоличество,
			|		СУММА(Док.ОтпущеноСверхЛимита * Док.Коэффициент /Док.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент) КАК ОтпущеноСверхЛимита
			|ПОМЕСТИТЬ ВременнаяТаблицаДокумента
			|	ИЗ
			|		Документ." + ИмяТаблицы + " КАК Док
			|	ГДЕ
			|		Док.Ссылка  =  &ДокументСсылка
			|	СГРУППИРОВАТЬ ПО
			|		Док.Номенклатура,
			|		Док.ХарактеристикаНоменклатуры,
			|		Док.Склад,
			|		Док.Ссылка.Подразделение
			|";
	КонецЕсли;
	
		
	// Текст вложенного запроса, ограничивающего номенклатуру при получении остатков
	ТекстЗапросаСписокНоменклатуры = "
		|ВЫБРАТЬ
		|	Док.Склад, Док.Номенклатура, Док.Подразделение
		|ИЗ
		|	ВременнаяТаблицаДокумента КАК Док";
	
	ТекстЗапроса = " // ФОРМИРОВАНИЕ ВРЕМЕННОЙ ТАБЛИЦЫ ДОКУМЕНТА
	|" + ТекстДокумент + "
	|
	|// ЗАПРОС ДЛЯ КОНТРОЛЯ ОСТАТКОВ
	|;
	|ВЫБРАТЬ
	|	Док.Номенклатура										КАК Номенклатура,
	|	Док.ХарактеристикаНоменклатуры							КАК ХарактеристикаНоменклатуры,
	|	Док.Номенклатура.Представление							КАК НоменклатураПредставление,
	|	Док.Номенклатура.ЕдиницаХраненияОстатков.Представление 	КАК ЕдиницаХраненияОстатковПредставление,
	|	Док.ХарактеристикаНоменклатуры.Представление           	КАК ХарактеристикаНоменклатурыПредставление,
	|	Док.Склад.Представление									КАК СкладПредставление,
	|	ЛимитыОтпуска.Периодичность								КАК Периодичность,
	|	МАКСИМУМ(Док.ДокументКоличество) 	КАК ДокументКоличество,
	|	МАКСИМУМ(Док.ОтпущеноСверхЛимита) 	КАК ОтпущеноСверхЛимита,
	|	МАКСИМУМ(ВЫБОР КОГДА ЛимитыОтпуска.ЛимитОтпуска ЕСТЬ NULL ТОГДА
	|				0
	|			ИНАЧЕ
	|				ЛимитыОтпуска.ЛимитОтпуска
	|			КОНЕЦ
	|	) КАК ЛимитОтпуска,
	|	МАКСИМУМ(ВЫБОР КОГДА ЛимитыОтпуска.КонтролироватьЛимит ЕСТЬ NULL ТОГДА
	|				Ложь
	|			ИНАЧЕ
	|				ЛимитыОтпуска.КонтролироватьЛимит
	|			КОНЕЦ
	|	) КАК КонтролироватьЛимит,
	|
	|	СУММА(	ВЫБОР КОГДА ЛимитноЗаборныеКартыДень.ОтпущеноОборот ЕСТЬ NULL ТОГДА
	|				0
	|			ИНАЧЕ
	|				ЛимитноЗаборныеКартыДень.ОтпущеноОборот
	|			КОНЕЦ -
	|			ВЫБОР КОГДА ЛимитноЗаборныеКартыДень.ОтпущеноСверхЛимитаОборот ЕСТЬ NULL ТОГДА
	|				0
	|			ИНАЧЕ
	|				ЛимитноЗаборныеКартыДень.ОтпущеноСверхЛимитаОборот
	|			КОНЕЦ -
	|			ВЫБОР КОГДА ЛимитноЗаборныеКартыДень.ВозвращеноОборот ЕСТЬ NULL ТОГДА
	|				0
	|			ИНАЧЕ
	|				ЛимитноЗаборныеКартыДень.ВозвращеноОборот
	|			КОНЕЦ
	|	) КАК ОтпущеноДень,
	|
	|	СУММА(	ВЫБОР КОГДА ЛимитноЗаборныеКартыНеделя.ОтпущеноОборот ЕСТЬ NULL ТОГДА
	|				0
	|			ИНАЧЕ
	|				ЛимитноЗаборныеКартыНеделя.ОтпущеноОборот
	|			КОНЕЦ -
	|			ВЫБОР КОГДА ЛимитноЗаборныеКартыНеделя.ОтпущеноСверхЛимитаОборот ЕСТЬ NULL ТОГДА
	|				0
	|			ИНАЧЕ
	|				ЛимитноЗаборныеКартыНеделя.ОтпущеноСверхЛимитаОборот
	|			КОНЕЦ -
	|			ВЫБОР КОГДА ЛимитноЗаборныеКартыНеделя.ВозвращеноОборот ЕСТЬ NULL ТОГДА
	|				0
	|			ИНАЧЕ
	|				ЛимитноЗаборныеКартыНеделя.ВозвращеноОборот
	|			КОНЕЦ
	|	) КАК ОтпущеноНеделя,
	|
	|	СУММА(	ВЫБОР КОГДА ЛимитноЗаборныеКартыМесяц.ОтпущеноОборот ЕСТЬ NULL ТОГДА
	|				0
	|			ИНАЧЕ
	|				ЛимитноЗаборныеКартыМесяц.ОтпущеноОборот
	|			КОНЕЦ -
	|			ВЫБОР КОГДА ЛимитноЗаборныеКартыМесяц.ОтпущеноСверхЛимитаОборот ЕСТЬ NULL ТОГДА
	|				0
	|			ИНАЧЕ
	|				ЛимитноЗаборныеКартыМесяц.ОтпущеноСверхЛимитаОборот
	|			КОНЕЦ -
	|			ВЫБОР КОГДА ЛимитноЗаборныеКартыМесяц.ВозвращеноОборот ЕСТЬ NULL ТОГДА
	|				0
	|			ИНАЧЕ
	|				ЛимитноЗаборныеКартыМесяц.ВозвращеноОборот
	|			КОНЕЦ
	|	) КАК ОтпущеноМесяц,
	|
	|	СУММА(	ВЫБОР КОГДА ЛимитноЗаборныеКартыКвартал.ОтпущеноОборот ЕСТЬ NULL ТОГДА
	|				0
	|			ИНАЧЕ
	|				ЛимитноЗаборныеКартыКвартал.ОтпущеноОборот
	|			КОНЕЦ -
	|			ВЫБОР КОГДА ЛимитноЗаборныеКартыКвартал.ОтпущеноСверхЛимитаОборот ЕСТЬ NULL ТОГДА
	|				0
	|			ИНАЧЕ
	|				ЛимитноЗаборныеКартыКвартал.ОтпущеноСверхЛимитаОборот
	|			КОНЕЦ -
	|			ВЫБОР КОГДА ЛимитноЗаборныеКартыКвартал.ВозвращеноОборот ЕСТЬ NULL ТОГДА
	|				0
	|			ИНАЧЕ
	|				ЛимитноЗаборныеКартыКвартал.ВозвращеноОборот
	|			КОНЕЦ
	|	) КАК ОтпущеноКвартал,
	|
	|	СУММА(	ВЫБОР КОГДА ЛимитноЗаборныеКартыГод.ОтпущеноОборот ЕСТЬ NULL ТОГДА
	|				0
	|			ИНАЧЕ
	|				ЛимитноЗаборныеКартыГод.ОтпущеноОборот
	|			КОНЕЦ -
	|			ВЫБОР КОГДА ЛимитноЗаборныеКартыГод.ОтпущеноСверхЛимитаОборот ЕСТЬ NULL ТОГДА
	|				0
	|			ИНАЧЕ
	|				ЛимитноЗаборныеКартыГод.ОтпущеноСверхЛимитаОборот
	|			КОНЕЦ -
	|			ВЫБОР КОГДА ЛимитноЗаборныеКартыГод.ВозвращеноОборот ЕСТЬ NULL ТОГДА
	|				0
	|			ИНАЧЕ
	|				ЛимитноЗаборныеКартыГод.ВозвращеноОборот
	|			КОНЕЦ
	|	) КАК ОтпущеноГод
	|ИЗ ВременнаяТаблицаДокумента КАК Док
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ЛимитыОтпускаМатериалов.СрезПоследних(&КонДата,
	|														(Склад, Номенклатура, Подразделение) В (" + ТекстЗапросаСписокНоменклатуры + ")
	|														И КонтролироватьЛимит
	|														) КАК ЛимитыОтпуска
	|	ПО 
	|		Док.Склад 			= ЛимитыОтпуска.Склад
	|		И Док.Подразделение = ЛимитыОтпуска.Подразделение
	|		И Док.Номенклатура 	= ЛимитыОтпуска.Номенклатура
	|		И Док.ХарактеристикаНоменклатуры = ЛимитыОтпуска.ХарактеристикаНоменклатуры
	|
	|	// Отпущено за текущий день.
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ЛимитноЗаборныеКарты.Обороты(&НачДатаДень, &КонДата, Период,
	|													(Склад, Номенклатура, Подразделение) В (" + ТекстЗапросаСписокНоменклатуры + ")
	|													) КАК ЛимитноЗаборныеКартыДень
	|	ПО 
	|		Док.Склад 			= ЛимитноЗаборныеКартыДень.Склад
	|		И Док.Подразделение = ЛимитноЗаборныеКартыДень.Подразделение
	|		И Док.Номенклатура 	= ЛимитноЗаборныеКартыДень.Номенклатура
	|		И Док.ХарактеристикаНоменклатуры = ЛимитноЗаборныеКартыДень.ХарактеристикаНоменклатуры
	|
	|	// Отпущено за текущую неделю.
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ЛимитноЗаборныеКарты.Обороты(&НачДатаНеделя, &КонДата, Период,
	|													(Склад, Номенклатура, Подразделение) В (" + ТекстЗапросаСписокНоменклатуры + ")
	|													) КАК ЛимитноЗаборныеКартыНеделя
	|	ПО 
	|		Док.Склад 			= ЛимитноЗаборныеКартыНеделя.Склад
	|		И Док.Подразделение = ЛимитноЗаборныеКартыНеделя.Подразделение
	|		И Док.Номенклатура 	= ЛимитноЗаборныеКартыНеделя.Номенклатура
	|		И Док.ХарактеристикаНоменклатуры = ЛимитноЗаборныеКартыНеделя.ХарактеристикаНоменклатуры
	|
	|	// Отпущено за текущий месяц.
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ЛимитноЗаборныеКарты.Обороты(&НачДатаМесяц, &КонДата, Период,
	|													(Склад, Номенклатура, Подразделение) В (" + ТекстЗапросаСписокНоменклатуры + ")
	|													) КАК ЛимитноЗаборныеКартыМесяц
	|	ПО 
	|		Док.Склад 			= ЛимитноЗаборныеКартыМесяц.Склад
	|		И Док.Подразделение = ЛимитноЗаборныеКартыМесяц.Подразделение
	|		И Док.Номенклатура 	= ЛимитноЗаборныеКартыМесяц.Номенклатура
	|		И Док.ХарактеристикаНоменклатуры = ЛимитноЗаборныеКартыМесяц.ХарактеристикаНоменклатуры
	|
	|	// Отпущено за текущий квартал.
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ЛимитноЗаборныеКарты.Обороты(&НачДатаКвартал, &КонДата, Период,
	|													(Склад, Номенклатура, Подразделение) В (" + ТекстЗапросаСписокНоменклатуры + ")
	|													) КАК ЛимитноЗаборныеКартыКвартал
	|	ПО 
	|		Док.Склад 			= ЛимитноЗаборныеКартыКвартал.Склад
	|		И Док.Подразделение = ЛимитноЗаборныеКартыКвартал.Подразделение
	|		И Док.Номенклатура 	= ЛимитноЗаборныеКартыКвартал.Номенклатура
	|		И Док.ХарактеристикаНоменклатуры = ЛимитноЗаборныеКартыКвартал.ХарактеристикаНоменклатуры
	|
	|	// Отпущено за текущий год.
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ЛимитноЗаборныеКарты.Обороты(&НачДатаГод, &КонДата, Период,
	|													(Склад, Номенклатура, Подразделение) В (" + ТекстЗапросаСписокНоменклатуры + ")
	|													) КАК ЛимитноЗаборныеКартыГод
	|	ПО 
	|		Док.Склад 			= ЛимитноЗаборныеКартыГод.Склад
	|		И Док.Подразделение = ЛимитноЗаборныеКартыГод.Подразделение
	|		И Док.Номенклатура 	= ЛимитноЗаборныеКартыГод.Номенклатура
	|		И Док.ХарактеристикаНоменклатуры = ЛимитноЗаборныеКартыГод.ХарактеристикаНоменклатуры
	|
	|СГРУППИРОВАТЬ ПО
	|	Док.Номенклатура,
	|	Док.ХарактеристикаНоменклатуры,
	|	Док.Склад,
	|	ЛимитыОтпуска.Периодичность,
	|	ЛимитыОтпуска.КонтролироватьЛимит
	|
	|ИМЕЮЩИЕ
	|	ЛимитыОтпуска.КонтролироватьЛимит = Истина
	|
	|ДЛЯ ИЗМЕНЕНИЯ РегистрНакопления.ЛимитноЗаборныеКарты.Обороты // Блокирующие чтение таблицы оборотов регистра для разрешения коллизий многопользовательской работы
	|
	|;
	|УНИЧТОЖИТЬ ВременнаяТаблицаДокумента";
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("НачДатаДень",    НачалоДня(ДокументОбъект.Дата));
	Запрос.УстановитьПараметр("НачДатаНеделя",  НачалоНедели(ДокументОбъект.Дата));
	Запрос.УстановитьПараметр("НачДатаМесяц",   НачалоМесяца(ДокументОбъект.Дата));
	Запрос.УстановитьПараметр("НачДатаКвартал", НачалоКвартала(ДокументОбъект.Дата));
	Запрос.УстановитьПараметр("НачДатаГод", 	НачалоГода(ДокументОбъект.Дата));
	Запрос.УстановитьПараметр("КонДата",        КонецДня(ДокументОбъект.Дата));
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ВидВыпуска", 	Перечисления.ВидыВыпуска.Выпуск);
	Запрос.УстановитьПараметр("НаправлениеВыпуска", Перечисления.НаправленияВыпуска.НаЗатраты);
	Запрос.УстановитьПараметр("НаправлениеВыпускаСписок", Перечисления.НаправленияВыпуска.НаЗатратыСписок);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	РазрешитьПревышениеЛимита = ДокументОбъект.РазрешитьПревышениеЛимита;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Не Выборка.КонтролироватьЛимит Тогда
			Продолжить;
		КонецЕсли;
		
		Если Выборка.Периодичность = Перечисления.Периодичность.День Тогда
			Отпущено = Выборка.ОтпущеноДень;
		ИначеЕсли Выборка.Периодичность = Перечисления.Периодичность.Неделя Тогда
			Отпущено = Выборка.ОтпущеноНеделя;
		ИначеЕсли Выборка.Периодичность = Перечисления.Периодичность.Месяц Тогда
			Отпущено = Выборка.ОтпущеноМесяц;
		ИначеЕсли Выборка.Периодичность = Перечисления.Периодичность.Квартал Тогда
			Отпущено = Выборка.ОтпущеноКвартал;
		ИначеЕсли Выборка.Периодичность = Перечисления.Периодичность.Год Тогда
			Отпущено = Выборка.ОтпущеноГод;
		Иначе
			Отпущено = 0;
		КонецЕсли;

		ЛимитОтпуска = Выборка.ЛимитОтпуска - Отпущено;
		ОтпущеноСверхЛимита = ?(РазрешитьПревышениеЛимита, Выборка.ОтпущеноСверхЛимита, 0);
		ДокументКоличество = Окр(Выборка.ДокументКоличество, 3, 1);

		Если ЛимитОтпуска < (ДокументКоличество - ОтпущеноСверхЛимита) Тогда

			СтрокаСообщения = "Лимита отпуска " + 
			УправлениеЗапасами.ПредставлениеНоменклатуры(Выборка.НоменклатураПредставление, 
			  Выборка.ХарактеристикаНоменклатурыПредставление) + 
				" со склада " + Выборка.СкладПредставление +
				" за период " + Выборка.Периодичность +
				" недостаточно.";

			УправлениеЗапасами.ОшибкаНетОстатка(СтрокаСообщения, ЛимитОтпуска, ДокументКоличество - ОтпущеноСверхЛимита,
			Выборка.ЕдиницаХраненияОстатковПредставление, Отказ, Заголовок);

		КонецЕсли;

	КонецЦикла;
	
КонецПроцедуры // КонтрольЛимитов()