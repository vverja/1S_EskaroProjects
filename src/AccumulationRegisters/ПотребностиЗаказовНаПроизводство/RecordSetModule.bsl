Перем мПериод          Экспорт; // Период движений
Перем мТаблицаДвижений Экспорт; // Таблица движений

// Выполняет приход по регистру.
//
// Параметры:
//  Нет.
//
Процедура ВыполнитьПриход() Экспорт

	ОбщегоНазначения.ВыполнитьДвижениеПоРегистру(ЭтотОбъект, ВидДвиженияНакопления.Приход);

КонецПроцедуры // ВыполнитьПриход()

// Выполняет расход по регистру.
//
// Параметры:
//  Нет.
//
Процедура ВыполнитьРасход() Экспорт

	ОбщегоНазначения.ВыполнитьДвижениеПоРегистру(ЭтотОбъект, ВидДвиженияНакопления.Расход);

КонецПроцедуры // ВыполнитьРасход()

// Функция создает строку с сообщением о нехватке остатка при проведении
//
Функция СоздатьСтрокуСообщения(Обход)
	Стр = " - По документу: " + Обход.ДокКоличествоЕдиницыДокумента + " Потребность: " + Обход.КолОстатокЕдиницыДокумента + " Превышение: " + Обход.Нехватка +
		  " (Материал: """    + СокрЛП(Обход.НоменклатураПред)         + ?(Обход.НоменклатураВестиУчетПоХарактеристикам, """ Хар-ка материала: """ + СокрЛП(Обход.ХарактеристикаНоменклатурыПред), """") +
		  " Ед. изм: """      + СокрЛП(Обход.ЕдиницаИзмеренияПред)     + """" +
		  ?(НЕ ЗначениеЗаполнено(Обход.ВидВоспроизводства), "", " Вид воспроизводства: """ + СокрЛП(Обход.ВидВоспроизводстваПред)+ """") +
		  ?(НЕ ЗначениеЗаполнено(Обход.Продукция), "",
		  	" Продукция: """     + СокрЛП(Обход.ПродукцияПред) + """" +
		  		?(Обход.ПродукцияВестиУчетПоХарактеристикам, " Хар-ка продукции: """ + СокрЛП(Обход.ХарактеристикаПродукцииПред) + """", "")) + ")";
	Возврат Стр;
КонецФункции // СоздатьСтрокуСообщения()

// Процедура контролирует возможность списания потребности по заказам на производство из регистра
//
Процедура КонтрольОстатков(ДокОбъект, ИмяТабЧасти, ДопПараметры = Неопределено, Отказ, Заголовок, РежимПроведения) Экспорт
	
	Если РежимПроведения <> РежимПроведенияДокумента.Оперативный Тогда
		Возврат;
	КонецЕсли;
	
	МетаДок = ДокОбъект.Метаданные();
	
	ФлагКолМинус = Ложь;         // Определяет что количество из документа надо брать со знаком минус. (напр. док. КорректировкаЗаказаНаПроизводство)
	Заказ        = Неопределено; // Заказ на производство
	ТабИмяЗаказа = "";           // Имя колонки Заказ в табличной части
	
	МетаРеквизиты = МетаДок.ТабличныеЧасти[ИмяТабЧасти].Реквизиты;
	ЕстьПолеВидВоспроизводства = МетаРеквизиты.Найти("ВидВоспроизводства") <> Неопределено;
	ЕстьПолеПродукция          = МетаРеквизиты.Найти("Продукция")          <> Неопределено;
	
	Если ДопПараметры <> Неопределено Тогда
		Если ДопПараметры.Свойство("КоличествоСМинусом") Тогда
			ФлагКолМинус = ДопПараметры.КоличествоСМинусом;
		КонецЕсли;
		Если ДопПараметры.Свойство("ЗаказНаПроизводство") Тогда
			Заказ = ДопПараметры.ЗаказНаПроизводство;
		КонецЕсли;
		Если ДопПараметры.Свойство("ИмяКолонкиЗаказ") Тогда
			ТабИмяЗаказа = ДопПараметры.ИмяКолонкиЗаказ;
		КонецЕсли;
	КонецЕсли;
	
	Если ПустаяСтрока(ТабИмяЗаказа) Тогда
		МассивЗаказов = Новый Массив;
		МассивЗаказов.Добавить(Заказ);
	Иначе
		МассивЗаказов = ДокОбъект[ИмяТабЧасти].ВыгрузитьКолонку(ТабИмяЗаказа);
	КонецЕсли;
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ДокРасходаПоЗаказу.ВидВоспроизводства,
		|	ПРЕДСТАВЛЕНИЕ(ДокРасходаПоЗаказу.ВидВоспроизводства) КАК ВидВоспроизводстваПред,
		|	ЕСТЬNULL(РегПотребности.КоличествоОстаток, 0) КАК КолОстаток,
		|	ВЫРАЗИТЬ(ЕСТЬNULL(РегПотребности.КоличествоОстаток, 0)* ДокРасходаПоЗаказу.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент / ДокРасходаПоЗаказу.ЕдиницаИзмерения.Коэффициент КАК Число(15,3))
		|														КАК КолОстатокЕдиницыДокумента,
		|	ДокРасходаПоЗаказу.Номенклатура,
		|	ДокРасходаПоЗаказу.ХарактеристикаНоменклатуры,
		|	ДокРасходаПоЗаказу.Продукция,
		|	ДокРасходаПоЗаказу.ХарактеристикаПродукции,
		|	ДокРасходаПоЗаказу.ЕдиницаИзмерения,
		|	ДокРасходаПоЗаказу.Номенклатура.ВестиУчетПоХарактеристикам,
		|	ДокРасходаПоЗаказу.Продукция.ВестиУчетПоХарактеристикам,
		|	ПРЕДСТАВЛЕНИЕ(ДокРасходаПоЗаказу.Номенклатура) КАК НоменклатураПред,
		|	ПРЕДСТАВЛЕНИЕ(ДокРасходаПоЗаказу.ХарактеристикаНоменклатуры) КАК ХарактеристикаНоменклатурыПред,
		|	ПРЕДСТАВЛЕНИЕ(ДокРасходаПоЗаказу.Продукция) КАК ПродукцияПред,
		|	ПРЕДСТАВЛЕНИЕ(ДокРасходаПоЗаказу.ХарактеристикаПродукции) КАК ХарактеристикаПродукцииПред,
		|	ПРЕДСТАВЛЕНИЕ(ДокРасходаПоЗаказу.ЕдиницаИзмерения) КАК ЕдиницаИзмеренияПред,
		|	ДокРасходаПоЗаказу.Количество КАК ДокКоличествоЕдиницыДокумента,
		|	ВЫРАЗИТЬ(ДокРасходаПоЗаказу.Количество * ДокРасходаПоЗаказу.ЕдиницаИзмерения.Коэффициент / ДокРасходаПоЗаказу.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент КАК Число(15,3))
		|														КАК ДокКоличество,
		|	ДокРасходаПоЗаказу.Количество - ВЫРАЗИТЬ(ЕСТЬNULL(РегПотребности.КоличествоОстаток, 0)* ДокРасходаПоЗаказу.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент / ДокРасходаПоЗаказу.ЕдиницаИзмерения.Коэффициент КАК Число(15,3))  
		|														КАК Нехватка
		|ИЗ
		|	(ВЫБРАТЬ
		|		ДокРасходаПоЗаказу.Номенклатура КАК Номенклатура,
		|		ДокРасходаПоЗаказу.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|		" + ?(ЕстьПолеПродукция,
						"ДокРасходаПоЗаказу.Продукция КАК Продукция,
		|				 ДокРасходаПоЗаказу.ХарактеристикаПродукции КАК ХарактеристикаПродукции,",
						"&ПустНоменклатура КАК Продукция,
		|		 		 &ПустНоменклатура КАК ХарактеристикаПродукции,") + "
		|		" + ?(ЕстьПолеВидВоспроизводства, "ДокРасходаПоЗаказу.ВидВоспроизводства", "&ПустВидВоспроизводства") + " КАК ВидВоспроизводства,
		|		ДокРасходаПоЗаказу.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|		" + ?(ФлагКолМинус, "-", "") + "СУММА(ДокРасходаПоЗаказу.Количество) КАК Количество
		|	ИЗ
		|	Документ." + МетаДок.Имя + "." + ИмяТабЧасти + " КАК ДокРасходаПоЗаказу
		|	ГДЕ
		|		ДокРасходаПоЗаказу.Ссылка = &ДокСсылка
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ДокРасходаПоЗаказу.Номенклатура,
		|		ДокРасходаПоЗаказу.ХарактеристикаНоменклатуры,
		|		" + ?(ЕстьПолеПродукция, "ДокРасходаПоЗаказу.Продукция,
		|		ДокРасходаПоЗаказу.ХарактеристикаПродукции,", "") + "
		|		" + ?(ЕстьПолеВидВоспроизводства, "ДокРасходаПоЗаказу.ВидВоспроизводства,", "") + "
		|		ДокРасходаПоЗаказу.ЕдиницаИзмерения) КАК ДокРасходаПоЗаказу
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПотребностиЗаказовНаПроизводство.Остатки(, ЗаказНаПроизводство В(&МассивЗаказов)) КАК РегПотребности
		|		ПО ДокРасходаПоЗаказу.Номенклатура = РегПотребности.Номенклатура
		|			И ДокРасходаПоЗаказу.ХарактеристикаНоменклатуры = РегПотребности.ХарактеристикаНоменклатуры
		|			И ДокРасходаПоЗаказу.ЕдиницаИзмерения = РегПотребности.ЕдиницаИзмерения
		|			" + ?(ЕстьПолеПродукция, "И ДокРасходаПоЗаказу.Продукция = РегПотребности.Продукция
		|			И ДокРасходаПоЗаказу.ХарактеристикаПродукции = РегПотребности.ХарактеристикаПродукции", "") + "
		|			" + ?(ЕстьПолеВидВоспроизводства, "И ДокРасходаПоЗаказу.ВидВоспроизводства = РегПотребности.ВидВоспроизводства", "") + "
		|ГДЕ
		|	ДокРасходаПоЗаказу.Количество - ВЫРАЗИТЬ(ЕСТЬNULL(РегПотребности.КоличествоОстаток, 0)* ДокРасходаПоЗаказу.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент / ДокРасходаПоЗаказу.ЕдиницаИзмерения.Коэффициент КАК Число(15,3)) > 0";
		
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.УстановитьПараметр("КонДата",       ДокОбъект.Дата);
	Запрос.УстановитьПараметр("ДокСсылка",     ДокОбъект.Ссылка);
	Запрос.УстановитьПараметр("МассивЗаказов", МассивЗаказов);
	Запрос.УстановитьПараметр("ПустВидВоспроизводства", Перечисления.ВидыВоспроизводстваНоменклатуры.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустНоменклатура",       Справочники.Номенклатура.ПустаяСсылка());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
	
		СтрОшибка = "Превышение потребности по заказу на производство: " + Заказ;
		Обход = РезультатЗапроса.Выбрать();
		Пока Обход.Следующий() Цикл
			СтрОшибка = СтрОшибка + Символы.ПС + СоздатьСтрокуСообщения(Обход);
		КонецЦикла;
		ОбщегоНазначения.СообщитьОбОшибке(СтрОшибка, Отказ, Заголовок);
		
	КонецЕсли;
	
КонецПроцедуры // КонтрольОстатков()