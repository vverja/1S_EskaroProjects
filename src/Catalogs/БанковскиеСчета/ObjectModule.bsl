Перем мВалютаРегламентированногоУчета Экспорт;
Перем ЭтоВалютныйСчет Экспорт;

Функция ПроверитьКорректностьНомераСчета(Номер, ТекстОшибки = "") Экспорт

	Если НЕ ТипЗнч(Номер) = Тип("Строка") Тогда
		ТекстОшибки = "Номер счета должен быть строкой.";
		Возврат Ложь;
	КонецЕсли;
	
	Если ПустаяСтрока(Номер) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если НЕ ОбщегоНазначения.ТолькоЦифрыВСтроке(Номер) Тогда
		ТекстОшибки = "В номере счета есть не только цифры. Возможно, номер указан неверно.";
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции // ПроверитьКорректностьНомераСчета()

Функция ПроверитьКорректностьМФО(МФО, ТекстОшибки = "") Экспорт

	Если НЕ ТипЗнч(МФО) = Тип("Строка") Тогда
		ТекстОшибки = "МФО банка должен быть строкой.";
		Возврат Ложь;
	КонецЕсли;
	
	Если ПустаяСтрока(МФО) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если НЕ ОбщегоНазначения.ТолькоЦифрыВСтроке(МФО) Тогда
		ТекстОшибки = "В составе МФО банка должны быть только цифры.";
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции // ПроверитьКорректностьМФО()

Функция ПроверитьКорректностьКоррсчета(Коррсчет, ТекстОшибки = "") Экспорт

	Если НЕ ТипЗнч(Коррсчет) = Тип("Строка") Тогда
		ТекстОшибки = "Корр.счет банка должен быть строкой.";
		Возврат Ложь;
	КонецЕсли;
	
	Если ПустаяСтрока(Коррсчет) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если НЕ ОбщегоНазначения.ТолькоЦифрыВСтроке(Коррсчет) Тогда
		ТекстОшибки = "В составе корр.счета банка должны быть только цифры.";
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции // ПроверитьКорректностьКоррсчета()

// Обработчик события ПередЗаписью формы.
//
Процедура ПередЗаписью(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если ПометкаУдаления ИЛИ Ссылка.ПометкаУдаления Тогда
		Возврат;
	КонецЕсли;
	
	Заголовок = "Запись банковского счета (" + Владелец + ") " + Наименование + ", код " + Код;
	
	Если НЕ ОбщегоНазначения.ТолькоЦифрыВСтроке(СокрЛП(НомерСчета)) Тогда 
		ОбщегоНазначения.СообщитьОбОшибке("В номере банковского счета присутствуют не только цифры. 
		|Возможно, номер указан неправильно", , Заголовок);
	КонецЕсли;

	// Проверка заполнения обязательных реквизитов
	Если НЕ ЗначениеЗаполнено(ВалютаДенежныхСредств) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не выбрана валюта.", Отказ, Заголовок);
	КонецЕсли;
	Если ВалютаДенежныхСредств = мВалютаРегламентированногоУчета Тогда
	
		Если НЕ ЗначениеЗаполнено(НомерСчета) Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Не указан номер счета.", Отказ, Заголовок);
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Банк) Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Не выбран банк.", Отказ, Заголовок);
		КонецЕсли;
	
	КонецЕсли;


КонецПроцедуры

мВалютаРегламентированногоУчета = глЗначениеПеременной("ВалютаРегламентированногоУчета");

