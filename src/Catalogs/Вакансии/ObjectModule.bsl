///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// Заголовок для сообщений об ошибках записи.
	Заголовок	= "Запись элемента справочника Вакансии"
				+ ?(ПустаяСтрока(Код), "", " с кодом " + Код)
				+ ?(ПустаяСтрока(Наименование), "", " и наименованием <" + Наименование + ">");
	
	Если Должность.Пустая() Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указана должность!", Отказ, Заголовок);
	КонецЕсли;
	
	РежимНабораПерсонала = Константы.РежимНабораПерсонала.Получить();
	Если РежимНабораПерсонала = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц Тогда
		Если Организация.Пустая() Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Не указана организация!", Отказ, Заголовок);
		КонецЕсли;
		Если Подразделение.Пустая() Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Не указано подразделение организации!", Отказ, Заголовок);
		КонецЕсли;
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
		Если Организация <> Подразделение.Владелец Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Указано подразделение, принадлежащее другой организации!", Отказ, Заголовок);
		КонецЕсли;
	Иначе
		Если Подразделение.Пустая() Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Не указано подразделение!", Отказ, Заголовок);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
