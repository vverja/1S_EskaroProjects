
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	//Заполнение переменных формы
	ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	СписокВыбораВидаДоговора = ПолучитьСписокВыбораВидаДоговора(Объект.Владелец);
		
	ВидДоговораПрочее 			= Перечисления.ВидыДоговоровКонтрагентов.Прочее;
	ВидДоговораСКомиссионером 	= Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером;
	ВидДоговораСКомитентом 		= Перечисления.ВидыДоговоровКонтрагентов.СКомитентом;
	ВидДоговораСПокупателем 	= Перечисления.ВидыДоговоровКонтрагентов.СПокупателем;
	ВидДоговораСПоставщиком 	= Перечисления.ВидыДоговоровКонтрагентов.СПоставщиком;
	
	ВедениеВзаиморасчетовПоДоговоруВЦелом = Перечисления.ВедениеВзаиморасчетовПоДоговорам.ПоДоговоруВЦелом;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступность()

	Элементы.ВедениеВзаиморасчетов.Доступность = (Объект.ВидДоговора <> ВидДоговораПрочее
														И ЗначениеЗаполнено(Объект.ВидДоговора));
	Элементы.ВедениеВзаиморасчетовРегл.Доступность = (Объект.ВидДоговора <> ВидДоговораПрочее
														И ЗначениеЗаполнено(Объект.ВидДоговора));

	// Установка видимости реквизита реализации на экспорт
	Если Объект.ВидДоговора <> ВидДоговораСПокупателем Тогда
		Элементы.РеализацияНаЭкспорт.Доступность = Ложь;
	Иначе
		Элементы.РеализацияНаЭкспорт.Доступность = Истина;
	КонецЕсли;

	Элементы.ВестиПоДокументамРасчетовСКонтрагентом.Доступность = Объект.ВидДоговора <> ВидДоговораПрочее;
КонецПроцедуры // УстановитьВидимость()

&НаСервереБезКонтекста
Функция ПолучитьСписокВыбораВидаДоговора(Контрагент)
	СписокВыбораВидаДоговора = Новый СписокЗначений;
	Если НЕ ЗначениеЗаполнено(Контрагент) Тогда
		СписокВыбораВидаДоговора = УправлениеВзаиморасчетами.ПолучитьСписокВидовДоговоровВзаиморасчетовПрочее();
	Иначе
		Если Контрагент.Покупатель И Контрагент.Поставщик Тогда
			СписокВыбораВидаДоговора = ОбщегоНазначения.ПолучитьСписокЭлементовПеречисления("ВидыДоговоровКонтрагентов");
		ИначеЕсли Контрагент.Покупатель Тогда
			СписокВыбораВидаДоговора = УправлениеВзаиморасчетами.ПолучитьСписокВидовДоговоровВзаиморасчетовДляПокупателя();
		ИначеЕсли Контрагент.Поставщик Тогда
			СписокВыбораВидаДоговора = УправлениеВзаиморасчетами.ПолучитьСписокВидовДоговоровВзаиморасчетовДляПоставщика();
		Иначе
			СписокВыбораВидаДоговора = УправлениеВзаиморасчетами.ПолучитьСписокВидовДоговоровВзаиморасчетовПрочее();
		КонецЕсли; 
	КонецЕсли;
	Возврат СписокВыбораВидаДоговора;
КонецФункции

&НаКлиенте
Процедура ЗаполнитьСписокВыбораВидаДоговора()
	Элементы.ВидДоговора.СписокВыбора.Очистить();
	Для Каждого ЭлементСписка ИЗ СписокВыбораВидаДоговора Цикл
		НовыйЭлементСписка = Элементы.ВидДоговора.СписокВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ВладелецПриИзменении(Элемент)
	СписокВыбораВидаДоговора = ПолучитьСписокВыбораВидаДоговора(Объект.Владелец);
	ЗаполнитьСписокВыбораВидаДоговора();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьТипЦенДоговора()
	Если Объект.ВидДоговора = ВидДоговораСПокупателем ИЛИ 
		Объект.ВидДоговора = ВидДоговораСКомиссионером Тогда
		ОписаниеТипов = Новый ОписаниеТипов("СправочникСсылка.ТипыЦенНоменклатуры");
	ИначеЕсли Объект.ВидДоговора = ВидДоговораСПоставщиком ИЛИ 
		Объект.ВидДоговора = ВидДоговораСКомитентом Тогда
		ОписаниеТипов = Новый ОписаниеТипов("СправочникСсылка.ТипыЦенНоменклатурыКонтрагентов");
	Иначе
		Возврат;
	КонецЕсли; 
	Объект.ТипЦен = ОписаниеТипов.ПривестиЗначение(Объект.ТипЦен);
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		УстановитьТипЦенДоговора();
		ЗаполнитьВедениеВзаиморасчетов();
		УстановитьСхемуНалогообложения();
	КонецЕсли;
	ЗаполнитьСписокВыбораВидаДоговора();
	ЗаполнитьСписокВыбораВедениеВзаиморасчетовРегл();
	УстановитьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура ВидДоговораПриИзменении(Элемент)
	Если НЕ ЗначениеЗаполнено(Объект.ВидДоговора) ИЛИ Объект.ВидДоговора = ВидДоговораПрочее Тогда
		Объект.ВедениеВзаиморасчетов                    = ВедениеВзаиморасчетовПоДоговоруВЦелом;
		Объект.ТипЦен                                   = Неопределено;
		Объект.ВестиПоДокументамРасчетовСКонтрагентом   = Ложь;
	КонецЕсли;

	//Зачистка флага "Реализация на экспорт" для всех видов договоров кроме договора с покупателем
	Если Объект.ВидДоговора <> ВидДоговораСПокупателем Тогда
		Объект.РеализацияНаЭкспорт = Ложь;
	КонецЕсли;
	
	УстановитьТипЦенДоговора();
	УстановитьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура ТипЦенОчистка(Элемент, СтандартнаяОбработка)
	УстановитьТипЦенДоговора();
КонецПроцедуры

&НаКлиенте
Процедура ВалютаВзаиморасчетовПриИзменении(Элемент)
	УстановитьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура ВедениеВзаиморасчетовПриИзменении(Элемент)
	
	ЗаполнитьСписокВыбораВедениеВзаиморасчетовРегл();
	
	Объект.ВедениеВзаиморасчетовРегл = Объект.ВедениеВзаиморасчетов;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСписокВыбораВедениеВзаиморасчетовРегл(ВедениеВзаиморасчетов)
	СписокВыбораВедениеВзаиморасчетов = Новый СписокЗначений;
	Если ВедениеВзаиморасчетов = Перечисления.ВедениеВзаиморасчетовПоДоговорам.ПоСчетам Тогда
		СписокВыбораВедениеВзаиморасчетов.Добавить(Перечисления.ВедениеВзаиморасчетовПоДоговорам.ПоДоговоруВЦелом);
		СписокВыбораВедениеВзаиморасчетов.Добавить(Перечисления.ВедениеВзаиморасчетовПоДоговорам.ПоСчетам);
	ИначеЕсли ВедениеВзаиморасчетов = Перечисления.ВедениеВзаиморасчетовПоДоговорам.ПоЗаказам Тогда
		СписокВыбораВедениеВзаиморасчетов.Добавить(Перечисления.ВедениеВзаиморасчетовПоДоговорам.ПоДоговоруВЦелом);
		СписокВыбораВедениеВзаиморасчетов.Добавить(Перечисления.ВедениеВзаиморасчетовПоДоговорам.ПоЗаказам);
	Иначе
		СписокВыбораВедениеВзаиморасчетов.Добавить(Перечисления.ВедениеВзаиморасчетовПоДоговорам.ПоДоговоруВЦелом);
	КонецЕсли;
	
	Возврат СписокВыбораВедениеВзаиморасчетов;
КонецФункции

&НаСервере
Функция ЗаполнитьСписокВыбораВедениеВзаиморасчетовРегл()
	
	СписокВыбораВедениеВзаиморасчетов = ПолучитьСписокВыбораВедениеВзаиморасчетовРегл(Объект.ВедениеВзаиморасчетов);
    Элементы.ВедениеВзаиморасчетовРегл.СписокВыбора.Очистить();
	Для Каждого ЭлементСписка ИЗ СписокВыбораВедениеВзаиморасчетов Цикл
		НовыйЭлементСписка = Элементы.ВедениеВзаиморасчетовРегл.СписокВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
	КонецЦикла;
  	
КонецФункции

&НаСервере
Процедура ЗаполнитьВедениеВзаиморасчетов()
	 Если   Объект.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.Прочее
		ИЛИ Объект.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.Бартерный
		Тогда
			Объект.ВедениеВзаиморасчетов = Перечисления.ВедениеВзаиморасчетовПоДоговорам.ПоДоговоруВЦелом;
		Иначе
		Если НЕ ЗначениеЗаполнено(Объект.ВедениеВзаиморасчетов) Тогда
			Объект.ВедениеВзаиморасчетов = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ОсновноеВедениеВзаиморасчетовПоДоговорам");
			Если НЕ ЗначениеЗаполнено(Объект.ВедениеВзаиморасчетов) Тогда
				Объект.ВедениеВзаиморасчетов = Перечисления.ВедениеВзаиморасчетовПоДоговорам.ПоЗаказам;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
    Объект.ВедениеВзаиморасчетовРегл = Объект.ВедениеВзаиморасчетов;
КонецПроцедуры

&НаСервере
Процедура УстановитьСхемуНалогообложения()
	
	Если Объект.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером Тогда
		Объект.СхемаНалоговогоУчета = Справочники.СхемыНалоговогоУчетаПоДоговорамКонтрагентов.СКомиссионером;
	ИначеЕсли Объект.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомитентом Тогда
		Объект.СхемаНалоговогоУчета = Справочники.СхемыНалоговогоУчетаПоДоговорамКонтрагентов.СКомитентом;
	ИначеЕсли Объект.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.Бартерный Тогда
		Объект.СхемаНалоговогоУчета = Справочники.СхемыНалоговогоУчетаПоДоговорамКонтрагентов.Бартер;
	Иначе
		Если НЕ Объект.ВалютаВзаиморасчетов = глЗначениеПеременной("ВалютаРегламентированногоУчета") Тогда
			Объект.СхемаНалоговогоУчета = Справочники.СхемыНалоговогоУчетаПоДоговорамКонтрагентов.ВЭД;		
		Иначе	
			Объект.СхемаНалоговогоУчета = Справочники.СхемыНалоговогоУчетаПоДоговорамКонтрагентов.ПоПервомуСобытию;
		КонецЕсли;
	КонецЕсли;

	Объект.СхемаНалоговогоУчетаПоТаре = Справочники.СхемыНалоговогоУчетаПоДоговорамКонтрагентов.ПоПервомуСобытию;
	
КонецПроцедуры	

