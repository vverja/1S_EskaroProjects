Перем мУчетДенежныхСредствПоОбособленнымПодразделениям Экспорт;

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Обработчик события ПередЗаписью объекта.
//
Процедура ПередЗаписью(Отказ)
	
	Если НЕ ОбменДанными.Загрузка Тогда
	
		Если НЕ ЭтоГруппа И НЕ ЗначениеЗаполнено(ВалютаДенежныхСредств) Тогда
			Сообщить("Не указана валюта денежных средств.",СтатусСообщения.Важное);
			Отказ = Истина;
		КонецЕсли;

		Если НЕ ЭтоГруппа 
		   И НЕ ЭтоНовый() 
		   И ВалютаДенежныхСредств <> Ссылка.ВалютаДенежныхСредств 
		   И ПолныеПрава.Кассы_СуществуютСсылки(Ссылка) Тогда

			Сообщить("Существуют документы, проведенные по кассе """ + Наименование + """.
			         |Реквизит ""Валюта денежных средств"" не может быть изменен, элемент не записан.", 
			         СтатусСообщения.Важное);

			Отказ = Истина;
		КонецЕсли;
	
	КонецЕсли; 
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТИРУЕМЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура ОбновитьПараметрыУчетнойПолитики(Дата,Организация) Экспорт

	УчетнаяПолитикаНеЗадана = Ложь;
	мУчетнаяПолитикаБУ = ОбщегоНазначения.ПолучитьПараметрыУчетнойПолитики(Дата, УчетнаяПолитикаНеЗадана, Организация, "Бух");

	БУ = ПланыСчетов.Хозрасчетный.КассаВНациональнойВалюте;
	ЕстьАналитикаПоОбособленнымПодразделениям = ?(БУ.ВидыСубконто.Найти(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОбособленныеПодразделенияБезОбразованияЮрЛица, "ВидСубконто") = Неопределено, Ложь, Истина);

	мУчетДенежныхСредствПоОбособленнымПодразделениям = Ложь;
	Если ЗначениеЗаполнено(мУчетнаяПолитикаБУ) Тогда	
		мУчетДенежныхСредствПоОбособленнымПодразделениям = ЕстьАналитикаПоОбособленнымПодразделениям И мУчетнаяПолитикаБУ.ВестиУчетДенежныхСредствПоОбособленнымПодразделениям;
	КонецЕсли;
	
КонецПроцедуры
