
// Функция проверяет, существуют ли ссылки на налоговое назначение в движениях регистров накопления.
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//  Истина - если есть движения, Ложь - если нет.
//
Функция СуществуютСсылки() Экспорт

	Возврат ПолныеПрава.НалоговоеНазначение_СуществуютСсылки(Ссылка);

КонецФункции //  СуществуютСсылки()


Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;	
	
	Если ПометкаУдаления Тогда   
		// пометку удаления разрешаем всегда
		Возврат;		                   
	КонецЕсли; 
	
	
	Если Не ЗначениеЗаполнено(ГруппаНалоговогоНазначения) Тогда
		Сообщить("Не заполнена группа налогового назначения " + Наименование + " !",
				 СтатусСообщения.Важное);
		Отказ = Истина;
		Возврат;
	Иначе
		Если ГруппаНалоговогоНазначения = Перечисления.ГруппыНалоговыхНазначений.НалоговыеНазначенияАктивовИВзаиморасчетовПоНДС Тогда
			// в этой группе нельзя новые вводить новые назначения и изменять ВидНалоговойДеятельности и ВидДеятельностиНДС
			Если ЭтоНовый() ИЛИ (ВидНалоговойДеятельности <> Ссылка.ВидНалоговойДеятельности) ИЛИ (ВидДеятельностиНДС <> Ссылка.ВидДеятельностиНДС) Тогда
				Сообщить("Нельзя вводить новые и изменять налоговые назначения из группы налоговых назначений ""Активов и взаиморасчетов по НДС""!", 
						 СтатусСообщения.Важное);
				Отказ = Истина;
				Возврат;
			КонецЕсли;	
		ИначеЕсли ГруппаНалоговогоНазначения = Перечисления.ГруппыНалоговыхНазначений.НалоговыеНазначенияДоходовИЗатрат Тогда	
			// ВидДеятельностиНДС должен быть пустым
			ВидДеятельностиНДС = Перечисления.ВидыДеятельностиНДС.ПустаяСсылка();
		КонецЕсли; 
	КонецЕсли; 
	
	Если НЕ ЭтоНовый() 
		И ВидНалоговойДеятельности = Ссылка.ВидНалоговойДеятельности 
		И Амортизируется = Ссылка.Амортизируется
		И ВидДеятельностиНДС = Ссылка.ВидДеятельностиНДС
		И ГруппаНалоговогоНазначения = Ссылка.ГруппаНалоговогоНазначения
		
		Тогда
	    // 	Если не изменяем тройку налоговых реквизитов и группу налогового назначения (а только "Используется" или Наименование) - разрешаем это делать всегда
		Возврат;		                   
	КонецЕсли; 
	
	// Налоговое назначение нельзя записывать с такой же тройкой налоговых реквизитов и группой, как и существующие налоговые назначения
	УсловиеСсылка = "";
	
	Запрос = Новый Запрос;
	
	Если НЕ ЭтоНовый() Тогда  
		УсловиеСсылка = " И НалоговыеНазначенияАктивовИЗатрат.Ссылка <> &Ссылка ";
		Запрос.УстановитьПараметр("Ссылка", Ссылка); 
	КонецЕсли;	
	
	Запрос.Текст = "ВЫБРАТЬ
				   |	НалоговыеНазначенияАктивовИЗатрат.ГруппаНалоговогоНазначения,
				   |	НалоговыеНазначенияАктивовИЗатрат.ВидНалоговойДеятельности,
				   |	НалоговыеНазначенияАктивовИЗатрат.Амортизируется,
				   |	НалоговыеНазначенияАктивовИЗатрат.ВидДеятельностиНДС,
				   |	НалоговыеНазначенияАктивовИЗатрат.Представление
				   |ИЗ
				   |	Справочник.НалоговыеНазначенияАктивовИЗатрат КАК НалоговыеНазначенияАктивовИЗатрат
				   |ГДЕ
				   |	НалоговыеНазначенияАктивовИЗатрат.ГруппаНалоговогоНазначения = &ГруппаНалоговогоНазначения
				   |	И НалоговыеНазначенияАктивовИЗатрат.ВидНалоговойДеятельности = &ВидНалоговойДеятельности
				   |	И НалоговыеНазначенияАктивовИЗатрат.Амортизируется = &Амортизируется
				   |	И НалоговыеНазначенияАктивовИЗатрат.ВидДеятельностиНДС = &ВидДеятельностиНДС
				   |" + УсловиеСсылка + "
				   |";
				   
				   
	Запрос.УстановитьПараметр("ГруппаНалоговогоНазначения", ГруппаНалоговогоНазначения); 			   
	Запрос.УстановитьПараметр("ВидНалоговойДеятельности", ВидНалоговойДеятельности); 
	Запрос.УстановитьПараметр("Амортизируется", Амортизируется); 
	Запрос.УстановитьПараметр("ВидДеятельностиНДС", ВидДеятельностиНДС); 
				   
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	СуществующиеНН = "";
	Пока Выборка.Следующий() Цикл
		СуществующиеНН = СуществующиеНН + Выборка.Представление + Символы.ПС;
	КонецЦикла;
	
	Если СуществующиеНН <> "" Тогда
		Сообщить("Налоговое назначение не может быть записано, 
				 |так как уже существуют налоговые назначения с такими же налоговыми реквизитами:" 
				 + Символы.ПС + СуществующиеНН, 
				 СтатусСообщения.Важное);
		Отказ = Истина;
	КонецЕсли; 
	
	
	Если НЕ ЭтоНовый() Тогда   
		
		Если Предопределенный И Используется = Ссылка.Используется Тогда  
			// предопределенные нельзя изменять, кроме Используется	
			Сообщить("Запрещено изменять предопределенные налоговые назначения.
					 |Элемент не записан.", 
					 СтатусСообщения.Важное);
			Отказ = Истина;
		ИначеЕсли (ГруппаНалоговогоНазначения <> Ссылка.ГруппаНалоговогоНазначения)
			ИЛИ (ВидНалоговойДеятельности <> Ссылка.ВидНалоговойДеятельности)
		 	ИЛИ (Амортизируется <> Ссылка.Амортизируется)
		 	ИЛИ (ВидДеятельностиНДС <> Ссылка.ВидДеятельностиНДС)
		 		Тогда
		    // существующие НН нельзя менять, если на них уже есть ссылки в регистрах накопления
			Если ЭтотОбъект.СуществуютСсылки() Тогда

				Сообщить("Существуют документы, проведенные по налоговому назначению """ + Наименование + """.
						 |Реквизиты ""Группа налогового назначения"", ""Вид налоговой деятельности"", ""Вид деятельности НДС"", 
						 |не могут быть изменены, элемент не записан.", 
						 СтатусСообщения.Важное);
				Отказ = Истина;

			КонецЕсли;
		КонецЕсли;
	КонецЕсли; 
	

КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если НЕ ЭтоНовый() Тогда
		ОбновитьПовторноИспользуемыеЗначения();
	КонецЕсли;	
	
КонецПроцедуры
