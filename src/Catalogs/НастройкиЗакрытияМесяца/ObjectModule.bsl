//Процедура выполняет первоначальное заполнение настроек при вводе нового элемента справочника
Процедура ЗаполнитьСписокРегламентныхОпераций(ОбъектКопирования = Неопределено, Использовать = Истина) Экспорт
	
	РегламентныеОперации.Очистить();
	ТочкиМаршрута = БизнесПроцессЗакрытиеМесяца.ПолучитьТочкиМаршрутаЗакрытияМесяцаИРасчетаНДС();
	
	Макет = ПолучитьМакет("МакетНастроекПоУмолчанию");
	ОбластьСписокНастроек = Макет.Области.Найти("Список");
	
	Если ОбластьСписокНастроек <> Неопределено Тогда
		ОбластьСписокНастроек = Макет.ПолучитьОбласть("Список");
		Для Сч = 1 по ОбластьСписокНастроек.ВысотаТаблицы Цикл
			РеглОперация = СокрЛП(ОбластьСписокНастроек.Область(Сч,1,Сч,1).Текст);
			УУ = Булево(Число(СокрЛП(ОбластьСписокНастроек.Область(Сч,2,Сч,2).Текст)));
			БУ = Булево(Число(СокрЛП(ОбластьСписокНастроек.Область(Сч,3,Сч,3).Текст)));

			НоваяСтрока = РегламентныеОперации.Добавить();
			НоваяСтрока.РегламентнаяОперация = ТочкиМаршрута[РеглОперация];
			НоваяСтрока.ОтражатьВУправленческомУчете = УУ;
			НоваяСтрока.ОтражатьВБухгалтерскомУчете = БУ;
			НоваяСтрока.Использовать = Использовать;
			
			//Настройка скопирована - необходимо перенести настройку для добавленных регламентных операций из объекта копирования
			Если ОбъектКопирования <> Неопределено Тогда
				СтрокаОбъектаКопирования = ОбъектКопирования.РегламентныеОперации.Найти(ТочкиМаршрута[РеглОперация],"РегламентнаяОперация");
				Если СтрокаОбъектаКопирования <> Неопределено Тогда
					НоваяСтрока.Использовать 	= СтрокаОбъектаКопирования.Использовать;
					НоваяСтрока.Ответственный 	= СтрокаОбъектаКопирования.Ответственный;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли; 
	
КонецПроцедуры


Процедура ПередЗаписью(Отказ)
	Если ОбменДанными.Загрузка ИЛИ ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	Если  ДатаНачалаДействияНастройки = Дата('00010101000000') Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указана дата начала действия настройки", Отказ);
		Отказ = истина;
	КонецЕсли;
	Если НЕ (ОтражатьВУправленческомУчете или ОтражатьВБухгалтерскомУчете) Тогда	
		ОбщегоНазначения.СообщитьОбОшибке("Не установлены признаки отражения в учете", Отказ);
	КонецЕсли;
	
	Строка = РегламентныеОперации.Найти(истина,"Использовать");
	Если Строка = Неопределено Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не включена ни одна регламентная операция",Отказ);
	КонецЕсли;
		
КонецПроцедуры
