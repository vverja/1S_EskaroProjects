////////////////////////////////////////////////////////////////////////////////
// ФОРМА

// Процедура проверяет, совпадало ли ранее полное наименование с наименованием,
// и присваивает соответствующее значение переменной мФормироватьНаименованиеПолноеАвтоматически.
//
&НаКлиенте
Процедура УстановитьФлагФормироватьНаименованиеПолноеАвтоматически()

	Если ПустаяСтрока(Объект.НаименованиеПолное) 
		ИЛИ Объект.НаименованиеПолное = Объект.Наименование Тогда
		
		ФормироватьНаименованиеПолноеАвтоматически = Истина;
	Иначе
		ФормироватьНаименованиеПолноеАвтоматически = Ложь;
	КонецЕсли;

КонецПроцедуры // УстановитьФлагФормироватьНаименованиеПолноеАвтоматически()

// Процедура проверяет, необходимо ли формировать полное наименование автоматически или нет,
// и, если необходимо, формирует его.
//
&НаКлиенте
Процедура СформироватьНаименованиеПолноеАвтоматически()

	Если ФормироватьНаименованиеПолноеАвтоматически Тогда
		Объект.НаименованиеПолное = Объект.Наименование;
	КонецЕсли;

КонецПроцедуры // СформироватьНаименованиеПолноеАвтоматически()

// Процедура записывает единицу хранения остатков номенклатуры.
//
&НаСервере
Процедура ПроверитьЕдиницуХраненияОстатков(ТекущийОбъект, Отказ)

	Если ЗначениеЗаполнено(ТекущийОбъект.ЕдиницаХраненияОстатков) Тогда
		Возврат;
	КонецЕсли;

	ВыборкаЕдиниц = Справочники.ЕдиницыИзмерения.Выбрать(, ТекущийОбъект.Ссылка);
	Если ВыборкаЕдиниц.Следующий() Тогда
		НайденнаяЕдиница = ВыборкаЕдиниц.Ссылка;
	Иначе
		НайденнаяЕдиницаОбъект = Справочники.ЕдиницыИзмерения.СоздатьЭлемент();
		НайденнаяЕдиницаОбъект.Наименование            = ТекущийОбъект.БазоваяЕдиницаИзмерения.Наименование;
		НайденнаяЕдиницаОбъект.ЕдиницаПоКлассификатору = ТекущийОбъект.БазоваяЕдиницаИзмерения;
		НайденнаяЕдиницаОбъект.Коэффициент             = 1;
		НайденнаяЕдиницаОбъект.Владелец                = ТекущийОбъект.Ссылка;

		Попытка
			НайденнаяЕдиницаОбъект.Записать();
		Исключение
			ТекстОшибки = НСтр("ru = 'Не удалось записать единицу хранения остатков: '");
			ОбщегоНазначения.СообщитьОбОшибке(ТекстОшибки + ОписаниеОшибки(), Отказ,,, ТекущийОбъект.Ссылка);
			Возврат;
		КонецПопытки;

		НайденнаяЕдиница = НайденнаяЕдиницаОбъект.Ссылка;
	КонецЕсли;

	ТекущийОбъект.ЕдиницаХраненияОстатков = НайденнаяЕдиница;
	Попытка
	
		ТекущийОбъект.Записать();	
	
	Исключение
		Отказ = Истина;
	КонецПопытки; 

КонецПроцедуры // ПроверитьЕдиницуХраненияОстатков()

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	
	СформироватьНаименованиеПолноеАвтоматически();
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПолноеПриИзменении(Элемент)
	
	УстановитьФлагФормироватьНаименованиеПолноеАвтоматически();
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПолноеНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	// Формирование списка выбора.
	Список = Новый СписокЗначений;
	Список.Добавить(Объект.Наименование);

	// Выбор из списка и обработка выбора.
	РезультатВыбора = ВыбратьИзСписка(Список, Элемент);
	Если РезультатВыбора <> Неопределено Тогда
		Объект.НаименованиеПолное = РезультатВыбора.Значение;
		ФормироватьНаименованиеПолноеАвтоматически = Истина;
	КонецЕсли;
	
КонецПроцедуры

// Процедура заполняет реквизиты значениями по умолчанию.
//
&НаСервере
Процедура ЗаполнитьРеквизитыПоУмолчанию()

	ТекущийПользователь = глЗначениеПеременной("глТекущийПользователь");
	
	Если НЕ ЗначениеЗаполнено(Объект.БазоваяЕдиницаИзмерения) Тогда
		Объект.БазоваяЕдиницаИзмерения = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(ТекущийПользователь, "ОсновнаяЕдиницаПоКлассификатору");
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(Объект.СтавкаНДС) Тогда
		Объект.СтавкаНДС = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(ТекущийПользователь, "ОсновнаяСтавкаНДС");
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(Объект.ВидНоменклатуры) Тогда
		Если ЗначениеЗаполнено(Объект.Родитель.ВидНоменклатуры) Тогда
			Объект.ВидНоменклатуры = Объект.Родитель.ВидНоменклатуры;
		Иначе
			Объект.ВидНоменклатуры = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(ТекущийПользователь, "ОсновнойВидНоменклатуры");
		КонецЕсли;
		
		Объект.Услуга   = (Объект.ВидНоменклатуры.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Услуга);
		Объект.Набор    = (Объект.ВидНоменклатуры.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Набор);
		Объект.Комплект = (Объект.ВидНоменклатуры.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Комплект);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.НаправлениеВыпуска) Тогда
		Объект.НаправлениеВыпуска = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(ТекущийПользователь, "ОсновноеНаправлениеВыпуска");
	КонецЕсли;
	

КонецПроцедуры // ЗаполнитьРеквизитыПоУмолчанию()

// Процедура снимает флаги, имена которых переданы в структуре, если флаги установлены.
//
// Параметры:
//  СтруктураФлагов - структура с именами флагов.
//
&НаКлиенте
Процедура СнятьФлаги(СтруктураФлагов)

	// Не обращаемся напрямую к свойствам объекта, так как часть из них может быть
	// недоступна данному пользователю
	ЗаполнитьЗначенияСвойств(СтруктураФлагов, Объект);
	ТребуетсяСнятьФлаги = Новый Структура();
	Для Каждого ЗначениеФлага Из СтруктураФлагов Цикл
		Если ЗначениеФлага.Значение = Истина Тогда
			// Это флаг и его надо снять
			ТребуетсяСнятьФлаги.Вставить(ЗначениеФлага.Ключ, Ложь);
		КонецЕсли;
	КонецЦикла;
	
	ЗаполнитьЗначенияСвойств(Объект, ТребуетсяСнятьФлаги);

КонецПроцедуры // СнятьФлаги()

&НаСервереБезКонтекста
Функция ПолучитьТипНоменклатурыПоВиду(ВидНоменклатуры)
	
	Возврат ВидНоменклатуры.ТипНоменклатуры;
	
КонецФункции // 

// Процедура заполняет по реквизиту формы ВидНоменлатуры другие признаки
//
&НаКлиенте
Процедура ЗаполнитьПризнакиПоВидуНоменклатуры()

	ТипНоменклатуры = ПолучитьТипНоменклатурыПоВиду(Объект.ВидНоменклатуры);
	
	Объект.Услуга   = (ТипНоменклатуры = ТипУслуга);
	Объект.Набор    = (ТипНоменклатуры = ТипНабор);
	Объект.Комплект = (ТипНоменклатуры = ТипКомплект);
	// сбросим флаг ТранспортнаяУслуга, если он был выбран и выбрана не услуга
	Если Объект.Услуга Или Объект.Набор Или Объект.Комплект Тогда
		СтруктураФлагов = Новый Структура;
		СтруктураФлагов.Вставить("ВестиУчетПоСериям");
		СтруктураФлагов.Вставить("ВестиПартионныйУчетПоСериям");
		СтруктураФлагов.Вставить("Весовой");
		СтруктураФлагов.Вставить("ВестиОперативныйУчетОстатковНЗП");
		СтруктураФлагов.Вставить("ВестиУчетПоСериямВНЗП");
		СтруктураФлагов.Вставить("ВестиСерийныеНомера");

		Если Объект.Услуга Или Объект.Набор Тогда
			СтруктураФлагов.Вставить("ВестиУчетПоХарактеристикам");
		КонецЕсли;

		СнятьФлаги(СтруктураФлагов);
	КонецЕсли;

КонецПроцедуры // ЗаполнитьПризнакиПоВидуНоменклатуры()

// Процедура устанавливает доступность для элементов формы.
//
&НаКлиенте
Процедура УстановитьДоступность()

	НаборКомплект    = (Объект.Набор ИЛИ Объект.Комплект);
	Товар            = НЕ (Объект.Услуга ИЛИ НаборКомплект);
	Производство     = (Объект.ВидВоспроизводства = ВидВоспроизводстваПроизводство ИЛИ Объект.ВидВоспроизводства = ВидВоспроизводстваПереработка);
	
	ДоступностьСерия = (ИспользованиеСерийНоменклатуры И Объект.ВестиУчетПоСериям И Товар);
	
	Если ИспользованиеХарактеристикНоменклатуры Тогда
		Элементы.ВестиУчетПоХарактеристикам.Доступность = (Товар ИЛИ Объект.Комплект);
	КонецЕсли; 
	
 	Если ИспользованиеСерийНоменклатуры Тогда
		Элементы.ВестиУчетПоСериям.Доступность = Товар;
	КонецЕсли; 
	
	Если Элементы.Найти("ВестиОперативныйУчетОстатковНЗП") <> Неопределено Тогда
		// Реквизит может быть недоступен пользователю
		Элементы.ВестиОперативныйУчетОстатковНЗП.Доступность = Товар;
	КонецЕсли;
	
	Если Объект.Услуга Тогда
		ЗаполнятьЕдиницы = Ложь;
	Иначе
		ЗаполнятьЕдиницы = Неопределено; // Авто
	КонецЕсли; 
	Элементы.БазоваяЕдиницаИзмерения.АвтоОтметкаНезаполненного = ЗаполнятьЕдиницы;
	Элементы.ЕдиницаХраненияОстатков.АвтоОтметкаНезаполненного = ЗаполнятьЕдиницы;
	Элементы.ЕдиницаДляОтчетов.АвтоОтметкаНезаполненного       = ЗаполнятьЕдиницы;
	
КонецПроцедуры //
 

// Процедура, выполняющая необходимые действия при изменении вида номенклатуры.
//
&НаКлиенте
Процедура ПриИзмененииВидаНоменклатуры()

	ЗаполнитьПризнакиПоВидуНоменклатуры();
    УстановитьДоступность();
	
КонецПроцедуры // ПриИзмененииВидаНоменклатуры()

&НаКлиенте
Процедура ВидНоменклатурыПриИзменении(Элемент)
	
	ПриИзмененииВидаНоменклатуры();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СОБЫТИЯ ФОРМЫ

&НаСервере
Процедура УстановитьОтборЕдиницИзмерения()
	
	// В свойствах динамического списка добавлен отбор по владельцу,
	// ему задано представление "ОтборПоВладельцу",
	// элемент отбора не доступен пользователю
	Для Каждого ТекущийОтбор Из ЕдиницыИзмерения.Отбор.Элементы Цикл
		Если ТекущийОтбор.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный 
		   И ТекущийОтбор.Представление = "ОтборПоВладельцу" Тогда
		    // Элемент отбора - "Предопределенный"
			ТекущийОтбор.ПравоеЗначение = Объект.Ссылка;
		КонецЕсли;
	КонецЦикла;	
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ТекущийОбъект.Услуга Тогда
		Если ЗначениеЗаполнено(ТекущийОбъект.БазоваяЕдиницаИзмерения) Тогда
			ПроверитьЕдиницуХраненияОстатков(ТекущийОбъект, Отказ);
		КонецЕсли;
	Иначе
		ПроверитьЕдиницуХраненияОстатков(ТекущийОбъект, Отказ);
	КонецЕсли;
	
КонецПроцедуры

// Процедура устанавливает реквизиты-константы
// реквизиты-константы используются на клиенте
//
&НаСервере
Процедура УстановитьКонстантыФормы()

	ТипУслуга = Перечисления.ТипыНоменклатуры.Услуга;
	ТипНабор = Перечисления.ТипыНоменклатуры.Набор;
	ТипКомплект = Перечисления.ТипыНоменклатуры.Комплект;
	
	ВидВоспроизводстваПроизводство = Перечисления.ВидыВоспроизводстваНоменклатуры.Производство;
	ВидВоспроизводстваПереработка = Перечисления.ВидыВоспроизводстваНоменклатуры.Переработка;
	
	ИспользованиеСерийНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользованиеСерийНоменклатуры");
	ИспользованиеХарактеристикНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользованиеХарактеристикНоменклатуры");
	
КонецПроцедуры //
 
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Отказ = Истина;
	
	УстановитьКонстантыФормы();
	
	// Настройка списка единиц измерения
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		УстановитьОтборЕдиницИзмерения();
		Элементы.ЕдиницыИзмерения.ТолькоПросмотр = Ложь;
	Иначе
		// Новый элемент
		ЗаполнитьРеквизитыПоУмолчанию();
		Элементы.ЕдиницыИзмерения.ТолькоПросмотр = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// Настройка списка единиц измерения
	УстановитьОтборЕдиницИзмерения();
	Элементы.ЕдиницыИзмерения.ТолькоПросмотр = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьФлагФормироватьНаименованиеПолноеАвтоматически();
	
	УстановитьДоступность();
	
КонецПроцедуры

