Перем ТабИсточников Экспорт;

Функция ВыполнитьРасчет(ДатаНач='00010101000000',ДатаКон='00010101000000',ПоТекущимНастройкам=Ложь,Организация=Неопределено) Экспорт

	Если СоставФормулы.Количество()=0 Тогда
		Возврат 0;
	КонецЕсли;
	
	Результат = Неопределено;
	СтрокаВыражения = "Результат = ";
	
	//Состояние("Выполняется расчет показателя "+Наименование+"...");
	
	Для каждого Стр Из СоставФормулы Цикл
		Если ТипЗнч(Стр.ИсточникДанных) = Тип("Строка") Тогда
			СтрокаВыражения = СтрокаВыражения + Стр.ИсточникДанных;
		ИначеЕсли (ТипЗнч(Стр.ИсточникДанных) = Тип("СправочникСсылка.СтатьиФинансовойОтчетности")) ИЛИ (ТабИсточников = Неопределено) Тогда
			Спр = Стр.ИсточникДанных.ПолучитьОбъект();
			Спр.ТабИсточников = ТабИсточников;
			РезультатДочерний = Спр.ВыполнитьРасчет(ДатаНач,ДатаКон,,Организация);
			СтрокаВыражения = СтрокаВыражения + Формат(РезультатДочерний, "ЧРД=.; ЧН=; ЧГ=0");
		Иначе
			ОтборОрганизация = Организация;
			Если НЕ ЗначениеЗаполнено(ОтборОрганизация) Тогда
				ОтборОрганизация = NULL;
			КонецЕсли;
			СтруктураОтбора = Новый Структура("Источник,Организация,ДатаНач,ДатаКон",Стр.ИсточникДанных,ОтборОрганизация,ДатаНач,ДатаКон);
			Найденные = ТабИсточников.НайтиСтроки(СтруктураОтбора);
			Если Найденные.Количество()>0 Тогда
				РезультатДочерний = Найденные[0].Сумма;
			Иначе
				РезультатДочерний = 0;
			КонецЕсли;
			СтрокаВыражения = СтрокаВыражения + "(" + Формат(РезультатДочерний, "ЧРД=.; ЧН=; ЧГ=0") + ")";
		КонецЕсли;
	КонецЦикла;
	
	Попытка
		Выполнить(СтрокаВыражения);
	Исключение
		Сообщить("Ошибка компиляции формулы!");	
	КонецПопытки;
	
	//Состояние();
	Возврат Результат;
	
КонецФункции // ВыполнитьРасчет()

Функция ПроверочныйРасчет() Экспорт

	Если СоставФормулы.Количество()=0 Тогда
		Возврат Истина;
	КонецЕсли;
	
	Результат = Неопределено;
	СтрокаВыражения = "Результат = ";
	
	Для каждого Стр Из СоставФормулы Цикл
		Если ТипЗнч(Стр.ИсточникДанных) = Тип("Строка") Тогда
			СтрокаВыражения = СтрокаВыражения + Стр.ИсточникДанных;
		Иначе
			СтрокаВыражения = СтрокаВыражения + " 1 ";
		КонецЕсли;
	КонецЦикла;
	
	Попытка
		Выполнить(СтрокаВыражения);
		Ответ = Истина;
	Исключение
		Сообщить("Ошибка компиляции формулы!");	
		Ответ = Ложь;
	КонецПопытки;
	
	Возврат Ответ;

КонецФункции // ПроверочныйРасчет()
