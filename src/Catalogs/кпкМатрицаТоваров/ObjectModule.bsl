
Процедура ПередЗаписью(Отказ)
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	кпкМатрицаТоваров.Код,
	|	кпкМатрицаТоваров.Наименование,
	|	кпкМатрицаТоваров.НачПериода,
	|	кпкМатрицаТоваров.КонПериода
	|ИЗ
	|	Справочник.кпкМатрицаТоваров КАК кпкМатрицаТоваров
	|ГДЕ
	|	кпкМатрицаТоваров.Категория = &Категория
	|	И ВЫБОР
	|			КОГДА кпкМатрицаТоваров.НачПериода = &ПустаяДата
	|				ТОГДА кпкМатрицаТоваров.КонПериода >= &НачПериода
	|			ИНАЧЕ ВЫБОР
	|					КОГДА кпкМатрицаТоваров.КонПериода = &ПустаяДата
	|						ТОГДА кпкМатрицаТоваров.НачПериода <= &КонПериода
	|					ИНАЧЕ кпкМатрицаТоваров.НачПериода МЕЖДУ &НачПериода И &КонПериода
	|							ИЛИ кпкМатрицаТоваров.КонПериода МЕЖДУ &НачПериода И &КонПериода
	|				КОНЕЦ
	|		КОНЕЦ
	|	И кпкМатрицаТоваров.Состав.Номенклатура В(&СписНоменклатуры)
	|	И кпкМатрицаТоваров.Ссылка <> &ТекМатрица");	
	
	Запрос.УстановитьПараметр("ТекМатрица", 	  Ссылка);
	Запрос.УстановитьПараметр("ПустаяДата", 	  '00010101');
	Запрос.УстановитьПараметр("НачПериода", 	  НачПериода);
	Запрос.УстановитьПараметр("КонПериода", 	  КонПериода);
	Запрос.УстановитьПараметр("Категория",        Категория);
	Запрос.УстановитьПараметр("СписНоменклатуры", Состав.ВыгрузитьКолонку("Номенклатура"));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		ОбщегоНазначения.СообщитьОбОшибке("На заданный период уже есть матрица для категории """ + Категория + """ с пересекающимися позициями номенклатуры!");
		
		Если ЗначениеЗаполнено(Выборка.КонПериода) Тогда		
			ТекстПериода = ПредставлениеПериода(НачалоДня(Выборка.НачПериода), КонецДня(Выборка.КонПериода));		
		Иначе		
			ТекстПериода = Формат(Выборка.НачПериода, "ДФ=dd.MM.yyyy") + "-";		
		КонецЕсли;
		
		ОбщегоНазначения.СообщитьОбОшибке("Смотрите матрицу товаров с названием """ + СокрЛП(Выборка.Наименование) + """ за " + ТекстПериода);
		
		Отказ = Истина;
		Возврат;
		
	КонецЕсли;	
	
КонецПроцедуры
