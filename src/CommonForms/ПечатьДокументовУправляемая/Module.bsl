
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Перем КоллекцияПечатныхФорм, ПараметрыВывода;
	
	ЭкземпляровКомплекта = 1;
	
	ИмяМенеджераПечати = Параметры.СписокПараметров.Получить(0).Значение;
	ИменаМакетов       = Параметры.СписокПараметров.Получить(1).Значение;
	ПараметрКоманды    = Параметры.СписокПараметров.Получить(2).Значение;
	ПараметрыПечати    = Параметры.СписокПараметров.Получить(3).Значение;
	
	ПараметрыВывода = Неопределено;
	УправлениеПечатью.СформироватьПечатныеФормы(ИмяМенеджераПечати, ИменаМакетов, ПараметрКоманды, ПараметрыПечати, 
		КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
		
	ДоступнаПечатьПоКомплектно = ПараметрыВывода.ДоступнаПечатьПоКомплектно;
	
	Колво = КоллекцияПечатныхФорм.Количество();
	Для Сч = 1 По 5 Цикл
		Если Сч > Колво Тогда
			ЭтаФорма["Таб" + Сч] = Неопределено;
			Элементы["Группа" + Сч].Видимость = Ложь;
			Элементы["Копии"  + Сч].Видимость = Ложь;
			
		Иначе
			СтрМакета = КоллекцияПечатныхФорм[Сч-1];
			
			ЭтаФорма["Таб" + Сч] = СтрМакета.ТабличныйДокумент;
			Элементы["Группа" + Сч].Видимость = Истина;
			Элементы["Группа" + Сч].Заголовок = СтрМакета.СинонимМакета;
			Элементы["Копии"  + Сч].Видимость = Истина;
			Элементы["Копии"  + Сч].Заголовок = СтрМакета.СинонимМакета;
			
			ЭтаФорма["Копии" + Сч] = СтрМакета.Экземпляров;
			
			ИменаТабДокументов.Добавить(Сч, СтрМакета.СинонимМакета);
			
		КонецЕсли;
	КонецЦикла;
	
	Если Колво = 1 Тогда
		Элементы.ГруппаКопии.Видимость = Ложь;
		Элементы.Копии.Видимость = Истина;
		Копии = КоллекцияПечатныхФорм[0].Экземпляров;
	Иначе
		Элементы.Копии.Видимость = Ложь;
		Элементы.ГруппаКопии.Видимость = Истина;
	КонецЕсли;

	
	// Видимость кнопок
	Элементы.Перейти.Видимость = ДоступнаПечатьПоКомплектно И (ТипЗнч(ПараметрКоманды) = Тип("Массив")) И (ПараметрКоманды.Количество() > 1);
	
	Если Колво <= 1 Тогда
		Элементы.Страницы.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	КонецЕсли;
	
КонецПроцедуры
 
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
Процедура ПечатьВыполнить()
	
    ТабличныеДокументы = Новый СписокЗначений;
	
	Для Каждого ТабДокумент Из ИменаТабДокументов Цикл
		ТабличныеДокументы.Добавить(ЭтаФорма["Таб" + ТабДокумент.Значение], ТабДокумент.Представление);
	КонецЦикла;

	УправлениеПечатьюКлиент.РаспечататьТабличныеДокументы(ТабличныеДокументы, ДоступнаПечатьПоКомплектно, ОбъектыПечати);

КонецПроцедуры

&НаКлиенте
Процедура КопииПриИзменении(Элемент)
	
	Копий = ЭтаФорма[Элемент.Имя];
	ИмяТабДока = "Таб" + ?(Элемент.Имя = "Копии", "1", Сред(Элемент.Имя, 6));
	ТабДок = ЭтаФорма[ИмяТабДока];
	ТабДок.КоличествоЭкземпляров = Копий;

КонецПроцедуры

&НаКлиенте
Процедура ПерейтиВыполнить()

	спсВыбора = Новый СписокЗначений;
	Для Каждого Элемент Из ОбъектыПечати Цикл
		спсВыбора.Добавить(Элемент.Значение);
	КонецЦикла;
	
	Элемент = спсВыбора.ВыбратьЭлемент("Перейти к");
	Если Элемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Элемент = ОбъектыПечати.НайтиПоЗначению(Элемент.Значение);
	Если Элемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИмяОбласти = Элемент.Представление;
	Для Каждого ТабДокумент Из ИменаТабДокументов Цикл
		
		ИмяЭлемента = "Таб" + ТабДокумент.Значение;
		Таб = ЭтаФорма[ИмяЭлемента];
		Область = Таб.Области.Найти(ИмяОбласти);
		Если Область = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ТекущаяОбласть = Таб.Область(Область.Верх, , Область.Верх);
		Элементы[ИмяЭлемента].ТекущаяОбласть = ТекущаяОбласть;
		
	КонецЦикла;
	
КонецПроцедуры
