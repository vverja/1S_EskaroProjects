
// Начать замер времени выполнения ключевой операции
//
// Параметры:
//  КодКлючевойОперации - Строка, код элемента справочника "КлючевыеОперации"
//
// Возвращаемое значение:
//  Число - время начала замера
//
Функция НачатьЗамерВремени(КлючеваяОперация, Описание = "",ПодключатьОбработчикОжидания = Ложь) Экспорт
	
	КлючеваяОперация = APDEX_ОценкаПроизводительностиСерверВызовСервера.ПолучитьОперацию(КлючеваяОперация);
		
	Если ПодключатьОбработчикОжидания тогда
		#Если Клиент Тогда
			
			ВремяНачала = APDEX_ОценкаПроизводительностиСерверВызовСервера.ЗафиксироватьВремяНачала(КлючеваяОперация, Ложь, Описание);
			ПодключитьОбработчикОжидания("APDEX_ЗакончитьЗамерВремениАвто", 0.1, Истина);
			
		#ИначеЕсли Сервер Тогда
			
			ВремяНачала = APDEX_ОценкаПроизводительностиСерверВызовСервера.ЗафиксироватьВремяНачала(КлючеваяОперация, Истина, Описание);
			
		#КонецЕсли
		
	Иначе
		
		ВремяНачала = APDEX_ОценкаПроизводительностиСерверВызовСервера.ЗафиксироватьВремяНачала(КлючеваяОперация, Истина, Описание);
		
	КонецЕсли;
	
	Возврат ВремяНачала;
	
КонецФункции

// Закончить замер времени выполнения ключевой операции
//
// Возвращаемое значение:
//  Число - время окончания замера
//
Функция ЗакончитьЗамерВремени(КлючеваяОперация = Неопределено, Описание = "", ТекстЗапроса ="") Экспорт
	
	КлючеваяОперация = APDEX_ОценкаПроизводительностиСерверВызовСервера.ПолучитьОперацию(КлючеваяОперация);
	Возврат APDEX_ОценкаПроизводительностиСерверВызовСервера.ЗафиксироватьВремяОкончания(КлючеваяОперация, Ложь, Описание, ТекстЗапроса);
	
КонецФункции

Процедура НачатьЗамерПередЗаписью(Источник, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	Если (РежимЗаписи = РежимЗаписиДокумента.Проведение) Тогда
		Описание = ""+Источник.Ссылка;
		Операция = APDEX_ОценкаПроизводительностиСерверВызовСервера.ПолучитьОперацию(Источник.Ссылка);
		НачатьЗамерВремени(Операция, Описание);
	КонецЕсли;

КонецПроцедуры

Процедура ЗакончитьЗамерОбработкаПроведения(Источник, Отказ, РежимПроведения) Экспорт
	
	Описание = ""+Источник.Ссылка;
	Операция = APDEX_ОценкаПроизводительностиСерверВызовСервера.ПолучитьОперацию(Источник.Ссылка);
    ЗакончитьЗамерВремени(Операция, Описание);
	
КонецПроцедуры

	

