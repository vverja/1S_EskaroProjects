
//Функция получает типы регламентных документов, которые дожны быть созданы для переданой точки маршрута бизнес-процесса (регламентной операции)
//	Если точка маршрута не передана, получаются типы документов для всех регламентных операций
//Параметры: ТочкаМаршрута - ссылка на точку маршрута бизнес-процесса
//Возвращаемое значение: Массив (если точка маршрута передана) либо Таблица значений (если точка маршрута не передана)
Функция ПолучитьСписокТиповДокументов(ТочкаМаршрута = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДокументыРегламентныхОпераций.ТипДокумента,
	|	ДокументыРегламентныхОпераций.ТочкаМаршрута
	|ИЗ
	|	РегистрСведений.ДокументыРегламентныхОпераций КАК ДокументыРегламентныхОпераций
	|ГДЕ
	|	ДокументыРегламентныхОпераций.ТипДокумента <> """"";
	
	Если ТочкаМаршрута <> Неопределено Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|	И ДокументыРегламентныхОпераций.ТочкаМаршрута = &ТочкаМаршрута";
		Запрос.УстановитьПараметр("ТочкаМаршрута", ТочкаМаршрута);
	КонецЕсли;	
	
	Запрос.Текст = ТекстЗапроса;
	Результат = Запрос.Выполнить().Выгрузить();
	
	Если ТочкаМаршрута <> Неопределено Тогда
		Возврат Результат.ВыгрузитьКолонку("ТипДокумента");
	Иначе	
		Возврат Результат;
	КонецЕсли;
КонецФункции

//Функция получает таблицу документов, созданных в рамках выполнения регламентной операции
//Параметры: БизнесПроцесс	- процесс закрытия месяца,
//	СписокТиповДокументов	- типы документов которые должны быть созданы в рамках регламентной операции
//	ТочкаМаршрута 			- ссылка на точку маршрута бизнес-процесса
//Возвращаемое значение - ТаблицаЗначений, содержащая документы и их ключевые реквизиты (признаки отражения в учетах, признак проведения и т.д.)
Функция ЗаполнитьТаблицуСуществующихДокументов(БизнесПроцесс, СписокТиповДокументов, ТочкаМаршрута) Экспорт
	ТекстЗапроса = "";
	//Определяем, для какого бизнес-процесса была создана задача
	ГлавныйБизнесПроцесс = ОпределитьГлавныйБизнесПроцесс(БизнесПроцесс);
	СтруктураПризнаковОтраженияВУчете = новый Структура("ОтражатьВУправленческомУчете,ОтражатьВБухгалтерскомУчете",ГлавныйБизнесПроцесс.ОтражатьВУправленческомУчете,ГлавныйБизнесПроцесс.ОтражатьВБухгалтерскомУчете);
	Для каждого ТипДокумента Из СписокТиповДокументов Цикл
		Если ТипДокумента = "" Тогда
			Продолжить;
		КонецЕсли;
		
		МетаРеквизитыДок = Метаданные.Документы[ТипДокумента].Реквизиты;
		
		ЕстьРеквизитОрганизация 		= МетаРеквизитыДок.Найти("Организация") 					<> Неопределено;
		ЕстьРеквизитОтветственный 		= МетаРеквизитыДок.Найти("Ответственный") 					<> Неопределено;
		ЕстьРеквизитБУ 					= МетаРеквизитыДок.Найти("ОтражатьВБухгалтерскомУчете") 	<> Неопределено;
		ЕстьРеквизитУУ 					= МетаРеквизитыДок.Найти("ОтражатьВУправленческомУчете") 	<> Неопределено;
		ЕстьРеквизитПериодРегистрации 	= МетаРеквизитыДок.Найти("ПериодРегистрации") 				<> Неопределено;
		
		Если ВРег(ТипДокумента) = "НАЧИСЛЕНИЕНДСПОСМРХОЗСПОСОБОМ" Тогда  //Это особенный документ, создается раз в квартал
			ПериодичностьДокумента = "Квартал";
		Иначе
			ПериодичностьДокумента = "Месяц";
		КонецЕсли;
		

		ТекстЗапроса = ТекстЗапроса + ?(ТекстЗапроса = "","ВЫБРАТЬ РАЗРЕШЕННЫЕ","
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ
		|") + "
		|	Док.Ссылка 																					КАК Документ,
		|	Док.ПометкаУдаления 																		КАК ПометкаУдаления,
		|	Док.Проведен 																				КАК Проведен
		|	, ПРЕДСТАВЛЕНИЕ(Док.Ссылка) 																КАК ДокументПредставление
		|	, """ + ТипДокумента + """ 																	КАК ТипДокумента
		|	, " + ?(ЕстьРеквизитОрганизация,	"Док.Организация","&ПустаяОрганизация") + " 			КАК Организация
		|	, " + ?(ЕстьРеквизитОрганизация,	"ПРЕДСТАВЛЕНИЕ(Док.Организация)","""""") + " 			КАК ОрганизацияПредставление
		|	, " + ?(ЕстьРеквизитОтветственный,	"Док.Ответственный",	"&ПустойОтветственный") + "		КАК Ответственный
		|	, " + ?(ЕстьРеквизитОтветственный,	"ПРЕДСТАВЛЕНИЕ(Док.Ответственный)","""""") + " 			КАК ОтветственныйПредставление
		|	, " + ?(ЕстьРеквизитБУ,				"Док.ОтражатьВБухгалтерскомУчете","ЛОЖЬ") + " 			КАК ОтражатьВБухгалтерскомУчете
		|	, " + ?(ЕстьРеквизитУУ,				"Док.ОтражатьВУправленческомУчете","ЛОЖЬ") + " 			КАК ОтражатьВУправленческомУчете
		|
		|ИЗ Документ."+ТипДокумента + " КАК Док
		|ГДЕ  "+БизнесПроцессЗакрытиеМесяца.СформироватьУсловиеПериодРегистрации(ЕстьРеквизитПериодРегистрации,
																				"Док",
																				"&ЗакрытиеМесяцаПериодРегистрации",
																				ПериодичностьДокумента)+"
		| " + БизнесПроцессЗакрытиеМесяца.СформироватьУсловиеОрганизация(ЕстьРеквизитОрганизация,
																				"Док",
																				"&ЗакрытиеМесяцаОрганизация",
																				Истина)+"
		| "+БизнесПроцессЗакрытиеМесяца.СформироватьУсловиеОтражениеВУчете(Новый Структура("УУ,БУ",ЕстьРеквизитУУ,ЕстьРеквизитБУ), 
																				СтруктураПризнаковОтраженияВУчете,
																				"Док", 
																				ТочкаМаршрута, 
																				ГлавныйБизнесПроцесс.ПериодРегистрации);		
		
	КонецЦикла;
	
	Если ПустаяСтрока(ТекстЗапроса) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;

	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ЗакрытиеМесяцаПериодРегистрации", 	ГлавныйБизнесПроцесс.ПериодРегистрации);
	Запрос.УстановитьПараметр("ЗакрытиеМесяцаОрганизация", 			ГлавныйБизнесПроцесс.Организация);
	Запрос.УстановитьПараметр("ПустаяОрганизация", 					Справочники.Организации.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустойОтветственный", 				Справочники.Пользователи.ПустаяСсылка());
	
	Результат = Запрос.Выполнить().Выгрузить();
	Возврат Результат;
КонецФункции

//Функция получает строку: дополнение к запросу с условием по периоду регистрации
//Параметры:
//		ЕстьРеквизитПериодРегистрации 	- булево, признак наличия реквизита "ПериодРегистрации"
//		ПсевдонимТаблицы 				- строка, псеводним таблицы в запросе
//		ИмяПараметраСравнения 			- строка, параметр запроса с которым происходит сравнение реквизита
//		Периодичность 					- строка, периодичность к которой следует привести параметр сравнения и период регистрации
//Возвращаемое значение - строка, дополнение к запросу содержащее текст условия по периоду регистрации документа
Функция СформироватьУсловиеПериодРегистрации(ЕстьРеквизитПериодРегистрации,ПсевдонимТаблицы,ИмяПараметраСравнения,Периодичность="Месяц") Экспорт
	ТекстУсловия = "";
	Если ЕстьРеквизитПериодРегистрации Тогда
		ТекстУсловия = ТекстУсловия + ПсевдонимТаблицы+".ПериодРегистрации";
	Иначе
		ТекстУсловия = ТекстУсловия + ПсевдонимТаблицы+".Дата";
	КонецЕсли;
	ТекстУсловия = "НАЧАЛОПЕРИОДА("+ТекстУсловия+", "+Периодичность+") = НАЧАЛОПЕРИОДА("+ИмяПараметраСравнения+", "+Периодичность+")";
	Возврат ТекстУсловия;
КонецФункции

//Функция получает строку: дополнение к запросу с условием по организации
//Параметры:
//		ЕстьРеквизитОрганизация 	- булево, признак наличия реквизита "Организация"
//		ПсевдонимТаблицы 			- строка, псеводним таблицы в запросе
//		ИмяПараметраСравнения 		- строка, параметр запроса с которым происходит сравнение реквизита
//		УчитыватьПустую 			- булево, признак того что условие должно включать проверку пустой организации
//Возвращаемое значение - строка, дополнение к запросу содержащее текст условия по организации документа
Функция СформироватьУсловиеОрганизация(ЕстьРеквизитОрганизация,ПсевдонимТаблицы,ИмяПараметраСравнения,УчитыватьПустую) Экспорт
	Если НЕ ЕстьРеквизитОрганизация Тогда
		Возврат "";
	КонецЕсли;
	РеквизитОрганизация = ПсевдонимТаблицы+".Организация";
	ТекстУсловия = " И ("+РеквизитОрганизация+" = "+ИмяПараметраСравнения;
	Если УчитыватьПустую Тогда
		ТекстУсловия = ТекстУсловия + " ИЛИ "+РеквизитОрганизация+" = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))";
	Иначе
		ТекстУсловия = ТекстУсловия + ")";
	КонецЕсли;
	
	Возврат ТекстУсловия;
КонецФункции

//Функция получает строку: дополнение к запросу с условием по признакам отражения в учете
//Параметры:
//		ЕстьРеквизитыОтраженияВУчетах 		- структура, содержит признаки наличия реквизитов отражения в учетах (УУ, БУ, НУ)
//		ЗначенияПризнаковОтраженияВУчетах 	- структура, искомые признаки отражения в учетах, с которыми происходит сравнение
//		ПсевдонимТаблицы 					- строка, псеводним таблицы в запросе
//		ТочкаМаршрута 						- ссылка на точку маршрута бизнес-процесса
//		ПериодРегистрации 					- период, за который выполняется бизнес-процесс
//Возвращаемое значение - строка, дополнение к запросу содержащее текст условия по признакам отражения в учете документа
Функция СформироватьУсловиеОтражениеВУчете(ЕстьРеквизитыОтраженияВУчетах,ЗначенияПризнаковОтраженияВУчетах, ПсевдонимТаблицы, ТочкаМаршрута, ПериодРегистрации) Экспорт
	ТекстЗапросаУсловие = "";
	текЗначенияПризнаковОтраженияВУчетах = СкорректироватьОтражениеВУчете(ЗначенияПризнаковОтраженияВУчетах, ТочкаМаршрута, ПериодРегистрации);
	
	Если ЕстьРеквизитыОтраженияВУчетах.УУ Тогда
		Если текЗначенияПризнаковОтраженияВУчетах.ОтражатьВУправленческомУчете Тогда
			ТекстЗапросаУсловие = ПсевдонимТаблицы + ".ОтражатьВУправленческомУчете";
		КонецЕсли;
	КонецЕсли;
	Если ЕстьРеквизитыОтраженияВУчетах.БУ Тогда
		Если текЗначенияПризнаковОтраженияВУчетах.ОтражатьВБухгалтерскомУчете Тогда
			ТекстЗапросаУсловие = ТекстЗапросаУсловие + ?(ТекстЗапросаУсловие <> "", " ИЛИ ", "") + ПсевдонимТаблицы + ".ОтражатьВБухгалтерскомУчете";
		КонецЕсли;
	КонецЕсли;
	Если ТекстЗапросаУсловие <> "" Тогда
		Возврат " И (" + ТекстЗапросаУсловие + ")";
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции


//Функция корректирует значения признаков отражения в учете, применяемых для конкретной точки маршрута, с учетом специфики:
//	1) Расширенная аналитика учета затрат - при расчете себестоимости по регл. учету не используется признак отражения в НУ
//	2) В точке маршрута "Расчет себестоимости УУ" в документе не должны быть установлены признаки БУ и НУ
//	3) В точке маршрута "Расчет себестоимости регл" в документе не дожен быть установлен признак УУ
Функция СкорректироватьОтражениеВУчете(ЗначенияПризнаковОтраженияВУчетах, ТочкаМаршрута, ПериодРегистрации) Экспорт
	НовыеЗначенияПризнаковОтраженияВУчетах = Новый Структура;
	//копируем структуру, переданную в параметрах
	Для каждого ТекущееЗначение из ЗначенияПризнаковОтраженияВУчетах цикл
		НовыеЗначенияПризнаковОтраженияВУчетах.Вставить(ТекущееЗначение.Ключ, ТекущееЗначение.Значение);
	конецЦикла;

	Если ТочкаМаршрута = БизнесПроцессы.ЗакрытиеМесяца.ТочкиМаршрута.РассчитатьСебестоимость Тогда
		//не проверяем признак БУ
		НовыеЗначенияПризнаковОтраженияВУчетах.Вставить("ОтражатьВБухгалтерскомУчете",	Ложь);
	ИначеЕсли ТочкаМаршрута = БизнесПроцессы.ЗакрытиеМесяца.ТочкиМаршрута.РассчитатьСебестоимостьРегл Тогда
		//не проверяем признаки УУ 
		НовыеЗначенияПризнаковОтраженияВУчетах.Вставить("ОтражатьВУправленческомУчете",	Ложь);
	КонецЕсли;
	Возврат НовыеЗначенияПризнаковОтраженияВУчетах;
КонецФункции

//Функция возвращает массив регламентных операций, недоступных при текущих настройках параметров учета
//Вызывается из формы настройки закрытия месяца и из формы процедуры закрытия месяца
//Параметры:
//	ДатаАнализа (в зависимости от места вызова дата начала действия настройки / закрываемый период)
//	Организация, для которой выполняется закрытие  месяца (заполняется только при вызове из формы процедуры закрытия месяца)
//	Данные отбираются 
//1) из макета "МакетНастроекПоУмолчанию" по значению колонки "Расширенная аналитика" = 0
//2) на основе анализа использования механизма отложенного проведения
Функция ЗаполнитьМассивНедоступныхРегламентныхОпераций(ДатаАнализа, Организация = Неопределено) Экспорт
	МассивОпераций = Новый Массив;
	Если глЗначениеПеременной("ИспользоватьРасширеннуюАналитикуУчетаНоменклатурыИЗатрат") И
		глЗначениеПеременной("ДатаНачалаИспользованияРасширеннойАналитикиУчетаНоменклатурыИЗатрат") <= ДатаАнализа Тогда
		
		Макет = Справочники.НастройкиЗакрытияМесяца.ПолучитьМакет("МакетНастроекПоУмолчанию");
		ОбластьСписокНастроек = Макет.Области.Найти("Список");
		Если ОбластьСписокНастроек <> Неопределено Тогда
			ОбластьСписокНастроек = Макет.ПолучитьОбласть("Список");
			Для Сч = 1 по ОбластьСписокНастроек.ВысотаТаблицы Цикл
				РеглОперация = СокрЛП(ОбластьСписокНастроек.Область(Сч,1,Сч,1).Текст);
				Доступность = Булево(Число(СокрЛП(ОбластьСписокНастроек.Область(Сч,4,Сч,4).Текст)));
				Если НЕ Доступность Тогда
					МассивОпераций.Добавить(БизнесПроцессы.ЗакрытиеМесяца.ТочкиМаршрута[РеглОперация]);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли; 
	КонецЕсли;
	флИспользуетсяОтложенноеПроведение = Ложь;
	Если Организация = Неопределено Тогда
		ЗапросОрганизации = новый Запрос("
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ 
		|	Ссылка 
		|ИЗ Справочник.Организации
		|");
		Выборка = ЗапросОрганизации.Выполнить().Выбрать();
		Пока Выборка.Следующий() цикл
			ДатаНачалаДействияОтложенногоПроведения = ОтложенноеПроведениеДокументов.ПолучитьДатуНачалаОтложенногоПроведения(Выборка.Ссылка);
			//Если организация не указана - вызов происходит из формы настройки. В этом случае параметр ДатаАнализа не имеет смысла сравнивать
			//	с датой начала действия отложенного проведения
			флИспользуетсяОтложенноеПроведение = ЗначениеЗаполнено(ДатаНачалаДействияОтложенногоПроведения);
			Если флИспользуетсяОтложенноеПроведение Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
	Иначе
		ДатаНачалаДействияОтложенногоПроведения = ОтложенноеПроведениеДокументов.ПолучитьДатуНачалаОтложенногоПроведения(Организация);
		//Для конкретной организации анализируем дополнительно ДатаАнализа, т.к. в данном случае это дата закрываемого периода
		флИспользуетсяОтложенноеПроведение = ЗначениеЗаполнено(ДатаНачалаДействияОтложенногоПроведения) И ДатаНачалаДействияОтложенногоПроведения <= ДатаАнализа;
	КонецЕсли;
	Если НЕ флИспользуетсяОтложенноеПроведение Тогда
		МассивОпераций.Добавить(БизнесПроцессы.ЗакрытиеМесяца.ТочкиМаршрута.ВыполнитьДопроведение);
	КонецЕсли;
	
	Возврат МассивОпераций;
КонецФункции

#Если Клиент Тогда
	
//Процедура обновляет карту маршрута: устанавливает цвет фона, выделяет отключенные и недоступные регл операции, выделяет регл операции назначенные текущему пользователю	
//Параметры:
//	КартаМаршрута 							- карта маршрута бизнес-процесса
//	РегламентныеОперации 					- табличная часть РегламентныеОперации справочника НастройкиЗакрытияМесяца или бизнес-процесса ЗакрытиеМесяца
//	МассивНедоступныхРегламентныхОпераций 	- массив регламентных операций, недоступных для выполнения
//	ГруппыТекущегоПользователя 				- массив из ссылок на справочник ГруппыПользователей, группы в которые входит текущий пользователь
//	ТекущийПользователь 					- ссылка на справочник Пользователи (текущий пользователь)
Процедура ОбновитьКартуМаршрута(КартаМаршрута, РегламентныеОперации, МассивНедоступныхРегламентныхОпераций, ГруппыТекущегоПользователя, ТекущийПользователь) Экспорт
	
	ЦветИсключаемогоДействия 		= Новый Цвет(200, 200, 200); 	//серый
	ЦветТекстаНедоступнойОперации 	= Новый Цвет(51,51,51);   		//черный цвет
	ЦветРамкиНедоступнойОперации 	= Новый Цвет(192,192,192);   	//темно серый цвет
	ЖирнаяЛиния = Новый Линия(ТипСоединительнойЛинии.Сплошная,3);
	ТочкаМаршрутаРасчетСебестоимостиРегл = БизнесПроцессы.ЗакрытиеМесяца.ТочкиМаршрута.РассчитатьСебестоимостьРегл;
	Для каждого ТочкаРО из КартаМаршрута.ЭлементыГрафическойСхемы Цикл
		Если ТипЗнч(ТочкаРО) <> Тип("ЭлементГрафическойСхемыДействие") Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаТЧ = РегламентныеОперации.Найти(ТочкаРО.Значение, "РегламентнаяОперация");
		Если СтрокаТЧ = Неопределено Тогда
			//Такого не должно быть - все точки маршрута должны содержаться среди регламентных операций
			Продолжить;
		КонецЕсли;
		Если МассивНедоступныхРегламентныхОпераций.Количество() > 0 Тогда
			//Проверяем входит ли регламентная операция в массив недоступных
			Если МассивНедоступныхРегламентныхОпераций.Найти(ТочкаРО.Значение) <> Неопределено Тогда
				//Отключим использование недоступной регламентной операции
				Если СтрокаТЧ.Использовать Тогда
					СтрокаТЧ.Использовать = Ложь;
				КонецЕсли;
				//Выделим недоступную операцию на схеме особым образом
				ТочкаРО.Пояснение 		= "не выполняется";
				ТочкаРО.ЦветРамки 		= ЦветРамкиНедоступнойОперации;
				ТочкаРО.ЦветТекста 		= ЦветТекстаНедоступнойОперации;
				ТочкаРО.ПрозрачныйФон 	= Истина;

				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		Если СтрокаТЧ.Использовать Тогда
			//Прописываем ответственного за регл операцию на схеме
			ТочкаРО.Пояснение = СокрЛП(СтрокаТЧ.Ответственный);
			//Проверяем - это регл операция текущего пользователя?
			флНашаОперация = Ложь;
			Если ТипЗнч(СтрокаТЧ.Ответственный) = Тип("СправочникСсылка.ГруппыПользователей") Тогда
				Если ГруппыТекущегоПользователя.НайтиПоЗначению(СтрокаТЧ.Ответственный) <> Неопределено Тогда
					флНашаОперация = Истина;
				КонецЕсли;
			ИначеЕсли СтрокаТЧ.Ответственный = ТекущийПользователь Тогда
				флНашаОперация = Истина;
			КонецЕсли;
			//Если операция назначена текущему пользователю, элемент схемы обводится жирной рамкой
			Если флНашаОперация Тогда
				ТочкаРО.Рамка = ЖирнаяЛиния;
			КонецЕсли;
		Иначе
			//Для неиспользуемой регламентной операции меняем цвет фона
			ТочкаРО.ЦветФона = ЦветИсключаемогоДействия;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

#КонецЕсли

//Функция объединяет точки маршрута бизнес-процессов "Закрытие месяца" и "Расчет НДС" в одно соответствие
Функция ПолучитьТочкиМаршрутаЗакрытияМесяцаИРасчетаНДС() Экспорт

	ТочкиМаршрута = Новый Соответствие;
	Для Каждого ТочкаМаршрута Из БизнесПроцессы.ЗакрытиеМесяца.ТочкиМаршрута Цикл
		ТочкиМаршрута.Вставить(ТочкаМаршрута.Имя, ТочкаМаршрута);
	КонецЦикла;
	
	Возврат ТочкиМаршрута;
	
КонецФункции //ПолучитьВсеТочкиМаршрута()

//Процедура заполняет реквизиты создаваемых задач
//Вызывается из обработчиков "ПриСозданииЗадач" бизнес-процесса ЗакрытиеМесяца
//Параметры:
//    БизнесПроцессСсылка 			- ссылка на бизнес-процесс
//    ТочкаМаршрутаБизнесПроцесса 	- ссылка на точку маршрута, для которой создаются задачи
//    ФормируемыеЗадачи 			- массив формируемых задач
//	  Отказ 						- булево, признак отказа
Процедура ПриСозданииЗадачБизнесПроцессов(БизнесПроцессСсылка, ТочкаМаршрутаБизнесПроцесса, ФормируемыеЗадачи, Отказ) Экспорт
	
	ТочкаРО 			= ФормируемыеЗадачи[0].ТочкаМаршрута;
	
	Если ТочкаРО.Вид	<> ВидТочкиМаршрутаБизнесПроцесса.Действие Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РегламентныеОперации.РегламентнаяОперация КАК РегламентнаяОперация,
	|	РегламентныеОперации.Ответственный,
	|	РегламентныеОперации.Использовать
	|ИЗ
	|	БизнесПроцесс.ЗакрытиеМесяца.РегламентныеОперации КАК РегламентныеОперации
	|ГДЕ РегламентныеОперации.Ссылка = &текСсылка И РегламентныеОперации.РегламентнаяОперация = &РеглОперация
	|";
				
	ЗапросДействия 			= Новый Запрос;
	ЗапросДействия.Текст 	= ТекстЗапроса;
	
	ЗапросДействия.УстановитьПараметр("РеглОперация", ТочкаРО);
	ЗапросДействия.УстановитьПараметр("ТекСсылка", БизнесПроцессСсылка);

	Выборка = ЗапросДействия.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда   //В выборке будет только один элемент
		Если Выборка.Использовать Тогда
			ОбъектЗадача = ФормируемыеЗадачи[0];

			ТипОтветственного = ТипЗнч(Выборка.ответственный);
			ОбъектЗадача.Пользователь 			= ?(ТипОтветственного=Тип("СправочникСсылка.Пользователи"),Выборка.Ответственный,Неопределено);
			ОбъектЗадача.ГруппаПользователей 	= ?(ТипОтветственного=Тип("СправочникСсылка.ГруппыПользователей"),Выборка.Ответственный,Неопределено);
		Иначе
			ФормируемыеЗадачи.Очистить();
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

//Функция определяет главный бизнес-процесс
Функция ОпределитьГлавныйБизнесПроцесс(БизнесПроцессСсылка) Экспорт
	Если БизнесПроцессСсылка = Неопределено Тогда
		Возврат БизнесПроцессы.ЗакрытиеМесяца.ПустаяСсылка();
	Иначе
	    Возврат БизнесПроцессСсылка
	КонецЕсли;
	
КонецФункции //ОпределитьГлавныйБизнесПроцесс()

//Функция определяет, завершен ли бизнес-процесс
//
Функция БизнесПроцессЗаврешен(БизнесПроцессСсылка) Экспорт
	Если БизнесПроцессСсылка.Пустая() Тогда
		Возврат Ложь;
	КонецЕсли;
	Возврат БизнесПроцессСсылка.Завершен;
КонецФункции



