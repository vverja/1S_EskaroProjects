// Функция применяется при необходимости получить сведения об учетной политике организации.
//
// Параметры:
// Учет - строка. Определяет регистр сведений, из которого будут получены данные:
//   "Упр"	- регистр "УчетнаяПолитика"
//   "Регл"	- регистр "УчетнаяПолитикаОрганизаций"
//
// Возвращаемое значение - таблица значений. Таблица, каждая строка которой 
//  соответствует записи регистра. 
//
Функция СоздатьКЭШУчетнойПолитики(Учет)
	
	Если НРег(Учет) = "регл" Тогда
		ИмяРегистра = "УчетнаяПолитикаОрганизаций";
	Иначе
		ИмяРегистра = "УчетнаяПолитика";
	КонецЕсли;
	
	Запрос = Новый Запрос();
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		| //ДляРеглУчета СхемаНалогообложения.НалогНаПрибыль КАК ЕстьНалогНаПрибыльНал,
		| //ДляРеглУчета СхемаНалогообложения.НДС КАК ЕстьНДСНал,
		| //ДляРеглУчета СхемаНалогообложения.ЕдиныйНалог КАК ЕстьЕдиныйНалогНал,
		|	*
		|ИЗ
		|	РегистрСведений." + ИмяРегистра +" КАК УчетнаяПолитика" + Учет + "
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период УБЫВ";
		
	Если НРег(Учет) = "регл" Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "//ДляРеглУчета", "");
	КонецЕсли;
		
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции // СоздатьКЭШУчетнойПолитики()

// Функция предназначена для получения значения по имени переменной
// Вызывается из функции глЗначениеПеременной
//
// Параметры:
//	Имя - Строка, имя переменной
//
// Возвращаемое значение: 
//  Произвольное значение
//
Функция ПолучитьЗначениеПеременной(ИмяПараметра) Экспорт
	
	ИмяПараметраВР = ВРег(ИмяПараметра);
	Если ИмяПараметраВР = ВРег("глУчетнаяПолитикаПоПерсоналу")	Тогда
		Возврат ОбщегоНазначенияЗУП.ЗаполнениеУчетнойПолитикиПоПерсоналу();
		
	ИначеЕсли ИмяПараметраВР = ВРег("глУчетнаяПолитикаПоПерсоналуОрганизации") Тогда
		Возврат ОбщегоНазначенияЗУП.ЗаполнениеУчетнойПолитикиПоПерсоналуОрганизации();      
		
	ИначеЕсли ИмяПараметраВР = ВРег("УчетнаяПолитикаУпр") Тогда
		Возврат СоздатьКЭШУчетнойПолитики("Упр");
		
	ИначеЕсли ИмяПараметраВР = ВРег("УчетнаяПолитикаРегл") Тогда
		Возврат СоздатьКЭШУчетнойПолитики("Регл");
		
	ИначеЕсли ИмяПараметраВР = ВРег("ПараметрыПартионногоУчета") Тогда	
		Возврат УправлениеЗапасамиПартионныйУчет.ПолучитьПараметрыПартионногоУчета();
		
	ИначеЕсли ИмяПараметраВР = ВРег("ИспользоватьБлокировкуДанных") Тогда	
		Возврат Метаданные.РежимУправленияБлокировкойДанных <> Метаданные.СвойстваОбъектов.РежимУправленияБлокировкойДанныхПоУмолчанию.Автоматический;
		
	ИначеЕсли ИмяПараметраВР = ВРег("ПрофильПолномочийПользователя") Тогда 
		Возврат ПараметрыСеанса.ТекущийПользователь.ПрофильПолномочийПользователя;
		
	ИначеЕсли ИмяПараметраВР = ВРег("глКоличествоСекундОпросаОбмена") Тогда
		Возврат ПолучитьКоличествоСекундОпросаОбмена();
		
	
	ИначеЕсли ИмяПараметраВР =  ВРег("ДатаНачалаУчетаНКУ") Тогда
		Возврат '20110401';
	
	Иначе
		
		// Если имя переменной ни одно из перечисленных выше, считаем что это имя константы.
		// Если константы с таким именем в конфигурации нет, то произойдет ошибка времени выполнения,
		// это значит, что в функцию передали неверное имя переменной
		Возврат Константы[ИмяПараметраВР].Получить();
		
	КонецЕсли;
	
КонецФункции

Функция ПолучитьКоличествоСекундОпросаОбмена()
	
	НайденноеЗначение = Константы.ИнтервалДляОпросаРегламентныхЗаданийВФайловомВарианте.Получить();
	
	Если НайденноеЗначение = 0 Тогда
		НайденноеЗначение = 60;
	КонецЕсли;
	
	Возврат НайденноеЗначение;
	
КонецФункции // 

// Функция возвращает реквизиты объекта
//
// Параметры
//	Ссылка				- <Произвольный>, ссылка на объект
//	СтруктураПолей		- <Структура>, реквизиты объекта, которые необходимо получить
//	ПолноеИмяОбъекта	- <Строка>, полное имя объекта, например "Справочник.Пользователи"
//
// Возвращаемое значение
//	Структура
//
Функция ПолучитьЗначенияРеквизитовОбъекта(Ссылка, СтруктураПолей, ПолноеИмяОбъекта = Неопределено) Экспорт

	Если ПолноеИмяОбъекта = Неопределено Тогда
		ПолноеИмяОбъекта = Ссылка.Метаданные().ПолноеИмя();
	КонецЕсли; 

	ТекстЗапроса = "";
	
	Для Каждого Элемент Из СтруктураПолей Цикл
		
		ИмяПоля = Элемент.Значение;
		
		Если НЕ ЗначениеЗаполнено(ИмяПоля) Тогда
			ИмяПоля = СокрЛП(Элемент.Ключ);
		КонецЕсли;
		
		ТекстЗапроса  = ТекстЗапроса + ?(ПустаяСтрока(ТекстЗапроса), "", ",") + "
		|	" + ИмяПоля + " КАК " + СокрЛП(Элемент.Ключ);
	КонецЦикла;

	Запрос = Новый Запрос();

	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ" + ТекстЗапроса + "
	|ИЗ
	|	" + ПолноеИмяОбъекта + " КАК ТаблицаОбъекта
	|ГДЕ
	|	Ссылка = &Ссылка";

	Запрос.УстановитьПараметр("Ссылка" , Ссылка);

	Выборка = Запрос.Выполнить().Выбрать();
	
	Результат = Новый Структура;
	Если Выборка.Следующий() Тогда
		Для Каждого КлючИЗначение ИЗ СтруктураПолей Цикл
			Результат.Вставить(КлючИЗначение.Ключ, Выборка[КлючИЗначение.Ключ]);
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;

КонецФункции
