////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ПОДГОТОВКИ ВРЕМЕННОЙ ТАБЛИЦЫ ПО ЗАТРАТАМ НА ВЫПУСК

// Процедура формирования временной таблицы "АналитикаВидаУчетаЗатратыНаВыпуск".
//
// Параметры
//  СтруктураШапкиДокумента – Структура - Реквизиты документа "Расчет себестоимости выпуска"
//  СтруктураПараметров – Структура - Структура параметров способа распределения
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц
//
Процедура СформироватьВременнуюТаблицуАналитикаВидаУчетаЗатратыНаВыпуск(
	СтруктураШапкиДокумента,
	СтруктураПараметров,
	МенеджерВременныхТаблиц
	)

	ТекстЗапросаСКомментариями = "
	|ВЫБРАТЬ
	|	РегистрАналитикаВидаУчета.РазделУчета,
	|	РегистрАналитикаВидаУчета.Подразделение,
	|	РегистрАналитикаВидаУчета.ПодразделениеОрганизации,
	|	РегистрАналитикаВидаУчета.СчетУчета,
	|	РегистрАналитикаВидаУчета.НалоговоеНазначение,
	|	РегистрАналитикаВидаУчета.Ссылка
	|
	|ПОМЕСТИТЬ АналитикаВидаУчетаЗатратыНаВыпуск%СуффиксУчета%
	|ИЗ
	|	РегистрСведений.АналитикаВидаУчета КАК РегистрАналитикаВидаУчета
	|ГДЕ
	|	(РегистрАналитикаВидаУчета.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчета.ЗатратыНаВыпуск)
	|	ИЛИ РегистрАналитикаВидаУчета.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчета.ЗатратыПоНаработке))
	|	//ДляРеглУчета И РегистрАналитикаВидаУчета.Организация = &Организация
	|";
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Организация", СтруктураШапкиДокумента.Организация);
	
	// Цикл по всем видам отражения в учете, для которых рассчитывается база распределения.
	Для Каждого ВидОтраженияВУчете Из СтруктураПараметров.МассивВидовУчета Цикл
		
		ТекстЗапроса = УправлениеЗатратами.ЗаменитьКомментарииВТекстеЗапроса(
			ТекстЗапросаСКомментариями, 
			ВидОтраженияВУчете
		);
		Запрос.Текст = ТекстЗапроса;
		Запрос.Выполнить();
		
		СуффиксИмениРегистра = СтруктураПараметров.СуффиксыИмениРегистра[ВидОтраженияВУчете];
		ИмяВременнойТаблицы = "АналитикаВидаУчетаЗатратыНаВыпуск" + СуффиксИмениРегистра;
		
		// Вызывается процедура формирования запроса по временной таблице. Используется при отладке алгоритмов. 
		// Для просмотра временной таблицы в структуре шапки документа поле "ОтладочныйРежим" должно иметь значение "Истина".
		РасширеннаяАналитикаУчета.ПоказатьВременнуюТаблицу(
			СтруктураШапкиДокумента,
			МенеджерВременныхТаблиц,
			ИмяВременнойТаблицы
		);
		
	КонецЦикла;

КонецПроцедуры // СформироватьВременнуюТаблицуАналитикаВидаУчетаВыпуск()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ПОДГОТОВКИ ВРЕМЕННОЙ ТАБЛИЦЫ ПО ЗАТРАТАМ НА ВЫПУСК

// Функция формирования запроса для базы распределения по затратам на выпуск продукции.
//
// Возвращаемое значение:
//   Строка – Текст запроса
//
Функция СформироватьТекстЗапросаВременнаяТаблицаЗатратыНаВыпускРасширеннаяАналитика()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	УчетЗатрат.АналитикаВидаУчета,
	|	УчетЗатрат.АналитикаУчетаЗатрат,
	|	УчетЗатрат.АналитикаУчетаПартий,
	|	УчетЗатрат.АналитикаРаспределенияЗатрат,
	|
	|	УчетЗатрат.КорАналитикаВидаУчета,
	|	УчетЗатрат.КорАналитикаУчетаЗатрат,
	|	УчетЗатрат.КорАналитикаУчетаПартий,
	|	УчетЗатрат.КорАналитикаРаспределенияЗатрат,
	|
	|	СУММА(УчетЗатрат.Количество) КАК Количество,
	|	СУММА(УчетЗатрат.Стоимость) КАК Стоимость
	|
	|ПОМЕСТИТЬ ЗатратыНаВыпуск%СуффиксУчета%
	|ИЗ
	|	РегистрНакопления.УчетЗатрат%СуффиксРегл% КАК УчетЗатрат
	|ГДЕ
	|	УчетЗатрат.Период МЕЖДУ &НачДата И &КонДата
	|	И УчетЗатрат.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И УчетЗатрат.АналитикаВидаУчета В (
	|		ВЫБРАТЬ
	|			РегистрАналитикаВидаУчета.Ссылка
	|		ИЗ
	|			АналитикаВидаУчетаЗатратыНаВыпуск%СуффиксУчета% КАК РегистрАналитикаВидаУчета
	|	)
	|
	|СГРУППИРОВАТЬ ПО
	|	УчетЗатрат.АналитикаВидаУчета,
	|	УчетЗатрат.АналитикаУчетаЗатрат,
	|	УчетЗатрат.АналитикаУчетаПартий,
	|	УчетЗатрат.АналитикаРаспределенияЗатрат,
	|
	|	УчетЗатрат.КорАналитикаВидаУчета,
	|	УчетЗатрат.КорАналитикаУчетаЗатрат,
	|	УчетЗатрат.КорАналитикаУчетаПартий,
	|	УчетЗатрат.КорАналитикаРаспределенияЗатрат
	|";

	Возврат ТекстЗапроса;
	
КонецФункции // СформироватьТекстЗапросаВременнаяТаблицаЗатратыНаВыпускРасширеннаяАналитика()

// Функция формирования запроса для базы распределения по затратам на выпуск продукции.
//
// Возвращаемое значение:
//   Строка – Текст запроса
//
Функция СформироватьТекстЗапросаВременнаяТаблицаЗатратыНаВыпуск()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	//ДляРеглУчета ЗатратыНаВыпуск.Организация,
	|	//ДляБухУчета ЗатратыНаВыпуск.СчетУчета,
	|	//ДляБухУчета ЗатратыНаВыпуск.НалоговоеНазначение КАК НалоговоеНазначение,
	|	//ДляБухУчета ЗатратыНаВыпуск.НалоговоеНазначениеПоФакту КАК НалоговоеНазначениеПоФакту,
	|	ЗатратыНаВыпуск.Подразделение,
	|	
	|	ВЫБОР КОГДА ЗатратыНаВыпуск.ПодразделениеНЗП <>
	|		//ДляУпрУчета ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка) 
	|		//ДляРеглУчета ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) 
	|	ТОГДА
	|		ЗатратыНаВыпуск.ПодразделениеНЗП
	|	ИНАЧЕ
	|		ЗатратыНаВыпуск.Подразделение
	|	КОНЕЦ КАК ПодразделениеНЗП,
	|	
	|	ЗатратыНаВыпуск.НоменклатурнаяГруппаНЗП,
	|	ЗатратыНаВыпуск.ЗаказНЗП,
	|	ЗатратыНаВыпуск.СтатьяЗатрат,
	|	
	|	ЗатратыНаВыпуск.Затрата,
	|	ЗатратыНаВыпуск.ХарактеристикаЗатраты,
	|	ЗатратыНаВыпуск.СерияЗатраты,
	|	
	|	ЗатратыНаВыпуск.Продукция,
	|	ЗатратыНаВыпуск.ХарактеристикаПродукции,
	|	ЗатратыНаВыпуск.СерияПродукции,
	|	ЗатратыНаВыпуск.НоменклатурнаяГруппа,
	|	ЗатратыНаВыпуск.Заказ,
	|	ЗатратыНаВыпуск.Спецификация,
	|	ЗатратыНаВыпуск.КодОперации,
	|
	|	СУММА(ЗатратыНаВыпуск.Количество) КАК Количество,
	|	СУММА(ЗатратыНаВыпуск.Сумма) КАК Сумма,
	|	
	|	СУММА(	
	|		ЗатратыНаВыпуск.Количество -
	|		ВЫБОР КОГДА ЗатратыНаВыпуск.КодОперации В (&МассивКодыОперацийПрямыеЗатраты) ТОГДА
	|			ЗатратыНаВыпуск.Количество
	|		ИНАЧЕ
	|			0
	|		КОНЕЦ
	|	) КАК КоличествоРасход,
	
	|	СУММА(
	|		ЗатратыНаВыпуск.Сумма -
	|		ВЫБОР КОГДА ЗатратыНаВыпуск.КодОперации В (&МассивКодыОперацийПрямыеЗатраты) ТОГДА
	|			ЗатратыНаВыпуск.Сумма
	|		ИНАЧЕ
	|			0
	|		КОНЕЦ
	|	) КАК СуммаРасход
	|
	|ПОМЕСТИТЬ ЗатратыНаВыпуск%СуффиксУчета%
	|
	|ИЗ
	|	РегистрНакопления.ЗатратыНаВыпускПродукции%СуффиксУчета% КАК ЗатратыНаВыпуск
	|
	|ГДЕ
	|	ЗатратыНаВыпуск.Период МЕЖДУ &НачДата И &КонДата
	|	И Не ЗатратыНаВыпуск.Затрата Ссылка Перечисление.ХарактерЗатрат
	|	//ДляРеглУчета И ЗатратыНаВыпуск.Организация = &Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	//ДляРеглУчета ЗатратыНаВыпуск.Организация,
	|	ЗатратыНаВыпуск.Подразделение,
	|	//ДляБухУчета ЗатратыНаВыпуск.СчетУчета,
	|	//ДляБухУчета ЗатратыНаВыпуск.НалоговоеНазначение,
	|	//ДляБухУчета ЗатратыНаВыпуск.НалоговоеНазначениеПоФакту,
	|	ЗатратыНаВыпуск.НоменклатурнаяГруппа,
	|	ЗатратыНаВыпуск.Продукция,
	|	ЗатратыНаВыпуск.ХарактеристикаПродукции,
	|	ЗатратыНаВыпуск.СерияПродукции,
	|	ЗатратыНаВыпуск.Спецификация,
	|	ЗатратыНаВыпуск.Заказ,
	|    
	|   ЗатратыНаВыпуск.СтатьяЗатрат,
	|	ЗатратыНаВыпуск.Затрата,
	|	ЗатратыНаВыпуск.ХарактеристикаЗатраты,
	|	ЗатратыНаВыпуск.СерияЗатраты,
	|
	|	ЗатратыНаВыпуск.НоменклатурнаяГруппаНЗП,
	|	ЗатратыНаВыпуск.ЗаказНЗП,
	|	
	|	ВЫБОР КОГДА ЗатратыНаВыпуск.ПодразделениеНЗП <>
	|		//ДляУпрУчета ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка) 
	|		//ДляРеглУчета ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) 
	|	ТОГДА
	|		ЗатратыНаВыпуск.ПодразделениеНЗП
	|	ИНАЧЕ
	|		ЗатратыНаВыпуск.Подразделение
	|	КОНЕЦ,
	|	
	|	ЗатратыНаВыпуск.КодОперации
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	//ДляРеглУчета ЗатратыНаВыпуск.Организация,
	|	//ДляБухУчета ЗатратыНаВыпуск.СчетУчета,
	|	//ДляБухУчета ЗатратыНаВыпуск.НалоговоеНазначение КАК НалоговоеНазначение,
	|	//ДляБухУчета ЗатратыНаВыпуск.НалоговоеНазначениеПоФакту КАК НалоговоеНазначениеПоФакту,
	|	ЗатратыНаВыпуск.Подразделение,
	|	ВЫБОР КОГДА ЗатратыНаВыпуск.ПодразделениеНЗП <>
	|		//ДляУпрУчета ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка) 
	|		//ДляРеглУчета ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) 
	|	ТОГДА
	|		ЗатратыНаВыпуск.ПодразделениеНЗП
	|	ИНАЧЕ
	|		ЗатратыНаВыпуск.Подразделение
	|	КОНЕЦ КАК ПодразделениеНЗП,
	|	ЗатратыНаВыпуск.НоменклатурнаяГруппаНЗП,
	|	ЗатратыНаВыпуск.ЗаказНЗП,
	|	ЗатратыНаВыпуск.СтатьяЗатрат,
	|	
	|	ЗатратыНаВыпуск.Затрата,
	|	ЗатратыНаВыпуск.ХарактеристикаЗатраты,
	|	ЗатратыНаВыпуск.СерияЗатраты,
	|	
	|	ЗатратыНаВыпуск.Продукция,
	|	ЗатратыНаВыпуск.ХарактеристикаПродукции,
	|	Неопределено, // СерияПродукции,
	|	ЗатратыНаВыпуск.НоменклатурнаяГруппа,
	|	ЗатратыНаВыпуск.Заказ,
	|	Неопределено, // Спецификация,
	|	ЗатратыНаВыпуск.КодОперации,
	|	
	|	СУММА(
	|		ВЫБОР КОГДА ЗатратыНаВыпуск.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|			ЗатратыНаВыпуск.Количество
	|		ИНАЧЕ
	|			-ЗатратыНаВыпуск.Количество
	|		КОНЕЦ
	|	) КАК Количество,
	|	СУММА(
	|		ВЫБОР КОГДА ЗатратыНаВыпуск.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|			ЗатратыНаВыпуск.Сумма
	|		ИНАЧЕ
	|			-ЗатратыНаВыпуск.Сумма
	|		КОНЕЦ
	|	) КАК Сумма,
	|	0 КАК КоличествоРасход,
	|	0 КАК СуммаРасход
	|
	|ИЗ
	|	РегистрНакопления.ЗатратыНаВыпускПродукцииНаработка%СуффиксУчета% КАК ЗатратыНаВыпуск
	|
	|ГДЕ
	|	ЗатратыНаВыпуск.Период МЕЖДУ &НачДата И &КонДата
	|	//ДляРеглУчета И ЗатратыНаВыпуск.Организация = &Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	//ДляРеглУчета ЗатратыНаВыпуск.Организация,
	|	ЗатратыНаВыпуск.Подразделение,
	|	//ДляБухУчета ЗатратыНаВыпуск.СчетУчета,
	|	//ДляБухУчета ЗатратыНаВыпуск.НалоговоеНазначение,
	|	//ДляБухУчета ЗатратыНаВыпуск.НалоговоеНазначениеПоФакту,
	|	ЗатратыНаВыпуск.НоменклатурнаяГруппа,
	|	ЗатратыНаВыпуск.Продукция,
	|	ЗатратыНаВыпуск.ХарактеристикаПродукции,
	|	ЗатратыНаВыпуск.Заказ,
	|	
	|	ЗатратыНаВыпуск.СтатьяЗатрат,
	|	ЗатратыНаВыпуск.Затрата,
	|	ЗатратыНаВыпуск.ХарактеристикаЗатраты,
	|	ЗатратыНаВыпуск.СерияЗатраты,
	|
	|	ЗатратыНаВыпуск.НоменклатурнаяГруппаНЗП,
	|	ЗатратыНаВыпуск.ЗаказНЗП,
	|	ВЫБОР КОГДА ЗатратыНаВыпуск.ПодразделениеНЗП <>
	|		//ДляУпрУчета ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка) 
	|		//ДляРеглУчета ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) 
	|	ТОГДА
	|		ЗатратыНаВыпуск.ПодразделениеНЗП
	|	ИНАЧЕ
	|		ЗатратыНаВыпуск.Подразделение
	|	КОНЕЦ,
	|	
	|	ЗатратыНаВыпуск.КодОперации
	|";

	Возврат ТекстЗапроса;
	
КонецФункции // СформироватьТекстЗапросаВременнаяТаблицаЗатратыНаВыпуск()

// Функция формирования запроса для базы распределения по затратам на выпуск продукции.
//
// Возвращаемое значение:
//   Строка – Текст запроса
//
Функция СформироватьТекстЗапросаБазыРаспределенияПоЗатратамНаВыпускМежд()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	//ДляРеглУчета ЗатратыНаВыпуск.Организация,
	|	//ДляРеглУчета ЗатратыНаВыпуск.СчетУчета,
	|	ЗатратыНаВыпуск.Подразделение,
	|	
	|	ВЫБОР КОГДА ЗатратыНаВыпуск.ПодразделениеНЗП <>
	|		//ДляУпрУчета ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка) 
	|		//ДляРеглУчета ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) 
	|	ТОГДА
	|		ЗатратыНаВыпуск.ПодразделениеНЗП
	|	ИНАЧЕ
	|		ЗатратыНаВыпуск.Подразделение
	|	КОНЕЦ КАК ПодразделениеНЗП,
	|	
	|	ЗатратыНаВыпуск.НоменклатурнаяГруппаНЗП,
	|	ЗатратыНаВыпуск.ЗаказНЗП,
	|	ЗатратыНаВыпуск.СтатьяЗатрат,
	|	
	|	ЗатратыНаВыпуск.Затрата,
	|	ЗатратыНаВыпуск.ХарактеристикаЗатраты,
	|	ЗатратыНаВыпуск.СерияЗатраты,
	|	
	|	ЗатратыНаВыпуск.Продукция,
	|	ЗатратыНаВыпуск.ХарактеристикаПродукции,
	|	ЗатратыНаВыпуск.СерияПродукции,
	|	ЗатратыНаВыпуск.НоменклатурнаяГруппа,
	|	ЗатратыНаВыпуск.Заказ,
	|	ЗатратыНаВыпуск.Спецификация,
	|	ЗатратыНаВыпуск.КодОперации,
	|
	|	СУММА(ЗатратыНаВыпуск.Количество) КАК Количество,
	|	//ДляНалУчета СУММА(ЗатратыНаВыпуск.ВременнаяРазница) КАК ВременнаяРазница,
	|	//ДляНалУчета СУММА(ЗатратыНаВыпуск.ПостояннаяРазница) КАК ПостояннаяРазница,
	|	СУММА(ЗатратыНаВыпуск.Сумма) КАК Сумма,
	|	
	|	СУММА(	
	|		ЗатратыНаВыпуск.Количество -
	|		ВЫБОР КОГДА ЗатратыНаВыпуск.КодОперации В (&МассивКодыОперацийПрямыеЗатраты) ТОГДА
	|			ЗатратыНаВыпуск.Количество
	|		ИНАЧЕ
	|			0
	|		КОНЕЦ
	|	) КАК КоличествоРасход,
	|	СУММА(
	|		ЗатратыНаВыпуск.Сумма -
	|		ВЫБОР КОГДА ЗатратыНаВыпуск.КодОперации В (&МассивКодыОперацийПрямыеЗатраты) ТОГДА
	|			ЗатратыНаВыпуск.Сумма
	|		ИНАЧЕ
	|			0
	|		КОНЕЦ
	|	) КАК СуммаРасход
	|
	|ПОМЕСТИТЬ ЗатратыНаВыпуск%СуффиксУчета%
	|
	|ИЗ
	|	РегистрНакопления.ЗатратыНаВыпускПродукции%СуффиксУчета% КАК ЗатратыНаВыпуск
	|
	|ГДЕ
	|	ЗатратыНаВыпуск.Период МЕЖДУ &НачДата И &КонДата
	|	И Не ЗатратыНаВыпуск.Затрата Ссылка Перечисление.ХарактерЗатрат
	|	//ДляРеглУчета И ЗатратыНаВыпуск.Организация = &Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	//ДляРеглУчета ЗатратыНаВыпуск.Организация,
	|	ЗатратыНаВыпуск.Подразделение,
	|	//ДляРеглУчета ЗатратыНаВыпуск.СчетУчета,
	|	ЗатратыНаВыпуск.НоменклатурнаяГруппа,
	|	ЗатратыНаВыпуск.Продукция,
	|	ЗатратыНаВыпуск.ХарактеристикаПродукции,
	|	ЗатратыНаВыпуск.СерияПродукции,
	|	ЗатратыНаВыпуск.Спецификация,
	|	ЗатратыНаВыпуск.Заказ,
	|    
	|    ЗатратыНаВыпуск.СтатьяЗатрат,
	|	ЗатратыНаВыпуск.Затрата,
	|	ЗатратыНаВыпуск.ХарактеристикаЗатраты,
	|	ЗатратыНаВыпуск.СерияЗатраты,
	|
	|	ЗатратыНаВыпуск.НоменклатурнаяГруппаНЗП,
	|	ЗатратыНаВыпуск.ЗаказНЗП,
	|	
	|	ВЫБОР КОГДА ЗатратыНаВыпуск.ПодразделениеНЗП <>
	|		//ДляУпрУчета ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка) 
	|		//ДляРеглУчета ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) 
	|	ТОГДА
	|		ЗатратыНаВыпуск.ПодразделениеНЗП
	|	ИНАЧЕ
	|		ЗатратыНаВыпуск.Подразделение
	|	КОНЕЦ,
	|	
	|	ЗатратыНаВыпуск.КодОперации
	|
	|";

	Возврат ТекстЗапроса;
	
КонецФункции // СформироватьТекстЗапросаБазаРаспределенияПоЗатратамНаВыпускМежд()

// Процедура устанавливает параметры запроса по затратам на выпуск продукции.
//
// Параметры
//  СтруктураШапкиДокумента – Структура - Реквизиты документа "Расчет себестоимости выпуска"
//	Запрос - Запрос.
//
Процедура УстановитьПараметрыЗапросаБазыРаспределенияПоЗатратамНаВыпуск(
	СтруктураШапкиДокумента, 
	Запрос
	)
	
	Запрос.УстановитьПараметр("НачДата", СтруктураШапкиДокумента.мНачДата);
	Запрос.УстановитьПараметр("КонДата", СтруктураШапкиДокумента.мКонДата);
	Запрос.УстановитьПараметр("НачГраница", СтруктураШапкиДокумента.мНачГраница);
	Запрос.УстановитьПараметр("КонГраница", СтруктураШапкиДокумента.мКонГраница);
	Запрос.УстановитьПараметр("Организация", СтруктураШапкиДокумента.Организация);
	
	МассивКодыОперацийПрямыеЗатраты = Новый Массив;
	МассивКодыОперацийПрямыеЗатраты.Добавить(Перечисления.КодыОперацийЗатратыНаВыпускПродукции.ПрямыеЗатраты);
	МассивКодыОперацийПрямыеЗатраты.Добавить(Перечисления.КодыОперацийЗатратыНаВыпускПродукции.ОтрицательныеЗатраты);
	Запрос.УстановитьПараметр("МассивКодыОперацийПрямыеЗатраты", МассивКодыОперацийПрямыеЗатраты);
	
КонецПроцедуры // УстановитьПараметрыЗапросаБазыРаспределенияПоЗатратамНаВыпуск()

// Процедура формирует временную таблицу "Затраты на выпуск".
//
// Параметры:
//  СтруктураШапкиДокумента – Структура - Реквизиты документа "Расчет себестоимости выпуска"
//  СтруктураПараметров – Структура - Структура параметров способа распределения
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Менеджер временных таблиц
//
Процедура СформироватьВременнуюТаблицуЗатратыНаВыпуск(
	СтруктураШапкиДокумента,
	СтруктураПараметров,
	МенеджерВременныхТаблиц
	)
	
	Если СтруктураШапкиДокумента.ОтражатьВМеждународномУчете Тогда
		ТекстЗапросаСКомментариями = СформироватьТекстЗапросаБазыРаспределенияПоЗатратамНаВыпускМежд();
		
	ИначеЕсли СтруктураШапкиДокумента.ИспользоватьРасширеннуюАналитику Тогда
		ТекстЗапросаСКомментариями = СформироватьТекстЗапросаВременнаяТаблицаЗатратыНаВыпускРасширеннаяАналитика();
		
	Иначе
		ТекстЗапросаСКомментариями = СформироватьТекстЗапросаВременнаяТаблицаЗатратыНаВыпуск();
		
	КонецЕсли;
	
	// Цикл по всем видам отражения в учете, для которых рассчитывается база распределения.
	Для Каждого ВидОтраженияВУчете Из СтруктураПараметров.МассивВидовУчета Цикл
		
		ТекстЗапроса = УправлениеЗатратами.ЗаменитьКомментарииВТекстеЗапроса(
			ТекстЗапросаСКомментариями, 
			ВидОтраженияВУчете
		);
		
		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапроса;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		
		УстановитьПараметрыЗапросаБазыРаспределенияПоЗатратамНаВыпуск(
			СтруктураШапкиДокумента, 
			Запрос
		);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		СуффиксИмениРегистра = СтруктураПараметров.СуффиксыИмениРегистра[ВидОтраженияВУчете];
		ИмяВременнойТаблицы = "ЗатратыНаВыпуск" + СуффиксИмениРегистра;
		
		// Вызывается процедура формирования запроса по временной таблице. Используется при отладке алгоритмов. 
		// Для просмотра временной таблицы в структуре шапки документа поле "ОтладочныйРежим" должно иметь значение "Истина".
		РасширеннаяАналитикаУчета.ПоказатьВременнуюТаблицу(
			СтруктураШапкиДокумента,
			МенеджерВременныхТаблиц,
			ИмяВременнойТаблицы
		);
		
	КонецЦикла;
	
КонецПроцедуры // СформироватьВременнуюТаблицуЗатратыНаВыпуск()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ПОДГОТОВКИ ВРЕМЕННОЙ ТАБЛИЦЫ ПО ВЫПУСКУ ПРОДУКЦИИ

// Функция формирования запроса для базы распределения по выпуску продукции.
//
// Возвращаемое значение:
//   Строка – Текст запроса
//
Функция СформироватьТекстЗапросаВременнаяТаблицаВыпускПродукцииРасширеннаяАналитика()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	УчетЗатрат.АналитикаВидаУчета КАК АналитикаВидаУчета,
	|	УчетЗатрат.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|	УчетЗатрат.АналитикаРаспределенияЗатрат КАК АналитикаРаспределенияЗатрат,
	|
	|
	|	УчетЗатрат.КоличествоНачальныйОстаток,
	|	УчетЗатрат.КоличествоПриход,
	|	УчетЗатрат.СтоимостьНачальныйОстаток,
	|	УчетЗатрат.СтоимостьПриход
	|
	|ПОМЕСТИТЬ УчетЗатратОстаткиИОбороты
	|ИЗ
	|	РегистрНакопления.УчетЗатрат%СуффиксРегл%.ОстаткиИОбороты(&НачГраница, &КонГраница, Период,,
	|		АналитикаВидаУчета В (
	|			ВЫБРАТЬ
	|				РегистрАналитикаВидаУчета.Ссылка
	|			ИЗ
	|				РегистрСведений.АналитикаВидаУчета КАК РегистрАналитикаВидаУчета
	|			ГДЕ
	|				(РегистрАналитикаВидаУчета.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчета.Выпуск)
	|				ИЛИ РегистрАналитикаВидаУчета.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчета.Наработка))
	|				//ДляРеглУчета И РегистрАналитикаВидаУчета.Организация = &Организация
	|		)
	|	) КАК УчетЗатрат
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаВидаУчета,
	|	АналитикаУчетаПартий,
	|	АналитикаРаспределенияЗатрат
	|;
	|///////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УчетЗатрат.АналитикаВидаУчета КАК АналитикаВидаУчета,
	|	УчетЗатрат.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|	УчетЗатрат.АналитикаРаспределенияЗатрат КАК АналитикаРаспределенияЗатрат,
	|	МАКСИМУМ(УчетЗатрат.ВариантВыпускаПродукции) КАК ВариантВыпускаПродукции
	|
	|ПОМЕСТИТЬ ВариантыВыпускаПродукции
	|ИЗ (
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		УчетЗатрат.АналитикаВидаУчета КАК АналитикаВидаУчета,
	|		УчетЗатрат.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|		УчетЗатрат.АналитикаРаспределенияЗатрат КАК АналитикаРаспределенияЗатрат,
	|	
	|		ВЫБОР КОГДА ВЫРАЗИТЬ(РегистрАналитикаУчетаПартий.Заказ КАК Документ.ЗаказПокупателя).ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗаказПокупателя.Переработка)
	|		ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыВыпускаПродукции.ПродукцияИзДавальческогоСырья)
	|			
	|		КОГДА УчетЗатрат.Регистратор ССЫЛКА Документ.ПоступлениеТоваровИзПереработки ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыВыпускаПродукции.ПродукцияСтороннегоПереработчика)
	|			
	|		КОГДА УчетЗатрат.Регистратор ССЫЛКА Документ.КомплектацияНоменклатуры
	|			И ВЫРАЗИТЬ(УчетЗатрат.Регистратор КАК Документ.КомплектацияНоменклатуры).ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияНоменклатуры.ПоступлениеОтПереработчика)
	|		ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыВыпускаПродукции.ПродукцияСтороннегоПереработчика)
	|			
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыВыпускаПродукции.СобственнаяПродукция)
	|			
	|		КОНЕЦ КАК ВариантВыпускаПродукции
	|	ИЗ
	|		РегистрНакопления.УчетЗатрат%СуффиксРегл% КАК УчетЗатрат
	|	
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			РегистрСведений.АналитикаУчетаПартий КАК РегистрАналитикаУчетаПартий
	|		ПО
	|			УчетЗатрат.АналитикаУчетаПартий = РегистрАналитикаУчетаПартий.Ссылка
	|	ГДЕ
	|		УчетЗатрат.Период МЕЖДУ &НачДата И &КонДата
	|		И УчетЗатрат.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И Не УчетЗатрат.Регистратор ССЫЛКА Документ.РасчетСебестоимостиВыпуска
	|		И УчетЗатрат.АналитикаВидаУчета В (
	|			ВЫБРАТЬ
	|				РегистрАналитикаВидаУчета.Ссылка
	|			ИЗ
	|				РегистрСведений.АналитикаВидаУчета КАК РегистрАналитикаВидаУчета
	|			ГДЕ
	|				РегистрАналитикаВидаУчета.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчета.Выпуск)
	|				//ДляРеглУчета И РегистрАналитикаВидаУчета.Организация = &Организация
	|		)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		УчетЗатрат.АналитикаВидаУчета КАК АналитикаВидаУчета,
	|		УчетЗатрат.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|		УчетЗатрат.АналитикаРаспределенияЗатрат КАК АналитикаРаспределенияЗатрат,
	|	
	|		ВЫБОР КОГДА ВЫРАЗИТЬ(РегистрАналитикаУчетаПартий.Заказ КАК Документ.ЗаказПокупателя).ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗаказПокупателя.Переработка)
	|		ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыВыпускаПродукции.ПродукцияИзДавальческогоСырья)
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыВыпускаПродукции.СобственнаяПродукция)
	|		КОНЕЦ КАК ВариантВыпускаПродукции
	|	
	|	ИЗ
	|		УчетЗатратОстаткиИОбороты КАК УчетЗатрат
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			РегистрСведений.АналитикаВидаУчета КАК РегистрАналитикаВидаУчета
	|		ПО
	|			УчетЗатрат.АналитикаВидаУчета = РегистрАналитикаВидаУчета.Ссылка	
	|	
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			РегистрСведений.АналитикаУчетаПартий КАК РегистрАналитикаУчетаПартий
	|		ПО
	|			УчетЗатрат.АналитикаУчетаПартий = РегистрАналитикаУчетаПартий.Ссылка	
	|	ГДЕ
	|		РегистрАналитикаВидаУчета.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчета.Наработка)
	|
	|	) КАК УчетЗатрат
	|
	|СГРУППИРОВАТЬ ПО
	|	УчетЗатрат.АналитикаВидаУчета,
	|	УчетЗатрат.АналитикаУчетаПартий,
	|	УчетЗатрат.АналитикаРаспределенияЗатрат
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаВидаУчета,
	|	АналитикаУчетаПартий,
	|	АналитикаРаспределенияЗатрат
	|;
	|///////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ
	|	УчетЗатрат.АналитикаВидаУчета,
	|	УчетЗатрат.АналитикаУчетаПартий,
	|	УчетЗатрат.АналитикаРаспределенияЗатрат,
	|	
	|	РегистрАналитикаРаспределенияЗатрат.Продукция,
	|	РегистрАналитикаРаспределенияЗатрат.ХарактеристикаПродукции,
	|	РегистрАналитикаРаспределенияЗатрат.СерияПродукции,
	|	
	|	ЕСТЬNULL(ВариантыВыпускаПродукции.ВариантВыпускаПродукции, Неопределено) КАК ВариантВыпускаПродукции,
	|	
	|	
	|	ВЫБОР КОГДА РегистрАналитикаВидаУчета.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчета.Наработка) ТОГДА
	|		УчетЗатрат.КоличествоНачальныйОстаток
	|		+ УчетЗатрат.КоличествоПриход
	|	ИНАЧЕ
	|		 УчетЗатрат.КоличествоПриход
	|	КОНЕЦ КАК Количество,
	|
	|	ВЫБОР КОГДА РегистрАналитикаВидаУчета.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчета.Наработка) ТОГДА
	|		УчетЗатрат.СтоимостьНачальныйОстаток
	|		+ УчетЗатрат.СтоимостьПриход
	|	ИНАЧЕ
	|		УчетЗатрат.СтоимостьПриход
	|	КОНЕЦ КАК Сумма
	|	
	|ПОМЕСТИТЬ ВыпускПродукции%СуффиксУчета%
	|
	|ИЗ
	|	УчетЗатратОстаткиИОбороты КАК УчетЗатрат
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВариантыВыпускаПродукции КАК ВариантыВыпускаПродукции
	|	ПО
	|		УчетЗатрат.АналитикаВидаУчета = ВариантыВыпускаПродукции.АналитикаВидаУчета
	|		И УчетЗатрат.АналитикаРаспределенияЗатрат = ВариантыВыпускаПродукции.АналитикаРаспределенияЗатрат
	|		И УчетЗатрат.АналитикаУчетаПартий = ВариантыВыпускаПродукции.АналитикаУчетаПартий
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаВидаУчета КАК РегистрАналитикаВидаУчета
	|	ПО
	|		УчетЗатрат.АналитикаВидаУчета = РегистрАналитикаВидаУчета.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаРаспределенияЗатрат КАК РегистрАналитикаРаспределенияЗатрат
	|	ПО
	|		УчетЗатрат.АналитикаРаспределенияЗатрат = РегистрАналитикаРаспределенияЗатрат.Ссылка
	|;
	|///////////////////////////////////////////////////////////////////////////////
	|
	|УНИЧТОЖИТЬ ВариантыВыпускаПродукции
	|;
	|///////////////////////////////////////////////////////////////////////////////
	|
	|УНИЧТОЖИТЬ УчетЗатратОстаткиИОбороты
	|";

	Возврат ТекстЗапроса;
	
КонецФункции // СформироватьТекстЗапросаВременнаяТаблицаВыпускПродукцииРасширеннаяАналитика()

// Функция формирования запроса для базы распределения по выпуску продукции.
//
// Возвращаемое значение:
//   Строка – Текст запроса
//
Функция СформироватьТекстЗапросаВременнаяТаблицаВыпускПродукции()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	//ДляРеглУчета ВыпускПродукции.СчетУчетаНЗП,
	|	//ДляБухУчета ВыпускПродукции.НалоговоеНазначение,
	|	ВыпускПродукции.Подразделение,
	|	ВыпускПродукции.Продукция,
	|	ВыпускПродукции.ХарактеристикаПродукции,
	|	ВыпускПродукции.НоменклатурнаяГруппа,
	|	ВыпускПродукции.Заказ,
	|	ВыпускПродукции.ВариантВыпускаПродукции,
	|	ВыпускПродукции.КодОперации,
	|	
	|	СУММА(
	|		ВЫБОР КОГДА ВыпускПродукции.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|			ВыпускПродукции.Количество
	|		ИНАЧЕ
	|			0
	|		КОНЕЦ
	|	) КАК Количество
	|	
	|ПОМЕСТИТЬ ВыпускПродукцииНаработка
	|ИЗ
	|	РегистрНакопления.ВыпускПродукцииНаработка%СуффиксУчета% КАК ВыпускПродукции
	|
	|ГДЕ
	|	ВыпускПродукции.Период МЕЖДУ &НачДата И &КонДата
	|	//ДляРеглУчета И ВыпускПродукции.Организация = &Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	ВыпускПродукции.Подразделение,
	|	//ДляРеглУчета ВыпускПродукции.СчетУчетаНЗП,
	|	//ДляБухУчета  ВыпускПродукции.НалоговоеНазначение,
	|	ВыпускПродукции.НоменклатурнаяГруппа,
	|	ВыпускПродукции.Продукция,
	|	ВыпускПродукции.ХарактеристикаПродукции,
	|	ВыпускПродукции.Заказ,
	|	ВыпускПродукции.ВариантВыпускаПродукции,
	|	ВыпускПродукции.КодОперации
	|	
	|ИМЕЮЩИЕ
	|	СУММА(
	|		ВЫБОР КОГДА ВыпускПродукции.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|			ВыпускПродукции.Количество
	|		ИНАЧЕ
	|			0
	|		КОНЕЦ
	|	) > 0	
	|	
	|;
	|///////////////////////////////////////////////////////////////////////////////	
    |
	|ВЫБРАТЬ
	|	//ДляРеглУчета ВыпускПродукции.Организация,
	|	//ДляРеглУчета ВыпускПродукции.СчетУчетаНЗП,
	|	//ДляБухУчета  ВыпускПродукции.НалоговоеНазначение,
	|	ВыпускПродукции.Подразделение,
	|	ВыпускПродукции.Продукция,
	|	ВыпускПродукции.ХарактеристикаПродукции,
	|	ВыпускПродукции.СерияПродукции,
	|	ВыпускПродукции.НоменклатурнаяГруппа,
	|	ВыпускПродукции.Заказ,
	|	ВыпускПродукции.Спецификация,
	|	ВыпускПродукции.ВариантВыпускаПродукции,
	|	ВыпускПродукции.КодОперации,
	|	
	|	СУММА(ВыпускПродукции.Количество) КАК Количество
	|	
	|ПОМЕСТИТЬ ВыпускПродукции%СуффиксУчета%
	|
	|ИЗ
	|	РегистрНакопления.ВыпускПродукции%СуффиксУчета% КАК ВыпускПродукции
	|
	|ГДЕ
	|	ВыпускПродукции.Период МЕЖДУ &НачДата И &КонДата
	|	И Не ВыпускПродукции.КодОперации В (&ИсключаемыеКодыОпераций)
	|	//ДляРеглУчета И ВыпускПродукции.Организация = &Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	//ДляРеглУчета ВыпускПродукции.Организация,
	|	//ДляРеглУчета ВыпускПродукции.СчетУчетаНЗП,
	|	//ДляБухУчета  ВыпускПродукции.НалоговоеНазначение,
	|	ВыпускПродукции.Подразделение,
	|	ВыпускПродукции.НоменклатурнаяГруппа,
	|	ВыпускПродукции.Продукция,
	|	ВыпускПродукции.ХарактеристикаПродукции,
	|	ВыпускПродукции.СерияПродукции,
	|	ВыпускПродукции.Спецификация,
	|	ВыпускПродукции.Заказ,
	|    
	|	ВыпускПродукции.ВариантВыпускаПродукции,
	|	ВыпускПродукции.КодОперации
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	//ДляРеглУчета ВыпускПродукцииОстатки.Организация,
	|	//ДляРеглУчета ВыпускПродукции.СчетУчетаНЗП,
	|	//ДляБухУчета  ВыпускПродукции.НалоговоеНазначение,
	|	ВыпускПродукцииОстатки.Подразделение,
	|	ВыпускПродукцииОстатки.Продукция,
	|	ВыпускПродукцииОстатки.ХарактеристикаПродукции,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) КАК СерияПродукции,
	|	ВыпускПродукцииОстатки.НоменклатурнаяГруппа,
	|	ВыпускПродукцииОстатки.Заказ,
	|	Неопределено КАК Спецификация,
	|	ВыпускПродукции.ВариантВыпускаПродукции,
	|	ВыпускПродукции.КодОперации,
	|	
	|	ВЫБОР КОГДА МАКСИМУМ(ВыпускПродукцииОстатки.КоличествоПриход) <> 0 ТОГДА
	|		МАКСИМУМ(ВыпускПродукцииОстатки.КоличествоКонечныйОстаток)
	|			* СУММА(ВыпускПродукции.Количество)
	|			/ МАКСИМУМ(ВыпускПродукцииОстатки.КоличествоПриход)
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК Количество
	|	
	|ИЗ
	|	РегистрНакопления.ВыпускПродукцииНаработка%СуффиксУчета%.ОстаткиИОбороты(&НачГраница, &КонГраница, Период,,
	|		//ДляРеглУчета Организация = &Организация
	|	) КАК ВыпускПродукцииОстатки
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВыпускПродукцииНаработка КАК ВыпускПродукции
	|	ПО
	|		ВыпускПродукцииОстатки.Подразделение = ВыпускПродукции.Подразделение
	|		И ВыпускПродукцииОстатки.Продукция = ВыпускПродукции.Продукция
	|		И ВыпускПродукцииОстатки.ХарактеристикаПродукции = ВыпускПродукции.ХарактеристикаПродукции
	|		И ВыпускПродукцииОстатки.НоменклатурнаяГруппа = ВыпускПродукции.НоменклатурнаяГруппа
	|		И ВыпускПродукцииОстатки.Заказ = ВыпускПродукции.Заказ
	|
	|ГДЕ
	|	ВыпускПродукцииОстатки.КоличествоКонечныйОстаток > 0
	|	
	|СГРУППИРОВАТЬ ПО
	|	//ДляРеглУчета ВыпускПродукцииОстатки.Организация,
	|	//ДляРеглУчета ВыпускПродукции.СчетУчетаНЗП,
	|	//ДляБухУчета  ВыпускПродукции.НалоговоеНазначение,
	|	ВыпускПродукцииОстатки.Подразделение,
	|	ВыпускПродукцииОстатки.НоменклатурнаяГруппа,
	|	ВыпускПродукцииОстатки.Продукция,
	|	ВыпускПродукцииОстатки.ХарактеристикаПродукции,
	|	ВыпускПродукцииОстатки.Заказ,
	|    
	|	ВыпускПродукции.ВариантВыпускаПродукции,
	|	ВыпускПродукции.КодОперации
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВыпускПродукцииНаработка
	|";

	Возврат ТекстЗапроса;
	
КонецФункции // СформироватьТекстЗапросаВременнаяТаблицаВыпускПродукции()

// Функция формирования запроса для базы распределения по выпуску продукции.
//
// Возвращаемое значение:
//   Строка – Текст запроса
//
Функция СформироватьТекстЗапросаБазыРаспределенияПоВыпускуПродукцииМежд()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	//ДляРеглУчета ВыпускПродукции.Организация,
	|	//ДляРеглУчета ВыпускПродукции.СчетУчетаНЗП,
	|	ВыпускПродукции.Подразделение,
	|	ВыпускПродукции.Продукция,
	|	ВыпускПродукции.ХарактеристикаПродукции,
	|	ВыпускПродукции.СерияПродукции,
	|	ВыпускПродукции.НоменклатурнаяГруппа,
	|	ВыпускПродукции.Заказ,
	|	ВыпускПродукции.Спецификация,
	|	ВыпускПродукции.ВариантВыпускаПродукции,
	|	ВыпускПродукции.КодОперации,
	|	
	|	СУММА(ВыпускПродукции.Количество) КАК Количество
	|	
	|ПОМЕСТИТЬ ВыпускПродукции%СуффиксУчета%
	|
	|ИЗ
	|	РегистрНакопления.ВыпускПродукции%СуффиксУчета% КАК ВыпускПродукции
	|
	|ГДЕ
	|	ВыпускПродукции.Период МЕЖДУ &НачДата И &КонДата
	|	И Не ВыпускПродукции.КодОперации В (&ИсключаемыеКодыОпераций)
	|	//ДляРеглУчета И ВыпускПродукции.Организация = &Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	//ДляРеглУчета ВыпускПродукции.Организация,
	|	ВыпускПродукции.Подразделение,
	|	//ДляРеглУчета ВыпускПродукции.СчетУчетаНЗП,
	|	ВыпускПродукции.НоменклатурнаяГруппа,
	|	ВыпускПродукции.Продукция,
	|	ВыпускПродукции.ХарактеристикаПродукции,
	|	ВыпускПродукции.СерияПродукции,
	|	ВыпускПродукции.Спецификация,
	|	ВыпускПродукции.Заказ,
	|    
	|	ВыпускПродукции.ВариантВыпускаПродукции,
	|	ВыпускПродукции.КодОперации
	|";

	Возврат ТекстЗапроса;
	
КонецФункции // СформироватьТекстЗапросаБазыРаспределенияПоВыпускуПродукцииМежд()

// Процедура устанавливает параметры запроса по выпуску продукции.
//
// Параметры
//  СтруктураШапкиДокумента – Структура - Реквизиты документа "Расчет себестоимости выпуска"
//	Запрос - Запрос.
//
Процедура УстановитьПараметрыЗапросаБазыРаспределенияПоВыпускуПродукции(
	СтруктураШапкиДокумента, 
	Запрос
	)
	
	Запрос.УстановитьПараметр("НачДата", СтруктураШапкиДокумента.мНачДата);
	Запрос.УстановитьПараметр("КонДата", СтруктураШапкиДокумента.мКонДата);
	Запрос.УстановитьПараметр("НачГраница", СтруктураШапкиДокумента.мНачГраница);
	Запрос.УстановитьПараметр("КонГраница", СтруктураШапкиДокумента.мКонГраница);
	Запрос.УстановитьПараметр("Организация", СтруктураШапкиДокумента.Организация);
	
	МассивИсключаемыхКодовОпераций = Новый Массив;
	МассивИсключаемыхКодовОпераций.Добавить(Перечисления.КодыОперацийВыпускПродукции.СписаниеНЗП);
	МассивИсключаемыхКодовОпераций.Добавить(Перечисления.КодыОперацийВыпускПродукции.СписаниеНЗПФикс);
	МассивИсключаемыхКодовОпераций.Добавить(Перечисления.КодыОперацийВыпускПродукции.ОприходованиеНЗП);
	МассивИсключаемыхКодовОпераций.Добавить(Перечисления.КодыОперацийВыпускПродукции.КорректировкаНЗПРасч);
	МассивИсключаемыхКодовОпераций.Добавить(Перечисления.КодыОперацийВыпускПродукции.КорректировкаНЗПФикс);
	МассивИсключаемыхКодовОпераций.Добавить(Перечисления.КодыОперацийВыпускПродукции.ВозвратМатериаловИзПроизводстваРасч);
	МассивИсключаемыхКодовОпераций.Добавить(Перечисления.КодыОперацийВыпускПродукции.ВозвратМатериаловИзПроизводстваФикс);
	МассивИсключаемыхКодовОпераций.Добавить(Перечисления.КодыОперацийВыпускПродукции.ВыпускПродукцииПоФиксированнойСтоимости);
	Запрос.УстановитьПараметр("ИсключаемыеКодыОпераций", МассивИсключаемыхКодовОпераций);
	
КонецПроцедуры // УстановитьПараметрыЗапросаБазыРаспределенияПоВыпускуПродукции()

// Процедура формирует временную таблицу "Выпуск продукции".
//
// Параметры:
//  СтруктураШапкиДокумента – Структура - Реквизиты документа "Расчет себестоимости выпуска"
//  СтруктураПараметров – Структура - Структура параметров способа распределения
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Менеджер временных таблиц
//
Процедура СформироватьВременнуюТаблицуВыпускПродукции(
	СтруктураШапкиДокумента,
	СтруктураПараметров,
	МенеджерВременныхТаблиц
	)
	
	// Формирование текста запроса для временной таблицы "ВыпускПродукции".
	Если СтруктураШапкиДокумента.ОтражатьВМеждународномУчете Тогда
		ТекстЗапросаСКомментариями = СформироватьТекстЗапросаБазыРаспределенияПоВыпускуПродукцииМежд();
		
	ИначеЕсли СтруктураШапкиДокумента.ИспользоватьРасширеннуюАналитику Тогда
		ТекстЗапросаСКомментариями = СформироватьТекстЗапросаВременнаяТаблицаВыпускПродукцииРасширеннаяАналитика();
		
	Иначе
		ТекстЗапросаСКомментариями = СформироватьТекстЗапросаВременнаяТаблицаВыпускПродукции();
		
	КонецЕсли;
	
	// Цикл по всем видам отражения в учете, для которых рассчитывается база распределения.
	Для Каждого ВидОтраженияВУчете Из СтруктураПараметров.МассивВидовУчета Цикл
		
		ТекстЗапроса = УправлениеЗатратами.ЗаменитьКомментарииВТекстеЗапроса(
			ТекстЗапросаСКомментариями, 
			ВидОтраженияВУчете
		);
		
		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапроса;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		
		УстановитьПараметрыЗапросаБазыРаспределенияПоВыпускуПродукции(
			СтруктураШапкиДокумента, 
			Запрос
		);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		СуффиксИмениРегистра = СтруктураПараметров.СуффиксыИмениРегистра[ВидОтраженияВУчете];
		ИмяВременнойТаблицы = "ВыпускПродукции" + СуффиксИмениРегистра;
		
		// Вызывается процедура формирования запроса по временной таблице. Используется при отладке алгоритмов. 
		// Для просмотра временной таблицы в структуре шапки документа поле "ОтладочныйРежим" должно иметь значение "Истина".
		РасширеннаяАналитикаУчета.ПоказатьВременнуюТаблицу(
			СтруктураШапкиДокумента,
			МенеджерВременныхТаблиц,
			ИмяВременнойТаблицы
		);
		
	КонецЦикла;
	
КонецПроцедуры // СформироватьВременнуюТаблицуВыпускПродукции()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ПОДГОТОВКИ ВРЕМЕННОЙ ТАБЛИЦЫ ПО НЕЗАВЕРШЕННОМУ ПРОИЗВОДСТВУ

// Функция формирования запроса для базы распределения по выпуску продукции.
//
// Возвращаемое значение:
//   Строка – Текст запроса
//
Функция СформироватьТекстЗапросаВременнаяТаблицаНезавершенноеПроизводствоРасширеннаяАналитика()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	УчетЗатрат.АналитикаВидаУчета,
	|	УчетЗатрат.АналитикаУчетаЗатрат,
	|	УчетЗатрат.АналитикаУчетаПартий,
	|	УчетЗатрат.АналитикаРаспределенияЗатрат,
	|
	|	УчетЗатрат.СтоимостьПриход,
	|	УчетЗатрат.СтоимостьКонечныйОстаток КАК СтоимостьОстаток,
	|
	|	ВЫБОР КОГДА РаспределяемыеСтатьиЗатрат.Распределяемая ЕСТЬ NULL
	|		И РегистрАналитикаУчетаЗатрат.СпособРаспределенияЗатрат = ЗНАЧЕНИЕ(Справочник.СпособыРаспределенияЗатратНаВыпуск.ПустаяСсылка)
	|	ТОГДА
	|		УчетЗатрат.СтоимостьКонечныйОстаток
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК СтоимостьКонечныйОстаток,
	|	
	|	УчетЗатрат.КоличествоПриход,
	|	УчетЗатрат.КоличествоКонечныйОстаток КАК КоличествоОстаток,
	|
	|	ВЫБОР КОГДА РаспределяемыеСтатьиЗатрат.Распределяемая ЕСТЬ NULL
	|		И РегистрАналитикаУчетаЗатрат.СпособРаспределенияЗатрат = ЗНАЧЕНИЕ(Справочник.СпособыРаспределенияЗатратНаВыпуск.ПустаяСсылка)
	|	ТОГДА
	|		УчетЗатрат.КоличествоКонечныйОстаток
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК КоличествоКонечныйОстаток
	|	
	|ПОМЕСТИТЬ НезавершенноеПроизводство%СуффиксУчета%
	|
	|ИЗ
	|	РегистрНакопления.УчетЗатрат%СуффиксРегл%.ОстаткиИОбороты(&НачГраница, &КонГраница, Период,,
	|		АналитикаВидаУчета В (
	|			ВЫБРАТЬ
	|				РегистрАналитикаВидаУчета.Ссылка
	|			ИЗ
	|				РегистрСведений.АналитикаВидаУчета КАК РегистрАналитикаВидаУчета
	|			ГДЕ
	|				РегистрАналитикаВидаУчета.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчета.Затраты)
	|				//ДляРеглУчета И РегистрАналитикаВидаУчета.Организация = &Организация
	|		)
	|		И АналитикаУчетаЗатрат В (
	|			ВЫБРАТЬ
	|				РегистрАналитикаУчетаЗатрат.Ссылка
	|			ИЗ
	|				РегистрСведений.АналитикаУчетаЗатрат КАК РегистрАналитикаУчетаЗатрат
	|			ГДЕ
	|				РегистрАналитикаУчетаЗатрат.ХарактерЗатрат = ЗНАЧЕНИЕ(Перечисление.ХарактерЗатрат.ПроизводственныеРасходы)
	|		)
	|	) КАК УчетЗатрат
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаВидаУчета КАК РегистрАналитикаВидаУчета
	|	ПО
	|   	УчетЗатрат.АналитикаВидаУчета = РегистрАналитикаВидаУчета.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаЗатрат КАК РегистрАналитикаУчетаЗатрат
	|	ПО
	|   	УчетЗатрат.АналитикаУчетаЗатрат = РегистрАналитикаУчетаЗатрат.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			//ДляРеглУчета СчетЗатрат,
	|			Подразделение,
	|			СтатьяЗатрат,
	|			СтатьяЗатрат.СтатусМатериальныхЗатрат КАК СтатусМатериальныхЗатрат,
	|			Истина КАК Распределяемая
	|		ИЗ
	|			РаспределяемыеСтатьиЗатрат%СуффиксУчета%
	|		ГДЕ
	|			Не НеРаспределять
	|			И ХарактерРаспределенияЗатрат В (&МассивХарактеровРаспределения)
	|		) РаспределяемыеСтатьиЗатрат
	|	ПО 
	|		РегистрАналитикаУчетаЗатрат.СтатьяЗатрат = РаспределяемыеСтатьиЗатрат.СтатьяЗатрат
	|		//ДляУпрУчета И РегистрАналитикаВидаУчета.Подразделение = РаспределяемыеСтатьиЗатрат.Подразделение
	|		//ДляРеглУчета И РегистрАналитикаВидаУчета.ПодразделениеОрганизации = РаспределяемыеСтатьиЗатрат.Подразделение
	|		//ДляРеглУчета И (РегистрАналитикаВидаУчета.СчетУчета = РаспределяемыеСтатьиЗатрат.СчетЗатрат
	|		//ДляРеглУчета	ИЛИ РаспределяемыеСтатьиЗатрат.СтатусМатериальныхЗатрат =
	|		//ДляРеглУчета			ЗНАЧЕНИЕ(Перечисление.СтатусыМатериальныхЗатратНаПроизводство.ПринятыеВПереработку))
	|			
	|";

	Возврат ТекстЗапроса;
	
КонецФункции // СформироватьТекстЗапросаВременнаяТаблицаНезавершенноеПроизводствоРасширеннаяАналитика()

// Функция формирования запроса для базы распределения по незавершенному производству.
//
// Возвращаемое значение:
//   Строка – Текст запроса
//
Функция СформироватьТекстЗапросаВременнаяТаблицаНезавершенноеПроизводство()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	//ДляРеглУчета ЗатратыНаВыпуск.СчетУчета,
	|	//ДляБухУчета ЗатратыНаВыпуск.НалоговоеНазначение,
	|	ЗатратыНаВыпуск.Подразделение,
	|	ЗатратыНаВыпуск.НоменклатурнаяГруппаНЗП,
	|	ЗатратыНаВыпуск.СтатьяЗатрат,
	|	ЗатратыНаВыпуск.ЗаказНЗП,
	|
	|	МАКСИМУМ(ЗатратыНаВыпуск.Количество) 			КАК Количество,
	|	МАКСИМУМ(ЗатратыНаВыпускВсего.Количество) 		КАК КоличествоВсего,
	|	МАКСИМУМ(ЗатратыНаВыпуск.Сумма) 				КАК Сумма,
	|	МАКСИМУМ(ЗатратыНаВыпускВсего.Сумма) 			КАК СуммаВсего
	|
	|ПОМЕСТИТЬ ИсключаемыеЗатраты
	|ИЗ (
	|	ВЫБРАТЬ
	|	    //ДляРеглУчета ЗатратыНаВыпуск.СчетУчета,
	|		//ДляБухУчета ЗатратыНаВыпуск.НалоговоеНазначение,
	|		ЗатратыНаВыпуск.Подразделение,
	|		ЗатратыНаВыпуск.НоменклатурнаяГруппаНЗП,
	|		ЗатратыНаВыпуск.ЗаказНЗП,
	|		ЗатратыНаВыпуск.СтатьяЗатрат,
	|		СУММА(ЗатратыНаВыпуск.Количество) КАК Количество,
	|		СУММА(ЗатратыНаВыпуск.Сумма) КАК Сумма
	|	ИЗ
	|		РегистрНакопления.ЗатратыНаВыпускПродукции%СуффиксУчета% КАК ЗатратыНаВыпуск
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ (
	|			ВЫБРАТЬ
	|				ИсключаемаяНоменклатура.Номенклатура,
	|				ИсключаемаяНоменклатура.ХарактеристикаНоменклатуры,
	|				ИсключаемаяНоменклатура.СерияНоменклатуры,
	|				Истина КАК ЕстьИсключаемаяНоменклатура
	|			ИЗ
	|				РегистрСведений.НоменклатураИсключаемаяИзБазыРаспределения%СуффиксОрганизаций% КАК ИсключаемаяНоменклатура
	|			ГДЕ
	|				ИсключаемаяНоменклатура.Период МЕЖДУ &НачДата И &КонДата
	|				И &РаспределениеКосвенныхЗатрат
	|				//ДляРеглУчета И ИсключаемаяНоменклатура.Организация = &Организация
	|			) КАК ИсключаемаяНоменклатура
	|		ПО
	|			ЗатратыНаВыпуск.Продукция               	= ИсключаемаяНоменклатура.Номенклатура
	|			И ЗатратыНаВыпуск.ХарактеристикаПродукции 	= ИсключаемаяНоменклатура.ХарактеристикаНоменклатуры
	|			И ЗатратыНаВыпуск.СерияПродукции 			= ИсключаемаяНоменклатура.СерияНоменклатуры
	|
	|	ГДЕ
	|		ЗатратыНаВыпуск.Период МЕЖДУ &НачДата И &КонДата
	|		И &РаспределениеКосвенныхЗатрат
	|		И ЗатратыНаВыпуск.КодОперации В (&МассивКодыОперацийПрямыеЗатраты)
	|		И Не ИсключаемаяНоменклатура.ЕстьИсключаемаяНоменклатура ЕСТЬ NULL
	|		//ДляРеглУчета И ЗатратыНаВыпуск.Организация = &Организация
	|
	|	СГРУППИРОВАТЬ ПО
	|	    //ДляРеглУчета ЗатратыНаВыпуск.СчетУчета,
	|		//ДляБухУчета ЗатратыНаВыпуск.НалоговоеНазначение,
	|		ЗатратыНаВыпуск.Подразделение,
	|		ЗатратыНаВыпуск.НоменклатурнаяГруппаНЗП,
	|		ЗатратыНаВыпуск.ЗаказНЗП,
	|		ЗатратыНаВыпуск.СтатьяЗатрат
	|
	|	) КАК ЗатратыНаВыпуск
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ
	|		    //ДляРеглУчета ЗатратыНаВыпуск.СчетУчета,
	|			//ДляБухУчета  ЗатратыНаВыпуск.НалоговоеНазначение,
	|			ЗатратыНаВыпуск.Подразделение,
	|			ЗатратыНаВыпуск.НоменклатурнаяГруппаНЗП,
	|			ЗатратыНаВыпуск.ЗаказНЗП,
	|			ЗатратыНаВыпуск.СтатьяЗатрат,
	|			СУММА(ЗатратыНаВыпуск.Количество) КАК Количество,
	|			СУММА(ЗатратыНаВыпуск.Сумма) КАК Сумма
	|		ИЗ
	|			РегистрНакопления.ЗатратыНаВыпускПродукции%СуффиксУчета% КАК ЗатратыНаВыпуск
	|		ГДЕ
	|			ЗатратыНаВыпуск.Период МЕЖДУ &НачДата И &КонДата
	|			И &РаспределениеКосвенныхЗатрат
	|			И ЗатратыНаВыпуск.КодОперации В (&МассивКодыОперацийПрямыеЗатраты)
	|			//ДляРеглУчета И ЗатратыНаВыпуск.Организация = &Организация
	|			
	|		СГРУППИРОВАТЬ ПО
	|    		//ДляРеглУчета ЗатратыНаВыпуск.СчетУчета,
	|			//ДляБухУчета ЗатратыНаВыпуск.НалоговоеНазначение,
	|			ЗатратыНаВыпуск.Подразделение,
	|			ЗатратыНаВыпуск.НоменклатурнаяГруппаНЗП,
	|			ЗатратыНаВыпуск.ЗаказНЗП,
	|			ЗатратыНаВыпуск.СтатьяЗатрат
	|
	|		) КАК ЗатратыНаВыпускВсего
	|	ПО
	|		ЗатратыНаВыпуск.Подразделение = ЗатратыНаВыпускВсего.Подразделение
	|		И ЗатратыНаВыпуск.НоменклатурнаяГруппаНЗП = ЗатратыНаВыпускВсего.НоменклатурнаяГруппаНЗП
	|		И ЗатратыНаВыпуск.СтатьяЗатрат = ЗатратыНаВыпускВсего.СтатьяЗатрат
	|		И ЗатратыНаВыпуск.ЗаказНЗП = ЗатратыНаВыпускВсего.ЗаказНЗП
	|		//ДляРеглУчета И ЗатратыНаВыпуск.СчетУчета = ЗатратыНаВыпускВсего.СчетУчета
	|		//ДляБухУчета И ЗатратыНаВыпуск.НалоговоеНазначение = ЗатратыНаВыпускВсего.НалоговоеНазначение
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗатратыНаВыпуск.Подразделение,
	|	ЗатратыНаВыпуск.НоменклатурнаяГруппаНЗП,
	|	ЗатратыНаВыпуск.СтатьяЗатрат,
	|	//ДляРеглУчета ЗатратыНаВыпуск.СчетУчета,
	|	//ДляБухУчета ЗатратыНаВыпуск.НалоговоеНазначение,
	|	ЗатратыНаВыпуск.ЗаказНЗП
	|
	|ИМЕЮЩИЕ
	|	(МАКСИМУМ(ЗатратыНаВыпуск.Количество) >= МАКСИМУМ(ЗатратыНаВыпускВсего.Количество))
	|	ИЛИ 
	|	(МАКСИМУМ(ЗатратыНаВыпуск.Сумма) >= МАКСИМУМ(ЗатратыНаВыпускВсего.Сумма))
    |
	|ИНДЕКСИРОВАТЬ ПО
	|	//ДляРеглУчета ЗатратыНаВыпуск.СчетУчета,
	|	//ДляБухУчета ЗатратыНаВыпуск.НалоговоеНазначение,
	|	ЗатратыНаВыпуск.Подразделение,
	|	ЗатратыНаВыпуск.НоменклатурнаяГруппаНЗП,
	|	ЗатратыНаВыпуск.СтатьяЗатрат,
	|	ЗатратыНаВыпуск.ЗаказНЗП
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ
	|	//ДляРеглУчета НезавершенноеПроизводство.Организация,
	|	//ДляРеглУчета НезавершенноеПроизводство.СчетУчета,
	|	//ДляБухУчета  НезавершенноеПроизводство.НалоговоеНазначение, 
	|	НезавершенноеПроизводство.Подразделение,
	|	НезавершенноеПроизводство.НоменклатурнаяГруппа,
	|	НезавершенноеПроизводство.СтатьяЗатрат,
	|	НезавершенноеПроизводство.Заказ,
	|	
	|	НезавершенноеПроизводство.Затрата,
	|	НезавершенноеПроизводство.ХарактеристикаЗатраты,
	|	НезавершенноеПроизводство.СерияЗатраты,
	|	
	|	НезавершенноеПроизводство.КоличествоПриход,
	|	(
	|	ВЫБОР КОГДА НезавершенноеПроизводство.КоличествоОстаток <> 0 
	|		И Не &РаспределениеКосвенныхЗатрат
	|	ТОГДА
	|		НезавершенноеПроизводство.КоличествоПриход *
	|		НезавершенноеПроизводство.СтоимостьОстаток /
	|		НезавершенноеПроизводство.КоличествоОстаток
	|	ИНАЧЕ
	|		НезавершенноеПроизводство.СтоимостьПриход
	|	КОНЕЦ
	|	) КАК СтоимостьПриход,
	|	
	|	НезавершенноеПроизводство.КоличествоКонечныйОстаток,
	|	
	|	(
	|	ВЫБОР КОГДА НезавершенноеПроизводство.КоличествоОстаток <> 0 ТОГДА
	|		НезавершенноеПроизводство.КоличествоКонечныйОстаток *
	|		НезавершенноеПроизводство.СтоимостьОстаток /
	|		НезавершенноеПроизводство.КоличествоОстаток
	|	ИНАЧЕ
	|		НезавершенноеПроизводство.СтоимостьКонечныйОстаток
	|	КОНЕЦ
	|	) КАК СтоимостьКонечныйОстаток
	|
	|ПОМЕСТИТЬ НезавершенноеПроизводство%СуффиксУчета%
	|
	|ИЗ (
	|
	|	ВЫБРАТЬ
	|		//ДляРеглУчета НезавершенноеПроизводство.Организация,
	|		
	|		//ДляРеглУчета ВЫБОР КОГДА ЗатратыНаВыпуск.СтатусМатериальныхЗатрат =
	|		//ДляРеглУчета		ЗНАЧЕНИЕ(Перечисление.СтатусыМатериальныхЗатратНаПроизводство.ПринятыеВПереработку)
	|		//ДляРеглУчета ТОГДА
	|		//ДляРеглУчета 		ЗатратыНаВыпуск.СчетУчета
	|		//ДляРеглУчета ИНАЧЕ
	|		//ДляРеглУчета 		НезавершенноеПроизводство.СчетУчета
	|		//ДляРеглУчета КОНЕЦ КАК СчетУчета,
	|		//ДляБухУчета  НезавершенноеПроизводство.НалоговоеНазначение, 
	|		
	|		НезавершенноеПроизводство.Подразделение,
	|		НезавершенноеПроизводство.НоменклатурнаяГруппа,
	|		НезавершенноеПроизводство.СтатьяЗатрат,
	|		НезавершенноеПроизводство.Заказ,
	|		
	|		НезавершенноеПроизводство.Затрата,
	|		НезавершенноеПроизводство.ХарактеристикаЗатраты,
	|		НезавершенноеПроизводство.СерияЗатраты,
	|		
	|		ВЫБОР КОГДА Не ЕСТЬNULL(РаспределяемыеСтатьиЗатрат.Распределяемая, Ложь)
	|			ИЛИ ЕСТЬNULL(ЗатратыНаВыпуск.Количество, 0) <> 0
	|		ТОГДА
	|			НезавершенноеПроизводство.КоличествоПриход -
	|			ВЫБОР КОГДА ЗатратыНаВыпуск.КоличествоРасход ЕСТЬ NULL ТОГДА
	|				0
	|			КОГДА (ЗатратыНаВыпуск.КоличествоРасход -
	|				НезавершенноеПроизводство.КоличествоНачальныйОстаток) > 0
	|			ТОГДА
	|				ЗатратыНаВыпуск.КоличествоРасход - 
	|				НезавершенноеПроизводство.КоличествоНачальныйОстаток
	|			ИНАЧЕ
	|				0
	|			КОНЕЦ
	|		ИНАЧЕ
	|			0
	|		КОНЕЦ КАК КоличествоПриход,
	|
	|		ВЫБОР КОГДА Не ЕСТЬNULL(РаспределяемыеСтатьиЗатрат.Распределяемая, Ложь)
	|			ИЛИ ЕСТЬNULL(ЗатратыНаВыпуск.Сумма, 0) <> 0
	|		ТОГДА
	|			НезавершенноеПроизводство.СтоимостьПриход -
	|			ВЫБОР КОГДА ЗатратыНаВыпуск.СуммаРасход ЕСТЬ NULL ТОГДА
	|				0
	|			КОГДА (ЗатратыНаВыпуск.СуммаРасход -
	|				НезавершенноеПроизводство.СтоимостьНачальныйОстаток) > 0
	|			ТОГДА
	|				ЗатратыНаВыпуск.СуммаРасход - 
	|				НезавершенноеПроизводство.СтоимостьНачальныйОстаток
	|			ИНАЧЕ
	|				0
	|			КОНЕЦ
	|		ИНАЧЕ
	|			0
	|		КОНЕЦ КАК СтоимостьПриход,
	|		
	|		(
	|		ВЫБОР КОГДА (НезавершенноеПроизводство.КоличествоПриход <> 0
	|			ИЛИ НезавершенноеПроизводство.КоличествоРасход <> 0)
	|			И РаспределяемыеСтатьиЗатрат.Распределяемая ЕСТЬ NULL
	|		ТОГДА
	|			НезавершенноеПроизводство.КоличествоКонечныйОстаток -
	|			ВЫБОР КОГДА ЗатратыНаВыпуск.Количество ЕСТЬ NULL
	|				ИЛИ ЗатратыНаВыпуск.Количество > НезавершенноеПроизводство.КоличествоКонечныйОстаток
	|				ИЛИ (&РаспределениеКосвенныхЗатрат
	|					И Распределение.ЗатратаВстречногоВыпуска ЕСТЬ NULL)
	|			ТОГДА
	|				0
	|			ИНАЧЕ
	|				ЗатратыНаВыпуск.Количество
	|			КОНЕЦ
	|		ИНАЧЕ
	|			0
	|		КОНЕЦ
	|		) КАК КоличествоКонечныйОстаток,
	|		
	|		(
	|		ВЫБОР КОГДА (НезавершенноеПроизводство.СтоимостьПриход <> 0
	|			ИЛИ НезавершенноеПроизводство.СтоимостьРасход <> 0)
	|			И РаспределяемыеСтатьиЗатрат.Распределяемая ЕСТЬ NULL
	|		ТОГДА
	|			НезавершенноеПроизводство.СтоимостьКонечныйОстаток -
	|			ВЫБОР КОГДА ЗатратыНаВыпуск.Сумма ЕСТЬ NULL
	|				ИЛИ ЗатратыНаВыпуск.Сумма > НезавершенноеПроизводство.СтоимостьКонечныйОстаток
	|				ИЛИ (&РаспределениеКосвенныхЗатрат
	|					И Распределение.ЗатратаВстречногоВыпуска ЕСТЬ NULL)
	|			ТОГДА
	|				0
	|			ИНАЧЕ
	|				ЗатратыНаВыпуск.Сумма
	|			КОНЕЦ
	|		ИНАЧЕ
	|			0
	|		КОНЕЦ
	|		) КАК СтоимостьКонечныйОстаток,
	|		
	|		ЕСТЬNULL(НезавершенноеПроизводство.КоличествоКонечныйОстаток, 0) КАК КоличествоОстаток,
	|		ЕСТЬNULL(НезавершенноеПроизводство.СтоимостьКонечныйОстаток, 0) КАК СтоимостьОстаток
	|		
	|	ИЗ
	|		РегистрНакопления.НезавершенноеПроизводство%СуффиксУчета%.ОстаткиИОбороты(&НачГраница, &КонГраница, Период,,
	|			Не Затрата Ссылка Перечисление.ХарактерЗатрат
	|			И НЕ НеУчитыватьВРасчетеБазы
	|			//ДляРеглУчета И Организация = &Организация
	|		) КАК НезавершенноеПроизводство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ (
	|			ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				//ДляРеглУчета СчетЗатрат,
	|				Подразделение,
	|				СтатьяЗатрат,
	|				СтатьяЗатрат.СтатусМатериальныхЗатрат КАК СтатусМатериальныхЗатрат,
	|				Истина КАК Распределяемая
	|			ИЗ
	|				РаспределяемыеСтатьиЗатрат%СуффиксУчета%
	|			ГДЕ
	|				Не НеРаспределять
	|				И ХарактерРаспределенияЗатрат В (&МассивХарактеровРаспределения)
	|			) РаспределяемыеСтатьиЗатрат
	|		ПО 
	|			НезавершенноеПроизводство.Подразделение = РаспределяемыеСтатьиЗатрат.Подразделение
	|			И НезавершенноеПроизводство.СтатьяЗатрат = РаспределяемыеСтатьиЗатрат.СтатьяЗатрат
	|			//ДляРеглУчета И ((РаспределяемыеСтатьиЗатрат.СтатусМатериальныхЗатрат <> 
	|			//ДляРеглУчета			ЗНАЧЕНИЕ(Перечисление.СтатусыМатериальныхЗатратНаПроизводство.ПринятыеВПереработку)
	|			//ДляРеглУчета	И НезавершенноеПроизводство.СчетУчета = РаспределяемыеСтатьиЗатрат.СчетЗатрат)
	|			//ДляРеглУчета	ИЛИ РаспределяемыеСтатьиЗатрат.СтатусМатериальныхЗатрат =
	|			//ДляРеглУчета			ЗНАЧЕНИЕ(Перечисление.СтатусыМатериальныхЗатратНаПроизводство.ПринятыеВПереработку))
	|			
	|		ЛЕВОЕ СОЕДИНЕНИЕ (
	|			ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				Распределение.Подразделение,
	|				Распределение.СтатьяЗатрат,
	|				Распределение.Затрата,
	|				Распределение.ХарактеристикаЗатраты,
	|				Распределение.СерияЗатраты,
	|				Истина КАК ЗатратаВстречногоВыпуска
	|			ИЗ			
	|				РегистрСведений.РаспределениеЗатратПоПеределам%СуффиксОрганизаций% КАК Распределение
	|			ГДЕ
	|				Распределение.Период МЕЖДУ &НачДата И &КонДата
	|				И Распределение.ВстречныйВыпуск
	|				//ДляРеглУчета И Распределение.Организация = &Организация
	|			) КАК Распределение
	|		ПО
	|			НезавершенноеПроизводство.Подразделение = Распределение.Подразделение
	|			И НезавершенноеПроизводство.СтатьяЗатрат = Распределение.СтатьяЗатрат
	|			И НезавершенноеПроизводство.Затрата = Распределение.Затрата
	|			И НезавершенноеПроизводство.ХарактеристикаЗатраты = Распределение.ХарактеристикаЗатраты
	|			И НезавершенноеПроизводство.СерияЗатраты = Распределение.СерияЗатраты
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ (
	|			ВЫБРАТЬ
	|				//ДляРеглУчета ЗатратыНаВыпуск.СчетУчета,
	|				//ДляБухУчета  ЗатратыНаВыпуск.НалоговоеНазначение, 
	|				ЗатратыНаВыпуск.Подразделение,
	|				ЗатратыНаВыпуск.НоменклатурнаяГруппаНЗП,
	|				ЗатратыНаВыпуск.ЗаказНЗП,
	|				ЗатратыНаВыпуск.СтатьяЗатрат,
	|				ЗатратыНаВыпуск.СтатьяЗатрат.СтатусМатериальныхЗатрат КАК СтатусМатериальныхЗатрат,
	|				ЗатратыНаВыпуск.Затрата,
	|				ЗатратыНаВыпуск.ХарактеристикаЗатраты,
	|				ЗатратыНаВыпуск.СерияЗатраты,
	|
	|				СУММА(Количество) КАК Количество,
	|				СУММА(Сумма) КАК Сумма,
	|				СУММА(КоличествоРасход) КАК КоличествоРасход,
	|				СУММА(СуммаРасход) КАК СуммаРасход
	|			ИЗ
	|				ЗатратыНаВыпуск%СуффиксУчета% КАК ЗатратыНаВыпуск
	|
	|			СГРУППИРОВАТЬ ПО
	|			    //ДляРеглУчета ЗатратыНаВыпуск.СчетУчета,
	|				//ДляБухУчета  ЗатратыНаВыпуск.НалоговоеНазначение, 
	|				ЗатратыНаВыпуск.Подразделение,
	|				ЗатратыНаВыпуск.НоменклатурнаяГруппаНЗП,
	|				ЗатратыНаВыпуск.ЗаказНЗП,
	|				ЗатратыНаВыпуск.СтатьяЗатрат,
	|				ЗатратыНаВыпуск.Затрата,
	|				ЗатратыНаВыпуск.ХарактеристикаЗатраты,
	|				ЗатратыНаВыпуск.СерияЗатраты
	|
	|			) КАК ЗатратыНаВыпуск
	|
	|		ПО 
	|			НезавершенноеПроизводство.Подразделение = ЗатратыНаВыпуск.Подразделение
	|			И НезавершенноеПроизводство.НоменклатурнаяГруппа = ЗатратыНаВыпуск.НоменклатурнаяГруппаНЗП
	|			И НезавершенноеПроизводство.Заказ = ЗатратыНаВыпуск.ЗаказНЗП
	|			И НезавершенноеПроизводство.СтатьяЗатрат = ЗатратыНаВыпуск.СтатьяЗатрат
	|			И НезавершенноеПроизводство.Затрата = ЗатратыНаВыпуск.Затрата
	|			И НезавершенноеПроизводство.ХарактеристикаЗатраты = ЗатратыНаВыпуск.ХарактеристикаЗатраты
	|			И НезавершенноеПроизводство.СерияЗатраты = ЗатратыНаВыпуск.СерияЗатраты
	|			
	|			//ДляРеглУчета И ((ЗатратыНаВыпуск.СтатусМатериальныхЗатрат <>
	|			//ДляРеглУчета			ЗНАЧЕНИЕ(Перечисление.СтатусыМатериальныхЗатратНаПроизводство.ПринятыеВПереработку)
	|			//ДляРеглУчета	И НезавершенноеПроизводство.СчетУчета = ЗатратыНаВыпуск.СчетУчета)
	|			//ДляРеглУчета	ИЛИ ЗатратыНаВыпуск.СтатусМатериальныхЗатрат =
	|			//ДляРеглУчета			ЗНАЧЕНИЕ(Перечисление.СтатусыМатериальныхЗатратНаПроизводство.ПринятыеВПереработку))
	|			//ДляБухУчета	И НезавершенноеПроизводство.НалоговоеНазначение = ЗатратыНаВыпуск.НалоговоеНазначение 
	|			
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			ИсключаемыеЗатраты КАК ИсключаемыеЗатраты
	|		ПО
	|			НезавершенноеПроизводство.Подразделение = ИсключаемыеЗатраты.Подразделение
	|			И НезавершенноеПроизводство.НоменклатурнаяГруппа = ИсключаемыеЗатраты.НоменклатурнаяГруппаНЗП
	|			И НезавершенноеПроизводство.СтатьяЗатрат = ИсключаемыеЗатраты.СтатьяЗатрат
	|			И НезавершенноеПроизводство.Заказ = ИсключаемыеЗатраты.ЗаказНЗП
	|			//ДляРеглУчета И НезавершенноеПроизводство.СчетУчета = ИсключаемыеЗатраты.СчетУчета
	|			//ДляБухУчета  И НезавершенноеПроизводство.НалоговоеНазначение = ИсключаемыеЗатраты.НалоговоеНазначение 
	|	ГДЕ
	|		ИсключаемыеЗатраты.СтатьяЗатрат ЕСТЬ NULL
	|
	|	) КАК НезавершенноеПроизводство
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|
	|УНИЧТОЖИТЬ ИсключаемыеЗатраты
	|";

	Возврат ТекстЗапроса;
	
КонецФункции // СформироватьТекстЗапросаВременнаяТаблицаНезавершенноеПроизводство()

// Процедура устанавливает параметры запроса по незавершенному производству.
//
// Параметры
//  СтруктураШапкиДокумента – Структура - Реквизиты документа "Расчет себестоимости выпуска"
//	РаспределениеКосвенныхЗатрат - Булево - Признак расчета базы распределения косвенных затрат
//	ВидОтраженияВУчете - ПеречислениеСсылка.ВидыОтраженияВУчете - Вид отражения в учете
//	Запрос - Запрос.
//
Процедура УстановитьПараметрыЗапросаБазыРаспределенияПоНезавершенномуПроизводству(
	СтруктураШапкиДокумента,
	РаспределениеКосвенныхЗатрат,
	ВидОтраженияВУчете,
	Запрос
	)
	
	Запрос.УстановитьПараметр("НачДата", СтруктураШапкиДокумента.мНачДата);
	Запрос.УстановитьПараметр("КонДата", СтруктураШапкиДокумента.мКонДата);
	Запрос.УстановитьПараметр("НачГраница", СтруктураШапкиДокумента.мНачГраница);
	Запрос.УстановитьПараметр("КонГраница", СтруктураШапкиДокумента.мКонГраница);
	Запрос.УстановитьПараметр("Организация", СтруктураШапкиДокумента.Организация);
	Запрос.УстановитьПараметр("РаспределениеКосвенныхЗатрат", РаспределениеКосвенныхЗатрат);
	
	МассивКодыОперацийПрямыеЗатраты = Новый Массив;
	МассивКодыОперацийПрямыеЗатраты.Добавить(Перечисления.КодыОперацийЗатратыНаВыпускПродукции.ПрямыеЗатраты);
	МассивКодыОперацийПрямыеЗатраты.Добавить(Перечисления.КодыОперацийЗатратыНаВыпускПродукции.ОтрицательныеЗатраты);
	Запрос.УстановитьПараметр("МассивКодыОперацийПрямыеЗатраты", МассивКодыОперацийПрямыеЗатраты);
	
	МассивХарактеровРаспределения = Новый Массив;
	Если РаспределениеКосвенныхЗатрат
	   И Не СтруктураШапкиДокумента.ИспользоватьРасширеннуюАналитику
	Тогда
		МассивХарактеровРаспределения.Добавить(Перечисления.ХарактерРаспределенияЗатрат.НеУчитыватьПодразделение);
		МассивХарактеровРаспределения.Добавить(Перечисления.ХарактерРаспределенияЗатрат.УчитыватьПодразделение);
		МассивХарактеровРаспределения.Добавить(Перечисления.ХарактерРаспределенияЗатрат.ПустаяСсылка());
	Иначе
		МассивХарактеровРаспределения.Добавить(Перечисления.ХарактерРаспределенияЗатрат.НеУчитыватьПодразделение);
		МассивХарактеровРаспределения.Добавить(Перечисления.ХарактерРаспределенияЗатрат.УчитыватьПодразделение);
		МассивХарактеровРаспределения.Добавить(Перечисления.ХарактерРаспределенияЗатрат.ПустаяСсылка());
		МассивХарактеровРаспределения.Добавить(Перечисления.ХарактерРаспределенияЗатрат.ПроизводственныеРасходы);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("МассивХарактеровРаспределения", МассивХарактеровРаспределения);
	
	
КонецПроцедуры // УстановитьПараметрыЗапросаБазыРаспределенияПоНезавершенномуПроизводству()

// Процедура формирует временную таблицу "Незавершенное производство".
//
// Параметры:
//  СтруктураШапкиДокумента – Структура - Реквизиты документа "Расчет себестоимости выпуска"
//  СтруктураПараметров – Структура - Структура параметров способа распределения
//	РаспределениеКосвенныхЗатрат - Булево - Признак расчета базы распределения косвенных затрат
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Менеджер временных таблиц
//
Процедура СформироватьВременнуюТаблицуНезавершенноеПроизводство(
	СтруктураШапкиДокумента,
	СтруктураПараметров,
	РаспределениеКосвенныхЗатрат,
	МенеджерВременныхТаблиц
	)
	
	Если СтруктураШапкиДокумента.ИспользоватьРасширеннуюАналитику Тогда
		ТекстЗапросаСКомментариями = СформироватьТекстЗапросаВременнаяТаблицаНезавершенноеПроизводствоРасширеннаяАналитика();
	Иначе
		ТекстЗапросаСКомментариями = СформироватьТекстЗапросаВременнаяТаблицаНезавершенноеПроизводство();
	КонецЕсли;
	
	// Цикл по всем видам отражения в учете, для которых рассчитывается база распределения.
	Для Каждого ВидОтраженияВУчете Из СтруктураПараметров.МассивВидовУчета Цикл
		
		ТекстЗапроса = УправлениеЗатратами.ЗаменитьКомментарииВТекстеЗапроса(
			ТекстЗапросаСКомментариями, 
			ВидОтраженияВУчете
		);
	
		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапроса;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		
		УстановитьПараметрыЗапросаБазыРаспределенияПоНезавершенномуПроизводству(
			СтруктураШапкиДокумента,
			РаспределениеКосвенныхЗатрат,
			ВидОтраженияВУчете,
			Запрос
		);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		СуффиксИмениРегистра = СтруктураПараметров.СуффиксыИмениРегистра[ВидОтраженияВУчете];
		ИмяВременнойТаблицы = "НезавершенноеПроизводство" + СуффиксИмениРегистра;
		
		// Вызывается процедура формирования запроса по временной таблице. Используется при отладке алгоритмов. 
		// Для просмотра временной таблицы в структуре шапки документа поле "ОтладочныйРежим" должно иметь значение "Истина".
		РасширеннаяАналитикаУчета.ПоказатьВременнуюТаблицу(
			СтруктураШапкиДокумента,
			МенеджерВременныхТаблиц,
			ИмяВременнойТаблицы
		);
		
	КонецЦикла;
	
КонецПроцедуры // СформироватьВременнуюТаблицуНезавершенноеПроизводство()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ПОДГОТОВКИ ВРЕМЕННОЙ ТАБЛИЦЫ ПО ЦЕНАМ НОМЕНКЛАТУРЫ

// Функция формирования запроса для базы распределения по ценам номенклатуры.
//
// Возвращаемое значение:
//   Строка – Текст запроса
//
Функция СформироватьТекстЗапросаБазыРаспределенияПоЦенамНоменклатуры()
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Номенклатура,
	|	ХарактеристикаНоменклатуры,
	|	ТипЦен,
	|	(
	|	ЦеныНоменклатуры.Цена * 
	|	ЕСТЬNULL(КурсыВалют.Курс, 0) *
	|	ВЫБОР КОГДА ЦеныНоменклатуры.ЕдиницаИзмерения.Коэффициент ЕСТЬ NULL 
	|	  ИЛИ ЦеныНоменклатуры.ЕдиницаИзмерения.Коэффициент = 0 
	|	ТОГДА
	|		1
	|	ИНАЧЕ
	|		ЦеныНоменклатуры.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент / 
	|		ЦеныНоменклатуры.ЕдиницаИзмерения.Коэффициент
	|	КОНЕЦ *
	|	ЕСТЬNULL(КурсВалютыУчета.Кратность, 1)
	|	/ 
	|	(
	|	ЕСТЬNULL(КурсВалютыУчета.Курс, 1) *
	|	ВЫБОР КОГДА КурсыВалют.Кратность ЕСТЬ NULL 
	|	  ИЛИ КурсыВалют.Кратность = 0 
	|	Тогда
	|		1
	|	ИНАЧЕ 
	|		КурсыВалют.Кратность
	|	КОНЕЦ
	|	)
	|	) КАК Цена
	|
	|ПОМЕСТИТЬ ЦеныНоменклатуры%СуффиксУчета%
	|ИЗ
	|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&КонДата, 
	|		ТипЦен В (
	|			ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				СпособыРаспределения.ТипЦен
	|			ИЗ
	|				Справочник.СпособыРаспределенияЗатратНаВыпуск КАК СпособыРаспределения
	|			ГДЕ
	|				СпособыРаспределения.ПоказательБазыРаспределения = ЗНАЧЕНИЕ(Перечисление.ВидыПоказателейБазыРаспределения.ЦеныНоменклатуры)
	|				И СпособыРаспределения.ТипЦен <> ЗНАЧЕНИЕ(Справочник.ТипыЦенНоменклатуры.ПустаяСсылка)
	|			)
	|		И Номенклатура В (
	|			ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				Продукция 
	|			ИЗ
	|				ВыпускПродукции%СуффиксУчета%
	|				
	|			ОБЪЕДИНИТЬ ВСЕ
	|			
	|			ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				ОсновноеСырьеСостав.Номенклатура КАК Номенклатура
	|			ИЗ
	|				Справочник.ОсновноеСырье.Состав КАК ОсновноеСырьеСостав
	|			)
	|
	|		) КАК ЦеныНоменклатуры
	|		
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.КурсыВалют.СрезПоследних(&КонДата) КАК КурсыВалют
	|	ПО 
	|		КурсыВалют.Валюта = ЦеныНоменклатуры.Валюта
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.КурсыВалют.СрезПоследних(&КонДата, Валюта = &ВалютаУчета) КАК КурсВалютыУчета
	|	ПО ИСТИНА
	|";

	Возврат ТекстЗапроса;
	
КонецФункции // СформироватьТекстЗапросаБазыРаспределенияПоЦенамНоменклатуры()

// Процедура формирует временную таблицу "Цены номенклатуры".
//
// Параметры:
//  СтруктураШапкиДокумента – Структура – Реквизиты документа
//  СтруктураПараметров – Структура - Структура параметров способа распределения
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Менеджер временных таблиц
//
Процедура СформироватьВременнуюТаблицуЦеныНоменклатуры(
	СтруктураШапкиДокумента,
	СтруктураПараметров,
	МенеджерВременныхТаблиц
	)
	
	ТекстЗапросаСКомментариями = СформироватьТекстЗапросаБазыРаспределенияПоЦенамНоменклатуры();
	
	// Цикл по всем видам отражения в учете, для которых рассчитывается база распределения.
	Для Каждого ВидОтраженияВУчете Из СтруктураПараметров.МассивВидовУчета Цикл
		
		ТекстЗапроса = УправлениеЗатратами.ЗаменитьКомментарииВТекстеЗапроса(
			ТекстЗапросаСКомментариями, 
			ВидОтраженияВУчете
		);
		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапроса;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		
		Запрос.УстановитьПараметр("КонДата", СтруктураПараметров.КонДата);
		Запрос.УстановитьПараметр("ВалютаУчета", СтруктураПараметров.ВалютаУчета);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		СуффиксИмениРегистра = СтруктураПараметров.СуффиксыИмениРегистра[ВидОтраженияВУчете];
		ИмяВременнойТаблицы = "ЦеныНоменклатуры" + СуффиксИмениРегистра;
		
		// Вызывается процедура формирования запроса по временной таблице. Используется при отладке алгоритмов. 
		// Для просмотра временной таблицы в структуре шапки документа поле "ОтладочныйРежим" должно иметь значение "Истина".
		РасширеннаяАналитикаУчета.ПоказатьВременнуюТаблицу(
			СтруктураШапкиДокумента,
			МенеджерВременныхТаблиц,
			ИмяВременнойТаблицы
		);
		
	КонецЦикла;
	
КонецПроцедуры // СформироватьВременнуюТаблицуЦеныНоменклатуры()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ПОДГОТОВКИ ВРЕМЕННОЙ ТАБЛИЦЫ ПО ПРОДАЖАМ

// Функция формирования запроса для базы распределения по выручке от продаж.
//
// Возвращаемое значение:
//   Строка – Текст запроса
//
Функция СформироватьТекстЗапросаВременнаяТаблицаПродажиУпр()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ВЫБОР КОГДА Продажи.Номенклатура.Услуга ТОГДА
	|		Продажи.Подразделение
	|	ИНАЧЕ
	|		Неопределено
	|	КОНЕЦ КАК Подразделение,
	|	Продажи.Номенклатура КАК Продукция,
	|	Продажи.ХарактеристикаНоменклатуры КАК ХарактеристикаПродукции,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) КАК СерияПродукции,
	|	Продажи.ЗаказПокупателя КАК Заказ,
	|	Продажи.КоличествоОборот КАК Количество,
	|	(Продажи.СтоимостьОборот - Продажи.НДСОборот) КАК Стоимость
	|
	|ПОМЕСТИТЬ Продажи%СуффиксУчета%
	|ИЗ
	|	РегистрНакопления.Продажи.Обороты(&НачГраница, &КонГраница, Период,
	|		(Номенклатура,
	|		ХарактеристикаНоменклатуры
	|		) В (
	|			ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				Продукция,
	|				ХарактеристикаПродукции
	|			ИЗ
	|				ВыпускПродукции%СуффиксУчета%
	|			)
	|	) КАК Продажи
	|	
	|ГДЕ
	|	Продажи.КоличествоОборот <> 0	
	|";

	Возврат ТекстЗапроса;
	
КонецФункции // СформироватьТекстЗапросаВременнаяТаблицаПродажиУпр()

// Функция формирования запроса для базы распределения по выручке от продаж.
//
// Возвращаемое значение:
//   Строка – Текст запроса
//
Функция СформироватьТекстЗапросаВременнаяТаблицаПродажиРегл()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ПродажиУслуги.ПодразделениеОрганизации КАК Подразделение,
	|	ПродажиУслуги.Номенклатура КАК Продукция,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК ХарактеристикаПродукции,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) КАК СерияПродукции,
	|	ПродажиУслуги.Заказ,
	|	ПродажиУслуги.КоличествоОборот КАК Количество,
	|	(ПродажиУслуги.СтоимостьОборот - ПродажиУслуги.НДСОборот) КАК Стоимость
	|	
	|ПОМЕСТИТЬ Продажи%СуффиксУчета%
	|
	|ИЗ
	|	РегистрНакопления.ПродажиУслугиРегл.Обороты(&НачГраница, &КонГраница) КАК ПродажиУслуги
	|	
	|ГДЕ
	|	ПродажиУслуги.КоличествоОборот <> 0	
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ
	|	Неопределено КАК Подразделение,
	|	СписанныеТовары.Номенклатура,
	|	СписанныеТовары.ХарактеристикаНоменклатуры,
	|	СписанныеТовары.СерияНоменклатуры,
	|	СписанныеТовары.ЗаказСписания,
	|	СУММА(СписанныеТовары.Количество) КАК Количество,
	|	СУММА(СписанныеТовары.ПроводкиСуммаСНДСРегл - СписанныеТовары.ПроводкиСуммаНДСРегл) КАК Стоимость
	|ИЗ
	|	РегистрСведений.СписанныеТовары КАК СписанныеТовары	
	|ГДЕ
	|	СписанныеТовары.Период МЕЖДУ &НачДата И &КонДата
	|	И СписанныеТовары.ПроводкиСуммаСНДСРегл <> 0
	|	И СписанныеТовары.ОтражатьВБухгалтерскомУчете
	|	И СписанныеТовары.Организация = &Организация
	|	И (	СписанныеТовары.Номенклатура,
	|		СписанныеТовары.ХарактеристикаНоменклатуры,
	|		СписанныеТовары.СерияНоменклатуры
	|		) В (
	|			ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				Продукция,
	|				ХарактеристикаПродукции,
	|				СерияПродукции
	|			ИЗ
	|				ВыпускПродукции%СуффиксУчета%
	|			)
	|	
	|СГРУППИРОВАТЬ ПО
	|	СписанныеТовары.Номенклатура,
	|	СписанныеТовары.ХарактеристикаНоменклатуры,
	|	СписанныеТовары.СерияНоменклатуры,
	|	СписанныеТовары.ЗаказСписания
	|	
	|ИМЕЮЩИЕ
	|	СУММА(СписанныеТовары.Количество) <> 0	
	|
	|";

	Возврат ТекстЗапроса;
	
КонецФункции // СформироватьТекстЗапросаВременнаяТаблицаПродажиРегл()

// Функция формирования запроса для базы распределения по выручке от продаж.
//
// Возвращаемое значение:
//   Строка – Текст запроса
//
Функция СформироватьТекстЗапросаВременнаяТаблицаПродажиРеглРасширеннаяАналитика()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	АналитикаВидаУчета.Ссылка,
	|	АналитикаВидаУчета.ПодразделениеОрганизации КАК Подразделение
	|ПОМЕСТИТЬ АналитикаВидаУчета_УчетПродажИСебестоимости
	|ИЗ
	|	РегистрСведений.АналитикаВидаУчета КАК АналитикаВидаУчета
	|ГДЕ
	|	АналитикаВидаУчета.Организация = &Организация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	АналитикаУчетаПрочихЗатрат.Ссылка,
	|	НЕОПРЕДЕЛЕНО
	|ИЗ
	|	РегистрСведений.АналитикаУчетаПрочихЗатрат КАК АналитикаУчетаПрочихЗатрат
	|ГДЕ
	|	АналитикаУчетаПрочихЗатрат.Организация = &Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РегистрАналитикаРаспределенияЗатрат.Ссылка,
	|	РегистрАналитикаРаспределенияЗатрат.Продукция,
	|	РегистрАналитикаРаспределенияЗатрат.ХарактеристикаПродукции,
	|	РегистрАналитикаРаспределенияЗатрат.СерияПродукции
	|ПОМЕСТИТЬ АналитикаРаспределенияЗатрат_УчетПродажИСебестоимости
	|ИЗ
	|	РегистрСведений.АналитикаРаспределенияЗатрат КАК РегистрАналитикаРаспределенияЗатрат
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВыпускПродукции%СуффиксУчета% КАК ВыпускПродукции
	|		ПО РегистрАналитикаРаспределенияЗатрат.Продукция = ВыпускПродукции.Продукция
	|			И РегистрАналитикаРаспределенияЗатрат.ХарактеристикаПродукции = ВыпускПродукции.ХарактеристикаПродукции
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА РегистрАналитикаВидаУчета.Подразделение = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ РегистрАналитикаВидаУчета.Подразделение
	|	КОНЕЦ КАК Подразделение,
	|	РегистрАналитикаРаспределенияЗатрат.Продукция КАК Продукция,
	|	РегистрАналитикаРаспределенияЗатрат.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
	|	РегистрАналитикаРаспределенияЗатрат.СерияПродукции КАК СерияПродукции,
	|	ВЫБОР
	|		КОГДА УчетПродажИСебестоимости.Регистратор ССЫЛКА Документ.АктОбОказанииПроизводственныхУслуг
	|				И УчетПродажИСебестоимости.Регистратор.ИспользоватьЗаказы = ИСТИНА
	|			ТОГДА УчетПродажИСебестоимости.Регистратор.Сделка
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	КОНЕЦ КАК Заказ,
	|	СУММА(УчетПродажИСебестоимости.Количество) КАК Количество,
	|	СУММА(УчетПродажИСебестоимости.Стоимость - УчетПродажИСебестоимости.НДС) КАК Стоимость
	|ПОМЕСТИТЬ Продажи%СуффиксУчета%
	|ИЗ
	|	РегистрНакопления.УчетПродажИСебестоимости КАК УчетПродажИСебестоимости
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаВидаУчета_УчетПродажИСебестоимости КАК РегистрАналитикаВидаУчета
	|		ПО УчетПродажИСебестоимости.АналитикаВидаУчета = РегистрАналитикаВидаУчета.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаРаспределенияЗатрат_УчетПродажИСебестоимости КАК РегистрАналитикаРаспределенияЗатрат
	|		ПО УчетПродажИСебестоимости.АналитикаУчетаНоменклатуры = РегистрАналитикаРаспределенияЗатрат.Ссылка
	|ГДЕ
	|	УчетПродажИСебестоимости.Активность
	|	И УчетПродажИСебестоимости.Период МЕЖДУ &НачДата И &КонДата
	|
	|СГРУППИРОВАТЬ ПО
	|	РегистрАналитикаВидаУчета.Подразделение,
	|	ВЫБОР
	|		КОГДА УчетПродажИСебестоимости.Регистратор ССЫЛКА Документ.АктОбОказанииПроизводственныхУслуг
	|				И УчетПродажИСебестоимости.Регистратор.ИспользоватьЗаказы = ИСТИНА
	|			ТОГДА УчетПродажИСебестоимости.Регистратор.Сделка
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	КОНЕЦ,
	|	РегистрАналитикаРаспределенияЗатрат.Продукция,
	|	РегистрАналитикаРаспределенияЗатрат.ХарактеристикаПродукции,
	|	РегистрАналитикаРаспределенияЗатрат.СерияПродукции
	|
	|ИМЕЮЩИЕ
	|	СУММА(УчетПродажИСебестоимости.Количество) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ АналитикаВидаУчета_УчетПродажИСебестоимости
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ АналитикаРаспределенияЗатрат_УчетПродажИСебестоимости";
	
	Возврат ТекстЗапроса;
	
КонецФункции // СформироватьТекстЗапросаВременнаяТаблицаПродажиРегл()

// Процедура формирует временную таблицу "Продажи".
//
// Параметры:
//  СтруктураШапкиДокумента – Структура – Реквизиты документа
//  СтруктураПараметров – Структура - Структура параметров способа распределения
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Менеджер временных таблиц
//
Процедура СформироватьВременнуюТаблицуПродажи(
	СтруктураШапкиДокумента,
	СтруктураПараметров,
	МенеджерВременныхТаблиц
	)
	
	ТекстЗапросаСКомментариямиУпр = СформироватьТекстЗапросаВременнаяТаблицаПродажиУпр();
	
	Если СтруктураПараметров.ИспользоватьРасширеннуюАналитику Тогда
		ТекстЗапросаСКомментариямиРегл = СформироватьТекстЗапросаВременнаяТаблицаПродажиРеглРасширеннаяАналитика();
	Иначе
		ТекстЗапросаСКомментариямиРегл = СформироватьТекстЗапросаВременнаяТаблицаПродажиРегл();
	КонецЕсли;
	
	// Цикл по всем видам отражения в учете, для которых рассчитывается база распределения.
	Для Каждого ВидОтраженияВУчете Из СтруктураПараметров.МассивВидовУчета Цикл
		
		Если ВидОтраженияВУчете = Перечисления.ВидыОтраженияВУчете.ОтражатьВУправленческомУчете Тогда
			ТекстЗапроса = УправлениеЗатратами.ЗаменитьКомментарииВТекстеЗапроса(
				ТекстЗапросаСКомментариямиУпр,
				ВидОтраженияВУчете
			);
		Иначе
			ТекстЗапроса = УправлениеЗатратами.ЗаменитьКомментарииВТекстеЗапроса(
				ТекстЗапросаСКомментариямиРегл,
				ВидОтраженияВУчете
			);
		КонецЕсли;
			
		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапроса;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		
		Запрос.УстановитьПараметр("НачДата", СтруктураПараметров.НачДата);
		Запрос.УстановитьПараметр("КонДата", СтруктураПараметров.КонДата);
		Запрос.УстановитьПараметр("НачГраница", СтруктураПараметров.НачГраница);
		Запрос.УстановитьПараметр("КонГраница", СтруктураПараметров.КонГраница);
		Запрос.УстановитьПараметр("Организация", СтруктураПараметров.Организация);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		СуффиксИмениРегистра = СтруктураПараметров.СуффиксыИмениРегистра[ВидОтраженияВУчете];
		ИмяВременнойТаблицы = "Продажи" + СуффиксИмениРегистра;
		
		// Вызывается процедура формирования запроса по временной таблице. Используется при отладке алгоритмов. 
		// Для просмотра временной таблицы в структуре шапки документа поле "ОтладочныйРежим" должно иметь значение "Истина".
		РасширеннаяАналитикаУчета.ПоказатьВременнуюТаблицу(
			СтруктураШапкиДокумента,
			МенеджерВременныхТаблиц,
			ИмяВременнойТаблицы
		);
		
	КонецЦикла;
	
КонецПроцедуры // СформироватьВременнуюТаблицуПродажи()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ПОДГОТОВКИ ВРЕМЕННОЙ ТАБЛИЦЫ ПО РАСПРЕДЕЛЯЕМЫМ СТАТЬЯМ ЗАТРАТА

// Процедура формирует временную таблицу "Распределяемые статьи затрат".
//
// Параметры:
//  СтруктураШапкиДокумента – Структура – Реквизиты документа "Расчет себестоимости выпуска"
//  СтруктураПараметров – Структура - Структура параметров способа распределения
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Менеджер временных таблиц
//
Процедура СформироватьВременнуюТаблицуРаспределяемыеСтатьиЗатрат(
	СтруктураШапкиДокумента,
	СтруктураПараметров,
	МенеджерВременныхТаблиц
	)
	
	МассивХарактеровРаспределения = Новый Массив;
	МассивХарактеровРаспределения.Добавить(Перечисления.ХарактерРаспределенияЗатрат.НеУчитыватьПодразделение);
	МассивХарактеровРаспределения.Добавить(Перечисления.ХарактерРаспределенияЗатрат.УчитыватьПодразделение);
	МассивХарактеровРаспределения.Добавить(Перечисления.ХарактерРаспределенияЗатрат.ПроизводственныеРасходы);
	МассивХарактеровРаспределения.Добавить(Перечисления.ХарактерРаспределенияЗатрат.ПустаяСсылка());
	
	// Цикл по всем видам отражения в учете, для которых рассчитывается база распределения.
	Для Каждого ВидОтраженияВУчете Из СтруктураПараметров.МассивВидовУчета Цикл
		
		// Формируется запрос по данным регистров сведений "Способы распределения статей затрат"
		//	и "Способы распределения статей затрат организаций".
		Запрос = ПроцедурыРасчетаСебестоимостиВыпуска.СформироватьЗапросПоСпособамРаспределенияЗатрат(
			СтруктураШапкиДокумента,
			МассивХарактеровРаспределения,
			Истина, // ФормироватьВременнуюТаблицу
			ВидОтраженияВУчете,
			МенеджерВременныхТаблиц
		);
	
		РезультатЗапроса = Запрос.Выполнить();
		
		СуффиксИмениРегистра = СтруктураПараметров.СуффиксыИмениРегистра[ВидОтраженияВУчете];
		ИмяВременнойТаблицы = "РаспределяемыеСтатьиЗатрат" + СуффиксИмениРегистра;
		
		// Вызывается процедура формирования запроса по временной таблице. Используется при отладке алгоритмов. 
		// Для просмотра временной таблицы в структуре шапки документа поле "ОтладочныйРежим" должно иметь значение "Истина".
		РасширеннаяАналитикаУчета.ПоказатьВременнуюТаблицу(
			СтруктураШапкиДокумента,
			МенеджерВременныхТаблиц,
			ИмяВременнойТаблицы
		); 
		
	КонецЦикла;
	
КонецПроцедуры // СформироватьВременнуюТаблицуРаспределяемыеСтатьиЗатрат()

////////////////////////////////////////////////////////////////////////////////
// ФУНКЦИИ ФОРМИРОВАНИЯ ТЕКСТОВ ЗАПРОСА ДЛЯ РАСЧЕТА БАЗЫ РАСПРЕДЕЛЕНИЯ (РАСШИРЕННАЯ АНАЛИТИКА)

// Функция формирования запроса для базы распределения по стоимости затрат.
//
// Возвращаемое значение:
//   Строка – Текст запроса
//
Функция СформироватьТекстЗапросаБазаПоСтоимостиЗатратРасширеннаяАналитика()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&СпособРаспределенияЗатрат КАК РассчитанныйСпособРаспределения,
	|
	|	ЗатратыНаВыпуск.АналитикаВидаУчета,
	|	ЗатратыНаВыпуск.АналитикаУчетаПартий,
	|	ЗатратыНаВыпуск.АналитикаРаспределенияЗатрат,
	|	
	|	//ДляУпрУчета ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
	|	//ДляРеглУчета РегистрАналитикаВидаУчета.Организация,
	|
	|	//ДляУпрУчета ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетУчета,
	|	//ДляБухУчета РегистрАналитикаВидаУчета.СчетУчета,
	|	//ДляУпрУчета ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначение,
	|	//ДляБухУчета РегистрАналитикаВидаУчета.НалоговоеНазначение КАК НалоговоеНазначение,
	|
	|	//ДляУпрУчета РегистрАналитикаВидаУчета.Подразделение,
	|	//ДляРеглУчета РегистрАналитикаВидаУчета.ПодразделениеОрганизации КАК Подразделение,
	|	
	|	РегистрАналитикаРаспределенияЗатрат.НоменклатурнаяГруппа,
	|	
	|	РегистрАналитикаУчетаПартий.Заказ,
	|
	|	//ДляУпрУчета РегистрКорАналитикаВидаУчета.Подразделение КАК ПодразделениеНЗП,
	|	//ДляРеглУчета РегистрКорАналитикаВидаУчета.ПодразделениеОрганизации КАК ПодразделениеНЗП,
	|	РегистрКорАналитикаРаспределенияЗатрат.НоменклатурнаяГруппа КАК НоменклатурнаяГруппаНЗП,
	|	РегистрКорАналитикаУчетаПартий.Заказ КАК ЗаказНЗП,
	|
	|	РегистрАналитикаРаспределенияЗатрат.Продукция,
	|	РегистрАналитикаРаспределенияЗатрат.ХарактеристикаПродукции,
	|	РегистрАналитикаРаспределенияЗатрат.СерияПродукции,
	|	РегистрАналитикаРаспределенияЗатрат.Спецификация,
	|
	|	Неопределено КАК ВариантВыпускаПродукции,
	|	ВЫБОР КОГДА РегистрАналитикаВидаУчета.РазделУчета 
	|		= ЗНАЧЕНИЕ(Перечисление.РазделыУчета.ЗатратыПоНаработке)
	|	ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ВидыВыпуска.Наработка)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ВидыВыпуска.Выпуск)
	|	КОНЕЦ КАК ВидВыпуска,
	|
	|		
	|	СУММА(
	|		ВЫБОР КОГДА НезавершенноеПроизводство.КоличествоОстаток <> 0 ТОГДА
	|			ЗатратыНаВыпуск.Количество *
	|			(ЕСТЬNULL(НезавершенноеПроизводство.СтоимостьОстаток, 0) 
	|			) /
	|			ЕСТЬNULL(НезавершенноеПроизводство.КоличествоОстаток, 1)
	|		ИНАЧЕ
	|			ЗатратыНаВыпуск.Стоимость
	|		КОНЕЦ
	|	) КАК База,
	|	0 КАК БазаПриход,
	|	0 КАК БазаОстатокНЗП
	|
	|ПОМЕСТИТЬ БазаРаспределения%СуффиксУчета%
	|ИЗ
	|	РегистрНакопления.УчетЗатратРегл КАК ЗатратыНаВыпуск // заменяется на временную таблицу ЗатратыНаВыпуск%СуффиксУчета%
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.УчетЗатратРегл.Остатки(, ) КАК НезавершенноеПроизводство // заменяется на временную таблицу НезавершенноеПроизводство%СуффиксУчета%
	|	ПО
	|		ЗатратыНаВыпуск.КорАналитикаВидаУчета = НезавершенноеПроизводство.АналитикаВидаУчета
	|		И ЗатратыНаВыпуск.КорАналитикаУчетаЗатрат = НезавершенноеПроизводство.АналитикаУчетаЗатрат
	|		И ЗатратыНаВыпуск.КорАналитикаУчетаПартий = НезавершенноеПроизводство.АналитикаУчетаПартий
	|		И ЗатратыНаВыпуск.КорАналитикаРаспределенияЗатрат = НезавершенноеПроизводство.АналитикаРаспределенияЗатрат
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ
	|			*
	|		ИЗ
	|			РегистрСведений.АналитикаВидаУчета КАК РегистрАналитикаВидаУчета
	|		ГДЕ
	|			(РегистрАналитикаВидаУчета.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчета.ЗатратыНаВыпуск)
	|			ИЛИ РегистрАналитикаВидаУчета.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчета.ЗатратыПоНаработке))
	|			//ДляРеглУчета И РегистрАналитикаВидаУчета.Организация = &Организация
	|		{ГДЕ
	|			//ДляУпрУчета Подразделение.*
	|			//ДляРеглУчета ПодразделениеОрганизации.*
	|		}
	|		) КАК РегистрАналитикаВидаУчета
	|	ПО
	|		ЗатратыНаВыпуск.АналитикаВидаУчета = РегистрАналитикаВидаУчета.Ссылка
	|		
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ
	|			*
	|		ИЗ
	|			РегистрСведений.АналитикаУчетаЗатрат КАК РегистрАналитикаУчетаЗатрат
	|		{ГДЕ
	|			СтатьяЗатрат.* КАК СтатьяЗатрат,
	|			СтатьяЗатрат.ВидЗатрат.* КАК ВидЗатрат,
	|			СтатьяЗатрат.СтатусМатериальныхЗатрат.* КАК СтатусМатериальныхЗатрат,
	|			СтатьяЗатрат.ХарактерЗатрат.* КАК ХарактерЗатрат
	|		}
	|		) КАК РегистрАналитикаУчетаЗатрат
	|	ПО
	|		ЗатратыНаВыпуск.АналитикаУчетаЗатрат = РегистрАналитикаУчетаЗатрат.Ссылка
	|		
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ
	|			*
	|		ИЗ
	|			РегистрСведений.АналитикаРаспределенияЗатрат КАК РегистрАналитикаРаспределенияЗатрат
	|		{ГДЕ
	|			НоменклатурнаяГруппа.* КАК НоменклатурнаяГруппа,
	|			Продукция.* КАК Продукция
	|		}
	|		) КАК РегистрАналитикаРаспределенияЗатрат
	|	ПО
	|		ЗатратыНаВыпуск.АналитикаРаспределенияЗатрат = РегистрАналитикаРаспределенияЗатрат.Ссылка
	|		
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПартий КАК РегистрАналитикаУчетаПартий
	|	ПО
	|		ЗатратыНаВыпуск.АналитикаУчетаПартий = РегистрАналитикаУчетаПартий.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаВидаУчета КАК РегистрКорАналитикаВидаУчета
	|	ПО
	|		ЗатратыНаВыпуск.КорАналитикаВидаУчета = РегистрКорАналитикаВидаУчета.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаРаспределенияЗатрат КАК РегистрКорАналитикаРаспределенияЗатрат
	|	ПО
	|		ЗатратыНаВыпуск.КорАналитикаРаспределенияЗатрат = РегистрКорАналитикаРаспределенияЗатрат.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПартий КАК РегистрКорАналитикаУчетаПартий
	|	ПО
	|		ЗатратыНаВыпуск.КорАналитикаУчетаПартий = РегистрКорАналитикаУчетаПартий.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗатратыНаВыпуск.АналитикаВидаУчета,
	|	ЗатратыНаВыпуск.АналитикаУчетаПартий,
	|	ЗатратыНаВыпуск.АналитикаРаспределенияЗатрат,
	|	
	|	РегистрАналитикаВидаУчета.РазделУчета,
	|	
	|	//ДляУпрУчета ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка),
	|	//ДляРеглУчета РегистрАналитикаВидаУчета.Организация,
	|
	|	//ДляУпрУчета ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка),
	|	//ДляБухУчета РегистрАналитикаВидаУчета.СчетУчета,
	|	//ДляУпрУчета ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка),
	|	//ДляБухУчета РегистрАналитикаВидаУчета.НалоговоеНазначение,
	|
	|	//ДляУпрУчета РегистрАналитикаВидаУчета.Подразделение,
	|	//ДляРеглУчета РегистрАналитикаВидаУчета.ПодразделениеОрганизации,
	|	
	|	РегистрАналитикаРаспределенияЗатрат.НоменклатурнаяГруппа,
	|	РегистрАналитикаУчетаПартий.Заказ,
	|
	|	//ДляУпрУчета РегистрКорАналитикаВидаУчета.Подразделение,
	|	//ДляРеглУчета РегистрКорАналитикаВидаУчета.ПодразделениеОрганизации,
	|	РегистрКорАналитикаРаспределенияЗатрат.НоменклатурнаяГруппа,
	|	РегистрКорАналитикаУчетаПартий.Заказ,
	|
	|	РегистрАналитикаРаспределенияЗатрат.Продукция,
	|	РегистрАналитикаРаспределенияЗатрат.ХарактеристикаПродукции,
	|	РегистрАналитикаРаспределенияЗатрат.СерияПродукции,
	|	РегистрАналитикаРаспределенияЗатрат.Спецификация
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции // СформироватьТекстЗапросаБазаПоСтоимостиЗатратРасширеннаяАналитика()

// Функция формирования запроса для базы распределения по стоимости затрат.
//
// Возвращаемое значение:
//   Строка – Текст запроса
//
Функция СформироватьТекстЗапросаБазаПриходПоСтоимостиЗатратРасширеннаяАналитика()
	
	ТекстЗапроса = "
	|
	|ВЫБРАТЬ
	|	&СпособРаспределенияЗатрат КАК РассчитанныйСпособРаспределения,
	|
	|	НезавершенноеПроизводство.АналитикаВидаУчета,
	|	НезавершенноеПроизводство.АналитикаУчетаПартий,
	|	НезавершенноеПроизводство.АналитикаРаспределенияЗатрат,
	|	
	|	//ДляУпрУчета ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
	|	//ДляРеглУчета РегистрАналитикаВидаУчета.Организация,
	|
	|	//ДляУпрУчета ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетУчета,
	|	//ДляБухУчета РегистрАналитикаВидаУчета.СчетУчета,
	|	//ДляУпрУчета ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначение,
	|	//ДляБухУчета РегистрАналитикаВидаУчета.НалоговоеНазначение КАК НалоговоеНазначение,
	|
	|	//ДляУпрУчета РегистрАналитикаВидаУчета.Подразделение,
	|	//ДляРеглУчета РегистрАналитикаВидаУчета.ПодразделениеОрганизации КАК Подразделение,
	|	
	|	РегистрАналитикаРаспределенияЗатрат.НоменклатурнаяГруппа,
	|	РегистрАналитикаУчетаПартий.Заказ,
	|
	|	//ДляУпрУчета РегистрАналитикаВидаУчета.Подразделение КАК ПодразделениеНЗП,
	|	//ДляРеглУчета РегистрАналитикаВидаУчета.ПодразделениеОрганизации КАК ПодразделениеНЗП,
	|	РегистрАналитикаРаспределенияЗатрат.НоменклатурнаяГруппа КАК НоменклатурнаяГруппаНЗП,
	|	РегистрАналитикаУчетаПартий.Заказ КАК ЗаказНЗП,
	|	
	|	Неопределено КАК Продукция,
	|	Неопределено КАК ХарактеристикаПродукции,
	|	Неопределено КАК СерияПродукции,
	|	Неопределено КАК Спецификация,
	|	Неопределено КАК ВариантВыпускаПродукции,
	|	Неопределено КАК ВидВыпуска,
	|
	|
	|	0 КАК База,
	|	СУММА(НезавершенноеПроизводство.СтоимостьПриход
	|		) КАК БазаПриход,
	|	СУММА(НезавершенноеПроизводство.СтоимостьКонечныйОстаток
	|		) КАК БазаОстатокНЗП
	|	
	|ИЗ
	|	РегистрНакопления.УчетЗатратРегл.ОстаткиИОбороты(, , , , ) КАК НезавершенноеПроизводство // заменяется на временную таблицу НезавершенноеПроизводство%СуффиксУчета%
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ
	|			*
	|		ИЗ
	|			РегистрСведений.АналитикаВидаУчета КАК РегистрАналитикаВидаУчета
	|		ГДЕ
	|			РегистрАналитикаВидаУчета.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчета.Затраты)
	|			//ДляРеглУчета И РегистрАналитикаВидаУчета.Организация = &Организация
	|		{ГДЕ
	|			//ДляУпрУчета Подразделение.*
	|			//ДляРеглУчета ПодразделениеОрганизации.*
	|		}
	|		) КАК РегистрАналитикаВидаУчета
	|	ПО
	|		НезавершенноеПроизводство.АналитикаВидаУчета = РегистрАналитикаВидаУчета.Ссылка
	|		
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ
	|			*
	|		ИЗ
	|			РегистрСведений.АналитикаУчетаЗатрат КАК РегистрАналитикаУчетаЗатрат
	|		{ГДЕ
	|			СтатьяЗатрат.* КАК СтатьяЗатрат,
	|			СтатьяЗатрат.ВидЗатрат.* КАК ВидЗатрат,
	|			СтатьяЗатрат.СтатусМатериальныхЗатрат.* КАК СтатусМатериальныхЗатрат,
	|			СтатьяЗатрат.ХарактерЗатрат.* КАК ХарактерЗатрат
	|		}
	|		) КАК РегистрАналитикаУчетаЗатрат
	|	ПО
	|		НезавершенноеПроизводство.АналитикаУчетаЗатрат = РегистрАналитикаУчетаЗатрат.Ссылка
	|		
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ
	|			*
	|		ИЗ
	|			РегистрСведений.АналитикаРаспределенияЗатрат КАК РегистрАналитикаРаспределенияЗатрат
	|		{ГДЕ
	|			НоменклатурнаяГруппа.* КАК НоменклатурнаяГруппа
	|		}
	|		) КАК РегистрАналитикаРаспределенияЗатрат
	|	ПО
	|		НезавершенноеПроизводство.АналитикаРаспределенияЗатрат = РегистрАналитикаРаспределенияЗатрат.Ссылка
	|		
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПартий КАК РегистрАналитикаУчетаПартий
	|	ПО
	|		НезавершенноеПроизводство.АналитикаУчетаПартий = РегистрАналитикаУчетаПартий.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	НезавершенноеПроизводство.АналитикаВидаУчета,
	|	НезавершенноеПроизводство.АналитикаУчетаПартий,
	|	НезавершенноеПроизводство.АналитикаРаспределенияЗатрат,
	|	
	|	//ДляУпрУчета ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка),
	|	//ДляРеглУчета РегистрАналитикаВидаУчета.Организация,
	|
	|	//ДляУпрУчета ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка),
	|	//ДляБухУчета РегистрАналитикаВидаУчета.СчетУчета,
	|	//ДляУпрУчета ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка),
	|	//ДляБухУчета РегистрАналитикаВидаУчета.НалоговоеНазначение,
	|
	|	//ДляУпрУчета РегистрАналитикаВидаУчета.Подразделение,
	|	//ДляРеглУчета РегистрАналитикаВидаУчета.ПодразделениеОрганизации,
	|	
	|	РегистрАналитикаРаспределенияЗатрат.НоменклатурнаяГруппа,
	|	РегистрАналитикаУчетаПартий.Заказ,
	|	РегистрАналитикаРаспределенияЗатрат.Продукция,
	|	РегистрАналитикаРаспределенияЗатрат.ХарактеристикаПродукции,
	|	РегистрАналитикаРаспределенияЗатрат.СерияПродукции,
	|	РегистрАналитикаРаспределенияЗатрат.Спецификация
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции // СформироватьТекстЗапросаБазаПриходПоСтоимостиЗатратРасширеннаяАналитика()

// Функция формирования запроса для базы распределения по основному сырью.
//
// Возвращаемое значение:
//   Строка – Текст запроса
//
Функция СформироватьТекстЗапросаБазаПоОсновномуСырьюРасширеннаяАналитика()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&СпособРаспределенияЗатрат КАК РассчитанныйСпособРаспределения,
	|
	|	ЗатратыНаВыпуск.АналитикаВидаУчета,
	|	ЗатратыНаВыпуск.АналитикаУчетаПартий,
	|	ЗатратыНаВыпуск.АналитикаРаспределенияЗатрат,
	|	
	|	//ДляУпрУчета ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
	|	//ДляРеглУчета РегистрАналитикаВидаУчета.Организация,
	|
	|	//ДляУпрУчета ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетУчета,
	|	//ДляБухУчета РегистрАналитикаВидаУчета.СчетУчета,
	|	//ДляУпрУчета ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначение,
	|	//ДляБухУчета РегистрАналитикаВидаУчета.НалоговоеНазначение КАК НалоговоеНазначение,
	|
	|	//ДляУпрУчета РегистрАналитикаВидаУчета.Подразделение,
	|	//ДляРеглУчета РегистрАналитикаВидаУчета.ПодразделениеОрганизации КАК Подразделение,
	|	
	|	РегистрАналитикаРаспределенияЗатрат.НоменклатурнаяГруппа,
	|	
	|	РегистрАналитикаУчетаПартий.Заказ,
	|
	|	//ДляУпрУчета РегистрКорАналитикаВидаУчета.Подразделение КАК ПодразделениеНЗП,
	|	//ДляРеглУчета РегистрКорАналитикаВидаУчета.ПодразделениеОрганизации КАК ПодразделениеНЗП,
	|	РегистрКорАналитикаРаспределенияЗатрат.НоменклатурнаяГруппа КАК НоменклатурнаяГруппаНЗП,
	|	РегистрКорАналитикаУчетаПартий.Заказ КАК ЗаказНЗП,
	|
	|	РегистрАналитикаРаспределенияЗатрат.Продукция,
	|	РегистрАналитикаРаспределенияЗатрат.ХарактеристикаПродукции,
	|	РегистрАналитикаРаспределенияЗатрат.СерияПродукции,
	|	РегистрАналитикаРаспределенияЗатрат.Спецификация,
	|
	|	Неопределено КАК ВариантВыпускаПродукции,
	|	ВЫБОР КОГДА РегистрАналитикаВидаУчета.РазделУчета 
	|		= ЗНАЧЕНИЕ(Перечисление.РазделыУчета.ЗатратыПоНаработке)
	|	ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ВидыВыпуска.Наработка)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ВидыВыпуска.Выпуск)
	|	КОНЕЦ КАК ВидВыпуска,
	|
	|		
	|	СУММА(
	|		ЗатратыНаВыпуск.Количество *
	|		ВЫБОР КОГДА	&ПоказательБазыРаспределения 
	|			= ЗНАЧЕНИЕ(Перечисление.ВидыПоказателейБазыРаспределения.ЦеныНоменклатуры)
	|		ТОГДА
	|			ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0)
	|		ИНАЧЕ
	|			1
	|		КОНЕЦ
	|	) КАК База,
	|	0 КАК БазаПриход,
	|	0 КАК БазаОстатокНЗП
	|
	|ПОМЕСТИТЬ БазаРаспределения%СуффиксУчета%
	|ИЗ
	|	РегистрНакопления.УчетЗатратРегл КАК ЗатратыНаВыпуск // заменяется на временную таблицу ЗатратыНаВыпуск%СуффиксУчета%
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ
	|			*
	|		ИЗ
	|			РегистрСведений.АналитикаВидаУчета КАК РегистрАналитикаВидаУчета
	|		ГДЕ
	|			(РегистрАналитикаВидаУчета.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчета.ЗатратыНаВыпуск)
	|			ИЛИ РегистрАналитикаВидаУчета.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчета.ЗатратыПоНаработке))
	|			//ДляРеглУчета И РегистрАналитикаВидаУчета.Организация = &Организация
	|		{ГДЕ
	|			//ДляУпрУчета Подразделение.*
	|			//ДляРеглУчета ПодразделениеОрганизации.*
	|		}
	|		) КАК РегистрАналитикаВидаУчета
	|	ПО
	|		ЗатратыНаВыпуск.АналитикаВидаУчета = РегистрАналитикаВидаУчета.Ссылка
	|		
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ
	|			*
	|		ИЗ
	|			РегистрСведений.АналитикаУчетаЗатрат КАК РегистрАналитикаУчетаЗатрат
	|		{ГДЕ
	|			СтатьяЗатрат.* КАК СтатьяЗатрат,
	|			СтатьяЗатрат.ВидЗатрат.* КАК ВидЗатрат,
	|			СтатьяЗатрат.СтатусМатериальныхЗатрат.* КАК СтатусМатериальныхЗатрат,
	|			СтатьяЗатрат.ХарактерЗатрат.* КАК ХарактерЗатрат
	|		}
	|		ГДЕ
	|			Затрата В (
	|				ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					ОсновноеСырьеСостав.Номенклатура
	|				ИЗ
	|					Справочник.ОсновноеСырье.Состав КАК ОсновноеСырьеСостав
	|				ГДЕ
	|					ОсновноеСырьеСостав.Ссылка = &ОсновноеСырье
	|			)
	|		) КАК РегистрАналитикаУчетаЗатрат
	|	ПО
	|		ЗатратыНаВыпуск.АналитикаУчетаЗатрат = РегистрАналитикаУчетаЗатрат.Ссылка
	|		
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ
	|			*
	|		ИЗ
	|			РегистрСведений.АналитикаРаспределенияЗатрат КАК РегистрАналитикаРаспределенияЗатрат
	|		{ГДЕ
	|			НоменклатурнаяГруппа.* КАК НоменклатурнаяГруппа,
	|			Продукция.* КАК Продукция
	|		}
	|		) КАК РегистрАналитикаРаспределенияЗатрат
	|	ПО
	|		ЗатратыНаВыпуск.АналитикаРаспределенияЗатрат = РегистрАналитикаРаспределенияЗатрат.Ссылка
	|		
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПартий КАК РегистрАналитикаУчетаПартий
	|	ПО
	|		ЗатратыНаВыпуск.АналитикаУчетаПартий = РегистрАналитикаУчетаПартий.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаВидаУчета КАК РегистрКорАналитикаВидаУчета
	|	ПО
	|		ЗатратыНаВыпуск.КорАналитикаВидаУчета = РегистрКорАналитикаВидаУчета.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаРаспределенияЗатрат КАК РегистрКорАналитикаРаспределенияЗатрат
	|	ПО
	|		ЗатратыНаВыпуск.КорАналитикаРаспределенияЗатрат = РегистрКорАналитикаРаспределенияЗатрат.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПартий КАК РегистрКорАналитикаУчетаПартий
	|	ПО
	|		ЗатратыНаВыпуск.КорАналитикаУчетаПартий = РегистрКорАналитикаУчетаПартий.Ссылка
	|	
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			Номенклатура,
	|			ХарактеристикаНоменклатуры,
	|			Цена
	|		ИЗ
	|			РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры  // заменяется на временную таблицу ЦеныНоменклатуры
	|		ГДЕ
	|			ТипЦен = &ТипЦен
	|			И &ПоказательБазыРаспределения = ЗНАЧЕНИЕ(Перечисление.ВидыПоказателейБазыРаспределения.ЦеныНоменклатуры)
	|			
	|		) КАК ЦеныНоменклатуры
	|	ПО 
	|		РегистрАналитикаУчетаЗатрат.Затрата = ЦеныНоменклатуры.Номенклатура
	|		И РегистрАналитикаУчетаЗатрат.ХарактеристикаЗатраты = ЦеныНоменклатуры.ХарактеристикаНоменклатуры	
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗатратыНаВыпуск.АналитикаВидаУчета,
	|	ЗатратыНаВыпуск.АналитикаУчетаПартий,
	|	ЗатратыНаВыпуск.АналитикаРаспределенияЗатрат,
	|	
	|	РегистрАналитикаВидаУчета.РазделУчета,
	|	
	|	//ДляУпрУчета ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка),
	|	//ДляРеглУчета РегистрАналитикаВидаУчета.Организация,
	|
	|	//ДляУпрУчета ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка),
	|	//ДляБухУчета РегистрАналитикаВидаУчета.СчетУчета,
	|	//ДляУпрУчета ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка),
	|	//ДляБухУчета РегистрАналитикаВидаУчета.НалоговоеНазначение,
	|
	|	//ДляУпрУчета РегистрАналитикаВидаУчета.Подразделение,
	|	//ДляРеглУчета РегистрАналитикаВидаУчета.ПодразделениеОрганизации,
	|	
	|	РегистрАналитикаРаспределенияЗатрат.НоменклатурнаяГруппа,
	|	РегистрАналитикаУчетаПартий.Заказ,
	|
	|	//ДляУпрУчета РегистрКорАналитикаВидаУчета.Подразделение,
	|	//ДляРеглУчета РегистрКорАналитикаВидаУчета.ПодразделениеОрганизации,
	|	РегистрКорАналитикаРаспределенияЗатрат.НоменклатурнаяГруппа,
	|	РегистрКорАналитикаУчетаПартий.Заказ,
	|
	|	РегистрАналитикаРаспределенияЗатрат.Продукция,
	|	РегистрАналитикаРаспределенияЗатрат.ХарактеристикаПродукции,
	|	РегистрАналитикаРаспределенияЗатрат.СерияПродукции,
	|	РегистрАналитикаРаспределенияЗатрат.Спецификация
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции // СформироватьТекстЗапросаБазаПоОсновномуСырьюРасширеннаяАналитика()

// Функция формирования запроса для базы распределения по основному сырью.
//
// Возвращаемое значение:
//   Строка – Текст запроса
//
Функция СформироватьТекстЗапросаБазаПриходПоОсновномуСырьюРасширеннаяАналитика()
	
	ТекстЗапроса = "
	|
	|ВЫБРАТЬ
	|	&СпособРаспределенияЗатрат КАК РассчитанныйСпособРаспределения,
	|
	|	НезавершенноеПроизводство.АналитикаВидаУчета,
	|	НезавершенноеПроизводство.АналитикаУчетаПартий,
	|	НезавершенноеПроизводство.АналитикаРаспределенияЗатрат,
	|	
	|	//ДляУпрУчета ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
	|	//ДляРеглУчета РегистрАналитикаВидаУчета.Организация,
	|
	|	//ДляУпрУчета ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетУчета,
	|	//ДляБухУчета РегистрАналитикаВидаУчета.СчетУчета,
	|	//ДляУпрУчета ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначение,
	|	//ДляБухУчета РегистрАналитикаВидаУчета.НалоговоеНазначение КАК НалоговоеНазначение,
	|
	|	//ДляУпрУчета РегистрАналитикаВидаУчета.Подразделение,
	|	//ДляРеглУчета РегистрАналитикаВидаУчета.ПодразделениеОрганизации КАК Подразделение,
	|	
	|	РегистрАналитикаРаспределенияЗатрат.НоменклатурнаяГруппа,
	|	РегистрАналитикаУчетаПартий.Заказ,
	|
	|	//ДляУпрУчета РегистрАналитикаВидаУчета.Подразделение КАК ПодразделениеНЗП,
	|	//ДляРеглУчета РегистрАналитикаВидаУчета.ПодразделениеОрганизации КАК ПодразделениеНЗП,
	|	РегистрАналитикаРаспределенияЗатрат.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	РегистрАналитикаУчетаПартий.Заказ КАК ЗаказНЗП,
	|	
	|	Неопределено КАК Продукция,
	|	Неопределено КАК ХарактеристикаПродукции,
	|	Неопределено КАК СерияПродукции,
	|	Неопределено КАК Спецификация,
	|	Неопределено КАК ВариантВыпускаПродукции,
	|	Неопределено КАК ВидВыпуска,
	|
	|
	|	0 КАК База,
	|	СУММА(
	|		НезавершенноеПроизводство.КоличествоПриход *
	|		ВЫБОР КОГДА	&ПоказательБазыРаспределения 
	|			= ЗНАЧЕНИЕ(Перечисление.ВидыПоказателейБазыРаспределения.ЦеныНоменклатуры)
	|		ТОГДА
	|			ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0)
	|		ИНАЧЕ
	|			1
	|		КОНЕЦ
	|	) КАК БазаПриход,
	|		
	|	СУММА(
	|		НезавершенноеПроизводство.КоличествоКонечныйОстаток *
	|		ВЫБОР КОГДА	&ПоказательБазыРаспределения 
	|			= ЗНАЧЕНИЕ(Перечисление.ВидыПоказателейБазыРаспределения.ЦеныНоменклатуры)
	|		ТОГДА
	|			ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0)
	|		ИНАЧЕ
	|			1
	|		КОНЕЦ
	|	) КАК БазаОстатокНЗП
	|	
	|ИЗ
	|	РегистрНакопления.УчетЗатратРегл.ОстаткиИОбороты(, , , , ) КАК НезавершенноеПроизводство // заменяется на временную таблицу НезавершенноеПроизводство%СуффиксУчета%
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ
	|			*
	|		ИЗ
	|			РегистрСведений.АналитикаВидаУчета КАК РегистрАналитикаВидаУчета
	|		ГДЕ
	|			РегистрАналитикаВидаУчета.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчета.Затраты)
	|			//ДляРеглУчета И РегистрАналитикаВидаУчета.Организация = &Организация
	|		{ГДЕ
	|			//ДляУпрУчета Подразделение.*
	|			//ДляРеглУчета ПодразделениеОрганизации.*
	|		}
	|		) КАК РегистрАналитикаВидаУчета
	|	ПО
	|		НезавершенноеПроизводство.АналитикаВидаУчета = РегистрАналитикаВидаУчета.Ссылка
	|		
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ
	|			*
	|		ИЗ
	|			РегистрСведений.АналитикаУчетаЗатрат КАК РегистрАналитикаУчетаЗатрат
	|		{ГДЕ
	|			СтатьяЗатрат.* КАК СтатьяЗатрат,
	|			СтатьяЗатрат.ВидЗатрат.* КАК ВидЗатрат,
	|			СтатьяЗатрат.СтатусМатериальныхЗатрат.* КАК СтатусМатериальныхЗатрат,
	|			СтатьяЗатрат.ХарактерЗатрат.* КАК ХарактерЗатрат
	|		}
	|		ГДЕ
	|			Затрата В (
	|				ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					ОсновноеСырьеСостав.Номенклатура
	|				ИЗ
	|					Справочник.ОсновноеСырье.Состав КАК ОсновноеСырьеСостав
	|				ГДЕ
	|					ОсновноеСырьеСостав.Ссылка = &ОсновноеСырье
	|			)
	|		) КАК РегистрАналитикаУчетаЗатрат
	|	ПО
	|		НезавершенноеПроизводство.АналитикаУчетаЗатрат = РегистрАналитикаУчетаЗатрат.Ссылка
	|		
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ
	|			*
	|		ИЗ
	|			РегистрСведений.АналитикаРаспределенияЗатрат КАК РегистрАналитикаРаспределенияЗатрат
	|		{ГДЕ
	|			НоменклатурнаяГруппа.* КАК НоменклатурнаяГруппа
	|		}
	|		) КАК РегистрАналитикаРаспределенияЗатрат
	|	ПО
	|		НезавершенноеПроизводство.АналитикаРаспределенияЗатрат = РегистрАналитикаРаспределенияЗатрат.Ссылка
	|		
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПартий КАК РегистрАналитикаУчетаПартий
	|	ПО
	|		НезавершенноеПроизводство.АналитикаУчетаПартий = РегистрАналитикаУчетаПартий.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			Номенклатура,
	|			ХарактеристикаНоменклатуры,
	|			Цена
	|		ИЗ
	|			РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры  // заменяется на временную таблицу ЦеныНоменклатуры
	|		ГДЕ
	|			ТипЦен = &ТипЦен
	|			И &ПоказательБазыРаспределения = ЗНАЧЕНИЕ(Перечисление.ВидыПоказателейБазыРаспределения.ЦеныНоменклатуры)
	|			
	|		) КАК ЦеныНоменклатуры
	|	ПО 
	|		РегистрАналитикаУчетаЗатрат.Затрата = ЦеныНоменклатуры.Номенклатура
	|		И РегистрАналитикаУчетаЗатрат.ХарактеристикаЗатраты = ЦеныНоменклатуры.ХарактеристикаНоменклатуры	
	|
	|СГРУППИРОВАТЬ ПО
	|	НезавершенноеПроизводство.АналитикаВидаУчета,
	|	НезавершенноеПроизводство.АналитикаУчетаПартий,
	|	НезавершенноеПроизводство.АналитикаРаспределенияЗатрат,
	|	
	|	//ДляУпрУчета ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка),
	|	//ДляРеглУчета РегистрАналитикаВидаУчета.Организация,
	|
	|	//ДляУпрУчета ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка),
	|	//ДляБухУчета РегистрАналитикаВидаУчета.СчетУчета,
	|	//ДляУпрУчета ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка),
	|	//ДляБухУчета РегистрАналитикаВидаУчета.НалоговоеНазначение,
	|
	|	//ДляУпрУчета РегистрАналитикаВидаУчета.Подразделение,
	|	//ДляРеглУчета РегистрАналитикаВидаУчета.ПодразделениеОрганизации,
	|	
	|	РегистрАналитикаРаспределенияЗатрат.НоменклатурнаяГруппа,
	|	РегистрАналитикаУчетаПартий.Заказ,
	|	РегистрАналитикаРаспределенияЗатрат.Продукция,
	|	РегистрАналитикаРаспределенияЗатрат.ХарактеристикаПродукции,
	|	РегистрАналитикаРаспределенияЗатрат.СерияПродукции,
	|	РегистрАналитикаРаспределенияЗатрат.Спецификация
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции // СформироватьТекстЗапросаБазаПриходПоОсновномуСырьюРасширеннаяАналитика()

// Функция формирования запроса для базы распределения по объему выпуска
// по типу цен номенклатуры.
//
// Возвращаемое значение:
//   Строка – Текст запроса
//
Функция СформироватьТекстЗапросаБазаПоОбъемуВыпускаЦеныНоменклатурыРасширеннаяАналитика()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&СпособРаспределенияЗатрат КАК РассчитанныйСпособРаспределения,
	|
	|	ВыпускПродукции.АналитикаВидаУчета,
	|	ВыпускПродукции.АналитикаУчетаПартий,
	|	ВыпускПродукции.АналитикаРаспределенияЗатрат,
	|
	|	//ДляУпрУчета ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
	|	//ДляРеглУчета РегистрАналитикаВидаУчета.Организация,
	|
	|	//ДляУпрУчета ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетУчета,
	|	//ДляБухУчета РегистрАналитикаВидаУчета.СчетУчета,
	|	//ДляУпрУчета ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначение,
	|	//ДляБухУчета РегистрАналитикаВидаУчета.НалоговоеНазначение КАК НалоговоеНазначение,
	|
	|	//ДляУпрУчета РегистрАналитикаВидаУчета.Подразделение,
	|	//ДляРеглУчета РегистрАналитикаВидаУчета.ПодразделениеОрганизации КАК Подразделение,
	|	РегистрАналитикаРаспределенияЗатрат.НоменклатурнаяГруппа,
	|	РегистрАналитикаУчетаПартий.Заказ,
	|
	|	//ДляУпрУчета РегистрАналитикаВидаУчета.Подразделение КАК ПодразделениеНЗП,
	|	//ДляРеглУчета РегистрАналитикаВидаУчета.ПодразделениеОрганизации КАК ПодразделениеНЗП,
	|	РегистрАналитикаРаспределенияЗатрат.НоменклатурнаяГруппа КАК НоменклатурнаяГруппаНЗП,
	|	РегистрАналитикаУчетаПартий.Заказ КАК ЗаказНЗП,
	|
	|	РегистрАналитикаРаспределенияЗатрат.Продукция,
	|	РегистрАналитикаРаспределенияЗатрат.ХарактеристикаПродукции,
	|	РегистрАналитикаРаспределенияЗатрат.СерияПродукции,
	|	РегистрАналитикаРаспределенияЗатрат.Спецификация,
	|	""ВариантВыпускаПродукции"" КАК ВариантВыпускаПродукции, // Заменяется на ВыпускПродукции.ВариантВыпускаПродукции
	|	
	|	ВЫБОР КОГДА РегистрАналитикаВидаУчета.РазделУчета 
	|		= ЗНАЧЕНИЕ(Перечисление.РазделыУчета.Выпуск)
	|	ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ВидыВыпуска.Выпуск)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ВидыВыпуска.Наработка)
	|	КОНЕЦ КАК ВидВыпуска,
	|
	|
	|	(
	|	ВыпускПродукции.Количество *
	|	ВЫБОР КОГДА	&ПоказательБазыРаспределения 
	|		= ЗНАЧЕНИЕ(Перечисление.ВидыПоказателейБазыРаспределения.ЦеныНоменклатуры)
	|	ТОГДА
	|		ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0)
	|	ИНАЧЕ
	|		1
	|	КОНЕЦ 
	|	) КАК База,
	|	(
	|	ВыпускПродукции.Количество *
	|	ВЫБОР КОГДА	&ПоказательБазыРаспределения 
	|		= ЗНАЧЕНИЕ(Перечисление.ВидыПоказателейБазыРаспределения.ЦеныНоменклатуры)
	|	ТОГДА
	|		ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0)
	|	ИНАЧЕ
	|		1
	|	КОНЕЦ
	|	) КАК БазаПриход,
	|	0 КАК БазаОстатокНЗП
	|
	|ПОМЕСТИТЬ БазаРаспределения%СуффиксУчета%
	|ИЗ
	|	РегистрНакопления.УчетЗатрат КАК ВыпускПродукции // заменяется на временную таблицу ВыпускПродукции%СуффиксУчета%
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ
	|			*
	|		ИЗ
	|			РегистрСведений.АналитикаВидаУчета КАК РегистрАналитикаВидаУчета // Заменяется на АналитикаВидаУчетаЗатратыНаВыпуск
	|		{ГДЕ
	|			//ДляУпрУчета Подразделение.*
	|			//ДляРеглУчета ПодразделениеОрганизации.*
	|		}
	|		) КАК РегистрАналитикаВидаУчета
	|	ПО
	|		ВыпускПродукции.АналитикаВидаУчета = РегистрАналитикаВидаУчета.Ссылка
	|		
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ
	|			*
	|		ИЗ
	|			РегистрСведений.АналитикаРаспределенияЗатрат КАК РегистрАналитикаРаспределенияЗатрат
	|		{ГДЕ
	|			НоменклатурнаяГруппа.* КАК НоменклатурнаяГруппа,
	|			Продукция.* КАК Продукция
	|		}
	|		) КАК РегистрАналитикаРаспределенияЗатрат
	|	ПО
	|		ВыпускПродукции.АналитикаРаспределенияЗатрат = РегистрАналитикаРаспределенияЗатрат.Ссылка
	|		
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПартий КАК РегистрАналитикаУчетаПартий
	|	ПО
	|		ВыпускПродукции.АналитикаУчетаПартий = РегистрАналитикаУчетаПартий.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			Номенклатура,
	|			ХарактеристикаНоменклатуры,
	|			Цена
	|		ИЗ
	|			РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры  // заменяется на временную таблицу ЦеныНоменклатуры
	|		ГДЕ
	|			ТипЦен = &ТипЦен
	|			И &ПоказательБазыРаспределения = ЗНАЧЕНИЕ(Перечисление.ВидыПоказателейБазыРаспределения.ЦеныНоменклатуры)
	|			
	|		) КАК ЦеныНоменклатуры
	|	ПО 
	|		РегистрАналитикаРаспределенияЗатрат.Продукция = ЦеныНоменклатуры.Номенклатура
	|		И РегистрАналитикаРаспределенияЗатрат.ХарактеристикаПродукции = ЦеныНоменклатуры.ХарактеристикаНоменклатуры
	|
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции // СформироватьТекстЗапросаБазаПоОбъемуВыпускаЦеныНоменклатурыРасширеннаяАналитика()

// Функция формирования запроса для базы распределения по объему выпуска
// по типу цен номенклатуры.
//
// Возвращаемое значение:
//   Строка – Текст запроса
//
Функция СформироватьТекстЗапросаБазаФактическаяЦенаРеализацииРасширеннаяАналитика()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&СпособРаспределенияЗатрат КАК РассчитанныйСпособРаспределения,
	|
	|	ВыпускПродукции.АналитикаВидаУчета,
	|	ВыпускПродукции.АналитикаУчетаПартий,
	|	ВыпускПродукции.АналитикаРаспределенияЗатрат,
	|
	|	//ДляУпрУчета ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
	|	//ДляРеглУчета РегистрАналитикаВидаУчета.Организация,
	|
	|	//ДляУпрУчета ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетУчета,
	|	//ДляБухУчета РегистрАналитикаВидаУчета.СчетУчета,
	|	//ДляУпрУчета ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначение,
	|	//ДляБухУчета РегистрАналитикаВидаУчета.НалоговоеНазначение КАК НалоговоеНазначение,
	|
	|	//ДляУпрУчета РегистрАналитикаВидаУчета.Подразделение,
	|	//ДляРеглУчета РегистрАналитикаВидаУчета.ПодразделениеОрганизации КАК Подразделение,
	|	РегистрАналитикаРаспределенияЗатрат.НоменклатурнаяГруппа,
	|	РегистрАналитикаУчетаПартий.Заказ,
	|
	|	//ДляУпрУчета РегистрАналитикаВидаУчета.Подразделение КАК ПодразделениеНЗП,
	|	//ДляРеглУчета РегистрАналитикаВидаУчета.ПодразделениеОрганизации КАК ПодразделениеНЗП,
	|	РегистрАналитикаРаспределенияЗатрат.НоменклатурнаяГруппа КАК НоменклатурнаяГруппаНЗП,
	|	РегистрАналитикаУчетаПартий.Заказ КАК ЗаказНЗП,
	|
	|	РегистрАналитикаРаспределенияЗатрат.Продукция,
	|	РегистрАналитикаРаспределенияЗатрат.ХарактеристикаПродукции,
	|	РегистрАналитикаРаспределенияЗатрат.СерияПродукции,
	|	РегистрАналитикаРаспределенияЗатрат.Спецификация,
	|	""ВариантВыпускаПродукции"" КАК ВариантВыпускаПродукции, // Заменяется на ВыпускПродукции.ВариантВыпускаПродукции
	|	
	|	ВЫБОР КОГДА РегистрАналитикаВидаУчета.РазделУчета 
	|		= ЗНАЧЕНИЕ(Перечисление.РазделыУчета.Выпуск)
	|	ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ВидыВыпуска.Выпуск)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ВидыВыпуска.Наработка)
	|	КОНЕЦ КАК ВидВыпуска,
	|
	|		
	|	(
	|	ВыпускПродукции.Количество *
	|	ВЫБОР КОГДА Продажи.Количество <> 0 ТОГДА
	|   	Продажи.Стоимость /
	|   	Продажи.Количество
	|	ИНАЧЕ
	|		1
	|	КОНЕЦ
	|	) КАК База,
	|	(
	|	ВыпускПродукции.Количество *
	|	ВЫБОР КОГДА Продажи.Количество <> 0 ТОГДА
	|   	Продажи.Стоимость /
	|   	Продажи.Количество
	|	ИНАЧЕ
	|		1
	|	КОНЕЦ
	|	) КАК БазаПриход,
	|	0 КАК БазаОстатокНЗП
	|
	|ПОМЕСТИТЬ БазаРаспределения%СуффиксУчета%
	|ИЗ
	|	РегистрНакопления.УчетЗатрат КАК ВыпускПродукции // заменяется на временную таблицу ВыпускПродукции%СуффиксУчета%
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ
	|			*
	|		ИЗ
	|			РегистрСведений.АналитикаВидаУчета КАК РегистрАналитикаВидаУчета // Заменяется на АналитикаВидаУчетаЗатратыНаВыпуск
	|		{ГДЕ
	|			//ДляУпрУчета Подразделение.*
	|			//ДляРеглУчета ПодразделениеОрганизации.*
	|		}
	|		) КАК РегистрАналитикаВидаУчета
	|	ПО
	|		ВыпускПродукции.АналитикаВидаУчета = РегистрАналитикаВидаУчета.Ссылка
	|		
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ
	|			*
	|		ИЗ
	|			РегистрСведений.АналитикаРаспределенияЗатрат КАК РегистрАналитикаРаспределенияЗатрат
	|		{ГДЕ
	|			НоменклатурнаяГруппа.* КАК НоменклатурнаяГруппа,
	|			Продукция.* КАК Продукция
	|		}
	|		) КАК РегистрАналитикаРаспределенияЗатрат
	|	ПО
	|		ВыпускПродукции.АналитикаРаспределенияЗатрат = РегистрАналитикаРаспределенияЗатрат.Ссылка
	|		
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПартий КАК РегистрАналитикаУчетаПартий
	|	ПО
	|		ВыпускПродукции.АналитикаУчетаПартий = РегистрАналитикаУчетаПартий.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ВыпускПродукции КАК Продажи // заменяется на временную таблицу Продажи
	|	ПО
	|		РегистрАналитикаРаспределенияЗатрат.Продукция = Продажи.Продукция
	|		И РегистрАналитикаРаспределенияЗатрат.ХарактеристикаПродукции = Продажи.ХарактеристикаПродукции
	|		И РегистрАналитикаРаспределенияЗатрат.СерияПродукции = Продажи.СерияПродукции
	|		И (РегистрАналитикаУчетаПартий.Заказ = Продажи.Заказ
	|			ИЛИ РегистрАналитикаУчетаПартий.Заказ = Неопределено
	|			ИЛИ РегистрАналитикаУчетаПартий.Заказ ССЫЛКА Документ.ЗаказНаПроизводство)
	|
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции // СформироватьТекстЗапросаБазаФактическаяЦенаРеализацииРасширеннаяАналитика()

// Функция формирования запроса для базы распределения по нормативам.
//
// Возвращаемое значение:
//   Строка – Текст запроса
//
Функция СформироватьТекстЗапросаБазаПоНормативамРасширеннаяАналитика()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&СпособРаспределенияЗатрат КАК РассчитанныйСпособРаспределения,
	|
	|	ВыпускПродукции.АналитикаВидаУчета,
	|	ВыпускПродукции.АналитикаУчетаПартий,
	|	ВыпускПродукции.АналитикаРаспределенияЗатрат,
	|
	|	//ДляУпрУчета ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
	|	//ДляРеглУчета РегистрАналитикаВидаУчета.Организация,
	|
	|	//ДляУпрУчета ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетУчета,
	|	//ДляБухУчета РегистрАналитикаВидаУчета.СчетУчета,
	|	//ДляУпрУчета ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначение,
	|	//ДляБухУчета РегистрАналитикаВидаУчета.НалоговоеНазначение КАК НалоговоеНазначение,
	|
	|	//ДляУпрУчета РегистрАналитикаВидаУчета.Подразделение,
	|	//ДляРеглУчета РегистрАналитикаВидаУчета.ПодразделениеОрганизации КАК Подразделение,
	|	РегистрАналитикаРаспределенияЗатрат.НоменклатурнаяГруппа,
	|	РегистрАналитикаУчетаПартий.Заказ,
	|
	|	//ДляУпрУчета РегистрАналитикаВидаУчета.Подразделение КАК ПодразделениеНЗП,
	|	//ДляРеглУчета РегистрАналитикаВидаУчета.ПодразделениеОрганизации КАК ПодразделениеНЗП,
	|	РегистрАналитикаРаспределенияЗатрат.НоменклатурнаяГруппа КАК НоменклатурнаяГруппаНЗП,
	|	РегистрАналитикаУчетаПартий.Заказ КАК ЗаказНЗП,
	|
	|	РегистрАналитикаРаспределенияЗатрат.Продукция,
	|	РегистрАналитикаРаспределенияЗатрат.ХарактеристикаПродукции,
	|	РегистрАналитикаРаспределенияЗатрат.СерияПродукции,
	|	РегистрАналитикаРаспределенияЗатрат.Спецификация,
	|	""ВариантВыпускаПродукции"" КАК ВариантВыпускаПродукции, // Заменяется на ВыпускПродукции.ВариантВыпускаПродукции
	|	
	|	ВЫБОР КОГДА РегистрАналитикаВидаУчета.РазделУчета 
	|		= ЗНАЧЕНИЕ(Перечисление.РазделыУчета.Выпуск)
	|	ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ВидыВыпуска.Выпуск)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ВидыВыпуска.Наработка)
	|	КОНЕЦ КАК ВидВыпуска,
	|
	|		
	|	(
	|	ВыпускПродукции.Количество *
	|	ЕСТЬNULL(ПлановаяСебестоимость.Цена, 0)
	|	) КАК База,
	|	(
	|	ВыпускПродукции.Количество *
	|	ЕСТЬNULL(ПлановаяСебестоимость.Цена, 0)
	|	) КАК БазаПриход,
	|	0 КАК БазаОстатокНЗП
	|
	|ПОМЕСТИТЬ БазаРаспределения%СуффиксУчета%
	|ИЗ
	|	РегистрНакопления.УчетЗатрат КАК ВыпускПродукции // заменяется на временную таблицу ВыпускПродукции%СуффиксУчета%
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ
	|			*
	|		ИЗ
	|			РегистрСведений.АналитикаВидаУчета КАК РегистрАналитикаВидаУчета // Заменяется на АналитикаВидаУчетаЗатратыНаВыпуск
	|		{ГДЕ
	|			//ДляУпрУчета Подразделение.*
	|			//ДляРеглУчета ПодразделениеОрганизации.*
	|		}
	|		) КАК РегистрАналитикаВидаУчета
	|	ПО
	|		ВыпускПродукции.АналитикаВидаУчета = РегистрАналитикаВидаУчета.Ссылка
	|		
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ
	|			*
	|		ИЗ
	|			РегистрСведений.АналитикаРаспределенияЗатрат КАК РегистрАналитикаРаспределенияЗатрат
	|		{ГДЕ
	|			НоменклатурнаяГруппа.* КАК НоменклатурнаяГруппа,
	|			Продукция.* КАК Продукция
	|		}
	|		) КАК РегистрАналитикаРаспределенияЗатрат
	|	ПО
	|		ВыпускПродукции.АналитикаРаспределенияЗатрат = РегистрАналитикаРаспределенияЗатрат.Ссылка
	|		
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПартий КАК РегистрАналитикаУчетаПартий
	|	ПО
	|		ВыпускПродукции.АналитикаУчетаПартий = РегистрАналитикаУчетаПартий.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ
	|			ПлановаяСебестоимость.Номенклатура,
	|			ПлановаяСебестоимость.ХарактеристикаНоменклатуры,
	|			СУММА(ПлановаяСебестоимость.Сумма) КАК Цена
	|		ИЗ
	|	 		РегистрСведений.ПлановаяСебестоимостьНоменклатуры.СрезПоследних(&КонДата,
	|			{
	|				СтатьяЗатрат.* 	КАК СтатьяЗатрат,
	|				СтатьяЗатрат.ВидЗатрат.* КАК ВидЗатрат,
	|				СтатьяЗатрат.СтатусМатериальныхЗатрат.* КАК СтатусМатериальныхЗатрат,
	|				СтатьяЗатрат.ХарактерЗатрат.* КАК ХарактерЗатрат
	|			}
	|			) КАК ПлановаяСебестоимость
	|		СГРУППИРОВАТЬ ПО
	|			ПлановаяСебестоимость.Номенклатура,
	|			ПлановаяСебестоимость.ХарактеристикаНоменклатуры
	|			
	|		) КАК ПлановаяСебестоимость
	|	ПО 
	|		РегистрАналитикаРаспределенияЗатрат.Продукция = ПлановаяСебестоимость.Номенклатура
	|		И РегистрАналитикаРаспределенияЗатрат.ХарактеристикаПродукции = ПлановаяСебестоимость.ХарактеристикаНоменклатуры
	|
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции // СформироватьТекстЗапросаБазаПоНормативамРасширеннаяАналитика()

// Функция формирования запроса для базы распределения по объему продаж
// по типу цен номенклатуры.
//
// Возвращаемое значение:
//   Строка – Текст запроса
//
Функция СформироватьТекстЗапросаБазаПоОбъемуПродажРасширеннаяАналитика()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&СпособРаспределенияЗатрат КАК РассчитанныйСпособРаспределения,
	|
	|	ВыпускПродукции.АналитикаВидаУчета,
	|	ВыпускПродукции.АналитикаУчетаПартий,
	|	ВыпускПродукции.АналитикаРаспределенияЗатрат,
	|
	|	//ДляУпрУчета ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
	|	//ДляРеглУчета РегистрАналитикаВидаУчета.Организация,
	|
	|	//ДляУпрУчета ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетУчета,
	|	//ДляБухУчета РегистрАналитикаВидаУчета.СчетУчета,
	|	//ДляУпрУчета ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначение,
	|	//ДляБухУчета РегистрАналитикаВидаУчета.НалоговоеНазначение КАК НалоговоеНазначение,
	|
	|	//ДляУпрУчета РегистрАналитикаВидаУчета.Подразделение,
	|	//ДляРеглУчета РегистрАналитикаВидаУчета.ПодразделениеОрганизации КАК Подразделение,
	|	РегистрАналитикаРаспределенияЗатрат.НоменклатурнаяГруппа,
	|	РегистрАналитикаУчетаПартий.Заказ,
	|
	|	//ДляУпрУчета РегистрАналитикаВидаУчета.Подразделение КАК ПодразделениеНЗП,
	|	//ДляРеглУчета РегистрАналитикаВидаУчета.ПодразделениеОрганизации КАК ПодразделениеНЗП,
	|	РегистрАналитикаРаспределенияЗатрат.НоменклатурнаяГруппа КАК НоменклатурнаяГруппаНЗП,
	|	РегистрАналитикаУчетаПартий.Заказ КАК ЗаказНЗП,
	|
	|	РегистрАналитикаРаспределенияЗатрат.Продукция,
	|	РегистрАналитикаРаспределенияЗатрат.ХарактеристикаПродукции,
	|	РегистрАналитикаРаспределенияЗатрат.СерияПродукции,
	|	РегистрАналитикаРаспределенияЗатрат.Спецификация,
	|	""ВариантВыпускаПродукции"" КАК ВариантВыпускаПродукции, // Заменяется на ВыпускПродукции.ВариантВыпускаПродукции
	|	
	|	ВЫБОР КОГДА РегистрАналитикаВидаУчета.РазделУчета 
	|		= ЗНАЧЕНИЕ(Перечисление.РазделыУчета.Выпуск)
	|	ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ВидыВыпуска.Выпуск)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ВидыВыпуска.Наработка)
	|	КОНЕЦ КАК ВидВыпуска,
	|
	|
	|	(
	|	ВыпускПродукции.Количество *
	|	ВЫБОР КОГДА ВыпускПродукцииВсего.Количество <> 0 ТОГДА
	|		Продажи.Количество /
	|		ВыпускПродукцииВсего.Количество
	|	ИНАЧЕ	
	|		1
	|	КОНЕЦ *
	|	ВЫБОР КОГДА	&ПоказательБазыРаспределения 
	|		= ЗНАЧЕНИЕ(Перечисление.ВидыПоказателейБазыРаспределения.ЦеныНоменклатуры)
	|	ТОГДА
	|		ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0)
	|	КОГДА &ПоказательБазыРаспределения 
	|		= ЗНАЧЕНИЕ(Перечисление.ВидыПоказателейБазыРаспределения.ФактическаяЦенаРеализации)
	|	ТОГДА
	|		ВЫБОР КОГДА Продажи.Количество <> 0 ТОГДА
	|      		Продажи.Стоимость /
	|      		Продажи.Количество
	|		ИНАЧЕ
	|			1
	|		КОНЕЦ
	|	ИНАЧЕ
	|		1
	|	КОНЕЦ
	|	) КАК База,
	|	(
	|	ВыпускПродукции.Количество *
	|	ВЫБОР КОГДА ВыпускПродукцииВсего.Количество <> 0 ТОГДА
	|		Продажи.Количество /
	|		ВыпускПродукцииВсего.Количество
	|	ИНАЧЕ	
	|		1
	|	КОНЕЦ *
	|	ВЫБОР КОГДА	&ПоказательБазыРаспределения 
	|		= ЗНАЧЕНИЕ(Перечисление.ВидыПоказателейБазыРаспределения.ЦеныНоменклатуры)
	|	ТОГДА
	|		ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0)
	|	КОГДА &ПоказательБазыРаспределения 
	|		= ЗНАЧЕНИЕ(Перечисление.ВидыПоказателейБазыРаспределения.ФактическаяЦенаРеализации)
	|	ТОГДА
	|		ВЫБОР КОГДА Продажи.Количество <> 0 ТОГДА
	|      		Продажи.Стоимость /
	|      		Продажи.Количество
	|		ИНАЧЕ
	|			1
	|		КОНЕЦ
	|	ИНАЧЕ
	|		1
	|	КОНЕЦ
	|	) КАК БазаПриход,
	|	0 КАК БазаОстатокНЗП
	|
	|ПОМЕСТИТЬ БазаРаспределения%СуффиксУчета%
	|ИЗ
	|	РегистрНакопления.УчетЗатрат КАК ВыпускПродукции // заменяется на временную таблицу ВыпускПродукции%СуффиксУчета%
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ
	|			*
	|		ИЗ
	|			РегистрСведений.АналитикаВидаУчета КАК РегистрАналитикаВидаУчета // Заменяется на АналитикаВидаУчетаЗатратыНаВыпуск
	|		{ГДЕ
	|			//ДляУпрУчета Подразделение.*
	|			//ДляРеглУчета ПодразделениеОрганизации.*
	|		}
	|		) КАК РегистрАналитикаВидаУчета
	|	ПО
	|		ВыпускПродукции.АналитикаВидаУчета = РегистрАналитикаВидаУчета.Ссылка
	|		
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ
	|			*
	|		ИЗ
	|			РегистрСведений.АналитикаРаспределенияЗатрат КАК РегистрАналитикаРаспределенияЗатрат
	|		{ГДЕ
	|			НоменклатурнаяГруппа.* КАК НоменклатурнаяГруппа,
	|			Продукция.* КАК Продукция
	|		}
	|		) КАК РегистрАналитикаРаспределенияЗатрат
	|	ПО
	|		ВыпускПродукции.АналитикаРаспределенияЗатрат = РегистрАналитикаРаспределенияЗатрат.Ссылка
	|		
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПартий КАК РегистрАналитикаУчетаПартий
	|	ПО
	|		ВыпускПродукции.АналитикаУчетаПартий = РегистрАналитикаУчетаПартий.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ВыпускПродукции КАК Продажи // заменяется на временную таблицу Продажи
	|	ПО
	|		РегистрАналитикаРаспределенияЗатрат.Продукция = Продажи.Продукция
	|		И РегистрАналитикаРаспределенияЗатрат.ХарактеристикаПродукции = Продажи.ХарактеристикаПродукции
	|		И РегистрАналитикаРаспределенияЗатрат.СерияПродукции = Продажи.СерияПродукции
	|		И (РегистрАналитикаУчетаПартий.Заказ = Продажи.Заказ
	|			ИЛИ РегистрАналитикаУчетаПартий.Заказ = Неопределено
	|			ИЛИ РегистрАналитикаУчетаПартий.Заказ ССЫЛКА Документ.ЗаказНаПроизводство)
	|		И (
	|			//ДляУпрУчета РегистрАналитикаВидаУчета.Подразделение = Продажи.Подразделение
	|			//ДляРеглУчета РегистрАналитикаВидаУчета.ПодразделениеОрганизации = Продажи.Подразделение
	|			ИЛИ Продажи.Подразделение = Неопределено)
	|			
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (
	|
	|		// Выпуск продукции по всем подразделениям.
	|		ВЫБРАТЬ
	|			Неопределено КАК Подразделение,
	|			РегистрАналитикаРаспределенияЗатрат.Продукция,
	|			РегистрАналитикаРаспределенияЗатрат.ХарактеристикаПродукции,
	|			РегистрАналитикаРаспределенияЗатрат.СерияПродукции,
	|			РегистрАналитикаУчетаПартий.Заказ,
	|			СУММА(ВыпускПродукции.Количество) КАК Количество
	|
	|		ИЗ
	|			РегистрНакопления.УчетЗатрат КАК ВыпускПродукции // заменяется на временную таблицу ВыпускПродукции
	|		
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|				РегистрСведений.АналитикаРаспределенияЗатрат КАК РегистрАналитикаРаспределенияЗатрат
	|			ПО
	|				ВыпускПродукции.АналитикаРаспределенияЗатрат = РегистрАналитикаРаспределенияЗатрат.Ссылка
	|		
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|				РегистрСведений.АналитикаУчетаПартий КАК РегистрАналитикаУчетаПартий
	|			ПО
	|				ВыпускПродукции.АналитикаУчетаПартий = РегистрАналитикаУчетаПартий.Ссылка
	|
	|		СГРУППИРОВАТЬ ПО
	|			РегистрАналитикаРаспределенияЗатрат.Продукция,
	|			РегистрАналитикаРаспределенияЗатрат.ХарактеристикаПродукции,
	|			РегистрАналитикаРаспределенияЗатрат.СерияПродукции,
	|			РегистрАналитикаУчетаПартий.Заказ
	|
	|		ОБЪЕДИНИТЬ ВСЕ
	|
	|		// Выпуск продукции по подразделению.
	|		ВЫБРАТЬ
	|			//ДляУпрУчета РегистрАналитикаВидаУчета.Подразделение КАК Подразделение,
	|			//ДляРеглУчета РегистрАналитикаВидаУчета.ПодразделениеОрганизации КАК Подразделение,
	|			РегистрАналитикаРаспределенияЗатрат.Продукция,
	|			РегистрАналитикаРаспределенияЗатрат.ХарактеристикаПродукции,
	|			РегистрАналитикаРаспределенияЗатрат.СерияПродукции,
	|			РегистрАналитикаУчетаПартий.Заказ,
	|			СУММА(ВыпускПродукции.Количество) КАК Количество
	|
	|		ИЗ
	|			РегистрНакопления.УчетЗатрат КАК ВыпускПродукции // заменяется на временную таблицу ВыпускПродукции
	|		
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|				РегистрСведений.АналитикаВидаУчета КАК РегистрАналитикаВидаУчета
	|			ПО
	|				ВыпускПродукции.АналитикаВидаУчета = РегистрАналитикаВидаУчета.Ссылка
	|		
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|				РегистрСведений.АналитикаРаспределенияЗатрат КАК РегистрАналитикаРаспределенияЗатрат
	|			ПО
	|				ВыпускПродукции.АналитикаРаспределенияЗатрат = РегистрАналитикаРаспределенияЗатрат.Ссылка
	|		
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|				РегистрСведений.АналитикаУчетаПартий КАК РегистрАналитикаУчетаПартий
	|			ПО
	|				ВыпускПродукции.АналитикаУчетаПартий = РегистрАналитикаУчетаПартий.Ссылка
	|
	|		СГРУППИРОВАТЬ ПО
	|			//ДляУпрУчета РегистрАналитикаВидаУчета.Подразделение,
	|			//ДляРеглУчета РегистрАналитикаВидаУчета.ПодразделениеОрганизации,
	|			РегистрАналитикаРаспределенияЗатрат.Продукция,
	|			РегистрАналитикаРаспределенияЗатрат.ХарактеристикаПродукции,
	|			РегистрАналитикаРаспределенияЗатрат.СерияПродукции,
	|			РегистрАналитикаУчетаПартий.Заказ
	|
	|		) КАК ВыпускПродукцииВсего
	|	ПО
	|		РегистрАналитикаРаспределенияЗатрат.Продукция = ВыпускПродукцииВсего.Продукция
	|		И РегистрАналитикаРаспределенияЗатрат.ХарактеристикаПродукции = ВыпускПродукцииВсего.ХарактеристикаПродукции
	|		И РегистрАналитикаРаспределенияЗатрат.СерияПродукции = ВыпускПродукцииВсего.СерияПродукции
	|		И РегистрАналитикаУчетаПартий.Заказ = ВыпускПродукцииВсего.Заказ
	|		И Продажи.Подразделение = ВыпускПродукцииВсего.Подразделение
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			Номенклатура,
	|			ХарактеристикаНоменклатуры,
	|			Цена
	|		ИЗ
	|			РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры  // заменяется на временную таблицу ЦеныНоменклатуры
	|		ГДЕ
	|			ТипЦен = &ТипЦен
	|			И &ПоказательБазыРаспределения = ЗНАЧЕНИЕ(Перечисление.ВидыПоказателейБазыРаспределения.ЦеныНоменклатуры)
	|			
	|		) КАК ЦеныНоменклатуры
	|	ПО 
	|		РегистрАналитикаРаспределенияЗатрат.Продукция = ЦеныНоменклатуры.Номенклатура
	|		И РегистрАналитикаРаспределенияЗатрат.ХарактеристикаПродукции = ЦеныНоменклатуры.ХарактеристикаНоменклатуры
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции // СформироватьТекстЗапросаБазаПоОбъемуПродажРасширеннаяАналитика()

// Функция формирования запроса для базы распределения "Вручную".
//
// Возвращаемое значение:
//   Строка – Текст запроса
//
Функция СформироватьТекстЗапросаБазаВручнуюРасширеннаяАналитика()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&СпособРаспределенияЗатрат КАК РассчитанныйСпособРаспределения,
	|
	|	БазаРаспределенияЗатрат.АналитикаВидаУчета,
	|	БазаРаспределенияЗатрат.АналитикаУчетаПартий,
	|	БазаРаспределенияЗатрат.АналитикаРаспределенияЗатрат,
	|
	|	//ДляУпрУчета ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
	|	//ДляРеглУчета БазаРаспределенияЗатрат.Организация,
	|
	|	//ДляУпрУчета ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетУчета,
	|	//ДляРеглУчета БазаРаспределенияЗатрат.СчетУчета,
	|
	|	//ДляУпрУчета  ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначение,
	|	//ДляБухУчета  БазаРаспределенияЗатрат.НалоговоеНазначение КАК НалоговоеНазначение,
	|
	|	БазаРаспределенияЗатрат.Подразделение,
	|	БазаРаспределенияЗатрат.НоменклатурнаяГруппа,
	|	БазаРаспределенияЗатрат.Заказ,
	|
	|	БазаРаспределенияЗатрат.Подразделение КАК ПодразделениеНЗП,
	|	БазаРаспределенияЗатрат.НоменклатурнаяГруппа КАК НоменклатурнаяГруппаНЗП,
	|	БазаРаспределенияЗатрат.Заказ КАК ЗаказНЗП,
	|
	|	БазаРаспределенияЗатрат.Продукция,
	|	БазаРаспределенияЗатрат.ХарактеристикаПродукции,
	|	БазаРаспределенияЗатрат.СерияПродукции,
	|	БазаРаспределенияЗатрат.Спецификация,
	|	
	|	БазаРаспределенияЗатрат.ВариантВыпускаПродукции,
	|	БазаРаспределенияЗатрат.ВидВыпуска,
	|
	|		
	|	БазаРаспределенияЗатрат.База,
	|	БазаРаспределенияЗатрат.БазаПриход,
	|	БазаРаспределенияЗатрат.БазаОстатокНЗП
	|
	|ПОМЕСТИТЬ БазаРаспределения%СуффиксУчета%
	|ИЗ
	|	//ДляУпрУчета  РегистрСведений.БазаРаспределенияЗатрат КАК БазаРаспределенияЗатрат
	|	//ДляРеглУчета РегистрСведений.БазаРаспределенияЗатратБухгалтерскийУчет КАК БазаРаспределенияЗатрат
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ
	|			*
	|		ИЗ
	|			РегистрСведений.АналитикаВидаУчета КАК РегистрАналитикаВидаУчета
	|		ГДЕ
	|			(РегистрАналитикаВидаУчета.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчета.Выпуск)
	|			ИЛИ РегистрАналитикаВидаУчета.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчета.Наработка))
	|			//ДляРеглУчета И РегистрАналитикаВидаУчета.Организация = &Организация
	|		) КАК РегистрАналитикаВидаУчета
	|	ПО
	|		БазаРаспределенияЗатрат.АналитикаВидаУчета = РегистрАналитикаВидаУчета.Ссылка
	|		
	|ГДЕ
	|	БазаРаспределенияЗатрат.Период МЕЖДУ &НачДата И &КонДата
	|	И БазаРаспределенияЗатрат.СпособРаспределенияЗатрат = &СпособРаспределенияЗатрат
	|	И Не РасчетСебестоимостиВыпуска
	|	//ДляРеглУчета И БазаРаспределенияЗатрат.Организация = &Организация
	|	
	|{ГДЕ
	|	БазаРаспределенияЗатрат.Подразделение.* КАК ПодразделениеОрганизации,
	|	БазаРаспределенияЗатрат.НоменклатурнаяГруппа.* КАК НоменклатурнаяГруппа,
	|	БазаРаспределенияЗатрат.Продукция.* КАК Продукция
	|}
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции // СформироватьТекстЗапросаБазаВручнуюРасширеннаяАналитика()

////////////////////////////////////////////////////////////////////////////////
// ФУНКЦИИ ФОРМИРОВАНИЯ ТЕКСТОВ ЗАПРОСА ДЛЯ РАСЧЕТА БАЗЫ РАСПРЕДЕЛЕНИЯ

// Функция формирования запроса для базы распределения по стоимости затрат.
//
// Возвращаемое значение:
//   Строка – Текст запроса
//
Функция СформироватьТекстЗапросаБазаПоСтоимостиЗатрат()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&СпособРаспределенияЗатрат КАК РассчитанныйСпособРаспределения,
	|
	|	//ДляУпрУчета ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
	|	//ДляРеглУчета ЗатратыНаВыпуск.Организация,
	|
	|	//ДляУпрУчета ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетУчета,
	|	//ДляРеглУчета ЗатратыНаВыпуск.СчетУчета,
	|	//ДляУпрУчета  ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначение,
	|	//ДляБухУчета  ЗатратыНаВыпуск.НалоговоеНазначениеПоФакту КАК НалоговоеНазначение,
	|
	|	ЗатратыНаВыпуск.Подразделение,
	|	ЗатратыНаВыпуск.НоменклатурнаяГруппа,
	|	ЗатратыНаВыпуск.Заказ,
	|
	|	ЗатратыНаВыпуск.ПодразделениеНЗП,
	|	ЗатратыНаВыпуск.НоменклатурнаяГруппаНЗП,
	|	ЗатратыНаВыпуск.ЗаказНЗП,
	|
	|	ЗатратыНаВыпуск.Продукция,
	|	ЗатратыНаВыпуск.ХарактеристикаПродукции,
	|	ЗатратыНаВыпуск.СерияПродукции,
	|	ЗатратыНаВыпуск.Спецификация,
	|	
	|	Неопределено КАК ВариантВыпускаПродукции,
	|	ВЫБОР КОГДА ЗатратыНаВыпуск.Спецификация = Неопределено ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ВидыВыпуска.Наработка)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ВидыВыпуска.Выпуск)
	|	КОНЕЦ КАК ВидВыпуска,
	|
	|		
	|	СУММА(
	|		ВЫБОР КОГДА НезавершенноеПроизводство.КоличествоОстаток <> 0
	|			И Не &РаспределениеКосвенныхЗатрат
	|		ТОГДА
	|			ЗатратыНаВыпуск.Количество *
	|			(ЕСТЬNULL(НезавершенноеПроизводство.СтоимостьОстаток, 0) 
	|			) /
	|			ЕСТЬNULL(НезавершенноеПроизводство.КоличествоОстаток, 1)
	|		ИНАЧЕ
	|			ЗатратыНаВыпуск.Сумма
	|		КОНЕЦ
	|	) КАК База,
	|	0 КАК БазаПриход,
	|	0 КАК БазаОстатокНЗП
	|
	|ПОМЕСТИТЬ БазаРаспределения%СуффиксУчета%
	|ИЗ
	|	РегистрНакопления.ЗатратыНаВыпускПродукции%СуффиксУчета% КАК ЗатратыНаВыпуск // заменяется на временную таблицу ЗатратыНаВыпуск
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.НезавершенноеПроизводство%СуффиксУчета%.Остатки(&КонГраница, 
	|			Не Затрата Ссылка Перечисление.ХарактерЗатрат
	|			//ДляРеглУчета И Организация = &Организация
	|			{
	|				//ДляУпрУчета Подразделение.* КАК Подразделение,
	|				//ДляРеглУчета Подразделение.* КАК ПодразделениеОрганизации,
	|				НоменклатурнаяГруппа.* КАК НоменклатурнаяГруппа,
	|				СтатьяЗатрат.* КАК СтатьяЗатрат,
	|				СтатьяЗатрат.ВидЗатрат.* КАК ВидЗатрат,
	|				СтатьяЗатрат.СтатусМатериальныхЗатрат.* КАК СтатусМатериальныхЗатрат,
	|				СтатьяЗатрат.ХарактерЗатрат.* КАК ХарактерЗатрат
	|			}
	|		) КАК НезавершенноеПроизводство
	|	ПО
	|		НезавершенноеПроизводство.Подразделение = ЗатратыНаВыпуск.ПодразделениеНЗП
	|		И НезавершенноеПроизводство.НоменклатурнаяГруппа = ЗатратыНаВыпуск.НоменклатурнаяГруппаНЗП
	|		И НезавершенноеПроизводство.СтатьяЗатрат = ЗатратыНаВыпуск.СтатьяЗатрат
	|		И НезавершенноеПроизводство.Затрата = ЗатратыНаВыпуск.Затрата
	|		И НезавершенноеПроизводство.ХарактеристикаЗатраты = ЗатратыНаВыпуск.ХарактеристикаЗатраты
	|		И НезавершенноеПроизводство.СерияЗатраты = ЗатратыНаВыпуск.СерияЗатраты
	|		И НезавершенноеПроизводство.Заказ = ЗатратыНаВыпуск.ЗаказНЗП
	|		
	|		//ДляРеглУчета И ((ЗатратыНаВыпуск.СтатьяЗатрат.СтатусМатериальныхЗатрат <>
	|		//ДляРеглУчета			ЗНАЧЕНИЕ(Перечисление.СтатусыМатериальныхЗатратНаПроизводство.ПринятыеВПереработку)
	|		//ДляРеглУчета	И НезавершенноеПроизводство.СчетУчета = ЗатратыНаВыпуск.СчетУчета
	|		//ДляБухУчета	И НезавершенноеПроизводство.НалоговоеНазначение = ЗатратыНаВыпуск.НалоговоеНазначение
	|       //ДляРеглУчета  )
	|		//ДляРеглУчета	ИЛИ ЗатратыНаВыпуск.СтатьяЗатрат.СтатусМатериальныхЗатрат =
	|		//ДляРеглУчета			ЗНАЧЕНИЕ(Перечисление.СтатусыМатериальныхЗатратНаПроизводство.ПринятыеВПереработку))
	|	
	|ГДЕ
	|	ЗатратыНаВыпуск.КодОперации В (&МассивКодыОперацийПрямыеЗатраты)
	|		
	|{ГДЕ
	|	//ДляУпрУчета ЗатратыНаВыпуск.Подразделение.* КАК Подразделение,
	|	//ДляРеглУчета ЗатратыНаВыпуск.Подразделение.* КАК ПодразделениеОрганизации,
	|	ЗатратыНаВыпуск.НоменклатурнаяГруппа.* КАК НоменклатурнаяГруппа,
	|	ЗатратыНаВыпуск.Продукция.* КАК Продукция,
	|	ЗатратыНаВыпуск.СтатьяЗатрат.* КАК СтатьяЗатрат,
	|	ЗатратыНаВыпуск.СтатьяЗатрат.ВидЗатрат.* КАК ВидЗатрат,
	|	ЗатратыНаВыпуск.СтатьяЗатрат.СтатусМатериальныхЗатрат.* КАК СтатусМатериальныхЗатрат,
	|	ЗатратыНаВыпуск.СтатьяЗатрат.ХарактерЗатрат.* КАК ХарактерЗатрат
	|}
	|
	|СГРУППИРОВАТЬ ПО
	|	//ДляРеглУчета ЗатратыНаВыпуск.Организация,
	|	//ДляРеглУчета ЗатратыНаВыпуск.СчетУчета,
	|	//ДляБухУчета ЗатратыНаВыпуск.НалоговоеНазначениеПоФакту,
	|	ЗатратыНаВыпуск.Подразделение,
	|	ЗатратыНаВыпуск.НоменклатурнаяГруппа,
	|	ЗатратыНаВыпуск.Заказ,
	|
	|	ЗатратыНаВыпуск.ПодразделениеНЗП,
	|	ЗатратыНаВыпуск.НоменклатурнаяГруппаНЗП,
	|	ЗатратыНаВыпуск.ЗаказНЗП,
	|
	|	ЗатратыНаВыпуск.Продукция,
	|	ЗатратыНаВыпуск.ХарактеристикаПродукции,
	|	ЗатратыНаВыпуск.СерияПродукции,
	|	ЗатратыНаВыпуск.Спецификация
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции // СформироватьТекстЗапросаБазаПоСтоимостиЗатрат()

// Функция формирования запроса для базы распределения по стоимости затрат.
//
// Возвращаемое значение:
//   Строка – Текст запроса
//
Функция СформироватьТекстЗапросаБазаПриходПоСтоимостиЗатрат()
	
	ТекстЗапроса = "
	|
	|ВЫБРАТЬ
	|	&СпособРаспределенияЗатрат КАК РассчитанныйСпособРаспределения,
	|
	|	//ДляУпрУчета ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
	|	//ДляРеглУчета НезавершенноеПроизводство.Организация,
	|
	|	//ДляУпрУчета ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетУчета,
	|	//ДляРеглУчета НезавершенноеПроизводство.СчетУчета,
	|	//ДляУпрУчета  ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначение,
	|	//ДляБухУчета  НезавершенноеПроизводство.НалоговоеНазначение,
	|
	|	НезавершенноеПроизводство.Подразделение,
	|	НезавершенноеПроизводство.НоменклатурнаяГруппа,
	|	НезавершенноеПроизводство.Заказ,
	|
	|	НезавершенноеПроизводство.Подразделение КАК ПодразделениеНЗП,
	|	НезавершенноеПроизводство.НоменклатурнаяГруппа КАК НоменклатурнаяГруппаНЗП,
	|	НезавершенноеПроизводство.Заказ КАК ЗаказНЗП,
	|	
	|	Неопределено КАК Продукция,
	|	Неопределено КАК ХарактеристикаПродукции,
	|	Неопределено КАК СерияПродукции,
	|	Неопределено КАК Спецификация,
	|	Неопределено КАК ВариантВыпускаПродукции,
	|	Неопределено КАК ВидВыпуска,
	|
	|
	|	0 КАК База,
	|	СУММА(НезавершенноеПроизводство.СтоимостьПриход
	|		) КАК БазаПриход,
	|	СУММА(НезавершенноеПроизводство.СтоимостьКонечныйОстаток
	|		) КАК БазаОстатокНЗП
	|	
	|ИЗ
	|	РегистрНакопления.НезавершенноеПроизводство%СуффиксУчета%.ОстаткиИОбороты(, , , , ) КАК НезавершенноеПроизводство // заменяется на временную таблицу НезавершенноеПроизводство
	|
	|{ГДЕ
	|	//ДляУпрУчета НезавершенноеПроизводство.Подразделение.* КАК Подразделение,
	|	//ДляРеглУчета НезавершенноеПроизводство.Подразделение.* КАК ПодразделениеОрганизации,
	|	НезавершенноеПроизводство.НоменклатурнаяГруппа.* КАК НоменклатурнаяГруппа,
	|	НезавершенноеПроизводство.СтатьяЗатрат.* КАК СтатьяЗатрат,
	|	НезавершенноеПроизводство.СтатьяЗатрат.ВидЗатрат.* КАК ВидЗатрат,
	|	НезавершенноеПроизводство.СтатьяЗатрат.СтатусМатериальныхЗатрат.* КАК СтатусМатериальныхЗатрат,
	|	НезавершенноеПроизводство.СтатьяЗатрат.ХарактерЗатрат.* КАК ХарактерЗатрат
	|}
	|
	|СГРУППИРОВАТЬ ПО
	|	//ДляРеглУчета НезавершенноеПроизводство.Организация,
	|	//ДляРеглУчета НезавершенноеПроизводство.СчетУчета,
	|	//ДляБухУчета  НезавершенноеПроизводство.НалоговоеНазначение,
	|	НезавершенноеПроизводство.Подразделение,
	|	НезавершенноеПроизводство.НоменклатурнаяГруппа,
	|	НезавершенноеПроизводство.Заказ
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции // СформироватьТекстЗапросаБазаПриходПоСтоимостиЗатрат()

// Функция формирования запроса для базы распределения по основному сырью.
//
// Возвращаемое значение:
//   Строка – Текст запроса
//
Функция СформироватьТекстЗапросаБазаПоОсновномуСырью()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&СпособРаспределенияЗатрат КАК РассчитанныйСпособРаспределения,
	|
	|	//ДляУпрУчета ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
	|	//ДляРеглУчета ЗатратыНаВыпуск.Организация,
	|
	|	//ДляУпрУчета ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетУчета,
	|	//ДляРеглУчета ЗатратыНаВыпуск.СчетУчета,
	|	//ДляУпрУчета  ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначение,
	|	//ДляБухУчета  ЗатратыНаВыпуск.НалоговоеНазначениеПоФакту КАК НалоговоеНазначение,
	|
	|	ЗатратыНаВыпуск.Подразделение,
	|	ЗатратыНаВыпуск.НоменклатурнаяГруппа,
	|	ЗатратыНаВыпуск.Заказ,
	|
	|	ЗатратыНаВыпуск.ПодразделениеНЗП,
	|	ЗатратыНаВыпуск.НоменклатурнаяГруппаНЗП,
	|	ЗатратыНаВыпуск.ЗаказНЗП,
	|
	|	ЗатратыНаВыпуск.Продукция,
	|	ЗатратыНаВыпуск.ХарактеристикаПродукции,
	|	ЗатратыНаВыпуск.СерияПродукции,
	|	ЗатратыНаВыпуск.Спецификация,
	|	
	|	Неопределено КАК ВариантВыпускаПродукции,
	|	ВЫБОР КОГДА ЗатратыНаВыпуск.Спецификация = Неопределено ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ВидыВыпуска.Наработка)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ВидыВыпуска.Выпуск)
	|	КОНЕЦ КАК ВидВыпуска,
	|
	|		
	|	СУММА(
	|		ЗатратыНаВыпуск.Количество *
	|		ВЫБОР КОГДА	&ПоказательБазыРаспределения 
	|			= ЗНАЧЕНИЕ(Перечисление.ВидыПоказателейБазыРаспределения.ЦеныНоменклатуры)
	|		ТОГДА
	|			ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0)
	|		ИНАЧЕ
	|			1
	|		КОНЕЦ
	|	) КАК База,
	|	0 КАК БазаПриход,
	|	0 КАК БазаОстатокНЗП
	|
	|ПОМЕСТИТЬ БазаРаспределения%СуффиксУчета%
	|ИЗ
	|	РегистрНакопления.ЗатратыНаВыпускПродукции%СуффиксУчета% КАК ЗатратыНаВыпуск // заменяется на временную таблицу ЗатратыНаВыпуск
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			Номенклатура,
	|			ХарактеристикаНоменклатуры,
	|			Цена
	|		ИЗ
	|			РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры  // заменяется на временную таблицу ЦеныНоменклатуры
	|		ГДЕ
	|			ТипЦен = &ТипЦен
	|			И &ПоказательБазыРаспределения = ЗНАЧЕНИЕ(Перечисление.ВидыПоказателейБазыРаспределения.ЦеныНоменклатуры)
	|			
	|		) КАК ЦеныНоменклатуры
	|	ПО 
	|		ЗатратыНаВыпуск.Затрата = ЦеныНоменклатуры.Номенклатура
	|		И ЗатратыНаВыпуск.ХарактеристикаЗатраты = ЦеныНоменклатуры.ХарактеристикаНоменклатуры	
	|	
	|ГДЕ
	|	ЗатратыНаВыпуск.КодОперации В (&МассивКодыОперацийПрямыеЗатраты)
	|	И ЗатратыНаВыпуск.Затрата В (
	|		ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			ОсновноеСырьеСостав.Номенклатура
	|		ИЗ
	|			Справочник.ОсновноеСырье.Состав КАК ОсновноеСырьеСостав
	|		ГДЕ
	|			ОсновноеСырьеСостав.Ссылка = &ОсновноеСырье
	|		)
	|		
	|{ГДЕ
	|	//ДляУпрУчета ЗатратыНаВыпуск.Подразделение.* КАК Подразделение,
	|	//ДляРеглУчета ЗатратыНаВыпуск.Подразделение.* КАК ПодразделениеОрганизации,
	|	ЗатратыНаВыпуск.НоменклатурнаяГруппа.* КАК НоменклатурнаяГруппа,
	|	ЗатратыНаВыпуск.Продукция.* КАК Продукция,
	|	ЗатратыНаВыпуск.СтатьяЗатрат.* КАК СтатьяЗатрат,
	|	ЗатратыНаВыпуск.СтатьяЗатрат.ВидЗатрат.* КАК ВидЗатрат,
	|	ЗатратыНаВыпуск.СтатьяЗатрат.СтатусМатериальныхЗатрат.* КАК СтатусМатериальныхЗатрат,
	|	ЗатратыНаВыпуск.СтатьяЗатрат.ХарактерЗатрат.* КАК ХарактерЗатрат
	|}
	|
	|СГРУППИРОВАТЬ ПО
	|	//ДляРеглУчета ЗатратыНаВыпуск.Организация,
	|	//ДляРеглУчета ЗатратыНаВыпуск.СчетУчета,
	|	//ДляБухУчета ЗатратыНаВыпуск.НалоговоеНазначениеПоФакту,
	|	ЗатратыНаВыпуск.Подразделение,
	|	ЗатратыНаВыпуск.НоменклатурнаяГруппа,
	|	ЗатратыНаВыпуск.Заказ,
	|
	|	ЗатратыНаВыпуск.ПодразделениеНЗП,
	|	ЗатратыНаВыпуск.НоменклатурнаяГруппаНЗП,
	|	ЗатратыНаВыпуск.ЗаказНЗП,
	|
	|	ЗатратыНаВыпуск.Продукция,
	|	ЗатратыНаВыпуск.ХарактеристикаПродукции,
	|	ЗатратыНаВыпуск.СерияПродукции,
	|	ЗатратыНаВыпуск.Спецификация
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции // СформироватьТекстЗапросаБазаПоОсновномуСырью()

// Функция формирования запроса для базы распределения по основному сырью.
//
// Возвращаемое значение:
//   Строка – Текст запроса
//
Функция СформироватьТекстЗапросаБазаПриходПоОсновномуСырью()
	
	ТекстЗапроса = "
	|
	|ВЫБРАТЬ
	|	&СпособРаспределенияЗатрат КАК РассчитанныйСпособРаспределения,
	|
	|	//ДляУпрУчета ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
	|	//ДляРеглУчета НезавершенноеПроизводство.Организация,
	|
	|	//ДляУпрУчета ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетУчета,
	|	//ДляРеглУчета НезавершенноеПроизводство.СчетУчета,
	|	//ДляУпрУчета  ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначение,
	|	//ДляБухУчета  НезавершенноеПроизводство.НалоговоеНазначение,
	|
	|	НезавершенноеПроизводство.Подразделение,
	|	НезавершенноеПроизводство.НоменклатурнаяГруппа,
	|	НезавершенноеПроизводство.Заказ,
	|
	|	НезавершенноеПроизводство.Подразделение КАК ПодразделениеНЗП,
	|	НезавершенноеПроизводство.НоменклатурнаяГруппа КАК НоменклатурнаяГруппаНЗП,
	|	НезавершенноеПроизводство.Заказ КАК ЗаказНЗП,
	|	
	|	Неопределено КАК Продукция,
	|	Неопределено КАК ХарактеристикаПродукции,
	|	Неопределено КАК СерияПродукции,
	|	Неопределено КАК Спецификация,
	|	Неопределено КАК ВариантВыпускаПродукции,
	|	Неопределено КАК ВидВыпуска,
	|
	|
	|	0 КАК База,
	|	СУММА(
	|		НезавершенноеПроизводство.КоличествоПриход *
	|		ВЫБОР КОГДА	&ПоказательБазыРаспределения 
	|			= ЗНАЧЕНИЕ(Перечисление.ВидыПоказателейБазыРаспределения.ЦеныНоменклатуры)
	|		ТОГДА
	|			ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0)
	|		ИНАЧЕ
	|			1
	|		КОНЕЦ
	|	) КАК БазаПриход,
	|		
	|	СУММА(
	|		НезавершенноеПроизводство.КоличествоКонечныйОстаток *
	|		ВЫБОР КОГДА	&ПоказательБазыРаспределения 
	|			= ЗНАЧЕНИЕ(Перечисление.ВидыПоказателейБазыРаспределения.ЦеныНоменклатуры)
	|		ТОГДА
	|			ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0)
	|		ИНАЧЕ
	|			1
	|		КОНЕЦ
	|	) КАК БазаОстатокНЗП
	|	
	|ИЗ
	|	РегистрНакопления.НезавершенноеПроизводство%СуффиксУчета%.ОстаткиИОбороты(, , , , ) КАК НезавершенноеПроизводство // заменяется на временную таблицу НезавершенноеПроизводство
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			Номенклатура,
	|			ХарактеристикаНоменклатуры,
	|			Цена
	|		ИЗ
	|			РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры  // заменяется на временную таблицу ЦеныНоменклатуры
	|		ГДЕ
	|			ТипЦен = &ТипЦен
	|			И &ПоказательБазыРаспределения = ЗНАЧЕНИЕ(Перечисление.ВидыПоказателейБазыРаспределения.ЦеныНоменклатуры)
	|			
	|		) КАК ЦеныНоменклатуры
	|	ПО 
	|		НезавершенноеПроизводство.Затрата = ЦеныНоменклатуры.Номенклатура
	|		И НезавершенноеПроизводство.ХарактеристикаЗатраты = ЦеныНоменклатуры.ХарактеристикаНоменклатуры	
	|
	|ГДЕ
	|	НезавершенноеПроизводство.Затрата В (
	|		ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			ОсновноеСырьеСостав.Номенклатура
	|		ИЗ
	|			Справочник.ОсновноеСырье.Состав КАК ОсновноеСырьеСостав
	|		ГДЕ
	|			ОсновноеСырьеСостав.Ссылка = &ОсновноеСырье
	|		)
	|
	|{ГДЕ
	|	//ДляУпрУчета НезавершенноеПроизводство.Подразделение.* КАК Подразделение,
	|	//ДляРеглУчета НезавершенноеПроизводство.Подразделение.* КАК ПодразделениеОрганизации,
	|	НезавершенноеПроизводство.НоменклатурнаяГруппа.* КАК НоменклатурнаяГруппа,
	|	НезавершенноеПроизводство.СтатьяЗатрат.* КАК СтатьяЗатрат,
	|	НезавершенноеПроизводство.СтатьяЗатрат.ВидЗатрат.* КАК ВидЗатрат,
	|	НезавершенноеПроизводство.СтатьяЗатрат.СтатусМатериальныхЗатрат.* КАК СтатусМатериальныхЗатрат,
	|	НезавершенноеПроизводство.СтатьяЗатрат.ХарактерЗатрат.* КАК ХарактерЗатрат
	|}
	|
	|СГРУППИРОВАТЬ ПО
	|	//ДляРеглУчета НезавершенноеПроизводство.Организация,
	|	//ДляРеглУчета НезавершенноеПроизводство.СчетУчета,
	|	//ДляБухУчета  НезавершенноеПроизводство.НалоговоеНазначение,
	|	НезавершенноеПроизводство.Подразделение,
	|	НезавершенноеПроизводство.НоменклатурнаяГруппа,
	|	НезавершенноеПроизводство.Заказ
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции // СформироватьТекстЗапросаБазаПриходПоОсновномуСырью()

// Функция формирования запроса для базы распределения по объему выпуска
// по типу цен номенклатуры.
//
// Возвращаемое значение:
//   Строка – Текст запроса
//
Функция СформироватьТекстЗапросаБазаПоОбъемуВыпускаЦеныНоменклатуры()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&СпособРаспределенияЗатрат КАК РассчитанныйСпособРаспределения,
	|
	|	//ДляУпрУчета ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
	|	//ДляРеглУчета ВыпускПродукции.Организация,
	|
	|	//ДляУпрУчета ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетУчета,
	|	//ДляРеглУчета ВыпускПродукции.СчетУчетаНЗП КАК СчетУчета,
	|	//ДляУпрУчета  ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначение,
	|	//ДляБухУчета  ВыпускПродукции.НалоговоеНазначение,
	|
	|	ВыпускПродукции.Подразделение,
	|	ВыпускПродукции.НоменклатурнаяГруппа,
	|	ВыпускПродукции.Заказ,
	|
	|	ВыпускПродукции.Подразделение КАК ПодразделениеНЗП,
	|	ВыпускПродукции.НоменклатурнаяГруппа КАК НоменклатурнаяГруппаНЗП,
	|	ВыпускПродукции.Заказ КАК ЗаказНЗП,
	|
	|	ВыпускПродукции.Продукция,
	|	ВыпускПродукции.ХарактеристикаПродукции,
	|	ВыпускПродукции.СерияПродукции,
	|	ВЫБОР КОГДА ВыпускПродукции.Спецификация = Неопределено ТОГДА
	|		ЗНАЧЕНИЕ(Справочник.СпецификацииНоменклатуры.ПустаяСсылка)
	|	ИНАЧЕ
	|		ВыпускПродукции.Спецификация
	|	КОНЕЦ КАК Спецификация,
	|	
	|	ВыпускПродукции.ВариантВыпускаПродукции,
	|	
	|	ВЫБОР КОГДА ВыпускПродукции.Спецификация = Неопределено ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ВидыВыпуска.Наработка)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ВидыВыпуска.Выпуск)
	|	КОНЕЦ КАК ВидВыпуска,
	|
	|
	|	СУММА(
	|		ВыпускПродукции.Количество *
	|		ВЫБОР КОГДА	&ПоказательБазыРаспределения 
	|			= ЗНАЧЕНИЕ(Перечисление.ВидыПоказателейБазыРаспределения.ЦеныНоменклатуры)
	|		ТОГДА
	|			ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0)
	|		ИНАЧЕ
	|			1
	|		КОНЕЦ
	|		) КАК База,
	|	СУММА(
	|		ВыпускПродукции.Количество *
	|		ВЫБОР КОГДА	&ПоказательБазыРаспределения 
	|			= ЗНАЧЕНИЕ(Перечисление.ВидыПоказателейБазыРаспределения.ЦеныНоменклатуры)
	|		ТОГДА
	|			ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0)
	|		ИНАЧЕ
	|			1
	|		КОНЕЦ
	|		) КАК БазаПриход,
	|	0 КАК БазаОстатокНЗП
	|
	|ПОМЕСТИТЬ БазаРаспределения%СуффиксУчета%
	|ИЗ
	|	РегистрНакопления.ВыпускПродукции%СуффиксУчета% КАК ВыпускПродукции // заменяется на временную таблицу ВыпускПродукции
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			Номенклатура,
	|			ХарактеристикаНоменклатуры,
	|			Цена
	|		ИЗ
	|			РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры  // заменяется на временную таблицу ЦеныНоменклатуры
	|		ГДЕ
	|			ТипЦен = &ТипЦен
	|			И &ПоказательБазыРаспределения = ЗНАЧЕНИЕ(Перечисление.ВидыПоказателейБазыРаспределения.ЦеныНоменклатуры)
	|			
	|		) КАК ЦеныНоменклатуры
	|	ПО 
	|		ВыпускПродукции.Продукция = ЦеныНоменклатуры.Номенклатура
	|		И ВыпускПродукции.ХарактеристикаПродукции = ЦеныНоменклатуры.ХарактеристикаНоменклатуры
	|	
	|{ГДЕ
	|	//ДляУпрУчета ВыпускПродукции.Подразделение.* КАК Подразделение,
	|	//ДляРеглУчета ВыпускПродукции.Подразделение.* КАК ПодразделениеОрганизации,
	|	ВыпускПродукции.НоменклатурнаяГруппа.* КАК НоменклатурнаяГруппа,
	|	ВыпускПродукции.Продукция.* КАК Продукция
	|}
	|
	|СГРУППИРОВАТЬ ПО
	|	//ДляРеглУчета ВыпускПродукции.Организация,
	|	//ДляРеглУчета ВыпускПродукции.СчетУчетаНЗП,
	|	//ДляБухУчета  ВыпускПродукции.НалоговоеНазначение,
	|	ВыпускПродукции.Подразделение,
	|	ВыпускПродукции.НоменклатурнаяГруппа,
	|	ВыпускПродукции.Заказ,
	|
	|	ВыпускПродукции.Продукция,
	|	ВыпускПродукции.ХарактеристикаПродукции,
	|	ВыпускПродукции.СерияПродукции,
	|	ВыпускПродукции.ВариантВыпускаПродукции,
	|	ВЫБОР КОГДА ВыпускПродукции.Спецификация = Неопределено ТОГДА
	|		ЗНАЧЕНИЕ(Справочник.СпецификацииНоменклатуры.ПустаяСсылка)
	|	ИНАЧЕ
	|		ВыпускПродукции.Спецификация
	|	КОНЕЦ,
	|	ВЫБОР КОГДА ВыпускПродукции.Спецификация = Неопределено ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ВидыВыпуска.Наработка)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ВидыВыпуска.Выпуск)
	|	КОНЕЦ
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции // СформироватьТекстЗапросаБазаПоОбъемуВыпускаЦеныНоменклатуры()

// Функция формирования запроса для базы распределения по объему выпуска
// по типу цен номенклатуры.
//
// Возвращаемое значение:
//   Строка – Текст запроса
//
Функция СформироватьТекстЗапросаБазаФактическаяЦенаРеализации()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&СпособРаспределенияЗатрат КАК РассчитанныйСпособРаспределения,
	|
	|	//ДляУпрУчета ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
	|	//ДляРеглУчета ВыпускПродукции.Организация,
	|
	|	//ДляУпрУчета ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетУчета,
	|	//ДляРеглУчета ВыпускПродукции.СчетУчетаНЗП КАК СчетУчета,
	|	//ДляУпрУчета  ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначение,
	|	//ДляБухУчета  ВыпускПродукции.НалоговоеНазначение,
	|
	|	ВыпускПродукции.Подразделение,
	|	ВыпускПродукции.НоменклатурнаяГруппа,
	|	ВыпускПродукции.Заказ,
	|
	|	ВыпускПродукции.Подразделение КАК ПодразделениеНЗП,
	|	ВыпускПродукции.НоменклатурнаяГруппа КАК НоменклатурнаяГруппаНЗП,
	|	ВыпускПродукции.Заказ КАК ЗаказНЗП,
	|
	|	ВыпускПродукции.Продукция,
	|	ВыпускПродукции.ХарактеристикаПродукции,
	|	ВыпускПродукции.СерияПродукции,
	|	ВЫБОР КОГДА ВыпускПродукции.Спецификация = Неопределено ТОГДА
	|		ЗНАЧЕНИЕ(Справочник.СпецификацииНоменклатуры.ПустаяСсылка)
	|	ИНАЧЕ
	|		ВыпускПродукции.Спецификация
	|	КОНЕЦ КАК Спецификация,
	|	
	|	ВыпускПродукции.ВариантВыпускаПродукции,
	|	
	|	ВЫБОР КОГДА ВыпускПродукции.Спецификация = Неопределено ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ВидыВыпуска.Наработка)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ВидыВыпуска.Выпуск)
	|	КОНЕЦ КАК ВидВыпуска,
	|
	|
	|	СУММА(
	|		ВыпускПродукции.Количество *
	|		ВЫБОР КОГДА Продажи.Количество <> 0 ТОГДА
	|   		Продажи.Стоимость /
	|   		Продажи.Количество
	|		ИНАЧЕ
	|			1
	|		КОНЕЦ
	|		) КАК База,
	|	СУММА(
	|		ВыпускПродукции.Количество *
	|		ВЫБОР КОГДА Продажи.Количество <> 0 ТОГДА
	|   		Продажи.Стоимость /
	|   		Продажи.Количество
	|		ИНАЧЕ
	|			1
	|		КОНЕЦ
	|		) КАК БазаПриход,
	|	0 КАК БазаОстатокНЗП
	|
	|ПОМЕСТИТЬ БазаРаспределения%СуффиксУчета%
	|ИЗ
	|	РегистрНакопления.ВыпускПродукции%СуффиксУчета% КАК ВыпускПродукции // заменяется на временную таблицу ВыпускПродукции
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ВыпускПродукции КАК Продажи // заменяется на временную таблицу Продажи
	|	ПО
	|		ВыпускПродукции.Продукция = Продажи.Продукция
	|		И ВыпускПродукции.ХарактеристикаПродукции = Продажи.ХарактеристикаПродукции
	|		И ВыпускПродукции.СерияПродукции = Продажи.СерияПродукции
	|		И (ВыпускПродукции.Заказ = Продажи.Заказ
	|			ИЛИ ВыпускПродукции.Заказ = Неопределено
	|			ИЛИ ВыпускПродукции.Заказ ССЫЛКА Документ.ЗаказНаПроизводство)
	|	
	|{ГДЕ
	|	//ДляУпрУчета ВыпускПродукции.Подразделение.* КАК Подразделение,
	|	//ДляРеглУчета ВыпускПродукции.Подразделение.* КАК ПодразделениеОрганизации,
	|	ВыпускПродукции.НоменклатурнаяГруппа.* КАК НоменклатурнаяГруппа,
	|	ВыпускПродукции.Продукция.* КАК Продукция
	|}
	|
	|СГРУППИРОВАТЬ ПО
	|	//ДляРеглУчета ВыпускПродукции.Организация,
	|	//ДляРеглУчета ВыпускПродукции.СчетУчетаНЗП,
	|	//ДляБухУчета  ВыпускПродукции.НалоговоеНазначение,
	|	ВыпускПродукции.Подразделение,
	|	ВыпускПродукции.НоменклатурнаяГруппа,
	|	ВыпускПродукции.Заказ,
	|
	|	ВыпускПродукции.Продукция,
	|	ВыпускПродукции.ХарактеристикаПродукции,
	|	ВыпускПродукции.СерияПродукции,
	|	ВыпускПродукции.ВариантВыпускаПродукции,
	|	ВЫБОР КОГДА ВыпускПродукции.Спецификация = Неопределено ТОГДА
	|		ЗНАЧЕНИЕ(Справочник.СпецификацииНоменклатуры.ПустаяСсылка)
	|	ИНАЧЕ
	|		ВыпускПродукции.Спецификация
	|	КОНЕЦ,
	|	ВЫБОР КОГДА ВыпускПродукции.Спецификация = Неопределено ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ВидыВыпуска.Наработка)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ВидыВыпуска.Выпуск)
	|	КОНЕЦ
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции // СформироватьТекстЗапросаБазаФактическаяЦенаРеализации()

// Функция формирования запроса для базы распределения по нормативам.
//
// Возвращаемое значение:
//   Строка – Текст запроса
//
Функция СформироватьТекстЗапросаБазаПоНормативам()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&СпособРаспределенияЗатрат КАК РассчитанныйСпособРаспределения,
	|
	|	//ДляУпрУчета ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
	|	//ДляРеглУчета ВыпускПродукции.Организация,
	|
	|	//ДляУпрУчета ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетУчета,
	|	//ДляРеглУчета ВыпускПродукции.СчетУчетаНЗП КАК СчетУчета,
	|	//ДляУпрУчета  ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначение,
	|	//ДляБухУчета  ВыпускПродукции.НалоговоеНазначение,
	|
	|	ВыпускПродукции.Подразделение,
	|	ВыпускПродукции.НоменклатурнаяГруппа,
	|	ВыпускПродукции.Заказ,
	|
	|	ВыпускПродукции.Подразделение КАК ПодразделениеНЗП,
	|	ВыпускПродукции.НоменклатурнаяГруппа КАК НоменклатурнаяГруппаНЗП,
	|	ВыпускПродукции.Заказ КАК ЗаказНЗП,
	|
	|	ВыпускПродукции.Продукция,
	|	ВыпускПродукции.ХарактеристикаПродукции,
	|	ВыпускПродукции.СерияПродукции,
	|	ВЫБОР КОГДА ВыпускПродукции.Спецификация = Неопределено ТОГДА
	|		ЗНАЧЕНИЕ(Справочник.СпецификацииНоменклатуры.ПустаяСсылка)
	|	ИНАЧЕ
	|		ВыпускПродукции.Спецификация
	|	КОНЕЦ КАК Спецификация,
	|	
	|	ВыпускПродукции.ВариантВыпускаПродукции,
	|	
	|	ВЫБОР КОГДА ВыпускПродукции.Спецификация = Неопределено ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ВидыВыпуска.Наработка)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ВидыВыпуска.Выпуск)
	|	КОНЕЦ КАК ВидВыпуска,
	|
	|		
	|	СУММА(
	|		ВыпускПродукции.Количество *
	|		ЕСТЬNULL(ПлановаяСебестоимость.Цена, 0)
	|		) КАК База,
	|	СУММА(
	|		ВыпускПродукции.Количество *
	|		ЕСТЬNULL(ПлановаяСебестоимость.Цена, 0)
	|		) КАК БазаПриход,
	|	0 КАК БазаОстатокНЗП
	|
	|ПОМЕСТИТЬ БазаРаспределения%СуффиксУчета%
	|ИЗ
	|	РегистрНакопления.ВыпускПродукции%СуффиксУчета% КАК ВыпускПродукции // заменяется на временную таблицу ВыпускПродукции
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ
	|			ПлановаяСебестоимость.Номенклатура,
	|			ПлановаяСебестоимость.ХарактеристикаНоменклатуры,
	|			СУММА(ПлановаяСебестоимость.Сумма) КАК Цена
	|		ИЗ
	|	 		РегистрСведений.ПлановаяСебестоимостьНоменклатуры.СрезПоследних(&КонДата,
	|			{
	|				СтатьяЗатрат.* 	КАК СтатьяЗатрат,
	|				СтатьяЗатрат.ВидЗатрат.* КАК ВидЗатрат,
	|				СтатьяЗатрат.СтатусМатериальныхЗатрат.* КАК СтатусМатериальныхЗатрат,
	|				СтатьяЗатрат.ХарактерЗатрат.* КАК ХарактерЗатрат
	|			}
	|			) КАК ПлановаяСебестоимость
	|		СГРУППИРОВАТЬ ПО
	|			ПлановаяСебестоимость.Номенклатура,
	|			ПлановаяСебестоимость.ХарактеристикаНоменклатуры
	|			
	|		) КАК ПлановаяСебестоимость
	|	ПО 
	|		ВыпускПродукции.Продукция = ПлановаяСебестоимость.Номенклатура
	|		И ВыпускПродукции.ХарактеристикаПродукции = ПлановаяСебестоимость.ХарактеристикаНоменклатуры
	|	
	|{ГДЕ
	|	//ДляУпрУчета ВыпускПродукции.Подразделение.* КАК Подразделение,
	|	//ДляРеглУчета ВыпускПродукции.Подразделение.* КАК ПодразделениеОрганизации,
	|	ВыпускПродукции.НоменклатурнаяГруппа.* КАК НоменклатурнаяГруппа,
	|	ВыпускПродукции.Продукция.* КАК Продукция
	|}
	|
	|СГРУППИРОВАТЬ ПО
	|	//ДляРеглУчета ВыпускПродукции.Организация,
	|	//ДляРеглУчета ВыпускПродукции.СчетУчетаНЗП,
	|	//ДляБухУчета  ВыпускПродукции.НалоговоеНазначение,
	|	ВыпускПродукции.Подразделение,
	|	ВыпускПродукции.НоменклатурнаяГруппа,
	|	ВыпускПродукции.Заказ,
	|
	|	ВыпускПродукции.Продукция,
	|	ВыпускПродукции.ХарактеристикаПродукции,
	|	ВыпускПродукции.СерияПродукции,
	|	ВыпускПродукции.ВариантВыпускаПродукции,
	|	ВЫБОР КОГДА ВыпускПродукции.Спецификация = Неопределено ТОГДА
	|		ЗНАЧЕНИЕ(Справочник.СпецификацииНоменклатуры.ПустаяСсылка)
	|	ИНАЧЕ
	|		ВыпускПродукции.Спецификация
	|	КОНЕЦ,
	|	ВЫБОР КОГДА ВыпускПродукции.Спецификация = Неопределено ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ВидыВыпуска.Наработка)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ВидыВыпуска.Выпуск)
	|	КОНЕЦ
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции // СформироватьТекстЗапросаБазаПоНормативам()

// Функция формирования запроса для базы распределения по объему продаж
// по типу цен номенклатуры.
//
// Возвращаемое значение:
//   Строка – Текст запроса
//
Функция СформироватьТекстЗапросаБазаПоОбъемуПродаж()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&СпособРаспределенияЗатрат КАК РассчитанныйСпособРаспределения,
	|
	|	//ДляУпрУчета ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
	|	//ДляРеглУчета ВыпускПродукции.Организация,
	|
	|	//ДляУпрУчета ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетУчета,
	|	//ДляРеглУчета ВыпускПродукции.СчетУчетаНЗП КАК СчетУчета,
	|	//ДляУпрУчета  ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначение,
	|	//ДляБухУчета  ВыпускПродукции.НалоговоеНазначение,
	|
	|	ВыпускПродукции.Подразделение,
	|	ВыпускПродукции.НоменклатурнаяГруппа,
	|	ВыпускПродукции.Заказ,
	|
	|	ВыпускПродукции.Подразделение КАК ПодразделениеНЗП,
	|	ВыпускПродукции.НоменклатурнаяГруппа КАК НоменклатурнаяГруппаНЗП,
	|	ВыпускПродукции.Заказ КАК ЗаказНЗП,
	|
	|	ВыпускПродукции.Продукция,
	|	ВыпускПродукции.ХарактеристикаПродукции,
	|	ВыпускПродукции.СерияПродукции,
	|	ВыпускПродукции.Спецификация,
	|	
	|	ВыпускПродукции.ВариантВыпускаПродукции,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыВыпуска.Выпуск) КАК ВидВыпуска,
	|
	|
	|	СУММА(
	|		ВыпускПродукции.Количество *
	|		ВЫБОР КОГДА ВыпускПродукцииВсего.Количество <> 0 ТОГДА
	|			Продажи.Количество /
	|			ВыпускПродукцииВсего.Количество
	|		ИНАЧЕ
	|			1
	|		КОНЕЦ *
	|		ВЫБОР КОГДА	&ПоказательБазыРаспределения 
	|			= ЗНАЧЕНИЕ(Перечисление.ВидыПоказателейБазыРаспределения.ЦеныНоменклатуры)
	|		ТОГДА
	|			ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0)
	|		КОГДА &ПоказательБазыРаспределения 
	|			= ЗНАЧЕНИЕ(Перечисление.ВидыПоказателейБазыРаспределения.ФактическаяЦенаРеализации)
	|			И Продажи.Количество <> 0
	|		ТОГДА
	|        	Продажи.Стоимость /
	|        	Продажи.Количество
	|		ИНАЧЕ
	|			1
	|		КОНЕЦ
	|		) КАК База,
	|	СУММА(
	|		ВыпускПродукции.Количество *
	|		ВЫБОР КОГДА ВыпускПродукцииВсего.Количество <> 0 ТОГДА
	|			Продажи.Количество /
	|			ВыпускПродукцииВсего.Количество
	|		ИНАЧЕ
	|			1
	|		КОНЕЦ *
	|		ВЫБОР КОГДА	&ПоказательБазыРаспределения 
	|			= ЗНАЧЕНИЕ(Перечисление.ВидыПоказателейБазыРаспределения.ЦеныНоменклатуры)
	|		ТОГДА
	|			ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0)
	|		КОГДА &ПоказательБазыРаспределения 
	|			= ЗНАЧЕНИЕ(Перечисление.ВидыПоказателейБазыРаспределения.ФактическаяЦенаРеализации)
	|			И Продажи.Количество <> 0
	|		ТОГДА
	|        	Продажи.Стоимость /
	|        	Продажи.Количество
	|		ИНАЧЕ
	|			1
	|		КОНЕЦ
	|		) КАК БазаПриход,
	|	0 КАК БазаОстатокНЗП
	|
	|ПОМЕСТИТЬ БазаРаспределения%СуффиксУчета%
	|ИЗ
	|	РегистрНакопления.ВыпускПродукции%СуффиксУчета% КАК ВыпускПродукции // заменяется на временную таблицу ВыпускПродукции
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ВыпускПродукции КАК Продажи // заменяется на временную таблицу Продажи
	|	ПО
	|		ВыпускПродукции.Продукция = Продажи.Продукция
	|		И ВыпускПродукции.ХарактеристикаПродукции = Продажи.ХарактеристикаПродукции
	|		И ВыпускПродукции.СерияПродукции = Продажи.СерияПродукции
	|		И (ВыпускПродукции.Заказ = Продажи.Заказ
	|			ИЛИ ВыпускПродукции.Заказ = Неопределено
	|			ИЛИ ВыпускПродукции.Заказ ССЫЛКА Документ.ЗаказНаПроизводство)
	|		И (ВыпускПродукции.Подразделение = Продажи.Подразделение
	|			ИЛИ Продажи.Подразделение = Неопределено)
	|			
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (
	|
	|		// Выпуск продукции по всем подразделениям.
	|		ВЫБРАТЬ
	|			Неопределено КАК Подразделение,
	|			ВыпускПродукции.Продукция,
	|			ВыпускПродукции.ХарактеристикаПродукции,
	|			ВыпускПродукции.СерияПродукции,
	|			ВыпускПродукции.Заказ,
	|			СУММА(ВыпускПродукции.Количество) КАК Количество
	|	
	|		ИЗ
	|			РегистрНакопления.ВыпускПродукции%СуффиксУчета% КАК ВыпускПродукции // заменяется на временную таблицу ВыпускПродукции
	|
	|		СГРУППИРОВАТЬ ПО
	|			ВыпускПродукции.Продукция,
	|			ВыпускПродукции.ХарактеристикаПродукции,
	|			ВыпускПродукции.СерияПродукции,
	|			ВыпускПродукции.Заказ
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|			
	|		// Выпуск продукции по подразделению.
	|		ВЫБРАТЬ
	|			ВыпускПродукции.Подразделение,
	|			ВыпускПродукции.Продукция,
	|			ВыпускПродукции.ХарактеристикаПродукции,
	|			ВыпускПродукции.СерияПродукции,
	|			ВыпускПродукции.Заказ,
	|			СУММА(ВыпускПродукции.Количество) КАК Количество
	|	
	|		ИЗ
	|			РегистрНакопления.ВыпускПродукции%СуффиксУчета% КАК ВыпускПродукции // заменяется на временную таблицу ВыпускПродукции
	|
	|		СГРУППИРОВАТЬ ПО
	|			ВыпускПродукции.Подразделение,
	|			ВыпускПродукции.Продукция,
	|			ВыпускПродукции.ХарактеристикаПродукции,
	|			ВыпускПродукции.СерияПродукции,
	|			ВыпускПродукции.Заказ	
	|
	|		) КАК ВыпускПродукцииВсего
	|	ПО
	|		ВыпускПродукции.Продукция = ВыпускПродукцииВсего.Продукция
	|		И ВыпускПродукции.ХарактеристикаПродукции = ВыпускПродукцииВсего.ХарактеристикаПродукции
	|		И ВыпускПродукции.СерияПродукции = ВыпускПродукцииВсего.СерияПродукции
	|		И ВыпускПродукции.Заказ = ВыпускПродукцииВсего.Заказ
	|		И Продажи.Подразделение = ВыпускПродукцииВсего.Подразделение
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			Номенклатура,
	|			ХарактеристикаНоменклатуры,
	|			Цена
	|		ИЗ
	|			РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры  // заменяется на временную таблицу ЦеныНоменклатуры
	|		ГДЕ
	|			ТипЦен = &ТипЦен
	|			И &ПоказательБазыРаспределения = ЗНАЧЕНИЕ(Перечисление.ВидыПоказателейБазыРаспределения.ЦеныНоменклатуры)
	|			
	|		) КАК ЦеныНоменклатуры
	|	ПО 
	|		ВыпускПродукции.Продукция = ЦеныНоменклатуры.Номенклатура
	|		И ВыпускПродукции.ХарактеристикаПродукции = ЦеныНоменклатуры.ХарактеристикаНоменклатуры
	|
	|ГДЕ
	|	ВыпускПродукции.Спецификация <> Неопределено
	|	
	|{ГДЕ
	|	//ДляУпрУчета ВыпускПродукции.Подразделение.* КАК Подразделение,
	|	//ДляРеглУчета ВыпускПродукции.Подразделение.* КАК ПодразделениеОрганизации,
	|	ВыпускПродукции.НоменклатурнаяГруппа.* КАК НоменклатурнаяГруппа,
	|	ВыпускПродукции.Продукция.* КАК Продукция
	|}
	|
	|СГРУППИРОВАТЬ ПО
	|	//ДляРеглУчета ВыпускПродукции.Организация,
	|	//ДляРеглУчета ВыпускПродукции.СчетУчетаНЗП,
	|	//ДляБухУчета  ВыпускПродукции.НалоговоеНазначение,
	|	ВыпускПродукции.Подразделение,
	|	ВыпускПродукции.НоменклатурнаяГруппа,
	|	ВыпускПродукции.Заказ,
	|
	|	ВыпускПродукции.Продукция,
	|	ВыпускПродукции.ХарактеристикаПродукции,
	|	ВыпускПродукции.СерияПродукции,
	|	ВыпускПродукции.Спецификация,
	|	
	|	ВыпускПродукции.ВариантВыпускаПродукции
	|
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции // СформироватьТекстЗапросаБазаПоОбъемуПродаж()

// Функция формирования запроса для базы распределения "Вручную".
//
// Возвращаемое значение:
//   Строка – Текст запроса
//
Функция СформироватьТекстЗапросаБазаВручную()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&СпособРаспределенияЗатрат КАК РассчитанныйСпособРаспределения,
	|
	|	//ДляУпрУчета  ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
	|	//ДляРеглУчета БазаРаспределенияЗатрат.Организация,
	|
	|	//ДляУпрУчета  ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетУчета,
	|	//ДляРеглУчета БазаРаспределенияЗатрат.СчетУчета,
	|
	|	//ДляУпрУчета ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначение,
	|	//ДляБухУчета БазаРаспределенияЗатрат.НалоговоеНазначение КАК НалоговоеНазначение,
	|
	|	БазаРаспределенияЗатрат.Подразделение,
	|	БазаРаспределенияЗатрат.НоменклатурнаяГруппа,
	|	БазаРаспределенияЗатрат.Заказ,
	|
	|	БазаРаспределенияЗатрат.Подразделение КАК ПодразделениеНЗП,
	|	БазаРаспределенияЗатрат.НоменклатурнаяГруппа КАК НоменклатурнаяГруппаНЗП,
	|	БазаРаспределенияЗатрат.Заказ КАК ЗаказНЗП,
	|
	|	БазаРаспределенияЗатрат.Продукция,
	|	БазаРаспределенияЗатрат.ХарактеристикаПродукции,
	|	БазаРаспределенияЗатрат.СерияПродукции,
	|	БазаРаспределенияЗатрат.Спецификация,
	|	
	|	БазаРаспределенияЗатрат.ВариантВыпускаПродукции,
	|	БазаРаспределенияЗатрат.ВидВыпуска,
	|
	|		
	|	БазаРаспределенияЗатрат.База,
	|	БазаРаспределенияЗатрат.БазаПриход,
	|	БазаРаспределенияЗатрат.БазаОстатокНЗП
	|
	|ПОМЕСТИТЬ БазаРаспределения%СуффиксУчета%
	|ИЗ
	|	РегистрСведений.БазаРаспределенияЗатрат%СуффиксУчета% КАК БазаРаспределенияЗатрат
	|
	|ГДЕ
	|	БазаРаспределенияЗатрат.Период МЕЖДУ &НачДата И &КонДата
	|	И БазаРаспределенияЗатрат.СпособРаспределенияЗатрат = &СпособРаспределенияЗатрат
	|	И Не РасчетСебестоимостиВыпуска
	|	//ДляРеглУчета И Организация = &Организация
	|	
	|{ГДЕ
	|	БазаРаспределенияЗатрат.Подразделение.* КАК ПодразделениеОрганизации,
	|	БазаРаспределенияЗатрат.НоменклатурнаяГруппа.* КАК НоменклатурнаяГруппа,
	|	БазаРаспределенияЗатрат.Продукция.* КАК Продукция
	|}
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции // СформироватьТекстЗапросаБазаВручную()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ИНИЦИАЛИЗАЦИИ ПОСТРОИТЕЛЯ ЗАПРОСА

// Процедура добавляет элемент отбора постоителя запроса с видом сравнения "Равно".
//
// Параметры:
//	ИмяЭлементаОтбора - Строка - Имя элемента отбора
//	СтруктураПараметров - Структура - Структура параметров способа распределения
//	ПостроительЗапроса - ПостроительЗапроса
//
Процедура ДобавитьЭлементОтбораПостроителяЗапросаРавно(
	ИмяЭлементаОтбора,
	СтруктураПараметров,
	ПостроительЗапроса
	)
	
	Если Не СтруктураПараметров.Свойство(ИмяЭлементаОтбора) Тогда
		Возврат;
	КонецЕсли;
	
	ТипЗначения = ТипЗнч(СтруктураПараметров[ИмяЭлементаОтбора]);
	Если ПостроительЗапроса.ДоступныеПоля.Найти(ИмяЭлементаОтбора) <> Неопределено
	   И ПостроительЗапроса.ДоступныеПоля[ИмяЭлементаОтбора].ТипЗначения.СодержитТип(ТипЗначения)
	   И ПостроительЗапроса.ДоступныеПоля[ИмяЭлементаОтбора].Отбор 
	Тогда
		ПостроительЗапроса.Отбор.Добавить(ИмяЭлементаОтбора);
		ПостроительЗапроса.Отбор[ИмяЭлементаОтбора].Использование = Истина;
		ПостроительЗапроса.Отбор[ИмяЭлементаОтбора].ВидСравнения = ВидСравнения.Равно;
		ПостроительЗапроса.Отбор[ИмяЭлементаОтбора].Значение = СтруктураПараметров[ИмяЭлементаОтбора];
	КонецЕсли;
	
КонецПроцедуры // ДобавитьЭлементОтбораПостроителяЗапросаРавно()

// Процедура добавляет элемент отбора постоителя запроса с видом сравнения "В списке".
//
// Параметры:
//	ИмяЭлементаОтбора - Строка - Имя элемента отбора
//	СтруктураПараметров - Структура - Структура параметров способа распределения
//	СписокЗначений - СписокЗначений - Список для отбора
//	ПостроительЗапроса - ПостроительЗапроса
//
Процедура ДобавитьЭлементОтбораПостроителяЗапросаВСписке(
	ИмяЭлементаОтбора,
	СтруктураПараметров,
	СписокЗначений,
	ПостроительЗапроса
	)
	
	Если ПостроительЗапроса.ДоступныеПоля.Найти(ИмяЭлементаОтбора) <> Неопределено
	   И ПостроительЗапроса.ДоступныеПоля[ИмяЭлементаОтбора].Отбор 
	Тогда
		ПостроительЗапроса.Отбор.Добавить(ИмяЭлементаОтбора);
		ПостроительЗапроса.Отбор[ИмяЭлементаОтбора].Использование = Истина;
		ПостроительЗапроса.Отбор[ИмяЭлементаОтбора].ВидСравнения = ВидСравнения.ВСписке;
		ПостроительЗапроса.Отбор[ИмяЭлементаОтбора].Значение = СписокЗначений;
	КонецЕсли;
	
КонецПроцедуры // ДобавитьЭлементОтбораПостроителяЗапросаВСписке()

// Процедура устанавливает параметры построителя запроса.
//
// Параметры:
//	СтруктураПараметров - Структура - Структура параметров способа распределения
//	ПостроительЗапроса - ПостроительЗапроса
//
Процедура УстановитьПараметрыПостроителяЗапроса(
	СтруктураПараметров,
	ПостроительЗапроса
	)
	
	ПостроительЗапроса.Параметры.Вставить("НачДата", СтруктураПараметров.НачДата);
	ПостроительЗапроса.Параметры.Вставить("КонДата", СтруктураПараметров.КонДата);
	ПостроительЗапроса.Параметры.Вставить("НачГраница", СтруктураПараметров.НачГраница);
	ПостроительЗапроса.Параметры.Вставить("КонГраница", СтруктураПараметров.КонГраница);
	ПостроительЗапроса.Параметры.Вставить("ВалютаУчета", СтруктураПараметров.ВалютаУчета);
	ПостроительЗапроса.Параметры.Вставить("ВалютаУпрУчета", глЗначениеПеременной("ВалютаУправленческогоУчета"));
	ПостроительЗапроса.Параметры.Вставить("Организация", СтруктураПараметров.Организация);
	
	ПостроительЗапроса.Параметры.Вставить("СтатусМатериальныхЗатратПринятые", Перечисления.СтатусыМатериальныхЗатратНаПроизводство.ПринятыеВПереработку);
	
	МассивПустыхЗаказов = Новый Массив;
	МассивПустыхЗаказов.Добавить(Документы.ЗаказПокупателя.ПустаяСсылка());
	МассивПустыхЗаказов.Добавить(Документы.ЗаказНаПроизводство.ПустаяСсылка());
	ПостроительЗапроса.Параметры.Вставить("ПустойЗаказ", МассивПустыхЗаказов);
	
	МассивИсключаемыхКодовОпераций = Новый Массив;
	МассивИсключаемыхКодовОпераций.Добавить(Перечисления.КодыОперацийВыпускПродукции.СписаниеНЗП);
	МассивИсключаемыхКодовОпераций.Добавить(Перечисления.КодыОперацийВыпускПродукции.СписаниеНЗПФикс);
	МассивИсключаемыхКодовОпераций.Добавить(Перечисления.КодыОперацийВыпускПродукции.ОприходованиеНЗП);
	МассивИсключаемыхКодовОпераций.Добавить(Перечисления.КодыОперацийВыпускПродукции.КорректировкаНЗПРасч);
	МассивИсключаемыхКодовОпераций.Добавить(Перечисления.КодыОперацийВыпускПродукции.КорректировкаНЗПФикс);
	МассивИсключаемыхКодовОпераций.Добавить(Перечисления.КодыОперацийВыпускПродукции.ВозвратМатериаловИзПроизводстваРасч);
	МассивИсключаемыхКодовОпераций.Добавить(Перечисления.КодыОперацийВыпускПродукции.ВозвратМатериаловИзПроизводстваФикс);
	МассивИсключаемыхКодовОпераций.Добавить(Перечисления.КодыОперацийВыпускПродукции.ВыпускПродукцииПоФиксированнойСтоимости);
	ПостроительЗапроса.Параметры.Вставить("ИсключаемыеКодыОпераций", МассивИсключаемыхКодовОпераций);
	
	МассивКодовОперацийСписаниеНаЗатраты = Новый Массив;
	МассивКодовОперацийСписаниеНаЗатраты.Добавить(Перечисления.КодыОперацийВыпускПродукции.ВыпускПродукцииФиксВнутрУслугиНаПостЗатраты);
	ПостроительЗапроса.Параметры.Вставить("КодыОперацийСписаниеНаЗатраты", МассивКодовОперацийСписаниеНаЗатраты);
	
	МассивКодовОперацийСписанияПартии = Новый Массив;
	МассивКодовОперацийСписанияПартии.Добавить(Перечисления.КодыОперацийПартииТоваров.СписаниеНаБрак);
	МассивКодовОперацийСписанияПартии.Добавить(Перечисления.КодыОперацийПартииТоваров.СписаниеНаЗатраты);
	ПостроительЗапроса.Параметры.Вставить("КодыОперацийСписанияПартии", МассивКодовОперацийСписанияПартии);
	
	МассивКодовОперацийРеализация = Новый Массив;
	МассивКодовОперацийРеализация.Добавить(Перечисления.КодыОперацийПартииТоваров.Реализация);
	МассивКодовОперацийРеализация.Добавить(Перечисления.КодыОперацийПартииТоваров.РеализацияКомиссия);
	МассивКодовОперацийРеализация.Добавить(Перечисления.КодыОперацийПартииТоваров.РеализацияРозница);
	ПостроительЗапроса.Параметры.Вставить("КодыОперацийПартииТоваров", МассивКодовОперацийРеализация);
	
	МассивКодыОперацийПрямыеЗатраты = Новый Массив;
	МассивКодыОперацийПрямыеЗатраты.Добавить(Перечисления.КодыОперацийЗатратыНаВыпускПродукции.ПрямыеЗатраты);
	МассивКодыОперацийПрямыеЗатраты.Добавить(Перечисления.КодыОперацийЗатратыНаВыпускПродукции.ОтрицательныеЗатраты);
	ПостроительЗапроса.Параметры.Вставить("МассивКодыОперацийПрямыеЗатраты", МассивКодыОперацийПрямыеЗатраты);
	
КонецПроцедуры // УстановитьПараметрыПостроителяЗапроса()

////////////////////////////////////////////////////////////////////////////////
// ФУНКЦИИ ПОЛУЧЕНИЯ ПОСТОИТЕЛЯ ЗАПРОСА ДЛЯ РАСЧЕТА БАЗЫ РАСПРЕДЕЛЕНИЯ

// Функция получает построитель запроса для базы распределения.
//
// Параметры:
//	СтруктураПараметров - Структура - Структура параметров способа распределения
//	ТекстЗапросаСКомментариями - Строка - Текст запроса
//	ВидОтраженияВУчете - ПеречислениеСсылка.ВидыОтраженияВУчете - Вид отражения в учете
//
// ВозвращаемоеЗначение:
//	ПостроительЗапроса - Постоитель запроса для базы по стоимости затрат
//
Функция ПолучитьПостроительЗапросаДляБазы(
	СтруктураПараметров,
	ТекстЗапросаСКомментариями,
	ВидОтраженияВУчете
	)
	
	ТекстЗапроса = УправлениеЗатратами.ЗаменитьКомментарииВТекстеЗапроса(
		ТекстЗапросаСКомментариями, 
		ВидОтраженияВУчете
	);
	
	ПостроительЗапроса = Новый ПостроительЗапроса();
	ПостроительЗапроса.Текст = ТекстЗапроса;
	
	УстановитьПараметрыПостроителяЗапроса(
		СтруктураПараметров,
		ПостроительЗапроса
	);
	
	Возврат ПостроительЗапроса;
	
КонецФункции // ПолучитьПостроительЗапросаДляБазы()

// Функция получает построитель запроса для способа распределения затрат.
//
// Параметры:
//	СтруктураПараметров - Структура - Структура параметров способа распределения
//	СтруктураПостроителиЗапроса - Структура - Структура с построителями запроса
//	ВидОтраженияВУчете - ПеречислениеСсылка.ВидыОтраженияВУчете - Вид отражения в учете
//
// ВозвращаемоеЗначение:
//	ПостроительЗапроса - Постоитель запроса для базы распределения
//
Функция ПолучитьПостроительЗапросаДляСпособаРаспределенияЗатрат(
	СтруктураПараметров,
	СтруктураПостроителиЗапроса,
	ВидОтраженияВУчете
	)
	
	ИмяБазыРаспределения = УправлениеПроизводством.ПолучитьИмяЭлементаПеречисления(СтруктураПараметров.БазаРаспределенияЗатрат);
	ИмяПоказательяБазы = УправлениеПроизводством.ПолучитьИмяЭлементаПеречисления(СтруктураПараметров.ПоказательБазыРаспределения);
	
	СуффиксИмениРегистра = СтруктураПараметров.СуффиксыИмениРегистра[ВидОтраженияВУчете];
	
	ИмяПостроителя = "Построитель" + ИмяБазыРаспределения + ИмяПоказательяБазы + СуффиксИмениРегистра;
	
	ПостроительЗапроса = Неопределено;
	Если Не СтруктураПостроителиЗапроса.Свойство(ИмяПостроителя, ПостроительЗапроса) Тогда
	
		ТекстЗапросаСКомментариями = "";
		Если СтруктураПараметров.БазаРаспределенияЗатрат = Перечисления.БазыРаспределенияЗатрат.ПоСтоимостиЗатрат Тогда
			Если СтруктураПараметров.ИспользоватьРасширеннуюАналитику Тогда
				ТекстЗапросаСКомментариями = СформироватьТекстЗапросаБазаПоСтоимостиЗатратРасширеннаяАналитика();
				ТекстЗапросаСКомментариями = ТекстЗапросаСКомментариями + "ОБЪЕДИНИТЬ ВСЕ"
					+ СформироватьТекстЗапросаБазаПриходПоСтоимостиЗатратРасширеннаяАналитика();
			Иначе
				ТекстЗапросаСКомментариями = СформироватьТекстЗапросаБазаПоСтоимостиЗатрат();
				ТекстЗапросаСКомментариями = ТекстЗапросаСКомментариями + "ОБЪЕДИНИТЬ ВСЕ"
					+ СформироватьТекстЗапросаБазаПриходПоСтоимостиЗатрат();
			КонецЕсли;
			
		ИначеЕсли СтруктураПараметров.БазаРаспределенияЗатрат = Перечисления.БазыРаспределенияЗатрат.ПоОсновномуСырью Тогда
			Если СтруктураПараметров.ИспользоватьРасширеннуюАналитику Тогда
				ТекстЗапросаСКомментариями = СформироватьТекстЗапросаБазаПоОсновномуСырьюРасширеннаяАналитика();
				ТекстЗапросаСКомментариями = ТекстЗапросаСКомментариями + "ОБЪЕДИНИТЬ ВСЕ"
					+ СформироватьТекстЗапросаБазаПриходПоОсновномуСырьюРасширеннаяАналитика();
			Иначе
				ТекстЗапросаСКомментариями = СформироватьТекстЗапросаБазаПоОсновномуСырью();
				ТекстЗапросаСКомментариями = ТекстЗапросаСКомментариями + "ОБЪЕДИНИТЬ ВСЕ"
					+ СформироватьТекстЗапросаБазаПриходПоОсновномуСырью();
			КонецЕсли;
			
		ИначеЕсли СтруктураПараметров.БазаРаспределенияЗатрат = Перечисления.БазыРаспределенияЗатрат.ПоОбъемуВыпуска Тогда
			
			Если СтруктураПараметров.ПоказательБазыРаспределения = Перечисления.ВидыПоказателейБазыРаспределения.НатуральныеЕдиницыИзмерения
			 ИЛИ СтруктураПараметров.ПоказательБазыРаспределения = Перечисления.ВидыПоказателейБазыРаспределения.ЦеныНоменклатуры
			 ИЛИ Не ЗначениеЗаполнено(СтруктураПараметров.ПоказательБазыРаспределения)
			Тогда
				Если СтруктураПараметров.ИспользоватьРасширеннуюАналитику Тогда
					ТекстЗапросаСКомментариями = СформироватьТекстЗапросаБазаПоОбъемуВыпускаЦеныНоменклатурыРасширеннаяАналитика();
				Иначе
					ТекстЗапросаСКомментариями = СформироватьТекстЗапросаБазаПоОбъемуВыпускаЦеныНоменклатуры();
				КонецЕсли;
				
			ИначеЕсли СтруктураПараметров.ПоказательБазыРаспределения = Перечисления.ВидыПоказателейБазыРаспределения.ФактическаяЦенаРеализации Тогда
				Если СтруктураПараметров.ИспользоватьРасширеннуюАналитику Тогда
					ТекстЗапросаСКомментариями = СформироватьТекстЗапросаБазаФактическаяЦенаРеализацииРасширеннаяАналитика();
				Иначе
					ТекстЗапросаСКомментариями = СформироватьТекстЗапросаБазаФактическаяЦенаРеализации();
				КонецЕсли;
				
			КонецЕсли;
			
		ИначеЕсли СтруктураПараметров.БазаРаспределенияЗатрат = Перечисления.БазыРаспределенияЗатрат.ПоОбъемуПродаж Тогда
			Если СтруктураПараметров.ИспользоватьРасширеннуюАналитику Тогда
				ТекстЗапросаСКомментариями = СформироватьТекстЗапросаБазаПоОбъемуПродажРасширеннаяАналитика();
			Иначе
				ТекстЗапросаСКомментариями = СформироватьТекстЗапросаБазаПоОбъемуПродаж();
			КонецЕсли;
			
		ИначеЕсли СтруктураПараметров.БазаРаспределенияЗатрат = Перечисления.БазыРаспределенияЗатрат.ПоНормативам Тогда
			Если СтруктураПараметров.ИспользоватьРасширеннуюАналитику Тогда
				ТекстЗапросаСКомментариями = СформироватьТекстЗапросаБазаПоНормативамРасширеннаяАналитика();
			Иначе
				ТекстЗапросаСКомментариями = СформироватьТекстЗапросаБазаПоНормативам();
			КонецЕсли;
			
		ИначеЕсли СтруктураПараметров.БазаРаспределенияЗатрат = Перечисления.БазыРаспределенияЗатрат.Вручную Тогда
			Если СтруктураПараметров.ИспользоватьРасширеннуюАналитику Тогда
				ТекстЗапросаСКомментариями = СформироватьТекстЗапросаБазаВручнуюРасширеннаяАналитика();
			Иначе
				ТекстЗапросаСКомментариями = СформироватьТекстЗапросаБазаВручную();
			КонецЕсли;
		КонецЕсли;
		
		Если Не ПустаяСтрока(ТекстЗапросаСКомментариями) Тогда
			ПостроительЗапроса = ПолучитьПостроительЗапросаДляБазы(
				СтруктураПараметров,
				ТекстЗапросаСКомментариями,
				ВидОтраженияВУчете
			);
			СтруктураПостроителиЗапроса.Вставить(ИмяПостроителя, ПостроительЗапроса);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ПостроительЗапроса;
	
КонецФункции // ПолучитьПостроительЗапросаДляСпособаРаспределенияЗатрат()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ФОРМИРОВАНИЯ ВРЕМЕННОЙ ТАБЛИЦЫ РАСЧЕТА БАЗЫ РАСПРЕДЕЛЕНИЯ

// Процедура устанавливает дополнительные параметры построителя запроса.
//
// Параметры:
//	СтруктураПараметров - Структура - Структура параметров способа распределения
//	РаспределениеКосвенныхЗатрат - Булево - Признак расчета базы распределения косвенных затрат
//	ПостроительЗапроса - ПостроительЗапроса
//
Процедура УстановитьДополнительныеПараметрыПостроителяЗапроса(
	СтруктураПараметров,
	РаспределениеКосвенныхЗатрат,
	ПостроительЗапроса
	)
	
	// Заданный в способе распределения тип цен для расчета базы.
	ПостроительЗапроса.Параметры.Вставить("ТипЦен", СтруктураПараметров.ТипЦен);
	
	// Заданное в способе распределения основное сырье.
	ПостроительЗапроса.Параметры.Вставить("ОсновноеСырье", СтруктураПараметров.ОсновноеСырье);
	
	// Показатель базы распределения, указанный в способе распределения.
	ПостроительЗапроса.Параметры.Вставить("ПоказательБазыРаспределения", СтруктураПараметров.ПоказательБазыРаспределения);
	
	// Способ распределения, для которого производится расчет базы.
	ПостроительЗапроса.Параметры.Вставить("СпособРаспределенияЗатрат", СтруктураПараметров.СпособРаспределенияЗатрат);
	
	// Признак расчета базы распределения для общепроизводственных, административных затрат,  затрат по браку в производстве.
	ПостроительЗапроса.Параметры.Вставить("РаспределениеКосвенныхЗатрат", РаспределениеКосвенныхЗатрат);
	
КонецПроцедуры // УстановитьПараметрыПостроителяЗапроса()

// Процедура устанавливает значения отбора постоителя запроса.
//
// Параметры:
//	СтруктураПараметров - Структура - Структура параметров способа распределения
//	ПостроительЗапроса - ПостроительЗапроса
//
Процедура ЗаполнитьОтборПостоителяЗапроса(
	СтруктураПараметров,
	ПостроительЗапроса
	)
	
	// Удалим установленный ранее отбор.
	ОтборКоличество = ПостроительЗапроса.Отбор.Количество();
	Для Сч = 1 По ОтборКоличество Цикл
		ПостроительЗапроса.Отбор.Удалить(ОтборКоличество - Сч);
	КонецЦикла;
	
	// Устанавливается отбор, заданный в справочнике «Способы распределения затрат на выпуск» для рассчитываемой базы распределения.
	СтруктураНастроек = СтруктураПараметров.СпособРаспределенияЗатрат.НастройкиПостроителя.Получить();
	Если ТипЗнч(СтруктураНастроек) = Тип("Структура") 
	   И СтруктураНастроек.Свойство("Отбор") 
	Тогда
		Для Каждого Строка Из СтруктураНастроек.Отбор Цикл
			Если ПостроительЗапроса.ДоступныеПоля.Найти(Строка.Имя) <> Неопределено 
			   И Строка.Использование
			   И ПостроительЗапроса.ДоступныеПоля[Строка.Имя].Отбор 
			Тогда
				ПостроительЗапроса.Отбор.Добавить(Строка.Имя);
				ПостроительЗапроса.Отбор[Строка.Имя].Использование = Строка.Использование;
				ПостроительЗапроса.Отбор[Строка.Имя].ВидСравнения = Строка.ВидСравнения;
				ПостроительЗапроса.Отбор[Строка.Имя].Значение = Строка.Значение;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// Устанавливается отбор по подразделению, указанному в документе распределения затрат.
	ДобавитьЭлементОтбораПостроителяЗапросаРавно(
		"Подразделение",
		СтруктураПараметров,
		ПостроительЗапроса
	);
	
	// Устанавливается отбор по подразделению организации, указанному в документе распределения затрат.
	ДобавитьЭлементОтбораПостроителяЗапросаРавно(
		"ПодразделениеОрганизации",
		СтруктураПараметров,
		ПостроительЗапроса
	);
	
	// Устанавливается отбор по статье затрат, которая распределяется по рассчитываемому способу распределения.
	Если СтруктураПараметров.БазаРаспределенияЗатрат = Перечисления.БазыРаспределенияЗатрат.ПоНормативам Тогда
		ДобавитьЭлементОтбораПостроителяЗапросаРавно(
			"СтатьяЗатрат",
			СтруктураПараметров,
			ПостроительЗапроса
		);
	КонецЕсли;
	
	// Устанавливается отбор по продукции, заданной в документе распределения затрат.
	Если СтруктураПараметров.Свойство("МассивПродукции") Тогда
		СписокПродукции = Новый СписокЗначений;
		СписокПродукции.ЗагрузитьЗначения(СтруктураПараметров.МассивПродукции);
		
		ДобавитьЭлементОтбораПостроителяЗапросаВСписке(
			"Продукция",
			СтруктураПараметров,
			СписокПродукции,
			ПостроительЗапроса
		);
	КонецЕсли;
	
	// Устанавливается отбор по характеристикам продукции, заданным в документе распределения затрат.
	Если СтруктураПараметров.Свойство("МассивХарактеристик") Тогда
		СписокХарактеристик = Новый СписокЗначений;
		СписокХарактеристик.ЗагрузитьЗначения(СтруктураПараметров.МассивХарактеристик);
		
		ДобавитьЭлементОтбораПостроителяЗапросаВСписке(
			"ХарактеристикаПродукции",
			СтруктураПараметров,
			СписокХарактеристик,
			ПостроительЗапроса
		);
	КонецЕсли;
	
	// Подготовим список значений для отбора в соответствии с типом фильтра, указанным в способе распределения затрат.
	Если ТипЗнч(СтруктураПараметров.Фильтры) = Тип("ТаблицаЗначений") Тогда
		
		СписокОтбора = Новый СписокЗначений;
		Для Каждого СтрокаФильтра Из СтруктураПараметров.Фильтры Цикл
			
			Если СтруктураПараметров.ТипФильтраПриРаспределенииЗатратНаВыпуск 
			   = Перечисления.ТипыФильтровПриРаспределенииЗатратНаВыпуск.НоменклатурныеГруппы 
			Тогда
				СписокОтбора.Добавить(СтрокаФильтра.НоменклатурнаяГруппа);
			ИначеЕсли СтруктураПараметров.ТипФильтраПриРаспределенииЗатратНаВыпуск 
			  = Перечисления.ТипыФильтровПриРаспределенииЗатратНаВыпуск.Продукция 
			Тогда
				СписокОтбора.Добавить(СтрокаФильтра.Продукция);
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		
		ВыборкаФильтры = СтруктураПараметров.Фильтры.Выбрать();
		СписокОтбора = Новый СписокЗначений;
		Пока ВыборкаФильтры.Следующий() Цикл
			
			Если СтруктураПараметров.ТипФильтраПриРаспределенииЗатратНаВыпуск 
			   = Перечисления.ТипыФильтровПриРаспределенииЗатратНаВыпуск.НоменклатурныеГруппы 
			Тогда
				СписокОтбора.Добавить(ВыборкаФильтры.НоменклатурнаяГруппа);
			ИначеЕсли СтруктураПараметров.ТипФильтраПриРаспределенииЗатратНаВыпуск 
			  = Перечисления.ТипыФильтровПриРаспределенииЗатратНаВыпуск.Продукция 
			Тогда
				СписокОтбора.Добавить(ВыборкаФильтры.Продукция);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	// Устанавливается отбор по номенклатурной группе или продукции в соответствии с типом фильтра, заданном
	// в способе распределения затрат.
	Если СписокОтбора.Количество() > 0 Тогда
		Если СтруктураПараметров.ТипФильтраПриРаспределенииЗатратНаВыпуск = Перечисления.ТипыФильтровПриРаспределенииЗатратНаВыпуск.НоменклатурныеГруппы Тогда
			ДобавитьЭлементОтбораПостроителяЗапросаВСписке(
				"НоменклатурнаяГруппа",
				СтруктураПараметров,
				СписокОтбора,
				ПостроительЗапроса
			);
		ИначеЕсли СтруктураПараметров.ТипФильтраПриРаспределенииЗатратНаВыпуск = Перечисления.ТипыФильтровПриРаспределенииЗатратНаВыпуск.Продукция Тогда
			ДобавитьЭлементОтбораПостроителяЗапросаВСписке(
				"Продукция",
				СтруктураПараметров,
				СписокОтбора,
				ПостроительЗапроса
			);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьОтборПостоителяЗапроса()

// Функция заменяет в тексте запроса имя таблицы регистра на имя временной таблицы.
//
// Параметры:
//	СтруктураПараметров - Структура - Структура параметров способа распределения
//	ИсходныйТекстЗапроса - Строка - Исходный текст запроса
//	ВидОтраженияВУчете - ПеречислениеСсылка.ВидыОтраженияВУчете - Вид отражения в учете
//
// Возвращаемое значение:
//   Строка – Текст запроса
//
Функция ЗаменитьИмяТаблицыВТекстеЗапроса(
	СтруктураПараметров,
	ИсходныйТекстЗапроса,
	ВидОтраженияВУчете
	)
	
	СуффиксИмениРегистра = СтруктураПараметров.СуффиксыИмениРегистра[ВидОтраженияВУчете];
	
	ИмяРегистраЗатратыНаВыпуск = "РегистрНакопления.ЗатратыНаВыпускПродукции";
	ТекстЗапроса = СтрЗаменить(ИсходныйТекстЗапроса, ИмяРегистраЗатратыНаВыпуск, "ЗатратыНаВыпуск");
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "РегистрНакопления.ВыпускПродукции КАК Продажи", "Продажи" + СуффиксИмениРегистра + " КАК Продажи");
	
	ИмяРегистраВыпускПродукции = "РегистрНакопления.ВыпускПродукции";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ИмяРегистраВыпускПродукции, "ВыпускПродукции");
	
	ИмяРегистраНезавершенноеПроизводство = "РегистрНакопления.НезавершенноеПроизводство" + СуффиксИмениРегистра + ".ОстаткиИОбороты(, , , , )";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ИмяРегистраНезавершенноеПроизводство, "НезавершенноеПроизводство" + СуффиксИмениРегистра);
	
	ИмяРегистраНезавершенноеПроизводство = "РегистрНакопления.УчетЗатратРегл КАК ЗатратыНаВыпуск";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ИмяРегистраНезавершенноеПроизводство, "ЗатратыНаВыпуск" + СуффиксИмениРегистра + " КАК ЗатратыНаВыпуск");
	
	ИмяРегистраНезавершенноеПроизводство = "РегистрНакопления.УчетЗатратРегл.ОстаткиИОбороты(, , , , )";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ИмяРегистраНезавершенноеПроизводство, "НезавершенноеПроизводство" + СуффиксИмениРегистра);
	
	ИмяРегистраНезавершенноеПроизводство = "РегистрНакопления.УчетЗатратРегл.Остатки(, )";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ИмяРегистраНезавершенноеПроизводство, "НезавершенноеПроизводство" + СуффиксИмениРегистра);
	
	ИмяРегистраНезавершенноеПроизводство = "РегистрНакопления.УчетЗатрат КАК ВыпускПродукции";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ИмяРегистраНезавершенноеПроизводство, "ВыпускПродукции" + СуффиксИмениРегистра + " КАК ВыпускПродукции");
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "РегистрСведений.ЦеныНоменклатуры", "ЦеныНоменклатуры" + СуффиксИмениРегистра);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, """ВариантВыпускаПродукции"" КАК ВариантВыпускаПродукции", "ВыпускПродукции.ВариантВыпускаПродукции");
	
	Возврат ТекстЗапроса;
	
КонецФункции // ЗаменитьИмяТаблицыВТекстеЗапроса()

// Функция формирует временную таблицу с результатом расчета базы распределения затрат.
//
// Параметры:
//	СтруктураПараметров - Структура - Структура параметров способа распределения
//	СтруктураПостроителиЗапроса - Структура - Структура с построителями запроса
//	РаспределениеКосвенныхЗатрат - Булево - Признак расчета базы распределения косвенных затрат
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц
//
Функция СформироватьВременнуюТаблицуБазаРаспределенияЗатрат(
	СтруктураПараметров,
	СтруктураПостроителиЗапроса,
	РаспределениеКосвенныхЗатрат,
	МенеджерВременныхТаблиц
	)
	
	БазаРассчитана = Ложь;
	
	// Цикл по всем видам отражения в учете, для которых рассчитывается база распределения.
	Для Каждого ВидОтраженияВУчете Из СтруктураПараметров.МассивВидовУчета Цикл
		
		ПостроительЗапроса = ПолучитьПостроительЗапросаДляСпособаРаспределенияЗатрат(
			СтруктураПараметров,
			СтруктураПостроителиЗапроса,
			ВидОтраженияВУчете
		);
		
		Если ТипЗнч(ПостроительЗапроса) <> Тип("ПостроительЗапроса") Тогда
			ОбщегоНазначения.Сообщение(
				"Не определен расчет базы для способа распределения: " + СтруктураПараметров.СпособРаспределенияЗатрат,
				Перечисления.ВидыСообщений.ВажнаяИнформация
			);
			Возврат Ложь;
		КонецЕсли;
		
		УстановитьДополнительныеПараметрыПостроителяЗапроса(
			СтруктураПараметров,
			РаспределениеКосвенныхЗатрат,
			ПостроительЗапроса
		);
		ЗаполнитьОтборПостоителяЗапроса(
			СтруктураПараметров,
			ПостроительЗапроса
		);
		
		Запрос = ПостроительЗапроса.ПолучитьЗапрос();
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		ИсходныйТекстЗапроса = Запрос.Текст;
		
		Запрос.Текст = ЗаменитьИмяТаблицыВТекстеЗапроса(
			СтруктураПараметров,
			ИсходныйТекстЗапроса,
			ВидОтраженияВУчете
		);
		
		РезультатЗапроса = Запрос.Выполнить();
		Если Не РезультатЗапроса.Пустой() Тогда
			БазаРассчитана = Истина;
		КонецЕсли;
		
		СуффиксИмениРегистра = СтруктураПараметров.СуффиксыИмениРегистра[ВидОтраженияВУчете];
		ИмяВременнойТаблицы = "БазаРаспределения" + СуффиксИмениРегистра;
		
		// Вызывается процедура формирования запроса по временной таблице. Используется при отладке алгоритмов. 
		// Для просмотра временной таблицы в структуре шапки документа поле "ОтладочныйРежим" должно иметь значение "Истина".
		РасширеннаяАналитикаУчета.ПоказатьВременнуюТаблицу(
			СтруктураПараметров,
			МенеджерВременныхТаблиц,
			ИмяВременнойТаблицы
		);
		
	КонецЦикла;
	
	Возврат БазаРассчитана;
	
КонецФункции // СформироватьВременнуюТаблицуБазаРаспределенияЗатрат()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ РАСЧЕТА БАЗЫ РАСПРЕДЕЛЕНИЯ

// Функция формирует текст запрос по базе распределения затрат.
//
// Возвращаемое значение:
//   Строка – Текст запроса
//
Функция СформироватьТекстЗапросаПоБазеРаспределенияЗатратИзВременнойТаблицыРасширеннаяАналитика()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	БазаРаспределенияЗатрат.АналитикаВидаУчета,
	|	БазаРаспределенияЗатрат.АналитикаРаспределенияЗатрат,
	|	БазаРаспределенияЗатрат.АналитикаУчетаПартий,
 	|
	|	//ДляУпрУчета ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
	|	//ДляРеглУчета БазаРаспределенияЗатрат.Организация,
	|
	|	//ДляУпрУчета БазаРаспределенияЗатрат.Подразделение,
	|	//ДляУпрУчета ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК ПодразделениеОрганизации,
	|	
	|	//ДляРеглУчета ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка) КАК Подразделение,
	|	//ДляРеглУчета БазаРаспределенияЗатрат.Подразделение КАК ПодразделениеОрганизации,
	|
	|	БазаРаспределенияЗатрат.НоменклатурнаяГруппа,
	|	БазаРаспределенияЗатрат.Заказ,
	|
	|	БазаРаспределенияЗатрат.ПодразделениеНЗП,
	|	БазаРаспределенияЗатрат.НоменклатурнаяГруппаНЗП,
	|	БазаРаспределенияЗатрат.ЗаказНЗП,
	|	
	|	БазаРаспределенияЗатрат.Продукция,
	|	БазаРаспределенияЗатрат.ХарактеристикаПродукции,
	|	БазаРаспределенияЗатрат.СерияПродукции,
	|	БазаРаспределенияЗатрат.Спецификация,
	|	
	|	БазаРаспределенияЗатрат.ВариантВыпускаПродукции,
	|	БазаРаспределенияЗатрат.ВидВыпуска,
	|
	|	//ДляУпрУчета ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетУчета,
	|	//ДляРеглУчета БазаРаспределенияЗатрат.СчетУчета,
	|
	|	//ДляУпрУчета ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетЗатрат,
	|	//ДляБухУчета БазаРаспределенияЗатрат.СчетУчета КАК СчетЗатрат,
	|	//ДляМеждУчета БазаРаспределенияЗатрат.СчетУчета КАК СчетЗатрат,
	|
	|	//ДляУпрУчета ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначение,
	|	//ДляБухУчета БазаРаспределенияЗатрат.НалоговоеНазначение КАК НалоговоеНазначение,
	|
	|	СУММА(БазаРаспределенияЗатрат.База
	|		* &Коэффициент * &Процент / 100
	|		* ЕСТЬNULL(Фильтры.Коэффициент, 1)
	|		* ЕСТЬNULL(ВыпускПродукции.ЕстьВыпускПродукции, 0)
	|	) КАК База,
	|	
	|
	|	СУММА(БазаРаспределенияЗатрат.БазаПриход
	|		* &Коэффициент * &Процент / 100
	|		* ЕСТЬNULL(Фильтры.Коэффициент, 1)
	|		* 
	|		ВЫБОР КОГДА &БазаРаспределенияЗатрат = ЗНАЧЕНИЕ(Перечисление.БазыРаспределенияЗатрат.ПоСтоимостиЗатрат)
	|			ИЛИ &БазаРаспределенияЗатрат = ЗНАЧЕНИЕ(Перечисление.БазыРаспределенияЗатрат.ПоОсновномуСырью)
	|			ИЛИ &БазаРаспределенияЗатрат = ЗНАЧЕНИЕ(Перечисление.БазыРаспределенияЗатрат.Вручную)
	|		ТОГДА
	|			1
	|		ИНАЧЕ
	|			ЕСТЬNULL(ВыпускПродукции.ЕстьВыпускПродукции, 0)
	|		КОНЕЦ
	|	) КАК БазаПриход,
	|	
	|
	|	СУММА(БазаРаспределенияЗатрат.БазаОстатокНЗП
	|		* &Коэффициент * &Процент / 100
	|		* ЕСТЬNULL(Фильтры.Коэффициент, 1)
	|	) КАК БазаОстатокНЗП
	|
	|//Поместить БазаРаспределенияЗатрат%СуффиксУчета%
	|ИЗ
	|	БазаРаспределения%СуффиксУчета% КАК БазаРаспределенияЗатрат
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			//ДляУпрУчета РегистрАналитикаВидаУчета.Подразделение,
	|			//ДляРеглУчета РегистрАналитикаВидаУчета.ПодразделениеОрганизации КАК Подразделение,
	|			//ДляРеглУчета РегистрАналитикаВидаУчета.СчетУчета,
	|
	|			ВыпускПродукции.АналитикаВидаУчета,
	|			ВыпускПродукции.АналитикаРаспределенияЗатрат,
	|			ВыпускПродукции.АналитикаУчетаПартий,
	|
	|			ВЫБОР КОГДА РегистрАналитикаВидаУчета.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчета.Наработка) ТОГДА
	|				ЗНАЧЕНИЕ(Перечисление.ВидыВыпуска.Наработка)
	|			ИНАЧЕ
	|				ЗНАЧЕНИЕ(Перечисление.ВидыВыпуска.Выпуск)
	|			КОНЕЦ КАК ВидВыпуска,
	|
	|			1 КАК ЕстьВыпускПродукции
	|		ИЗ
	|			ВыпускПродукции%СуффиксУчета% КАК ВыпускПродукции
	|	
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|				РегистрСведений.АналитикаВидаУчета КАК РегистрАналитикаВидаУчета
	|			ПО
	|				ВыпускПродукции.АналитикаВидаУчета = РегистрАналитикаВидаУчета.Ссылка
	|		ГДЕ
	|			ВыпускПродукции.ВариантВыпускаПродукции В (&ВариантыВыпускаПродукции)
	|			ИЛИ &БазаРаспределенияЗатрат = ЗНАЧЕНИЕ(Перечисление.БазыРаспределенияЗатрат.Вручную)
	|
	|		) КАК ВыпускПродукции
	|	ПО
	|		БазаРаспределенияЗатрат.Подразделение = ВыпускПродукции.Подразделение
	|		И БазаРаспределенияЗатрат.АналитикаРаспределенияЗатрат = ВыпускПродукции.АналитикаРаспределенияЗатрат
	|		И БазаРаспределенияЗатрат.АналитикаУчетаПартий = ВыпускПродукции.АналитикаУчетаПартий
	|		И БазаРаспределенияЗатрат.ВидВыпуска = ВыпускПродукции.ВидВыпуска
	|		//ДляРеглУчета И БазаРаспределенияЗатрат.СчетУчета = ВыпускПродукции.СчетУчета
	|		
	|	ЛЕВОЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ
	|			Фильтры.НоменклатурнаяГруппа,
	|			Фильтры.Продукция,
	|			Фильтры.Ссылка.ТипФильтраПриРаспределенииЗатратНаВыпуск КАК ТипФильтра,
	|			СУММА(Фильтры.Коэффициент) КАК Коэффициент
	|		ИЗ
	|			Справочник.СпособыРаспределенияЗатратНаВыпуск.Фильтры КАК Фильтры
	|		ГДЕ
	|			Фильтры.Ссылка = &СпособРаспределенияЗатрат
	|		СГРУППИРОВАТЬ ПО
	|			Фильтры.Ссылка,
	|			Фильтры.НоменклатурнаяГруппа,
	|			Фильтры.Продукция
	|		) КАК Фильтры
	|	ПО
	|		Фильтры.ТипФильтра = ЗНАЧЕНИЕ(Перечисление.ТипыФильтровПриРаспределенииЗатратНаВыпуск.Продукция)
	|		И БазаРаспределенияЗатрат.Продукция = Фильтры.Продукция
	|		ИЛИ
	|		Фильтры.ТипФильтра = ЗНАЧЕНИЕ(Перечисление.ТипыФильтровПриРаспределенииЗатратНаВыпуск.НоменклатурныеГруппы)
	|		И БазаРаспределенияЗатрат.НоменклатурнаяГруппа = Фильтры.НоменклатурнаяГруппа
	|
	|ГДЕ
	|	(Не &РаспределятьНаНаработку
	|		И БазаРаспределенияЗатрат.ВидВыпуска <> ЗНАЧЕНИЕ(Перечисление.ВидыВыпуска.Наработка)
	|	ИЛИ &РаспределятьНаНаработку
	|	ИЛИ &БазаРаспределенияЗатрат = ЗНАЧЕНИЕ(Перечисление.БазыРаспределенияЗатрат.Вручную)
	|	)
	|
	|СГРУППИРОВАТЬ ПО
	|	БазаРаспределенияЗатрат.АналитикаВидаУчета,
	|	БазаРаспределенияЗатрат.АналитикаРаспределенияЗатрат,
	|	БазаРаспределенияЗатрат.АналитикаУчетаПартий,
	|
	|	//ДляРеглУчета БазаРаспределенияЗатрат.Организация,
	|	//ДляРеглУчета БазаРаспределенияЗатрат.СчетУчета,
	|	//ДляБухУчета  БазаРаспределенияЗатрат.НалоговоеНазначение,
	|	БазаРаспределенияЗатрат.Подразделение,
	|	БазаРаспределенияЗатрат.НоменклатурнаяГруппа,
	|	БазаРаспределенияЗатрат.Заказ,
	|
	|	БазаРаспределенияЗатрат.ПодразделениеНЗП,
	|	БазаРаспределенияЗатрат.НоменклатурнаяГруппаНЗП,
	|	БазаРаспределенияЗатрат.ЗаказНЗП,
	|
	|	БазаРаспределенияЗатрат.Продукция,
	|	БазаРаспределенияЗатрат.ХарактеристикаПродукции,
	|	БазаРаспределенияЗатрат.СерияПродукции,
	|	БазаРаспределенияЗатрат.Спецификация,
	|	БазаРаспределенияЗатрат.ВариантВыпускаПродукции,
	|	БазаРаспределенияЗатрат.ВидВыпуска
	|		
	|ИМЕЮЩИЕ
	|	СУММА(БазаРаспределенияЗатрат.База
	|		* &Коэффициент * ВЫРАЗИТЬ(&Процент КАК Число(15,3)) / 100
	|	) > 0
	|	ИЛИ
	|	СУММА(БазаРаспределенияЗатрат.БазаПриход
	|		* &Коэффициент * ВЫРАЗИТЬ(&Процент КАК Число(15,3)) / 100
	|	) > 0
	|	ИЛИ
	|	СУММА(БазаРаспределенияЗатрат.БазаОстатокНЗП
	|		* &Коэффициент * ВЫРАЗИТЬ(&Процент КАК Число(15,3)) / 100
	|	) > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	БазаРаспределенияЗатрат.АналитикаВидаУчета,
	|	БазаРаспределенияЗатрат.АналитикаРаспределенияЗатрат,
	|	БазаРаспределенияЗатрат.АналитикаУчетаПартий,
	|	БазаРаспределенияЗатрат.ВидВыпуска
	|";

	Возврат ТекстЗапроса;
	
КонецФункции // СформироватьТекстЗапросаПоБазеРаспределенияЗатратИзВременнойТаблицыРасширеннаяАналитика()

// Функция формирует текст запрос по базе распределения затрат.
//
// Возвращаемое значение:
//   Строка – Текст запроса
//
Функция СформироватьТекстЗапросаПоБазеРаспределенияЗатратИзВременнойТаблицы()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Неопределено КАК АналитикаВидаУчета,
	|	Неопределено КАК АналитикаРаспределенияЗатрат,
	|	Неопределено КАК АналитикаУчетаПартий,
	|
	|	//ДляУпрУчета ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
	|	//ДляРеглУчета БазаРаспределенияЗатрат.Организация,
	|
	|	//ДляУпрУчета БазаРаспределенияЗатрат.Подразделение,
	|	//ДляУпрУчета ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК ПодразделениеОрганизации,
	|	
	|	//ДляРеглУчета ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка) КАК Подразделение,
	|	//ДляРеглУчета БазаРаспределенияЗатрат.Подразделение КАК ПодразделениеОрганизации,
	|
	|	БазаРаспределенияЗатрат.НоменклатурнаяГруппа,
	|	БазаРаспределенияЗатрат.Заказ,
	|
	|	БазаРаспределенияЗатрат.ПодразделениеНЗП,
	|	БазаРаспределенияЗатрат.НоменклатурнаяГруппаНЗП,
	|	БазаРаспределенияЗатрат.ЗаказНЗП,
	|	
	|	БазаРаспределенияЗатрат.Продукция,
	|	БазаРаспределенияЗатрат.ХарактеристикаПродукции,
	|	БазаРаспределенияЗатрат.СерияПродукции,
	|	БазаРаспределенияЗатрат.Спецификация,
	|	
	|	БазаРаспределенияЗатрат.ВариантВыпускаПродукции,
	|	БазаРаспределенияЗатрат.ВидВыпуска,
	|
	|	//ДляУпрУчета ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетУчета,
	|	//ДляРеглУчета БазаРаспределенияЗатрат.СчетУчета,
	|
	|	//ДляУпрУчета ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетЗатрат,
	|	//ДляБухУчета БазаРаспределенияЗатрат.СчетУчета КАК СчетЗатрат,
	|	//ДляМеждУчета БазаРаспределенияЗатрат.СчетУчета КАК СчетЗатрат,
	|
	|	//ДляУпрУчета ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначение,
	|	//ДляБухУчета БазаРаспределенияЗатрат.НалоговоеНазначение КАК НалоговоеНазначение,
	|
	|	СУММА(БазаРаспределенияЗатрат.База
	|		* &Коэффициент * &Процент / 100
	|		* ЕСТЬNULL(Фильтры.Коэффициент, 1)
	|		* ЕСТЬNULL(ИсключаемаяНоменклатура.ЕстьИсключаемаяНоменклатура, 1)
	|		* ЕСТЬNULL(ВыпускПродукции.ЕстьВыпускПродукции, 0)
	|	) КАК База,
	|	
	|
	|	СУММА(БазаРаспределенияЗатрат.БазаПриход
	|		* &Коэффициент * &Процент / 100
	|		* ЕСТЬNULL(Фильтры.Коэффициент, 1)
	|		* ЕСТЬNULL(ИсключаемаяНоменклатура.ЕстьИсключаемаяНоменклатура, 1)
	|		* 
	|		ВЫБОР КОГДА &БазаРаспределенияЗатрат = ЗНАЧЕНИЕ(Перечисление.БазыРаспределенияЗатрат.ПоСтоимостиЗатрат)
	|			ИЛИ &БазаРаспределенияЗатрат = ЗНАЧЕНИЕ(Перечисление.БазыРаспределенияЗатрат.ПоОсновномуСырью)
	|			ИЛИ &БазаРаспределенияЗатрат = ЗНАЧЕНИЕ(Перечисление.БазыРаспределенияЗатрат.Вручную)
	|		ТОГДА
	|			1
	|		ИНАЧЕ
	|			ЕСТЬNULL(ВыпускПродукции.ЕстьВыпускПродукции, 0)
	|		КОНЕЦ
	|	) КАК БазаПриход,
	|	
	|
	|	СУММА(БазаРаспределенияЗатрат.БазаОстатокНЗП
	|		* &Коэффициент * &Процент / 100
	|		* ЕСТЬNULL(Фильтры.Коэффициент, 1)
	|	) КАК БазаОстатокНЗП
	|
	|//Поместить БазаРаспределенияЗатрат%СуффиксУчета%
	|ИЗ
	|	БазаРаспределения%СуффиксУчета% КАК БазаРаспределенияЗатрат
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ (
	| 		ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			ИсключаемаяНоменклатура.Номенклатура,
	|			ИсключаемаяНоменклатура.ХарактеристикаНоменклатуры,
	|			ИсключаемаяНоменклатура.СерияНоменклатуры,
	|			0 КАК ЕстьИсключаемаяНоменклатура
	|		ИЗ
	|			РегистрСведений.НоменклатураИсключаемаяИзБазыРаспределения%СуффиксОрганизаций% КАК ИсключаемаяНоменклатура
	|		ГДЕ
	|			ИсключаемаяНоменклатура.Период МЕЖДУ &НачДата И &КонДата
	|			И &РаспределениеКосвенныхЗатрат
	|			//ДляРеглУчета И ИсключаемаяНоменклатура.Организация = &Организация
	|		) КАК ИсключаемаяНоменклатура
	|	ПО
	|		БазаРаспределенияЗатрат.Продукция = ИсключаемаяНоменклатура.Номенклатура
	|		И БазаРаспределенияЗатрат.ХарактеристикаПродукции = ИсключаемаяНоменклатура.ХарактеристикаНоменклатуры
	|		И БазаРаспределенияЗатрат.СерияПродукции = ИсключаемаяНоменклатура.СерияНоменклатуры
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			//ДляРеглУчета ВыпускПродукции.СчетУчетаНЗП,
	|			ВыпускПродукции.Подразделение,
	|			ВыпускПродукции.НоменклатурнаяГруппа,
	|			ВыпускПродукции.Продукция,
	|			ВыпускПродукции.ХарактеристикаПродукции,
	|			ВыпускПродукции.СерияПродукции,
	|			ВЫБОР КОГДА ВыпускПродукции.Спецификация = Неопределено ТОГДА
	|				ЗНАЧЕНИЕ(Справочник.СпецификацииНоменклатуры.ПустаяСсылка)
	|			ИНАЧЕ
	|				ВыпускПродукции.Спецификация
	|			КОНЕЦ КАК Спецификация,
	|			ВЫБОР КОГДА ВыпускПродукции.Спецификация = Неопределено ТОГДА
	|				ЗНАЧЕНИЕ(Перечисление.ВидыВыпуска.Наработка)
	|			ИНАЧЕ
	|				ЗНАЧЕНИЕ(Перечисление.ВидыВыпуска.Выпуск)
	|			КОНЕЦ КАК ВидВыпуска,
	|
	|			ВыпускПродукции.Заказ,
	|			1 КАК ЕстьВыпускПродукции
	|		ИЗ
	|			ВыпускПродукции%СуффиксУчета% КАК ВыпускПродукции
	|		ГДЕ
	|			ВыпускПродукции.ВариантВыпускаПродукции В (&ВариантыВыпускаПродукции)
	|			ИЛИ &БазаРаспределенияЗатрат = ЗНАЧЕНИЕ(Перечисление.БазыРаспределенияЗатрат.Вручную)
	|		) КАК ВыпускПродукции
	|	ПО
	|		БазаРаспределенияЗатрат.Подразделение = ВыпускПродукции.Подразделение
	|		И БазаРаспределенияЗатрат.НоменклатурнаяГруппа = ВыпускПродукции.НоменклатурнаяГруппа
	|		И БазаРаспределенияЗатрат.Продукция = ВыпускПродукции.Продукция
	|		И БазаРаспределенияЗатрат.ХарактеристикаПродукции = ВыпускПродукции.ХарактеристикаПродукции
	|		И БазаРаспределенияЗатрат.СерияПродукции = ВыпускПродукции.СерияПродукции
	|		И БазаРаспределенияЗатрат.Заказ = ВыпускПродукции.Заказ
	|		И БазаРаспределенияЗатрат.Спецификация = ВыпускПродукции.Спецификация
	|		И БазаРаспределенияЗатрат.ВидВыпуска = ВыпускПродукции.ВидВыпуска
	|		//ДляРеглУчета И БазаРаспределенияЗатрат.СчетУчета = ВыпускПродукции.СчетУчетаНЗП
	|		
	|	ЛЕВОЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ
	|			Фильтры.НоменклатурнаяГруппа,
	|			Фильтры.Продукция,
	|			Фильтры.Ссылка.ТипФильтраПриРаспределенииЗатратНаВыпуск КАК ТипФильтра,
	|			СУММА(Фильтры.Коэффициент) КАК Коэффициент
	|		ИЗ
	|			Справочник.СпособыРаспределенияЗатратНаВыпуск.Фильтры КАК Фильтры
	|		ГДЕ
	|			Фильтры.Ссылка = &СпособРаспределенияЗатрат
	|		СГРУППИРОВАТЬ ПО
	|			Фильтры.Ссылка,
	|			Фильтры.НоменклатурнаяГруппа,
	|			Фильтры.Продукция
	|		) КАК Фильтры
	|	ПО
	|		Фильтры.ТипФильтра = ЗНАЧЕНИЕ(Перечисление.ТипыФильтровПриРаспределенииЗатратНаВыпуск.Продукция)
	|		И БазаРаспределенияЗатрат.Продукция = Фильтры.Продукция
	|		ИЛИ
	|		Фильтры.ТипФильтра = ЗНАЧЕНИЕ(Перечисление.ТипыФильтровПриРаспределенииЗатратНаВыпуск.НоменклатурныеГруппы)
	|		И БазаРаспределенияЗатрат.НоменклатурнаяГруппа = Фильтры.НоменклатурнаяГруппа
	|
	|ГДЕ
	|	(Не &РаспределятьНаНаработку
	|		И БазаРаспределенияЗатрат.ВидВыпуска <> ЗНАЧЕНИЕ(Перечисление.ВидыВыпуска.Наработка)
	|	ИЛИ &РаспределятьНаНаработку
	|	ИЛИ &БазаРаспределенияЗатрат = ЗНАЧЕНИЕ(Перечисление.БазыРаспределенияЗатрат.Вручную)
	|	)
	|
	|СГРУППИРОВАТЬ ПО
	|	//ДляРеглУчета БазаРаспределенияЗатрат.Организация,
	|	//ДляРеглУчета БазаРаспределенияЗатрат.СчетУчета,
	|	//ДляБухУчета  БазаРаспределенияЗатрат.НалоговоеНазначение,
	|	БазаРаспределенияЗатрат.Подразделение,
	|	БазаРаспределенияЗатрат.НоменклатурнаяГруппа,
	|	БазаРаспределенияЗатрат.Заказ,
	|	БазаРаспределенияЗатрат.ПодразделениеНЗП,
	|	БазаРаспределенияЗатрат.НоменклатурнаяГруппаНЗП,
	|	БазаРаспределенияЗатрат.ЗаказНЗП,
	|	БазаРаспределенияЗатрат.Продукция,
	|	БазаРаспределенияЗатрат.ХарактеристикаПродукции,
	|	БазаРаспределенияЗатрат.СерияПродукции,
	|	БазаРаспределенияЗатрат.Спецификация,
	|	БазаРаспределенияЗатрат.ВариантВыпускаПродукции,
	|	БазаРаспределенияЗатрат.ВидВыпуска
	|		
	|ИМЕЮЩИЕ
	|	СУММА(БазаРаспределенияЗатрат.База
	|		* &Коэффициент * ВЫРАЗИТЬ(&Процент КАК Число(15,3)) / 100
	|		* ЕСТЬNULL(ИсключаемаяНоменклатура.ЕстьИсключаемаяНоменклатура, 1)
	|		* ЕСТЬNULL(ВыпускПродукции.ЕстьВыпускПродукции, 0)
	|	) > 0
	|	ИЛИ
	|	СУММА(БазаРаспределенияЗатрат.БазаПриход
	|		* &Коэффициент * ВЫРАЗИТЬ(&Процент КАК Число(15,3)) / 100
	|	) > 0
	|	ИЛИ
	|	СУММА(БазаРаспределенияЗатрат.БазаОстатокНЗП
	|		* &Коэффициент *ВЫРАЗИТЬ(&Процент КАК Число(15,3)) / 100
	|	) > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	//ДляРеглУчета БазаРаспределенияЗатрат.Организация,
	|	//ДляРеглУчета БазаРаспределенияЗатрат.СчетУчета,
	|	//ДляБухУчета  БазаРаспределенияЗатрат.НалоговоеНазначение,
	|	БазаРаспределенияЗатрат.Подразделение,
	|	БазаРаспределенияЗатрат.НоменклатурнаяГруппа,
	|	БазаРаспределенияЗатрат.Заказ,
	|	БазаРаспределенияЗатрат.Продукция,
	|	БазаРаспределенияЗатрат.ХарактеристикаПродукции,
	|	БазаРаспределенияЗатрат.СерияПродукции,
	|	БазаРаспределенияЗатрат.Спецификация,
	|	БазаРаспределенияЗатрат.ВидВыпуска
	|";

	Возврат ТекстЗапроса;
	
КонецФункции // СформироватьТекстЗапросаПоБазеРаспределенияЗатратИзВременнойТаблицы()

// Функция формирует запрос по базе распределения затрат.
//
// Параметры:
//  СтруктураШапкиДокумента – Структура - Реквизиты документа "Расчет себестоимости выпуска"
//	СтруктураПараметров - Структура - Структура параметров способа распределения
//	РаспределениеКосвенныхЗатрат - Булево - Признак расчета базы распределения косвенных затрат
//
// Возвращаемое значение:
//   Запрос – Запрос по базе распределения затрат
//
Функция СформироватьЗапросПоБазеРаспределенияЗатратИзВременнойТаблицы(
	СтруктураШапкиДокумента,
	СтруктураПараметров,
	РаспределениеКосвенныхЗатрат
	)
	
	Если СтруктураПараметров.ИспользоватьРасширеннуюАналитику Тогда
		ТекстЗапросаСКомментариями = СформироватьТекстЗапросаПоБазеРаспределенияЗатратИзВременнойТаблицыРасширеннаяАналитика();
	Иначе
		ТекстЗапросаСКомментариями = СформироватьТекстЗапросаПоБазеРаспределенияЗатратИзВременнойТаблицы();
	КонецЕсли;
	
	ТекстЗапроса = УправлениеЗатратами.ЗаменитьКомментарииВТекстеЗапроса(
		ТекстЗапросаСКомментариями, 
		СтруктураШапкиДокумента.ВидОтраженияВУчете
	);
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	
	НачДата = НачалоМесяца(СтруктураПараметров.Период);
	КонДата = КонецМесяца (СтруктураПараметров.Период);
	
	Запрос.УстановитьПараметр("НачДата", НачДата);
	Запрос.УстановитьПараметр("КонДата", КонДата);
	Запрос.УстановитьПараметр("Организация", СтруктураПараметров.Организация);
	Запрос.УстановитьПараметр("СпособРаспределенияЗатрат", СтруктураПараметров.СпособРаспределенияЗатрат);
	Запрос.УстановитьПараметр("РаспределятьНаНаработку", СтруктураПараметров.РаспределятьНаНаработку);
	Запрос.УстановитьПараметр("БазаРаспределенияЗатрат", СтруктураПараметров.БазаРаспределенияЗатрат);
	Запрос.УстановитьПараметр("РаспределениеКосвенныхЗатрат", РаспределениеКосвенныхЗатрат);
	
	МассивВариантовВыпуска = Новый Массив;
	МассивВариантовВыпуска.Добавить(Перечисления.ВариантыВыпускаПродукции.ПустаяСсылка());
	Если СтруктураПараметров.РаспределятьНаСобственнуюПродукцию Тогда
		МассивВариантовВыпуска.Добавить(Перечисления.ВариантыВыпускаПродукции.СобственнаяПродукция);
	КонецЕсли;
	Если СтруктураПараметров.РаспределятьНаПродукциюИзДавальческогоСырья Тогда
		МассивВариантовВыпуска.Добавить(Перечисления.ВариантыВыпускаПродукции.ПродукцияИзДавальческогоСырья);
	КонецЕсли;
	Если СтруктураПараметров.РаспределятьНаПродукциюСтороннегоПереработчика Тогда
		МассивВариантовВыпуска.Добавить(Перечисления.ВариантыВыпускаПродукции.ПродукцияСтороннегоПереработчика);
	КонецЕсли;
	Запрос.УстановитьПараметр("ВариантыВыпускаПродукции", МассивВариантовВыпуска);
	
	Если СтруктураПараметров.ИзменитьНаКоэффициент Тогда
		Запрос.УстановитьПараметр("Коэффициент", СтруктураПараметров.Коэффициент);
	Иначе
		Запрос.УстановитьПараметр("Коэффициент", 1);
	КонецЕсли;
	Если СтруктураПараметров.ИзменитьНаПроцент Тогда
		Запрос.УстановитьПараметр("Процент", СтруктураПараметров.Процент);
	Иначе
		Запрос.УстановитьПараметр("Процент", 100);
	КонецЕсли;
		
	Возврат Запрос;
	
КонецФункции // ПолучитьЗапросПоБазеРаспределенияЗатратИзВременнойТаблицы()

// Процедура рассчитывает базу распределения для указанного способа распределения затрат.
//
// Параметры:
//  СтруктураШапкиДокумента – Структура - Реквизиты документа "Расчет себестоимости выпуска"
//	СтруктураПараметров - Структура - Структура параметров способа распределения
//	СтруктураПостроителиЗапроса - Структура - Структура с построителями запроса
//	РаспределениеКосвенныхЗатрат - Булево - Признак расчета базы распределения косвенных затрат
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц
//	НаборЗаписейБазаРаспределенияЗатрат - РегистрСведенийНаборЗаписей - Набор записей регистра сведений "База распределения затрат".
//
Процедура РассчитатьБазуРаспределения(
	СтруктураШапкиДокумента,
	СтруктураПараметров,
	СтруктураПостроителиЗапроса,
	РаспределениеКосвенныхЗатрат,
	МенеджерВременныхТаблиц,
	НаборЗаписейБазаРаспределенияЗатрат
	)
	
	БазаРассчитана = СформироватьВременнуюТаблицуБазаРаспределенияЗатрат(
		СтруктураПараметров,
		СтруктураПостроителиЗапроса,
		РаспределениеКосвенныхЗатрат,
		МенеджерВременныхТаблиц
	);

	Если БазаРассчитана Тогда
		
		Запрос = СформироватьЗапросПоБазеРаспределенияЗатратИзВременнойТаблицы(
			СтруктураШапкиДокумента,
			СтруктураПараметров,
			РаспределениеКосвенныхЗатрат
		);
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		
		СуффиксИмениРегистра = СтруктураПараметров.СуффиксыИмениРегистра[СтруктураШапкиДокумента.ВидОтраженияВУчете];
		
		// Вызывается процедура формирования запроса по временной таблице. Используется при отладке алгоритмов. 
		// Для просмотра временной таблицы в структуре шапки документа поле "ОтладочныйРежим" должно иметь значение "Истина".
		ИмяВременнойТаблицы = "БазаРаспределения" + СуффиксИмениРегистра;
		РасширеннаяАналитикаУчета.ПоказатьВременнуюТаблицу(
			СтруктураПараметров,
			МенеджерВременныхТаблиц,
			ИмяВременнойТаблицы
		);
		ИмяВременнойТаблицы = "ВыпускПродукции" + СуффиксИмениРегистра;
		РасширеннаяАналитикаУчета.ПоказатьВременнуюТаблицу(
			СтруктураПараметров,
			МенеджерВременныхТаблиц,
			ИмяВременнойТаблицы
		);
		
		Индекс = 1;
		
		РезультатЗапроса = Запрос.Выполнить();
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			СформироватьДвиженияПоРегиструБазаРаспределенияЗатрат(
				СтруктураШапкиДокумента,
				Выборка,
				Выборка.АналитикаВидаУчета,
				Выборка.АналитикаРаспределенияЗатрат,
				Выборка.АналитикаУчетаПартий,
				СтруктураПараметров.СпособРаспределенияЗатрат,
				Индекс,
				РаспределениеКосвенныхЗатрат,
				Истина, // ЭтоРасчетСебестоимостиВыпуска
				СтруктураШапкиДокумента.ВидОтраженияВУчете,
				НаборЗаписейБазаРаспределенияЗатрат
			);
			
			Индекс = Индекс + 1;
			
		КонецЦикла;
		
		Запрос.Текст = "УНИЧТОЖИТЬ БазаРаспределения" + СуффиксИмениРегистра;
		Запрос.Выполнить();
	
	КонецЕсли;
	
КонецПроцедуры // РассчитатьБазуРаспределения()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ РАСЧЕТА ВСЕХ БАЗ РАСПРЕДЕЛЕНИЯ ЗАТРАТ

// Функция формирует текст запроса по используемым способам распределения затрат.
//
// Параметры:
//  СтруктураШапкиДокумента – Структура - Реквизиты документа "Расчет себестоимости выпуска"
//	РаспределениеКосвенныхЗатрат - Булево - Признак расчета базы распределения косвенных затрат
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция СформироватьЗапросПоИспользуемымСпособамРаспределения(
	СтруктураШапкиДокумента,
	РаспределениеКосвенныхЗатрат,
	МенеджерВременныхТаблиц
	)
	
	ТекстЗапросаСКомментариями = "
	|ВЫБРАТЬ
	|	Ссылка КАК СпособРаспределенияЗатрат,
	|	ТипФильтраПриРаспределенииЗатратНаВыпуск,
	|	БазаРаспределенияЗатрат,
	|	ПоказательБазыРаспределения,
	|	ТипЦен,
	|	ОсновноеСырье,
	|	СпособРаспределенияЗатратПоПодразделениям,
	|	РаспределятьНаСобственнуюПродукцию,
	|	РаспределятьНаПродукциюСтороннегоПереработчика,
	|	РаспределятьНаПродукциюИзДавальческогоСырья,
	|	РаспределятьНаНаработку,
	|	РаспределятьНаПодчиненныеПодразделения,
	|	Процент,
	|	Коэффициент,
	|	ПорядокОкругления,
	|	НастройкиПостроителя,
	|	ИзменитьНаПроцент,
	|	ИзменитьНаКоэффициент,
	|	ОкруглитьДо,
	|	СпособыРаспределения.Фильтры.(
	|		Ссылка,
	|		НомерСтроки,
	|		НоменклатурнаяГруппа,
	|		Продукция,
	|		Коэффициент
	|	)
	|ИЗ
	|	Справочник.СпособыРаспределенияЗатратНаВыпуск КАК СпособыРаспределения
	|	
	|ГДЕ
	|	Не СпособыРаспределения.ПометкаУдаления
	|	И СпособыРаспределения.Ссылка В (
	|		ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			СпособРаспределения
	|		ИЗ
	|			РаспределяемыеСтатьиЗатрат%СуффиксУчета%
	|		ГДЕ
	|			Не НеРаспределять
	|			И ХарактерРаспределенияЗатрат В (&МассивХарактеровРаспределения)
	|
	|		ОБЪЕДИНИТЬ ВСЕ
	|
	|		ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			СпособРаспределения.СпособРаспределенияЗатратПоПодразделениям КАК СпособРаспределения
	|		ИЗ
	|			РаспределяемыеСтатьиЗатрат%СуффиксУчета%
	|		ГДЕ
	|			Не НеРаспределять
	|			И РаспределятьНаПодчиненныеПодразделения
	|			И ХарактерРаспределенияЗатрат В (&МассивХарактеровРаспределения)
    |		)
	|";
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = УправлениеЗатратами.ЗаменитьКомментарииВТекстеЗапроса(
		ТекстЗапросаСКомментариями, 
		СтруктураШапкиДокумента.ВидОтраженияВУчете
	);
	Запрос.УстановитьПараметр("КонГраница",	СтруктураШапкиДокумента.мКонГраница);
	Запрос.УстановитьПараметр("Организация", СтруктураШапкиДокумента.Организация);
	
	МассивХарактеровРаспределения = Новый Массив;
	Если СтруктураШапкиДокумента.ИспользоватьРасширеннуюАналитику Тогда
		МассивХарактеровРаспределения.Добавить(Перечисления.ХарактерРаспределенияЗатрат.ПроизводственныеРасходы);
		МассивХарактеровРаспределения.Добавить(Перечисления.ХарактерРаспределенияЗатрат.НеУчитыватьПодразделение);
		МассивХарактеровРаспределения.Добавить(Перечисления.ХарактерРаспределенияЗатрат.УчитыватьПодразделение);
		МассивХарактеровРаспределения.Добавить(Перечисления.ХарактерРаспределенияЗатрат.ПустаяСсылка());
		
	ИначеЕсли РаспределениеКосвенныхЗатрат Тогда
		МассивХарактеровРаспределения.Добавить(Перечисления.ХарактерРаспределенияЗатрат.НеУчитыватьПодразделение);
		МассивХарактеровРаспределения.Добавить(Перечисления.ХарактерРаспределенияЗатрат.УчитыватьПодразделение);
		МассивХарактеровРаспределения.Добавить(Перечисления.ХарактерРаспределенияЗатрат.ПустаяСсылка());
		
	Иначе
		МассивХарактеровРаспределения.Добавить(Перечисления.ХарактерРаспределенияЗатрат.ПроизводственныеРасходы);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("МассивХарактеровРаспределения", МассивХарактеровРаспределения);

	Возврат Запрос;
	
КонецФункции // СформироватьЗапросПоИспользуемымСпособамРаспределения()

// Функция формирует структуру параметров для расчета базы распределения затрат.
//
// Параметры:
//  СтруктураШапкиДокумента – Структура - Реквизиты документа "Расчет себестоимости выпуска"
//
// Возвращаемое значение:
//	Структура - Структура параметров
//
Функция СформироватьСтруктуруПараметровРасчетаБазыРаспределения(
	СтруктураШапкиДокумента
	)
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ОтражатьВУправленческомУчете", СтруктураШапкиДокумента.ОтражатьВУправленческомУчете);
	СтруктураПараметров.Вставить("ОтражатьВБухгалтерскомУчете", СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете);
	СтруктураПараметров.Вставить("ОтражатьВМеждународномУчете", СтруктураШапкиДокумента.ОтражатьВМеждународномУчете);
	СтруктураПараметров.Вставить("Организация", СтруктураШапкиДокумента.Организация);
	СтруктураПараметров.Вставить("ИспользоватьРасширеннуюАналитику", СтруктураШапкиДокумента.ИспользоватьРасширеннуюАналитику);
	
	Если СтруктураШапкиДокумента.Свойство("ОтладочныйРежим") Тогда
		СтруктураПараметров.Вставить("ОтладочныйРежим", СтруктураШапкиДокумента.ОтладочныйРежим);
	Иначе
		СтруктураПараметров.Вставить("ОтладочныйРежим", Ложь);
	КонецЕсли;
	
	Если СтруктураШапкиДокумента.Свойство("Подразделение") Тогда
		СтруктураПараметров.Вставить("Подразделение", СтруктураШапкиДокумента.Подразделение);
	КонецЕсли;
	Если СтруктураШапкиДокумента.Свойство("ПодразделениеОрганизации") Тогда
		СтруктураПараметров.Вставить("ПодразделениеОрганизации", СтруктураШапкиДокумента.ПодразделениеОрганизации);
	КонецЕсли;
		
	НачГраница = Новый Граница(НачалоМесяца(СтруктураШапкиДокумента.Период), ВидГраницы.Включая);
	КонГраница = Новый Граница(КонецМесяца (СтруктураШапкиДокумента.Период), ВидГраницы.Включая);
	НачДата = НачалоМесяца(СтруктураШапкиДокумента.Период);
	КонДата = КонецМесяца (СтруктураШапкиДокумента.Период);
	
	СтруктураПараметров.Вставить("НачГраница", НачГраница);
	СтруктураПараметров.Вставить("КонГраница", КонГраница);
	СтруктураПараметров.Вставить("НачДата", НачДата);
	СтруктураПараметров.Вставить("КонДата", КонДата);
	
	Если СтруктураШапкиДокумента.ОтражатьВУправленческомУчете Тогда
		ВалютаУчета = глЗначениеПеременной("ВалютаУправленческогоУчета");
	ИначеЕсли СтруктураШапкиДокумента.ОтражатьВМеждународномУчете Тогда
		ВалютаУчета = глЗначениеПеременной("ВалютаМеждународногоУчета");
	Иначе
		ВалютаУчета = глЗначениеПеременной("ВалютаРегламентированногоУчета");
	КонецЕсли;
	
	СтруктураПараметров.Вставить("ВалютаУчета", ВалютаУчета);
	СтруктураПараметров.Вставить("Дата", СтруктураШапкиДокумента.Период);
	СтруктураПараметров.Вставить("Период", СтруктураШапкиДокумента.Период);
	СтруктураПараметров.Вставить("БазаРаспределенияЗатрат");
	СтруктураПараметров.Вставить("ПоказательБазыРаспределения");
	СтруктураПараметров.Вставить("ТипЦен");
	СтруктураПараметров.Вставить("РаспределятьНаСобственнуюПродукцию");
	СтруктураПараметров.Вставить("РаспределятьНаПродукциюИзДавальческогоСырья");
	СтруктураПараметров.Вставить("РаспределятьНаПродукциюСтороннегоПереработчика");
	СтруктураПараметров.Вставить("РаспределятьНаНаработку");
	СтруктураПараметров.Вставить("ИзменитьНаКоэффициент");
	СтруктураПараметров.Вставить("Коэффициент");
	СтруктураПараметров.Вставить("ИзменитьНаПроцент");
	СтруктураПараметров.Вставить("Процент");
	СтруктураПараметров.Вставить("ОкруглитьДо");
	СтруктураПараметров.Вставить("ПорядокОкругления");
	СтруктураПараметров.Вставить("СпособРаспределенияЗатрат");
	СтруктураПараметров.Вставить("ОсновноеСырье");
	СтруктураПараметров.Вставить("ТипФильтраПриРаспределенииЗатратНаВыпуск");
	СтруктураПараметров.Вставить("Фильтры");
	СтруктураПараметров.Вставить("СтатьяЗатрат");
	МассивВидовУчета = Новый Массив;
	Если СтруктураШапкиДокумента.ОтражатьВУправленческомУчете Тогда
		МассивВидовУчета.Добавить(Перечисления.ВидыОтраженияВУчете.ОтражатьВУправленческомУчете);
	КонецЕсли;
	Если СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете Тогда
		МассивВидовУчета.Добавить(Перечисления.ВидыОтраженияВУчете.ОтражатьВБухгалтерскомУчете);
	КонецЕсли;
	
	Если Не СтруктураШапкиДокумента.ИспользоватьРасширеннуюАналитику
	   И СтруктураШапкиДокумента.ОтражатьВМеждународномУчете
	Тогда
		МассивВидовУчета.Добавить(Перечисления.ВидыОтраженияВУчете.ОтражатьВМеждународномУчете);
	КонецЕсли;
	СтруктураПараметров.Вставить("МассивВидовУчета", МассивВидовУчета);
	
	СуффиксыИмениРегистра = Новый Соответствие;
	СуффиксыИмениРегистра.Вставить(Перечисления.ВидыОтраженияВУчете.ОтражатьВУправленческомУчете, "");
	СуффиксыИмениРегистра.Вставить(Перечисления.ВидыОтраженияВУчете.ОтражатьВБухгалтерскомУчете, "БухгалтерскийУчет");
	СуффиксыИмениРегистра.Вставить(Перечисления.ВидыОтраженияВУчете.ОтражатьВМеждународномУчете, "МеждународныйУчет");
	СтруктураПараметров.Вставить("СуффиксыИмениРегистра", СуффиксыИмениРегистра);
	
	Возврат СтруктураПараметров;
	
КонецФункции // СформироватьСтруктуруПараметровРасчетаБазыРаспределения()

// Функция определяет признак наличия баз распределения по стоимости затрат
// и по основному сырью.
//
// Параметры:
//  СтруктураШапкиДокумента – Структура - Реквизиты документа
//	СтруктураПараметров - Структура - Структура параметров способа распределения
//	РаспределениеКосвенныхЗатрат - Булево - Признак расчета базы распределения косвенных затрат
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц
//
// Возвращаемое значение:
//	Булево - Будет рассчитываться база распределения по стоимости затрат и по основному сырью
//
Функция ЕстьБазаРаспределенияПоСтоимостиЗатратИПоОсновномуСырью(
	СтруктураШапкиДокумента,
	СтруктураПараметров,
	РаспределениеКосвенныхЗатрат,
	МенеджерВременныхТаблиц
	)
	
	ТекстЗапросаСКомментариями = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1
	|	СпособРаспределения
	|ИЗ
	|	РаспределяемыеСтатьиЗатрат%СуффиксУчета%
	|ГДЕ
	|	Не НеРаспределять
	|	И ХарактерРаспределенияЗатрат В (&МассивХарактеровРаспределения)
	|	И (СпособРаспределения.БазаРаспределенияЗатрат =
	|	 	ЗНАЧЕНИЕ(Перечисление.БазыРаспределенияЗатрат.ПоСтоимостиЗатрат)
	|	 ИЛИ 
	|	 СпособРаспределения.БазаРаспределенияЗатрат =
	|	 	ЗНАЧЕНИЕ(Перечисление.БазыРаспределенияЗатрат.ПоОсновномуСырью)
	|	 ИЛИ 
	|	 СпособРаспределения.СпособРаспределенияЗатратПоПодразделениям.БазаРаспределенияЗатрат =
	|	 	ЗНАЧЕНИЕ(Перечисление.БазыРаспределенияЗатрат.ПоСтоимостиЗатрат)
	|	 ИЛИ 
	|	 СпособРаспределения.СпособРаспределенияЗатратПоПодразделениям.БазаРаспределенияЗатрат =
	|	 	ЗНАЧЕНИЕ(Перечисление.БазыРаспределенияЗатрат.ПоОсновномуСырью)
	|	 )
	|";
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	МассивХарактеровРаспределения = Новый Массив;
	Если СтруктураШапкиДокумента.ИспользоватьРасширеннуюАналитику Тогда
		МассивХарактеровРаспределения.Добавить(Перечисления.ХарактерРаспределенияЗатрат.ПроизводственныеРасходы);
		МассивХарактеровРаспределения.Добавить(Перечисления.ХарактерРаспределенияЗатрат.НеУчитыватьПодразделение);
		МассивХарактеровРаспределения.Добавить(Перечисления.ХарактерРаспределенияЗатрат.УчитыватьПодразделение);
		МассивХарактеровРаспределения.Добавить(Перечисления.ХарактерРаспределенияЗатрат.ПустаяСсылка());
		
	ИначеЕсли РаспределениеКосвенныхЗатрат Тогда
		МассивХарактеровРаспределения.Добавить(Перечисления.ХарактерРаспределенияЗатрат.НеУчитыватьПодразделение);
		МассивХарактеровРаспределения.Добавить(Перечисления.ХарактерРаспределенияЗатрат.УчитыватьПодразделение);
		МассивХарактеровРаспределения.Добавить(Перечисления.ХарактерРаспределенияЗатрат.ПустаяСсылка());
		
	Иначе
		МассивХарактеровРаспределения.Добавить(Перечисления.ХарактерРаспределенияЗатрат.ПроизводственныеРасходы);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("МассивХарактеровРаспределения", МассивХарактеровРаспределения);
	
	ЕстьБазаРаспределения = Ложь;
	
	// Цикл по всем видам отражения в учете, для которых рассчитывается база распределения.
	Для Каждого ВидОтраженияВУчете Из СтруктураПараметров.МассивВидовУчета Цикл
		
	  	Запрос.Текст = УправлениеЗатратами.ЗаменитьКомментарииВТекстеЗапроса(
			ТекстЗапросаСКомментариями, 
			ВидОтраженияВУчете
		);
	
		РезультатЗапроса = Запрос.Выполнить();
		Если Не РезультатЗапроса.Пустой() Тогда
			ЕстьБазаРаспределения = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ЕстьБазаРаспределения;
	
КонецФункции // ЕстьБазаРаспределенияПоСтоимостиЗатратИПоОсновномуСырью()

// Функция определяет признак наличия баз распределения по объему продаж.
//
// Параметры:
//  СтруктураШапкиДокумента – Структура - Реквизиты документа
//	СтруктураПараметров - Структура - Структура параметров способа распределения
//	РаспределениеКосвенныхЗатрат - Булево - Признак расчета базы распределения косвенных затрат
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц
//
// Возвращаемое значение:
//	Булево - Будет рассчитываться база распределения по объему продаж
//
Функция ЕстьБазаРаспределенияПоОбъемуПродаж(
	СтруктураШапкиДокумента,
	СтруктураПараметров,
	РаспределениеКосвенныхЗатрат,
	МенеджерВременныхТаблиц
	)
	
	ТекстЗапросаСКомментариями = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1
	|	СпособРаспределения
	|ИЗ
	|	РаспределяемыеСтатьиЗатрат%СуффиксУчета%
	|ГДЕ
	|	Не НеРаспределять
	|	И ХарактерРаспределенияЗатрат В (&МассивХарактеровРаспределения)
	|	И (СпособРаспределения.БазаРаспределенияЗатрат =
	|	 	ЗНАЧЕНИЕ(Перечисление.БазыРаспределенияЗатрат.ПоОбъемуПродаж)
	|	 ИЛИ 
	|	 СпособРаспределения.ПоказательБазыРаспределения =
	|	 	ЗНАЧЕНИЕ(Перечисление.ВидыПоказателейБазыРаспределения.ФактическаяЦенаРеализации)
	|	 ИЛИ 
	|	 СпособРаспределения.СпособРаспределенияЗатратПоПодразделениям.БазаРаспределенияЗатрат =
	|	 	ЗНАЧЕНИЕ(Перечисление.БазыРаспределенияЗатрат.ПоОбъемуПродаж)
	|	 ИЛИ 
	|	 СпособРаспределения.СпособРаспределенияЗатратПоПодразделениям.ПоказательБазыРаспределения =
	|	 	ЗНАЧЕНИЕ(Перечисление.ВидыПоказателейБазыРаспределения.ФактическаяЦенаРеализации)
	|	 )
	|";
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	МассивХарактеровРаспределения = Новый Массив;
	Если СтруктураШапкиДокумента.ИспользоватьРасширеннуюАналитику Тогда
		МассивХарактеровРаспределения.Добавить(Перечисления.ХарактерРаспределенияЗатрат.ПроизводственныеРасходы);
		МассивХарактеровРаспределения.Добавить(Перечисления.ХарактерРаспределенияЗатрат.НеУчитыватьПодразделение);
		МассивХарактеровРаспределения.Добавить(Перечисления.ХарактерРаспределенияЗатрат.УчитыватьПодразделение);
		МассивХарактеровРаспределения.Добавить(Перечисления.ХарактерРаспределенияЗатрат.ПустаяСсылка());
		
	ИначеЕсли РаспределениеКосвенныхЗатрат Тогда
		МассивХарактеровРаспределения.Добавить(Перечисления.ХарактерРаспределенияЗатрат.НеУчитыватьПодразделение);
		МассивХарактеровРаспределения.Добавить(Перечисления.ХарактерРаспределенияЗатрат.УчитыватьПодразделение);
		МассивХарактеровРаспределения.Добавить(Перечисления.ХарактерРаспределенияЗатрат.ПустаяСсылка());
		
	Иначе
		МассивХарактеровРаспределения.Добавить(Перечисления.ХарактерРаспределенияЗатрат.ПроизводственныеРасходы);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("МассивХарактеровРаспределения", МассивХарактеровРаспределения);
	
	ЕстьБазаРаспределения = Ложь;
	
	// Цикл по всем видам отражения в учете, для которых рассчитывается база распределения.
	Для Каждого ВидОтраженияВУчете Из СтруктураПараметров.МассивВидовУчета Цикл
		
	  	Запрос.Текст = УправлениеЗатратами.ЗаменитьКомментарииВТекстеЗапроса(
			ТекстЗапросаСКомментариями, 
			ВидОтраженияВУчете
		);
	
		РезультатЗапроса = Запрос.Выполнить();
		Если Не РезультатЗапроса.Пустой() Тогда
			ЕстьБазаРаспределения = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ЕстьБазаРаспределения;
	
КонецФункции // ЕстьБазаРаспределенияПоОбъемуПродаж()

// Процедура формирует временные таблицы для расчета баз распределения.
//
// Параметры:
//  СтруктураШапкиДокумента – Структура - Реквизиты документа "Расчет себестоимости выпуска"
//	СтруктураПараметров - Структура - Структура параметров способа распределения
//	РаспределениеКосвенныхЗатрат - Булево - Признак расчета базы распределения косвенных затрат
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц
//
Процедура СформироватьВременныеТаблицы(
	СтруктураШапкиДокумента,
	СтруктураПараметров,
	РаспределениеКосвенныхЗатрат,
	МенеджерВременныхТаблиц
	)
	
	// Формирование временной таблицы «РаспределяемыеСтатьиЗатрат» по данным регистров сведений 
	// «Способы распределения статей затрат» и «Способы распределения статей затрат организаций».
	СформироватьВременнуюТаблицуРаспределяемыеСтатьиЗатрат(
		СтруктураШапкиДокумента,
		СтруктураПараметров,
		МенеджерВременныхТаблиц
	);
	
	// Формирование временной таблицы "ВыпускПродукции" по выпущенной продукции и по наработке продукции в текущем месяце.
	СформироватьВременнуюТаблицуВыпускПродукции(
		СтруктураШапкиДокумента,
		СтруктураПараметров,
		МенеджерВременныхТаблиц
	);
	
	// Формирование временной таблицы "ЦеныНоменклатуры" для продукции, выпущенной в текущем месяце, по типам цен, указанным
	// в способах распределения затрат.
	СформироватьВременнуюТаблицуЦеныНоменклатуры(
		СтруктураШапкиДокумента,
		СтруктураПараметров,
		МенеджерВременныхТаблиц
	);
	
	// Определение признака использования баз распределения "По стоимости затрат" и "По основному сырью".
	// При расчете себестоимости выпуска признак определяется по данным временной таблицы "РаспределяемыеСтатьиЗатрат".
	Если ТипЗнч(СтруктураШапкиДокумента.Ссылка) = Тип("ДокументСсылка.РасчетСебестоимостиВыпуска") Тогда
		ЕстьБазаРаспределенияПоСтоимости = ЕстьБазаРаспределенияПоСтоимостиЗатратИПоОсновномуСырью(
			СтруктураШапкиДокумента,
			СтруктураПараметров,
			РаспределениеКосвенныхЗатрат,
			МенеджерВременныхТаблиц
		);
	// При заполнении документов распределения затрат признак определяется по данным из структуры параметров.	
	ИначеЕсли СтруктураПараметров.Свойство("ЕстьБазаРаспределенияПоСтоимости") Тогда
	    ЕстьБазаРаспределенияПоСтоимости = СтруктураПараметров.ЕстьБазаРаспределенияПоСтоимости;
	ИначеЕсли СтруктураПараметров.БазаРаспределенияЗатрат = Перечисления.БазыРаспределенияЗатрат.ПоСтоимостиЗатрат
		ИЛИ СтруктураПараметров.БазаРаспределенияЗатрат = Перечисления.БазыРаспределенияЗатрат.ПоОсновномуСырью
	Тогда
		ЕстьБазаРаспределенияПоСтоимости = Истина;
	Иначе
		ЕстьБазаРаспределенияПоСтоимости = Ложь;
	КонецЕсли;

	// Временные таблицы "ЗатратыНаВыпуск" и "НезавершенноеПроизводство" формируем только
	// при наличии баз распределения "По стоимости затрат" и "По основному сырью".
	Если ЕстьБазаРаспределенияПоСтоимости Тогда
		
		// Формирование временной таблицы "АналитикаВидаУчетаЗатратыНаВыпуск" по элементам справочника 
		// «Ключи аналитики вида учета», для которых задан раздел учета «ЗатратыНаВыпуск» или «ЗатратыПоНаработке».
		Если СтруктураШапкиДокумента.ИспользоватьРасширеннуюАналитику Тогда
			СформироватьВременнуюТаблицуАналитикаВидаУчетаЗатратыНаВыпуск(
				СтруктураШапкиДокумента,
				СтруктураПараметров,
				МенеджерВременныхТаблиц
			);
		КонецЕсли;
		
		// Формирование временной таблицы "ЗатратыНаВыпуск" по всем затратам текущего месяца.
		СформироватьВременнуюТаблицуЗатратыНаВыпуск(
			СтруктураШапкиДокумента,
			СтруктураПараметров,
			МенеджерВременныхТаблиц
		);
		
		// Формирование временной таблицы "НезавершенноеПроизводство" по всем затратам текущего месяца.
		СформироватьВременнуюТаблицуНезавершенноеПроизводство(
			СтруктураШапкиДокумента,
			СтруктураПараметров,
			РаспределениеКосвенныхЗатрат,
			МенеджерВременныхТаблиц
		);
	КонецЕсли;
	
	// При расчете себестоимости выпуска признак определяется по данным временной таблицы "РаспределяемыеСтатьиЗатрат".
	Если ТипЗнч(СтруктураШапкиДокумента.Ссылка) = Тип("ДокументСсылка.РасчетСебестоимостиВыпуска") Тогда
		ЕстьБазаПоОбъемуПродаж = ЕстьБазаРаспределенияПоОбъемуПродаж(
			СтруктураШапкиДокумента,
			СтруктураПараметров,
			РаспределениеКосвенныхЗатрат,
			МенеджерВременныхТаблиц
		);
		
	// При заполнении документов распределения затрат признак определяется по данным из структуры параметров.		
	ИначеЕсли СтруктураПараметров.Свойство("ЕстьБазаПоОбъемуПродаж") Тогда
	    ЕстьБазаПоОбъемуПродаж = СтруктураПараметров.ЕстьБазаПоОбъемуПродаж;
	ИначеЕсли СтруктураПараметров.БазаРаспределенияЗатрат = Перечисления.БазыРаспределенияЗатрат.ПоОбъемуПродаж
		ИЛИ СтруктураПараметров.ПоказательБазыРаспределения = Перечисления.ВидыПоказателейБазыРаспределения.ФактическаяЦенаРеализации
	Тогда
		ЕстьБазаПоОбъемуПродаж = Истина;
	Иначе
		ЕстьБазаПоОбъемуПродаж = Ложь;
	КонецЕсли;
	
	// Временная таблица "Продажи" формируем только
	// при наличии баз распределения "По объему продаж"
	// или при наличии показателя "Фактическая цена реализации".
	Если ЕстьБазаПоОбъемуПродаж Тогда
		СформироватьВременнуюТаблицуПродажи(
			СтруктураШапкиДокумента,
			СтруктураПараметров,
			МенеджерВременныхТаблиц
		);
	КонецЕсли;
		
КонецПроцедуры // СформироватьВременныетаблицы()

// Процедура рассчитывает базы распределения производственных и косвенных затрат.
//
// Параметры:
//  СтруктураШапкиДокумента – Структура - Реквизиты документа "Расчет себестоимости выпуска"
//	РаспределениеКосвенныхЗатрат - Булево - Признак расчета базы распределения косвенных затрат
//
Процедура РасчетБазыРаспределенияЗатрат(
	СтруктураШапкиДокумента,
	РаспределениеКосвенныхЗатрат
	) Экспорт
	
	СтруктураПараметров = СформироватьСтруктуруПараметровРасчетаБазыРаспределения(СтруктураШапкиДокумента);
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	СформироватьВременныеТаблицы(
		СтруктураШапкиДокумента,
		СтруктураПараметров,
		РаспределениеКосвенныхЗатрат,
		МенеджерВременныхТаблиц
	);
	
	СтруктураИмен = Новый Структура("БазаРаспределенияЗатрат");
	СтруктураДвижений = ПроцедурыРасчетаСебестоимостиВыпуска.ПолучитьНаборыДвижений(
		СтруктураШапкиДокумента, 
		СтруктураИмен
	);
	НаборЗаписейБазаРаспределенияЗатрат = СтруктураДвижений.ДвиженияБазаРаспределенияЗатрат;
	
	СтруктураПостроителиЗапроса = Новый Структура;
	
	Запрос = СформироватьЗапросПоИспользуемымСпособамРаспределения(
		СтруктураШапкиДокумента,
		РаспределениеКосвенныхЗатрат,
		МенеджерВременныхТаблиц
	);
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ЗаполнитьЗначенияСвойств(СтруктураПараметров, Выборка);
	
		РассчитатьБазуРаспределения(
			СтруктураШапкиДокумента,
			СтруктураПараметров,
			СтруктураПостроителиЗапроса,
			РаспределениеКосвенныхЗатрат,
			МенеджерВременныхТаблиц,
			НаборЗаписейБазаРаспределенияЗатрат
		);
		
	КонецЦикла;
	
	Если НаборЗаписейБазаРаспределенияЗатрат.Модифицированность() Тогда
		НаборЗаписейБазаРаспределенияЗатрат.Записать(Ложь);
	КонецЕсли;
	
	МенеджерВременныхТаблиц.Закрыть();

КонецПроцедуры // РасчетБазыРаспределенияЗатрат()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ РАСЧЕТА БАЗ РАСПРЕДЕЛЕНИЯ ДЛЯ ЗАПОЛНЕНИЯ ДОКУМЕНТОВ

// Функция формирует таблицу значений для отражения базы распределения.
//
// Возвращаемое значение:
//	ТаблицаЗначений - Таблица значений для базы распределения
//
Функция СформироватьПустуюТаблицуБазыРаспределения()
	
	МассивТиповЗаказа = Новый Массив;
	МассивТиповЗаказа.Добавить(Тип("ДокументСсылка.ЗаказПокупателя"));
	МассивТиповЗаказа.Добавить(Тип("ДокументСсылка.ЗаказНаПроизводство"));
	
	ТаблицаРезультата = Новый ТаблицаЗначений;
	ТаблицаРезультата.Колонки.Добавить("СпособРаспределенияЗатрат",		Новый ОписаниеТипов("СправочникСсылка.СпособыРаспределенияЗатратНаВыпуск"));
	ТаблицаРезультата.Колонки.Добавить("Подразделение",					Новый ОписаниеТипов("СправочникСсылка.Подразделения"));
	ТаблицаРезультата.Колонки.Добавить("ПодразделениеОрганизации",		Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	ТаблицаРезультата.Колонки.Добавить("НоменклатурнаяГруппа",			Новый ОписаниеТипов("СправочникСсылка.НоменклатурныеГруппы"));
	ТаблицаРезультата.Колонки.Добавить("Заказ",							Новый ОписаниеТипов(МассивТиповЗаказа));
	ТаблицаРезультата.Колонки.Добавить("Номенклатура",					Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаРезультата.Колонки.Добавить("ХарактеристикаНоменклатуры",	Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	ТаблицаРезультата.Колонки.Добавить("СерияНоменклатуры",				Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры"));
	ТаблицаРезультата.Колонки.Добавить("Продукция",						Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаРезультата.Колонки.Добавить("ХарактеристикаПродукции",		Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	ТаблицаРезультата.Колонки.Добавить("СерияПродукции",				Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры"));
	ТаблицаРезультата.Колонки.Добавить("Спецификация",					Новый ОписаниеТипов("СправочникСсылка.СпецификацииНоменклатуры"));
	ТаблицаРезультата.Колонки.Добавить("ВариантВыпускаПродукции",		Новый ОписаниеТипов("ПеречислениеСсылка.ВариантыВыпускаПродукции"));
	ТаблицаРезультата.Колонки.Добавить("ВидВыпуска",					Новый ОписаниеТипов("ПеречислениеСсылка.ВидыВыпуска"));
	ТаблицаРезультата.Колонки.Добавить("СчетЗатрат",					Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	ТаблицаРезультата.Колонки.Добавить("НалоговоеНазначение",			Новый ОписаниеТипов("СправочникСсылка.НалоговыеНазначенияАктивовИЗатрат"));
	ТаблицаРезультата.Колонки.Добавить("ТехнологическаяОперация",		Новый ОписаниеТипов("СправочникСсылка.ТехнологическиеОперации"));
	
	ТаблицаРезультата.Колонки.Добавить("ОтражатьВУправленческомУчете", 	Новый ОписаниеТипов("Булево"));
	ТаблицаРезультата.Колонки.Добавить("ОтражатьВБухгалтерскомУчете", 	Новый ОписаниеТипов("Булево"));
	
	ТаблицаРезультата.Колонки.Добавить("База", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 3)));
	ТаблицаРезультата.Колонки.Добавить("БазаПриход", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 3)));
	ТаблицаРезультата.Колонки.Добавить("БазаОстатокНЗП", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 3)));
	
	ТаблицаРезультата.Колонки.Добавить("БазаРегл", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 3)));
	ТаблицаРезультата.Колонки.Добавить("БазаПриходРегл", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 3)));
	ТаблицаРезультата.Колонки.Добавить("БазаОстатокНЗПРегл", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 3)));
	
	
	Возврат ТаблицаРезультата;
	
КонецФункции // СформироватьПустуюТаблицуБазыРаспределения()

// Функция формирует текст запроса по базам распределения.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция СформироватьТекстЗапросаПоБазамРаспределения()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	БазаРаспределенияЗатрат.ИндексУчета,
	|	ВЫБОР КОГДА &ЕстьПодразделениеВДокументе ТОГДА
	|		Неопределено
	|	КОГДА БазаРаспределенияЗатрат.Подразделение = Неопределено ТОГДА
	|		СоответствиеПодразделений.Подразделение
	|	ИНАЧЕ
	|		БазаРаспределенияЗатрат.Подразделение
	|	КОНЕЦ КАК Подразделение,
	|	
	|	ВЫБОР КОГДА &ЕстьПодразделениеВДокументе ТОГДА
	|		Неопределено
	|	КОГДА БазаРаспределенияЗатрат.ПодразделениеОрганизации = Неопределено ТОГДА
	|		СоответствиеПодразделений.ПодразделениеОрганизации
	|	ИНАЧЕ
	|		БазаРаспределенияЗатрат.ПодразделениеОрганизации
	|	КОНЕЦ КАК ПодразделениеОрганизации,
	|	БазаРаспределенияЗатрат.Продукция КАК Номенклатура,
	|	БазаРаспределенияЗатрат.ХарактеристикаПродукции КАК ХарактеристикаНоменклатуры,
	|	БазаРаспределенияЗатрат.СерияПродукции КАК СерияНоменклатуры,
	|	БазаРаспределенияЗатрат.Продукция,
	|	БазаРаспределенияЗатрат.ХарактеристикаПродукции,
	|	БазаРаспределенияЗатрат.СерияПродукции,
	|	БазаРаспределенияЗатрат.Заказ,
	|	БазаРаспределенияЗатрат.НоменклатурнаяГруппа,
	|	БазаРаспределенияЗатрат.Спецификация,
	|	БазаРаспределенияЗатрат.ВариантВыпускаПродукции,
	|	БазаРаспределенияЗатрат.ВидВыпуска,
	|	БазаРаспределенияЗатрат.СчетЗатрат КАК СчетЗатрат,
	|   БазаРаспределенияЗатрат.НалоговоеНазначение КАК НалоговоеНазначение,
	|	
	|	ВЫБОР КОГДА БазаРаспределенияЗатрат.ИндексУчета = 3 ТОГДА
	|		БазаРаспределенияЗатрат.База
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК База, 
	|	ВЫБОР КОГДА БазаРаспределенияЗатрат.ИндексУчета = 3 ТОГДА
	|		БазаРаспределенияЗатрат.БазаПриход
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК БазаПриход, 
	|	ВЫБОР КОГДА БазаРаспределенияЗатрат.ИндексУчета = 3 ТОГДА
	|		БазаРаспределенияЗатрат.БазаОстатокНЗП
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК БазаОстатокНЗП, 
	|	
	|	ВЫБОР КОГДА БазаРаспределенияЗатрат.ИндексУчета = 2 ТОГДА
	|		БазаРаспределенияЗатрат.База
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК БазаРегл, 
	|	ВЫБОР КОГДА БазаРаспределенияЗатрат.ИндексУчета = 2 ТОГДА
	|		БазаРаспределенияЗатрат.БазаПриход
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК БазаПриходРегл,
	|	ВЫБОР КОГДА БазаРаспределенияЗатрат.ИндексУчета = 2 ТОГДА
	|		БазаРаспределенияЗатрат.БазаОстатокНЗП
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК БазаОстатокНЗПРегл 
 	|
	|ИЗ (
	|	//ДляУпрУчета ВЫБРАТЬ
	|	//ДляУпрУчета 	3 КАК ИндексУчета,
	|	//ДляУпрУчета 	Подразделение КАК Подразделение,
	|	//ДляУпрУчета 	Неопределено КАК ПодразделениеОрганизации,
	|	//ДляУпрУчета 	Неопределено КАК СчетЗатрат,
	|	//ДляУпрУчета 	Неопределено КАК НалоговоеНазначение,
	|	//ДляУпрУчета 	*
	|   //ДляУпрУчета ИЗ
	|	//ДляУпрУчета 	БазаРаспределения
	|
	|	//ДляУпрУчета //ДляБухУчета ОБЪЕДИНИТЬ ВСЕ
	|
	|	//ДляБухУчета ВЫБРАТЬ
	|	//ДляБухУчета 	2 КАК ИндексУчета,
	|	//ДляБухУчета 	Неопределено КАК Подразделение,
	|	//ДляБухУчета 	Подразделение КАК ПодразделениеОрганизации,
	|	//ДляБухУчета 	СчетУчета КАК СчетЗатрат,
	|	//ДляБухУчета 	НалоговоеНазначение КАК НалоговоеНазначение,
	|	//ДляБухУчета 	*
	|   //ДляБухУчета ИЗ
	|	//ДляБухУчета 	БазаРаспределенияБухгалтерскийУчет
	|
	|	) КАК БазаРаспределенияЗатрат
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ
	|			СоответствиеПодразделений.Подразделение,
	|			СоответствиеПодразделений.Организация,
	|			СоответствиеПодразделений.ПодразделениеОрганизации
	|		ИЗ
	|			РегистрСведений.СоответствиеПодразделенийИПодразделенийОрганизаций КАК СоответствиеПодразделений
	|		) КАК СоответствиеПодразделений
	|	ПО
	|		БазаРаспределенияЗатрат.Организация = СоответствиеПодразделений.Организация
	|		И (БазаРаспределенияЗатрат.Подразделение = СоответствиеПодразделений.Подразделение
	|		ИЛИ 
	|		БазаРаспределенияЗатрат.ПодразделениеОрганизации = СоответствиеПодразделений.ПодразделениеОрганизации)
	|
	|{ГДЕ
	|	БазаРаспределенияЗатрат.Подразделение.* 			КАК Подразделение,
	|	БазаРаспределенияЗатрат.ПодразделениеОрганизации.* 	КАК ПодразделениеОрганизации,
	|	БазаРаспределенияЗатрат.Продукция.* КАК Продукция,
	|	БазаРаспределенияЗатрат.ХарактеристикаПродукции.* КАК ХарактеристикаПродукции
	|}
	|
	|УПОРЯДОЧИТЬ ПО
	|	БазаРаспределенияЗатрат.ИндексУчета УБЫВ
	|		
	|ИТОГИ 
	|	МАКСИМУМ(База), МАКСИМУМ(БазаПриход), МАКСИМУМ(БазаОстатокНЗП),
	|	МАКСИМУМ(БазаРегл), МАКСИМУМ(БазаПриходРегл), МАКСИМУМ(БазаОстатокНЗПРегл)
	|ПО
	|	БазаРаспределенияЗатрат.НоменклатурнаяГруппа,
	|	БазаРаспределенияЗатрат.Заказ,
	|	Продукция,
	|	ХарактеристикаПродукции,
	|	СерияПродукции,
	|	БазаРаспределенияЗатрат.Спецификация,
	|	БазаРаспределенияЗатрат.ВидВыпуска,
	|	Подразделение,
	|	ПодразделениеОрганизации,
	|	СчетЗатрат
	|{ИТОГИ ПО
	|	НоменклатурнаяГруппа,
	|	Заказ,
	|	Продукция,
	|	ХарактеристикаПродукции,
	|	СерияПродукции,
	|	Спецификация,
	|	ВидВыпуска,
	|	Подразделение,
	|	ПодразделениеОрганизации,
	|	СчетЗатрат
	|}
	|";

	Возврат ТекстЗапроса;
	
КонецФункции // СформироватьТекстЗапросаПоБазамРаспределения()

// Процедура устанавливает параметры запроса по базам распределения.
//
// Параметры:
//  СтруктураШапкиДокумента – Структура - Реквизиты документа
//	Запрос - Запрос
//
Процедура УстановитьПараметрыЗапросаПоБазамРаспределения(
	СтруктураШапкиДокумента,
	Запрос
	)
	
	Запрос.УстановитьПараметр("КонДата", СтруктураШапкиДокумента.мКонДата);
	Запрос.УстановитьПараметр("ЕстьПодразделениеВДокументе", СтруктураШапкиДокумента.ЕстьПодразделениеВДокументе);
	
КонецПроцедуры // УстановитьПараметрыЗапросаПоБазамРаспределения()

// Процедура формирует таблицу базы распределения из результата запроса.
//
// Параметры:
//	РезультатЗапроса - РезультатЗапроса - Результат запроса по базе распределения
//
// Выходные параметры:
//	ТаблицаРезультата - ТаблицаЗначений - Таблица базы распределения
//
Процедура СформироватьТаблицуБазыРаспределения(
	РезультатЗапроса,
	ТаблицаРезультата,
	СтруктураПараметров
	)
	
	ОбходПоНомГруппам = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ОбходПоНомГруппам.Следующий() Цикл
		ОбходПоЗаказам = ОбходПоНомГруппам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ОбходПоЗаказам.Следующий() Цикл
			ОбходПоПродукции = ОбходПоЗаказам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ОбходПоПродукции.Следующий() Цикл
				ОбходПоХарактеристике = ОбходПоПродукции.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ОбходПоХарактеристике.Следующий() Цикл
					ОбходПоСерии = ОбходПоХарактеристике.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					Пока ОбходПоСерии.Следующий() Цикл
						ОбходПоСпецификации = ОбходПоСерии.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
						Пока ОбходПоСпецификации.Следующий() Цикл
							ОбходПоВидуВыпуска = ОбходПоСпецификации.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
							Пока ОбходПоВидуВыпуска.Следующий() Цикл
								
								Если ОбходПоВидуВыпуска.База = 0
								   И ОбходПоВидуВыпуска.БазаПриход = 0
								   И ОбходПоВидуВыпуска.БазаРегл = 0
								   И ОбходПоВидуВыпуска.БазаПриходРегл = 0
								Тогда
									Продолжить;
								КонецЕсли;
								
								НоваяСтрока = ТаблицаРезультата.Добавить();
								ЗаполнитьЗначенияСвойств(НоваяСтрока, ОбходПоВидуВыпуска);
								
								НоваяСтрока.СпособРаспределенияЗатрат = СтруктураПараметров.СпособРаспределенияЗатрат;
								
								НоваяСтрока.База = 0;
								НоваяСтрока.БазаПриход = 0;
								НоваяСтрока.БазаОстатокНЗП = 0;
								НоваяСтрока.БазаРегл = 0;
								НоваяСтрока.БазаПриходРегл = 0;
								НоваяСтрока.БазаОстатокНЗПРегл = 0;
								
								ОбходПоПодразделению = ОбходПоВидуВыпуска.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
								Пока ОбходПоПодразделению.Следующий() Цикл
									ОбходПоПодразделениюОрганизации = ОбходПоПодразделению.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
									Пока ОбходПоПодразделениюОрганизации.Следующий() Цикл
										ОбходПоСчетуЗатрат = ОбходПоПодразделениюОрганизации.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
										Пока ОбходПоСчетуЗатрат.Следующий() Цикл
												
												Если ЗначениеЗаполнено(НоваяСтрока.СчетЗатрат) И НоваяСтрока.СчетЗатрат <> ОбходПоСчетуЗатрат.СчетЗатрат
												 ИЛИ ЗначениеЗаполнено(НоваяСтрока.НалоговоеНазначение) И НоваяСтрока.НалоговоеНазначение <> ОбходПоСчетуЗатрат.НалоговоеНазначение
												 ИЛИ ЗначениеЗаполнено(НоваяСтрока.Подразделение) И НоваяСтрока.Подразделение <> ОбходПоСчетуЗатрат.Подразделение
												 ИЛИ ЗначениеЗаполнено(НоваяСтрока.ПодразделениеОрганизации) И НоваяСтрока.ПодразделениеОрганизации <> ОбходПоСчетуЗатрат.ПодразделениеОрганизации Тогда
												 
													НоваяСтрока = ТаблицаРезультата.Добавить();
													ЗаполнитьЗначенияСвойств(НоваяСтрока, ОбходПоСчетуЗатрат);
													
													НоваяСтрока.СпособРаспределенияЗатрат = СтруктураПараметров.СпособРаспределенияЗатрат;
													
													НоваяСтрока.База = 0;
													НоваяСтрока.БазаПриход = 0;
													НоваяСтрока.БазаОстатокНЗП = 0;
													НоваяСтрока.БазаРегл = 0;
													НоваяСтрока.БазаПриходРегл = 0;
													НоваяСтрока.БазаОстатокНЗПРегл = 0;
													
												КонецЕсли;
													
												Обход = ОбходПоСчетуЗатрат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
												Пока Обход.Следующий() Цикл
													
													// Управленческий учет.
													Если Обход.ИндексУчета = 3 Тогда
														НоваяСтрока.ВариантВыпускаПродукции = Обход.ВариантВыпускаПродукции;
														НоваяСтрока.ВидВыпуска = Обход.ВидВыпуска;
														НоваяСтрока.Подразделение = Обход.Подразделение;
														НоваяСтрока.База = Обход.База;
														НоваяСтрока.БазаПриход = Обход.БазаПриход;
														НоваяСтрока.БазаОстатокНЗП = Обход.БазаОстатокНЗП;
														
													// Бухгалтерский учет.
													ИначеЕсли Обход.ИндексУчета = 2 Тогда
														НоваяСтрока.ПодразделениеОрганизации = Обход.ПодразделениеОрганизации;
														НоваяСтрока.ВариантВыпускаПродукции = Обход.ВариантВыпускаПродукции;
														НоваяСтрока.ВидВыпуска = Обход.ВидВыпуска;
														НоваяСтрока.СчетЗатрат = Обход.СчетЗатрат;
														НоваяСтрока.НалоговоеНазначение = Обход.НалоговоеНазначение;
														НоваяСтрока.БазаРегл = Обход.БазаРегл;
														НоваяСтрока.БазаПриходРегл = Обход.БазаПриходРегл;
														НоваяСтрока.БазаОстатокНЗПРегл = Обход.БазаОстатокНЗПРегл;
																
																
													КонецЕсли;
														
												КонецЦикла;
												Если СтруктураПараметров.ОкруглитьДо И ЗначениеЗаполнено(СтруктураПараметров.ПорядокОкругления) Тогда
													НоваяСтрока.База = Ценообразование.ОкруглитьЦену(НоваяСтрока.База, СтруктураПараметров.ПорядокОкругления, Истина);
													НоваяСтрока.БазаОстатокНЗП = Ценообразование.ОкруглитьЦену(НоваяСтрока.БазаОстатокНЗП, СтруктураПараметров.ПорядокОкругления, Истина);
													
												КонецЕсли;
										КонецЦикла;
									КонецЦикла;
								КонецЦикла;
							КонецЦикла;
						КонецЦикла;
					КонецЦикла;
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;	

КонецПроцедуры // СформироватьТаблицуБазыРаспределения()

// Функция рассчитывает базы распределения одновременно для нескольких видов учета.
//
Функция СформироватьТаблицуБазыРаспределенияЗатрат(
	СтруктураШапкиДокумента,
	ИсходнаяСтруктураПараметров,
	ТаблицаСпособыРаспределения
	) Экспорт
	
	
	ТаблицаРезультата = СформироватьПустуюТаблицуБазыРаспределения();
	
	Если Не СтруктураШапкиДокумента.ОтражатьВУправленческомУчете 
	   И Не СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете Тогда
		Возврат ТаблицаРезультата;
	КонецЕсли;
	Если  ТаблицаСпособыРаспределения.Количество()=0 Тогда
		Возврат ТаблицаРезультата;
	КонецЕсли;
	
	НачГраница = Новый Граница(НачалоМесяца(СтруктураШапкиДокумента.Дата), ВидГраницы.Включая);
	КонГраница = Новый Граница(КонецМесяца (СтруктураШапкиДокумента.Дата), ВидГраницы.Включая);
	НачДата    = НачалоМесяца(СтруктураШапкиДокумента.Дата);
	КонДата    = КонецМесяца (СтруктураШапкиДокумента.Дата);
	
	СтруктураШапкиДокумента.Вставить("Период", СтруктураШапкиДокумента.Дата);
	СтруктураШапкиДокумента.Вставить("мНачДата", НачДата);
	СтруктураШапкиДокумента.Вставить("мКонДата", КонДата);
	СтруктураШапкиДокумента.Вставить("мНачГраница", НачГраница);
	СтруктураШапкиДокумента.Вставить("мКонГраница", КонГраница);
	СтруктураШапкиДокумента.Вставить("ОтражатьВМеждународномУчете", Ложь);
	
	ЕстьПодразделениеВДокументе = ИсходнаяСтруктураПараметров.Свойство("Подразделение");
	СтруктураШапкиДокумента.Вставить("ЕстьПодразделениеВДокументе", ЕстьПодразделениеВДокументе);
	
	СтруктураПараметров = СформироватьСтруктуруПараметровРасчетаБазыРаспределения(СтруктураШапкиДокумента);
	Если ИсходнаяСтруктураПараметров.Свойство("ЕстьБазаРаспределенияПоСтоимости") Тогда
		СтруктураПараметров.Вставить("ЕстьБазаРаспределенияПоСтоимости");
	КонецЕсли;
	Если ИсходнаяСтруктураПараметров.Свойство("ЕстьБазаПоОбъемуПродаж") Тогда
		СтруктураПараметров.Вставить("ЕстьБазаПоОбъемуПродаж");
	КонецЕсли;
	ЗаполнитьЗначенияСвойств(СтруктураПараметров, ИсходнаяСтруктураПараметров);

	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	СформироватьВременныеТаблицы(
		СтруктураШапкиДокумента,
		СтруктураПараметров,
		Ложь, // РаспределениеКосвенныхЗатрат
		МенеджерВременныхТаблиц
	);
	
	СтруктураПостроителиЗапроса = Новый Структура;
	
	Для каждого Строка из ТаблицаСпособыРаспределения цикл
		//Подготовка структуры параметров
		СпособРаспределенияЗатрат = Строка.СпособРаспределенияЗатратНаВыпуск;
		Если НЕ ЗначениеЗаполнено(СпособРаспределенияЗатрат) Тогда
			Продолжить;
		КонецЕсли;
        ЗаполнитьЗначенияСвойств(СтруктураПараметров, ИсходнаяСтруктураПараметров);

		СтруктураПараметров.СпособРаспределенияЗатрат = СпособРаспределенияЗатрат;
		ЗаполнитьЗначенияСвойств(СтруктураПараметров, СпособРаспределенияЗатрат,,"Фильтры,ТипФильтраПриРаспределенииЗатратНаВыпуск");

		Если СтруктураПараметров.БазаРаспределенияЗатрат = Перечисления.БазыРаспределенияЗатрат.ПоНормативам Тогда
			СтруктураПараметров.СтатьяЗатрат = Строка.СтатьяЗатрат;
		КонецЕсли;

		СтруктураПараметров.ТипФильтраПриРаспределенииЗатратНаВыпуск 	= Строка.ТипФильтраПриРаспределенииЗатратНаВыпуск;
		СтруктураПараметров.Фильтры										= Строка.Фильтры.Скопировать();
		
		//Расчет базы распределения
		БазаРассчитана = СформироватьВременнуюТаблицуБазаРаспределенияЗатрат(
			СтруктураПараметров,
			СтруктураПостроителиЗапроса,
			Ложь, // РаспределениеКосвенныхЗатрат
			МенеджерВременныхТаблиц
		);
		
		Если БазаРассчитана Тогда
		
			ТекстЗапроса = СформироватьТекстЗапросаПоБазамРаспределения();
			
			// Цикл по всем видам отражения в учете, для которых рассчитывается база распределения.
			Для Каждого ВидОтраженияВУчете Из СтруктураПараметров.МассивВидовУчета Цикл
			
				ТекстЗапроса = УправлениеЗатратами.ЗаменитьКомментарииВТекстеЗапроса(
					ТекстЗапроса, 
					ВидОтраженияВУчете
				);
				
			КонецЦикла;
				
			Запрос = Новый Запрос;
			Запрос.Текст = ТекстЗапроса;
			Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
			
			УстановитьПараметрыЗапросаПоБазамРаспределения(
				СтруктураШапкиДокумента,
				Запрос
			);
			РезультатЗапроса = Запрос.Выполнить();
			
			Если не РезультатЗапроса.Пустой() Тогда
			
				СформироватьТаблицуБазыРаспределения(
					РезультатЗапроса,
					ТаблицаРезультата,
					СтруктураПараметров
				);
			КонецЕсли;

		КонецЕсли; //Если БазаРассчитана Тогда
		//Удаление временных таблиц
		Запрос = Новый Запрос; 			
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Для каждого ВидОтраженияВУчете Из СтруктураПараметров.МассивВидовУчета Цикл
			СуффиксИмениРегистра = СтруктураПараметров.СуффиксыИмениРегистра[ВидОтраженияВУчете];

            Запрос.Текст = "УНИЧТОЖИТЬ БазаРаспределения" + СуффиксИмениРегистра;
			Запрос.Выполнить();

		КонецЦикла;
		
	КонецЦикла;  //Для каждого Строка из ТаблицаСпособыРаспределения цикл

	ТаблицаРезультата.ЗаполнитьЗначения(СтруктураПараметров.ОтражатьВУправленческомУчете, 	"ОтражатьВУправленческомУчете");
	ТаблицаРезультата.ЗаполнитьЗначения(СтруктураПараметров.ОтражатьВБухгалтерскомУчете, 	"ОтражатьВБухгалтерскомУчете");
		
	Возврат ТаблицаРезультата;

КонецФункции // СформироватьТаблицуБазыРаспределенияЗатрат()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ФОРМИРОВАНИЯ ДВИЖЕНИЙ ПО РЕГИСТРАМ БАЗА РАСПРЕДЕЛЕНИЯ ЗАТРАТ

// Процедура формирует движение по регистру сведений "База распределения затрат".
//
// Параметры
//  СтруктураШапкиДокумента – Структура – Реквизиты документа "Расчет себестоимости выпуска"
//	СтрокаВыборки - ВыборкаИзРезультатаЗапроса - Строка выборки из результата запроса
//	АналитикаВидаУчета - СправочникСсылка.КлючиАналитикиВидаУчета - Ключ аналитики
//	АналитикаРаспределенияЗатрат - СправочникСсылка.КлючиАналитикиРаспределенияЗатрат - Ключ аналитики
//	АналитикаУчетаПартий - СправочникСсылка.КлючиАналитикиУчетаПартий - Ключ аналитики
//	СпособРаспределенияЗатрат - СправочникСсылка.СпособыРаспределенияЗатратНаВыпуск - Текущий способ распределения затрат
//	ИндексБазыРаспределения - Число - Индекс базы распределения
//	РаспределениеКосвенныхЗатрат - Булево - Признак расчета базы распределения косвенных затрат
//	ЭтоРасчетСебестоимостиВыпуска - Булево - Признак формирования движений документом "Расчет себестоимости выпуска"
//	ВидОтраженияВУчете - ПеречислениеСсылка.ВидыОтраженияВУчете - Вид отражения в учете
//	НаборЗаписейБазаРаспределенияЗатрат - РегистрСведенийНаборЗаписей - Набор записей регистра сведений "База распределения затрат".
//
Процедура СформироватьДвиженияПоРегиструБазаРаспределенияЗатрат(
	СтруктураШапкиДокумента,
	СтрокаВыборки,
	АналитикаВидаУчета,
	АналитикаРаспределенияЗатрат,
	АналитикаУчетаПартий,
	СпособРаспределенияЗатрат,
	ИндексБазыРаспределения,
	РаспределениеКосвенныхЗатрат,
	ЭтоРасчетСебестоимостиВыпуска,
	ВидОтраженияВУчете,
	НаборЗаписейБазаРаспределенияЗатрат
	)
	
	НоваяСтрока = НаборЗаписейБазаРаспределенияЗатрат.Добавить();
	НоваяСтрока.Активность = Истина;
	НоваяСтрока.Период = СтруктураШапкиДокумента.Период;
	
	ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаВыборки);
	
	Если ВидОтраженияВУчете = Перечисления.ВидыОтраженияВУчете.ОтражатьВУправленческомУчете Тогда
		НоваяСтрока.Подразделение = СтрокаВыборки.Подразделение;
	Иначе
		НоваяСтрока.Подразделение = СтрокаВыборки.ПодразделениеОрганизации;
	КонецЕсли;
	
	Если СтруктураШапкиДокумента.ИспользоватьРасширеннуюАналитику Тогда
		НоваяСтрока.АналитикаВидаУчета = АналитикаВидаУчета;
		НоваяСтрока.АналитикаРаспределенияЗатрат = АналитикаРаспределенияЗатрат;
		НоваяСтрока.АналитикаУчетаПартий = АналитикаУчетаПартий;
	КонецЕсли;
	
	НоваяСтрока.База = Окр(СтрокаВыборки.База, 3, 1);
	НоваяСтрока.БазаПриход = Окр(СтрокаВыборки.БазаПриход, 3, 1);
	НоваяСтрока.БазаОстатокНЗП = Окр(СтрокаВыборки.БазаОстатокНЗП, 3, 1);
	
	
	Если ВидОтраженияВУчете = Перечисления.ВидыОтраженияВУчете.ОтражатьВМеждународномУчете Тогда
		Если ТипЗнч(СтрокаВыборки.СчетУчета) <> Тип("ПланСчетовСсылка.Международный") Тогда
			НоваяСтрока.СчетУчета = МеждународныйУчет.ПреобразоватьСчетаБУвСчетМСФО(СтрокаВыборки.СчетУчета,,,,СтруктураШапкиДокумента.Период).Счет;
		КонецЕсли; 
	КонецЕсли;	
	
	НоваяСтрока.СпособРаспределенияЗатрат = СпособРаспределенияЗатрат;
	НоваяСтрока.РаспределениеКосвенныхЗатрат = РаспределениеКосвенныхЗатрат;
	НоваяСтрока.РасчетСебестоимостиВыпуска = ЭтоРасчетСебестоимостиВыпуска;
	
	Если ВидОтраженияВУчете = Перечисления.ВидыОтраженияВУчете.ОтражатьВУправленческомУчете
	 ИЛИ ВидОтраженияВУчете = Перечисления.ВидыОтраженияВУчете.ОтражатьВБухгалтерскомУчете
	Тогда
		НоваяСтрока.ИндексБазыРаспределения = ИндексБазыРаспределения;
	КонецЕсли;
	
	// Если в наборе записей 1000 строк, запишем набор.
	Если НаборЗаписейБазаРаспределенияЗатрат.Количество() = 1000 Тогда
		НаборЗаписейБазаРаспределенияЗатрат.Записать(Ложь);
	КонецЕсли;
	
КонецПроцедуры // СформироватьДвиженияПоРегиструБазаРаспределенияЗатрат()

// Функция формирует запрос по документу "Установка базы распределения затрат".
//
// Параметры:
//  СтруктураШапкиДокумента – Структура – Реквизиты документа
//
// Возвращаемое значение:
//	Запрос - Запрос по документу
//
Функция СформироватьЗапросПоДокументуУстановкиБазы(
	СтруктураШапкиДокумента
	)
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	БазаРаспределения.Ссылка.Организация КАК Организация,
	|	БазаРаспределения.Подразделение КАК Подразделение,
	|	БазаРаспределения.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	БазаРаспределения.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	БазаРаспределения.Заказ КАК Заказ,
	|	БазаРаспределения.Заказ КАК ЗаказРегл,
	|	
	|	БазаРаспределения.Продукция КАК Продукция,
	|	БазаРаспределения.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
	|	БазаРаспределения.СерияПродукции КАК СерияПродукции,
	|	БазаРаспределения.Спецификация КАК Спецификация,
	|	                                                   
	|	БазаРаспределения.СчетЗатрат КАК СчетЗатрат,
	|	БазаРаспределения.НалоговоеНазначение КАК НалоговоеНазначение,   
	|	БазаРаспределения.СчетЗатрат КАК СчетУчета,
	|	
	|	БазаРаспределения.ВариантВыпускаПродукции,
	|	ВЫБОР КОГДА БазаРаспределения.ВидВыпуска = ЗНАЧЕНИЕ(Перечисление.ВидыВыпуска.ПустаяСсылка) ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ВидыВыпуска.Выпуск)
	|	ИНАЧЕ
	|		БазаРаспределения.ВидВыпуска
	|	КОНЕЦ КАК ВидВыпуска,
	|	
	|	
	|	СУММА(БазаРаспределения.БазаПриход) КАК БазаПриход,
	|	СУММА(БазаРаспределения.База) КАК База,
	|	СУММА(БазаРаспределения.БазаОстатокНЗП) КАК БазаОстатокНЗП
	|ИЗ
	|	Документ.УстановкаБазыРаспределенияЗатрат.БазаРаспределения КАК БазаРаспределения
	|ГДЕ
	|	БазаРаспределения.ССылка = &ДокументСсылка
	|
	|СГРУППИРОВАТЬ ПО
	|	БазаРаспределения.Ссылка.Организация,
	|	БазаРаспределения.Подразделение,
	|	БазаРаспределения.ПодразделениеОрганизации,
	|	БазаРаспределения.НоменклатурнаяГруппа,
	|	БазаРаспределения.Заказ,
	|	
	|	БазаРаспределения.Продукция,
	|	БазаРаспределения.ХарактеристикаПродукции,
	|	БазаРаспределения.СерияПродукции,
	|	БазаРаспределения.Спецификация,
	|	
	|	БазаРаспределения.СчетЗатрат,
	|	БазаРаспределения.НалоговоеНазначение,
	|	
	|	БазаРаспределения.ВариантВыпускаПродукции,
	|	ВЫБОР КОГДА БазаРаспределения.ВидВыпуска = ЗНАЧЕНИЕ(Перечисление.ВидыВыпуска.ПустаяСсылка) ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ВидыВыпуска.Выпуск)
	|	ИНАЧЕ
	|		БазаРаспределения.ВидВыпуска
	|	КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Организация,
	|	Подразделение,
	|	ПодразделениеОрганизации,
	|	НоменклатурнаяГруппа,
	|	Заказ,
	|	Продукция,
	|	ХарактеристикаПродукции,
	|	СерияПродукции,
	|	Спецификация,
	|	СчетЗатрат,
	|	НалоговоеНазначение,
	|	ВариантВыпускаПродукции,
	|	ВидВыпуска
	|";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ДокументСсылка", СтруктураШапкиДокумента.Ссылка);
	
	Возврат Запрос;
	
КонецФункции // СформироватьЗапросПоДокументуУстановкиБазы()

// Процедура формирования движений по отражению базы распределения затрат "Вручную".
// 
// Параметры:
//  СтруктураШапкиДокумента – Структура – Реквизиты документа
//
Процедура СформироватьДвиженияПоБазеРаспределенияЗатратВручную(
	СтруктураШапкиДокумента
	) Экспорт
	
	Запрос = СформироватьЗапросПоДокументуУстановкиБазы(СтруктураШапкиДокумента);
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураНаборыЗаписей = Новый Структура;
	
	СоответствиеИменРегистров = Новый Соответствие;
	Если СтруктураШапкиДокумента.ОтражатьВУправленческомУчете Тогда
		СоответствиеИменРегистров.Вставить(Перечисления.ВидыОтраженияВУчете.ОтражатьВУправленческомУчете, "БазаРаспределенияЗатрат");
	КонецЕсли;
	Если СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете Тогда
		СоответствиеИменРегистров.Вставить(Перечисления.ВидыОтраженияВУчете.ОтражатьВБухгалтерскомУчете, "БазаРаспределенияЗатратБухгалтерскийУчет");
	КонецЕсли;
	Если СтруктураШапкиДокумента.ОтражатьВМеждународномУчете
	   И Не СтруктураШапкиДокумента.ИспользоватьРасширеннуюАналитику
	Тогда
		СоответствиеИменРегистров.Вставить(Перечисления.ВидыОтраженияВУчете.ОтражатьВМеждународномУчете, "БазаРаспределенияЗатратМеждународныйУчет");
	КонецЕсли;
	
	Для Каждого ЭлементСоответствия Из СоответствиеИменРегистров Цикл
		ИмяРегистра = ЭлементСоответствия.Значение;
		ПолученНаборЗаписей = УправлениеЗатратамиДвиженияПоРегистрам.ПолучитьНаборЗаписейРегистра(
			СтруктураШапкиДокумента, 
			ИмяРегистра,
			СтруктураНаборыЗаписей
		);
		Если Не ПолученНаборЗаписей Тогда
			Возврат;
		КонецЕсли;
	КонецЦикла;
	
	СтруктураКлючиАналитики = Новый Структура;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		// Цикл по всем видам отражения в учете, для которых рассчитывается база распределения.
		Для Каждого ВидОтраженияВУчете Из Перечисления.ВидыОтраженияВУчете Цикл
		
			ИмяРегистра = СоответствиеИменРегистров[ВидОтраженияВУчете];
			Если ИмяРегистра = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			АналитикаВидаУчета = Неопределено;
			АналитикаРаспределенияЗатрат = Неопределено;
			АналитикаУчетаПартий = Неопределено;
			
			Если СтруктураШапкиДокумента.ИспользоватьРасширеннуюАналитику Тогда
				УправлениеПроизводствомДвиженияПоРегистрам.ПолучитьАналитикуУчетаВыпуска(
					СтруктураШапкиДокумента,
					Выборка,
					Выборка.ВидВыпуска,
					ВидОтраженияВУчете,
					СтруктураКлючиАналитики,
					АналитикаВидаУчета,
					АналитикаРаспределенияЗатрат,
					АналитикаУчетаПартий
				);
			КонецЕсли;
			
			СформироватьДвиженияПоРегиструБазаРаспределенияЗатрат(
				СтруктураШапкиДокумента,
				Выборка,
				АналитикаВидаУчета,
				АналитикаРаспределенияЗатрат,
				АналитикаУчетаПартий,
				СтруктураШапкиДокумента.СпособРаспределенияЗатрат,
				0, // ИндексБазыРаспределения,
				Ложь, // РаспределениеКосвенныхЗатрат,
				Ложь, // ЭтоРасчетСебестоимостиВыпуска
				ВидОтраженияВУчете,
				СтруктураНаборыЗаписей[ИмяРегистра]
			);
		КонецЦикла;
		
	КонецЦикла;
	
	УправлениеЗатратамиДвиженияПоРегистрам.ЗаписатьНаборыЗаписейРегистров(СтруктураНаборыЗаписей);
	
КонецПроцедуры // СформироватьДвиженияПоБазеРаспределенияЗатратВручную()

