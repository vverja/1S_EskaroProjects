////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ОБЩЕГО НАЗНАЧЕНИЯ

// Функция получает дату для запроса по данным документа.
// Для новых (еще не записанных) документов возвращается дата конца дня, для записанных - дата документа.
//
// Параметры:
//	ДокументОбъект - ДокументОбъект - Документ
//	МетаданныеДокумента - Метаданные - Метаданные документа
//
// Возвращаемое значение:
//	Дата - Дата для формирования запроса
//
Функция ПолучитьДатуДляЗапроса(
	ДокументОбъект,
	МетаданныеДокумента = Неопределено
	) Экспорт
	
	Если МетаданныеДокумента <> Неопределено Тогда
		ЕстьДатаОкончанияПериода = (МетаданныеДокумента.Реквизиты.Найти("ДатаОкончанияПериода") <> Неопределено);
	Иначе
		ЕстьДатаОкончанияПериода = Ложь;
	КонецЕсли;
	
	Если ЕстьДатаОкончанияПериода
	   И ЗначениеЗаполнено(ДокументОбъект.ДатаОкончанияПериода)
	Тогда
		Дата = ДокументОбъект.ДатаОкончанияПериода;
	Иначе
		Дата = ДокументОбъект.Дата;
	КонецЕсли;
		
	Дата = ?(ДокументОбъект.ЭтоНовый(), КонецДня(Дата), Дата);
	
	Возврат Дата;
	
КонецФункции // ПолучитьДатуДляЗапроса()

// Процедура удаляет из строки имен реквизитов, проверяемых на заполненность
// реквизиты, которые зависят от типа учета документа
//
// Параметры:
//		ДокОбъект - проверяемый документ
//		СтрокаРекв   - Строка с именами реквизитов, которые надо проверять на заполненность
//      УпрРеквизиты - строка, с именами реквизитов имеющих смысл
// 					   только в случае если документ отражается в упр.учете
//      БухРеквизиты - строка, с именами реквизитов имеющих смысл
// 					   только в случае если документ отражается в регл.(бух.) учете
//      НалРеквизиты - строка, с именами реквизитов имеющих смысл
// 					   только в случае если документ отражается в регл.(нал.) учете
//		ИмяТабЧасти  - имя проверяемой табл. части документа
//
Процедура НепроверятьРеквизитыПоТипуУчета(ДокОбъект, СтрокаРекв, СтруктураШапкиДокумента, Знач УпрРеквизиты, Знач БухРеквизиты, ИмяТабЧасти = "") Экспорт

	Стр = СтрЗаменить(СтрокаРекв, " ", "");
	СтруктРекв = Новый Структура(Стр);
	СтрокаРекв = "";

	// Убрать пробелы и символы перевода строки
	УпрРекв = СтрЗаменить(УпрРеквизиты, " ", "");
	УпрРекв = СтрЗаменить(УпрРекв, Символы.ПС,  "");
	УпрРекв = "," + СтрЗаменить(УпрРекв, Символы.Таб, "") + ",";

	БухРекв = СтрЗаменить(БухРеквизиты, " ", "");
	БухРекв = СтрЗаменить(БухРекв, Символы.ПС,  "");
	БухРекв = "," + СтрЗаменить(БухРекв, Символы.Таб, "") + ",";


	
	УпрУчет = ?(СтруктураШапкиДокумента.Свойство("ОтражатьВУправленческомУчете"),СтруктураШапкиДокумента.ОтражатьВУправленческомУчете,Ложь);
	БухУчет = ?(СтруктураШапкиДокумента.Свойство("ОтражатьВБухгалтерскомУчете"), СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете, Ложь);

	// Исключим из списка проверяемых реквизитов, те которые относятся к конкретному
	// виду учета и этот вид учета выключен

	Для Каждого Рекв Из СтруктРекв Цикл

		ИмяРекв = ?(ПустаяСтрока(ИмяТабЧасти), "", ИмяТабЧасти + ".") + Рекв.Ключ;

		Если Не УпрУчет И Найти(УпрРекв, "," + ИмяРекв + ",") > 0 Тогда
			Продолжить;
		КонецЕсли;

		Если Не БухУчет И Найти(БухРекв, "," + ИмяРекв + ",") > 0 Тогда
			Продолжить;
		КонецЕсли;


		СтрокаРекв = ?(ПустаяСтрока(СтрокаРекв), "", СтрокаРекв + ", ") + Рекв.Ключ;

	КонецЦикла;

КонецПроцедуры // НепроверятьРеквизитыПоТипуУчета()

// Функция заменяет комментарии в тексте запроса в зависимости от отражения документа в учете.
//
// Параметры
//  ИсходныйТекстЗапроса – Строка – Текст запроса.
//	ВидОтраженияВУчете - ПеречислениеСсылка.ВидыОтраженияВУчете - Вид отражения в учете
//
// Возвращаемое значение
//   Строка – Текст запроса, в котором заменены комментарии.
//
Функция ЗаменитьКомментарииВТекстеЗапроса(
	ИсходныйТекстЗапроса, 
	ВидОтраженияВУчете
	) Экспорт
	
	Если ВидОтраженияВУчете = Перечисления.ВидыОтраженияВУчете.ОтражатьВУправленческомУчете Тогда
		ТекстЗапроса = СтрЗаменить(ИсходныйТекстЗапроса, "%СуффиксУчета%", "");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%СуффиксОрганизаций%", "");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%СуффиксРегл%", "");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ДляУпрУчета", "");
		
	ИначеЕсли ВидОтраженияВУчете = Перечисления.ВидыОтраженияВУчете.ОтражатьВРегламентированномУчете Тогда
		ТекстЗапроса = СтрЗаменить(ИсходныйТекстЗапроса, "%СуффиксОрганизаций%", "Организаций");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%СуффиксРегл%", "Регл");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ДляРеглУчета", "");
		
	ИначеЕсли ВидОтраженияВУчете = Перечисления.ВидыОтраженияВУчете.ОтражатьВБухгалтерскомУчете Тогда
		ТекстЗапроса = СтрЗаменить(ИсходныйТекстЗапроса, "%СуффиксОрганизаций%", "Организаций");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%СуффиксРегл%", "Регл");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ДляРеглУчета", "");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%СуффиксУчета%", "БухгалтерскийУчет");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяПланаСчетов%", "Хозрасчетный");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ДляБухУчета", "");
		
	ИначеЕсли ВидОтраженияВУчете = Перечисления.ВидыОтраженияВУчете.ОтражатьВНалоговомУчете Тогда
	  	ТекстЗапроса = СтрЗаменить(ИсходныйТекстЗапроса, "%СуффиксУчета%", "НалоговыйУчет");
	  	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%СуффиксОрганизаций%", "Организаций");
	  	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%СуффиксРегл%", "Регл");
	  	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяПланаСчетов%", "Налоговый");
	  	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ДляРеглУчета", "");
	  	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ДляНалУчета", "");
		
	ИначеЕсли ВидОтраженияВУчете = Перечисления.ВидыОтраженияВУчете.ОтражатьВМеждународномУчете	Тогда
		ТекстЗапроса = СтрЗаменить(ИсходныйТекстЗапроса, "%СуффиксУчета%", "МеждународныйУчет");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%СуффиксОрганизаций%", "Организаций");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяПланаСчетов%", "Международный");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ДляРеглУчета", "");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ДляМеждУчета", "");
		
	КонецЕсли;

	Возврат ТекстЗапроса;

КонецФункции // ЗаменитьКомментарииВТекстеЗапроса()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ВЫВОДА СООБЩЕНИЙ ОБ ОШИБКАХ

// Функция формирует таблицу ошибок.
//
// Возвращаемое значение:
//	ТаблицаЗначений - Таблица ошибок
//
Функция СформироватьТаблицуОшибок() Экспорт
	
	ТаблицаОшибок = Новый ТаблицаЗначений;
	Колонки = ТаблицаОшибок.Колонки;
	Колонки.Добавить("Группа", Новый ОписаниеТипов("Строка"));
	Колонки.Добавить("Сообщение", Новый ОписаниеТипов("Строка"));
	Колонки.Добавить("Объект");
	
	Возврат ТаблицаОшибок;
	
КонецФункции // СформироватьТаблицуОшибок()

// Функция формирует расшифровку для открытия отчета.
//
// Параметры:
//	ИмяОтчета - Строка - Имя отчета
//
Функция СформироватьРасшифровкуОткрытияОтчета(ИмяОтчета) Экспорт
	
	МассивРасшифровки = Новый Массив;
	МассивРасшифровки.Добавить("ОткрытьОтчет");
	МассивРасшифровки.Добавить(ИмяОтчета);
	
	СтруктураРасшифровки = Новый Структура;
	СтруктураРасшифровки.Вставить("Представление", " (открыть отчет)");
	СтруктураРасшифровки.Вставить("Расшифровка", МассивРасшифровки);
	
	Расшифровка = Новый Массив;
	Расшифровка.Добавить(СтруктураРасшифровки);
	
	Возврат Расшифровка;
	
КонецФункции // ПолучитьРасшифровкуОткрытьОтчет()

// Процедура выводит сообщения об ошибках.
//
Процедура ВывестиСообщенияОбОшибках(
	СтруктураШапкиДокумента,
	ТаблицаОшибок,
	Заголовок
	) Экспорт
	
	Если ТаблицаОшибок.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаОшибок.Свернуть("Группа, Сообщение, Объект", "");
	
	Если Не ПустаяСтрока(Заголовок) Тогда
		СтрокаГруппы = ОбщегоНазначения.Сообщение(
			Заголовок,
			Перечисления.ВидыСообщений.Раздел,
			, // Заголовок
			СтруктураШапкиДокумента.Ссылка, // РасшифровкаСообщения
			, // РодительскаяСтрока
			Истина // РаскрытьСообщение
		);
	Иначе
		СтрокаГруппы = Неопределено;
	КонецЕсли;
	
	ТекущаяГруппа = "";
	Для Каждого Строка Из ТаблицаОшибок Цикл
		
		Если Не ПустаяСтрока(Строка.Группа)
		   И ТекущаяГруппа <> Строка.Группа
		Тогда
			НоваяСтрокаГруппы = ОбщегоНазначения.Сообщение(
				Строка.Группа,
				Перечисления.ВидыСообщений.Ошибка,
				, // Заголовок
				, // РасшифровкаСообщения
				СтрокаГруппы, // РодительскаяСтрока
				Истина // РаскрытьСообщение
			);
			ТекущаяГруппа = Строка.Группа;
			СтрокаГруппы = НоваяСтрокаГруппы;
		КонецЕсли;
		
		ОбщегоНазначения.Сообщение(
			Строка.Сообщение,
			Перечисления.ВидыСообщений.Информация,
			, // Заголовок
			Строка.Объект, // РасшифровкаСообщения
			СтрокаГруппы, // СтрокаГруппы,
			// РаскрытьСообщение
		);
		
	КонецЦикла;
	
КонецПроцедуры // ВывестиСообщенияОбОшибках()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ РАБОТЫ СО СТАТЬЯМИ ЗАТРАТ И СЧЕТАМИ ЗАТРАТ

// Функция возвращает счет учета для указанной статьи затрат
//
// Параметры:
//  Подразделение - Подразделение, для которого определяются счет
//  СтатьяЗатрат  - Счатья затрат, для которой определяются счет
//  КоррСчет      - корреспондирующий счет
//	ВидУчета      - вид учета, счет которого надо получить
//
// Возвращаемое значение:
//  Структура - структура счетов по всем разделам учета, если не задано значение
//		параметра ВидУчета
//  Счет, если задано значение параметра ВидУчета. Счет возвращается
//		в соответствии с запришиваемым видом учета.
//
Функция ПолучитьСчетаУчетаСтатьиЗатрат(Подразделение, СтатьяЗатрат, ВидУчета = Неопределено) Экспорт
	
	ПустоеПодразделение = НЕ ЗначениеЗаполнено(Подразделение);
	ПустаяСтатьяЗатрат  = НЕ ЗначениеЗаполнено(СтатьяЗатрат);
	ПланСчетовБух       = ПланыСчетов.Хозрасчетный;
	Счет                = ПланСчетовБух.ПустаяСсылка();

	Если ПустаяСтатьяЗатрат И Не ПустоеПодразделение Тогда

		Если Подразделение.ВидПодразделения = Перечисления.ВидыПодразделений.ОсновноеПроизводство Тогда
			Счет = ПланСчетовБух.ОсновноеПроизводство;
		ИначеЕсли Подразделение.ВидПодразделения = Перечисления.ВидыПодразделений.ВспомогательноеПроизводство Тогда
			Счет = ПланСчетовБух.ВспомогательныеПроизводства;
		КонецЕсли;

	ИначеЕсли Не ПустаяСтатьяЗатрат Тогда

		Если СтатьяЗатрат.СтатусМатериальныхЗатрат = Перечисления.СтатусыМатериальныхЗатратНаПроизводство.ПринятыеВПереработку Тогда
			Счет = ПланСчетовБух.МатериалыПринятыеВПереработкуВПроизводстве;
		ИначеЕсли СтатьяЗатрат.ХарактерЗатрат = Перечисления.ХарактерЗатрат.ОбщепроизводственныеРасходы Тогда
			Счет = ПланСчетовБух.ОбщепроизводственныеРасходы;
		ИначеЕсли СтатьяЗатрат.ХарактерЗатрат = Перечисления.ХарактерЗатрат.АдминистративныеРасходы Тогда
			Счет = ПланСчетовБух.АдминистративныеРасходы;
		ИначеЕсли СтатьяЗатрат.ХарактерЗатрат = Перечисления.ХарактерЗатрат.БракВПроизводстве Тогда
			Счет = ПланСчетовБух.БракВПроизводстве;
		ИначеЕсли СтатьяЗатрат.ХарактерЗатрат = Перечисления.ХарактерЗатрат.РасходыНаСбыт Тогда
			Счет = ПланСчетовБух.РасходыНаСбыт;
		ИначеЕсли СтатьяЗатрат.ХарактерЗатрат = Перечисления.ХарактерЗатрат.ПрочиеОперационныеРасходы Тогда
			Счет = ПланСчетовБух.ДругиеЗатратыОперационнойДеятельности;
		ИначеЕсли СтатьяЗатрат.ХарактерЗатрат = Перечисления.ХарактерЗатрат.ВложенияВоВнеоборотныеАктивы Тогда
			Счет = ПланСчетовБух.ИзготовлениеОсновныхСредств;
		ИначеЕсли СтатьяЗатрат.ХарактерЗатрат = Перечисления.ХарактерЗатрат.ТранспортноЗаготовительныеРасходы Тогда
			Счет = ПланСчетовБух.ТранспортноЗаготовительныеРасходыМатериалы;
		Иначе
			Если СтатьяЗатрат.ХарактерЗатрат = Перечисления.ХарактерЗатрат.ПроизводственныеРасходы Тогда
				Если ПустоеПодразделение Тогда
					Счет = ПланСчетовБух.ОсновноеПроизводство;
				Иначе
					Если Подразделение.ВидПодразделения = Перечисления.ВидыПодразделений.ОсновноеПроизводство Тогда
						Счет = ПланСчетовБух.ОсновноеПроизводство;
					ИначеЕсли Подразделение.ВидПодразделения = Перечисления.ВидыПодразделений.ВспомогательноеПроизводство Тогда
						Счет = ПланСчетовБух.ВспомогательныеПроизводства;
					КонецЕсли;
				КонецЕсли;
			ИначеЕсли НЕ ЗначениеЗаполнено(СтатьяЗатрат.ХарактерЗатрат) Тогда
				ОбщегоНазначения.СообщитьОбОшибке("В статье затрат """+СтатьяЗатрат+""" не указан характер затрат");
			КонецЕсли;
		КонецЕсли;

	КонецЕсли;


	Результат = Новый Структура("СчетУчетаБУ");
	
	Если ВидУчета = Неопределено Тогда
		Результат.СчетУчетаБУ   = Счет;
	Иначе
		Если ВидУчета = "СчетУчетаБУ" Тогда
			Результат = Счет;
		Иначе
			ОбщегоНазначения.СообщитьОбОшибке("Неверное значение параметра 'ВидУчета' (" + ВидУчета + ")");
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;

КонецФункции // ПолучитьСчетаУчетаСтатьиЗатрат()

// Функция проверяет является ли статья затрат производственной
//
// Параметры:
//  СтатьяЗатрат  - Счатья затрат, для которой определяется отнесение к производственным расходам
//
// Возвращаемое значение:
//  Истина - статья затрат относится к производственным расходам.
//
Функция ПроверитьСтатьюЗатратНаПроизводственныеРасходы(СтатьяЗатрат) Экспорт

	Возврат НЕ (ЗначениеЗаполнено(СтатьяЗатрат) И СтатьяЗатрат.ХарактерЗатрат = Перечисления.ХарактерЗатрат.Прочие);

КонецФункции // ПроверитьСтатьюЗатратНаПроизводственныеРасходы()

// Функция проверяет является ли счет затрат производственным.
//
// Параметры:
//  СчетЗатрат  - счет затрат, для которой определяется отнесение к производственным расходам
//
// Возвращаемое значение:
//  Истина - счет затрат относится к производственным расходам.
//
Функция ПроверитьСчетЗатратНаПроизводственныеРасходы(СчетЗатрат) Экспорт	

	ХарактерЗатрат = ПолучитьХарактерЗатратПоСчетуЗатрат(СчетЗатрат, Неопределено);

	Возврат НЕ (ЗначениеЗаполнено(ХарактерЗатрат) И ХарактерЗатрат = Перечисления.ХарактерЗатрат.Прочие);

КонецФункции // ПроверитьСчетЗатратНаПроизводственныеРасходы()

// Функция возвращает значение характера затрат, соответствующего счету затрат.
//
// Параметры
//  СчетЗатрат - ПланСчетовСсылка - счет затрат, по которому определяется характер затрат.
//  СтатьяЗатрат - СправочникСсылка.СтатьиЗатрат - статья затрат для уточнения характера затрат.
//  ИмяПланСчетов - Строка - имя плана счетов, которому принадлежит счет затрат.
//
// Возвращаемое значение:
//   ПеречислениеСсылка.ХарактерЗатрат – характер затрат, соответствующий счету затрат.
//
Функция ПолучитьХарактерЗатратПоСчетуЗатрат(СчетЗатрат, СтатьяЗатрат) Экспорт	
	
	// Проверка счета затрат.
	Если НЕ ЗначениеЗаполнено(СчетЗатрат) Тогда
		Возврат Перечисления.ХарактерЗатрат.ПустаяСсылка();
		
	ИначеЕсли ТипЗнч(СчетЗатрат) <> Тип("ПланСчетовСсылка.Хозрасчетный") Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Счет затрат " + СчетЗатрат + " не соответствует плану счетов: """ + "Хозрасчетный" + """.
			|Функция <ПолучитьХарактерЗатратПоСчетуЗатрат>");
		Возврат Перечисления.ХарактерЗатрат.ПустаяСсылка();
	КонецЕсли;
	
		  
	Если СчетЗатрат = ПланыСчетов.Хозрасчетный.ОсновноеПроизводство
		  ИЛИ СчетЗатрат = ПланыСчетов.Хозрасчетный.ВспомогательныеПроизводства
		  ИЛИ СчетЗатрат = ПланыСчетов.Хозрасчетный.МатериалыПринятыеВПереработкуВПроизводстве
		  ИЛИ СчетЗатрат = ПланыСчетов.Хозрасчетный.Производство
		  ИЛИ (СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.Производство) 
		  И НЕ (СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ОбслуживаниеИРемонт) ИЛИ СчетЗатрат = ПланыСчетов.Хозрасчетный.ОбслуживаниеИРемонт)
		  И НЕ (СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ОбслуживающиеПроизводства) ИЛИ СчетЗатрат = ПланыСчетов.Хозрасчетный.ОбслуживающиеПроизводства)
			  ) Тогда
		  
		ХарактерЗатрат = Перечисления.ХарактерЗатрат.ПроизводственныеРасходы;
		
		  
	ИначеЕсли СчетЗатрат = ПланыСчетов.Хозрасчетный.ОбслуживающиеПроизводства
		  ИЛИ СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ОбслуживающиеПроизводства) Тогда
		  
		Если СтатьяЗатрат = Неопределено
		 ИЛИ СтатьяЗатрат = NULL
		Тогда
			ХарактерЗатрат = Перечисления.ХарактерЗатрат.ПроизводственныеРасходы;
		Иначе
			ХарактерЗатрат = СтатьяЗатрат.ХарактерЗатрат;
		КонецЕсли;
		
		  
	ИначеЕсли СчетЗатрат = ПланыСчетов.Хозрасчетный.БракВПроизводстве
		  ИЛИ СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.БракВПроизводстве) Тогда
	
		ХарактерЗатрат = Перечисления.ХарактерЗатрат.БракВПроизводстве;
		
		  
	ИначеЕсли СчетЗатрат = ПланыСчетов.Хозрасчетный.ОбщепроизводственныеРасходы
		  ИЛИ СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ОбщепроизводственныеРасходы) Тогда
	
		ХарактерЗатрат = Перечисления.ХарактерЗатрат.ОбщепроизводственныеРасходы;
		
	ИначеЕсли СчетЗатрат = ПланыСчетов.Хозрасчетный.АдминистративныеРасходы
		  ИЛИ СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.АдминистративныеРасходы) Тогда
		ХарактерЗатрат = Перечисления.ХарактерЗатрат.АдминистративныеРасходы;
		
	
	ИначеЕсли СчетЗатрат = ПланыСчетов.Хозрасчетный.ЗатратыОтПервоначальногоПризнанияИИзмененияСтоимостиАктивовПоСправедливойСтоимости
		  ИЛИ СчетЗатрат = ПланыСчетов.Хозрасчетный.ЗатратыНаИсследованияИРазработки
		  ИЛИ СчетЗатрат = ПланыСчетов.Хозрасчетный.СебестоимостьРеализованнойИностраннойВалюты
		  ИЛИ СчетЗатрат = ПланыСчетов.Хозрасчетный.СебестоимостьРеализованныхПроизводственныхЗапасов
		  ИЛИ СчетЗатрат = ПланыСчетов.Хозрасчетный.СомнительныеИБезнадежныеДолги
		  ИЛИ СчетЗатрат = ПланыСчетов.Хозрасчетный.ЗатратыОтОперационнойКурсовойРазницы
		  ИЛИ СчетЗатрат = ПланыСчетов.Хозрасчетный.ЗатратыОтОбесцениванияЗапасов
		  ИЛИ СчетЗатрат = ПланыСчетов.Хозрасчетный.НедостачиИПотериОтПорчиЦенностей
		  ИЛИ СчетЗатрат = ПланыСчетов.Хозрасчетный.ПризнанныеШтрафыПениНеустойки
		  ИЛИ СчетЗатрат = ПланыСчетов.Хозрасчетный.ДругиеЗатратыОперационнойДеятельности
		  ИЛИ СчетЗатрат = ПланыСчетов.Хозрасчетный.ДругиеЗатратыОперационнойДеятельностиГруппа
		  ИЛИ СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ДругиеЗатратыОперационнойДеятельностиГруппа) Тогда
	
		ХарактерЗатрат = Перечисления.ХарактерЗатрат.ПрочиеОперационныеРасходы;
		
		  
	ИначеЕсли СчетЗатрат = ПланыСчетов.Хозрасчетный.РасходыНаСбыт
		  ИЛИ СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.РасходыНаСбыт) Тогда
	
		ХарактерЗатрат = Перечисления.ХарактерЗатрат.РасходыНаСбыт;
		
		  
	ИначеЕсли СчетЗатрат = ПланыСчетов.Хозрасчетный.КапитальноеСтроительство
		  ИЛИ СчетЗатрат = ПланыСчетов.Хозрасчетный.ИзготовлениеОсновныхСредств
		  ИЛИ СчетЗатрат = ПланыСчетов.Хозрасчетный.ИзготовлениеДругихНеоборотныхМатериальныхАктивов
		  ИЛИ СчетЗатрат = ПланыСчетов.Хозрасчетный.ИзготовлениеНематериальныхАктивов
		  ИЛИ СчетЗатрат = ПланыСчетов.Хозрасчетный.ОбслуживаниеИРемонт
		  ИЛИ СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.КапитальноеСтроительство)
		  ИЛИ СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ИзготовлениеОсновныхСредств)
		  ИЛИ СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ИзготовлениеДругихНеоборотныхМатериальныхАктивов)
		  ИЛИ СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ИзготовлениеНематериальныхАктивов)
		  ИЛИ СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ОбслуживаниеИРемонт) Тогда
		  
		ХарактерЗатрат = Перечисления.ХарактерЗатрат.ВложенияВоВнеоборотныеАктивы;
	ИначеЕсли СчетЗатрат = ПланыСчетов.Хозрасчетный.ТранспортноЗаготовительныеРасходыМатериалы
		  ИЛИ СчетЗатрат = ПланыСчетов.Хозрасчетный.ТранспортноЗаготовительныеРасходыТовары 
		  ИЛИ СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ТранспортноЗаготовительныеРасходыМатериалы)
		  ИЛИ СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ТранспортноЗаготовительныеРасходыТовары) Тогда
		  
		  ХарактерЗатрат = Перечисления.ХарактерЗатрат.ТранспортноЗаготовительныеРасходы;
	
	// По счету 8 класса характер затраты по счету определить не можем
	// Поэтому для всех счетов 8-го класса возвращаем характер, указанный в статье затрат
	ИначеЕсли СчетЗатрат.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ЗатратыПоЭлементам) Тогда
		Если ЗначениеЗаполнено(СтатьяЗатрат) Тогда
			ХарактерЗатрат = СтатьяЗатрат.ХарактерЗатрат;
		Иначе
			ХарактерЗатрат = Перечисления.ХарактерЗатрат.Прочие;
		КонецЕсли;
	
	Иначе
		ХарактерЗатрат = Перечисления.ХарактерЗатрат.Прочие;
	КонецЕсли;
	
	Возврат ХарактерЗатрат;
	
КонецФункции // ПолучитьХарактерЗатратПоСчетуЗатрат()


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ОТРАЖЕНИЯ ЗАТРАТ В РЕГИСТРАХ НАКОПЛЕНИЯ

// Функция формирует структуру колонок для таблицы затрат.
//
// Возвращаемое значение:
//	Структура - Структура колонок
//
Функция СформироватьСтруктуруКолонокТаблицыЗатрат()
	
	СтруктураКолонок = Новый Структура;
	
	СтруктураКолонок.Вставить("ВидДвижения", Новый ОписаниеТипов("ВидДвиженияНакопления"));
	СтруктураКолонок.Вставить("КодОперацииНЗП", Новый ОписаниеТипов("ПеречислениеСсылка.КодыОперацийНезавершенноеПроизводство"));
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("ПеречислениеСсылка.КодыОперацийЗатраты"));
	МассивТипов.Добавить(Тип("ПеречислениеСсылка.КодыОперацийПартииМатериаловВЭксплуатации"));
	СтруктураКолонок.Вставить("КодОперации", Новый ОписаниеТипов(МассивТипов));
	
	СтруктураКолонок.Вставить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	
	СтруктураКолонок.Вставить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	СтруктураКолонок.Вставить("ХарактеристикаНоменклатуры", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	СтруктураКолонок.Вставить("СерияНоменклатуры", Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры"));
	
	СтруктураКолонок.Вставить("СтатьяЗатрат", Новый ОписаниеТипов("СправочникСсылка.СтатьиЗатрат"));
	СтруктураКолонок.Вставить("НоменклатурнаяГруппа", Новый ОписаниеТипов("СправочникСсылка.НоменклатурныеГруппы"));
	СтруктураКолонок.Вставить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.Подразделения"));
	СтруктураКолонок.Вставить("ПодразделениеОрганизации", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	СтруктураКолонок.Вставить("СпособРаспределенияЗатратНаВыпуск", Новый ОписаниеТипов("СправочникСсылка.СпособыРаспределенияЗатратНаВыпуск"));
	
	МассивТиповЗаказа = Новый Массив;
	МассивТиповЗаказа.Добавить(Тип("ДокументСсылка.ЗаказПокупателя"));
	МассивТиповЗаказа.Добавить(Тип("ДокументСсылка.ЗаказНаПроизводство"));
	
	СтруктураКолонок.Вставить("Заказ", Новый ОписаниеТипов(МассивТиповЗаказа));
	
	МассивТиповПроекта = Новый Массив;
	МассивТиповПроекта.Добавить(Тип("СправочникСсылка.Проекты"));
	МассивТиповПроекта.Добавить(Тип("СправочникСсылка.ВидыРаспределенияПоПроектам"));
	
	СтруктураКолонок.Вставить("Проект", Новый ОписаниеТипов(МассивТиповПроекта));
	
	СтруктураКолонок.Вставить("ОбъектСтроительства", Новый ОписаниеТипов("СправочникСсылка.ОбъектыСтроительства"));

	СтруктураКолонок.Вставить("Продукция", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	СтруктураКолонок.Вставить("ХарактеристикаПродукции", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	СтруктураКолонок.Вставить("СерияПродукции", Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры"));
	
	СтруктураКолонок.Вставить("Количество", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 3)));
	
	СтруктураКолонок.Вставить("КоличествоУпр", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 3)));
	СтруктураКолонок.Вставить("КоличествоБух", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 3)));
	СтруктураКолонок.Вставить("КоличествоНал", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 3)));
	
	СтруктураКолонок.Вставить("Сумма", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	СтруктураКолонок.Вставить("СуммаУпр", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	СтруктураКолонок.Вставить("НДСУпр", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	
	СтруктураКолонок.Вставить("НДСВключенВСтоимость", Новый ОписаниеТипов("Булево"));
	СтруктураКолонок.Вставить("ПроводкиСуммаБезНДСРегл", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	СтруктураКолонок.Вставить("СуммаНДСРегл", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	СтруктураКолонок.Вставить("ПроводкиСуммаНДСРегл", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	СтруктураКолонок.Вставить("СуммаБезНДСРегл", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	СтруктураКолонок.Вставить("СуммаРегл", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2))); // сумма затрат для отражения в бух. и нал. учете
	СтруктураКолонок.Вставить("СуммаБух", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2))); // сумма затрат для отражения в бух. учете
	СтруктураКолонок.Вставить("СуммаНал", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2))); // сумма затрат для отражения в нал. учете
	СтруктураКолонок.Вставить("СтоимостьНУ", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2))); // сумма затрат для отражения в нал. учете
	
	СтруктураКолонок.Вставить("НеКорректироватьНУ", Новый ОписаниеТипов("Булево"));
	СтруктураКолонок.Вставить("НалоговоеНазначение", Новый ОписаниеТипов("СправочникСсылка.НалоговыеНазначенияАктивовИЗатрат"));
	СтруктураКолонок.Вставить("НалоговоеНазначениеПолучатель", Новый ОписаниеТипов("СправочникСсылка.НалоговыеНазначенияАктивовИЗатрат"));
	СтруктураКолонок.Вставить("НалоговоеНазначениеДоходовИЗатрат", Новый ОписаниеТипов("СправочникСсылка.НалоговыеНазначенияАктивовИЗатрат"));
	
	СтруктураКолонок.Вставить("СчетУчетаБУ", Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	СтруктураКолонок.Вставить("СчетЗатрат", Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
		
	ОписаниеТиповСубконто = Метаданные.ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Тип;
	
	СтруктураКолонок.Вставить("Субконто1", ОписаниеТиповСубконто);
	СтруктураКолонок.Вставить("Субконто2", ОписаниеТиповСубконто);
	СтруктураКолонок.Вставить("Субконто3", ОписаниеТиповСубконто);
	

	СтруктураКолонок.Вставить("КорАналитикаВидаУчета", Новый ОписаниеТипов("СправочникСсылка.КлючиАналитикиВидаУчета"));
	СтруктураКолонок.Вставить("КорАналитикаУчетаЗатрат", Новый ОписаниеТипов("СправочникСсылка.КлючиАналитикиУчетаЗатрат"));
	СтруктураКолонок.Вставить("КорАналитикаРаспределенияЗатрат", Новый ОписаниеТипов("СправочникСсылка.КлючиАналитикиРаспределенияЗатрат"));
	СтруктураКолонок.Вставить("КорАналитикаУчетаПартий", Новый ОписаниеТипов("СправочникСсылка.КлючиАналитикиУчетаПартий"));
	
	Возврат СтруктураКолонок;
	
КонецФункции // СформироватьСтруктуруКолонокТаблицыЗатрат()

// Функция формирует структуру таблицы значений для отражения производственных затрат в регистрах.
//
Функция СформироватьТаблицуЗатрат() Экспорт
	
	СтруктураКолонок = СформироватьСтруктуруКолонокТаблицыЗатрат();
	
	ТаблицаЗатрат = Новый ТаблицаЗначений;
	
	Для Каждого Колонка Из СтруктураКолонок Цикл
		ТаблицаЗатрат.Колонки.Добавить(Колонка.Ключ, Колонка.Значение);
	КонецЦикла;
	
	Возврат ТаблицаЗатрат;
	
КонецФункции // СформироватьТаблицуЗатрат()

// Функция проверяет соотвествтие счетов и аналитики Дт и КТ и в формируемой проводке.
//
// Возвращаемое значение:
//   Истина – Счета и аналитика одинаковые.
//   Ложь   - Счета или аналитика разные.
//
Функция СчетаДтКтИАналитикаВПроводкеОдинаковые(СтрокаТабличнойЧасти, СтруктураШапкиДокумента) Экспорт
	
	СчетаДтКтИАналитикаОдинаковая = Истина;
	
	Если СтрокаТабличнойЧасти.СчетЗатрат <> СтрокаТабличнойЧасти.СчетЗатратПолучатель Тогда
		СчетаДтКтИАналитикаОдинаковая = Ложь;
	КонецЕсли;
	
	Если СтрокаТабличнойЧасти.НалоговоеНазначениеДоходовИЗатрат <> СтрокаТабличнойЧасти.НалоговоеНазначениеДоходовИЗатратПолучатель Тогда
		СчетаДтКтИАналитикаОдинаковая = Ложь;
	КонецЕсли;
	
	ПроизводственныеРасходы = ПроверитьСчетЗатратНаПроизводственныеРасходы(СтрокаТабличнойЧасти.СчетЗатрат);

	Если ПроизводственныеРасходы Тогда
		Если СтрокаТабличнойЧасти.СтатьяЗатрат <> СтрокаТабличнойЧасти.СтатьяЗатратПолучатель
		   ИЛИ СтруктураШапкиДокумента.ПодразделениеОрганизации <> СтрокаТабличнойЧасти.ПодразделениеОрганизацииПолучатель
		   ИЛИ СтрокаТабличнойЧасти.НоменклатурнаяГруппа <> СтрокаТабличнойЧасти.НоменклатурнаяГруппаПолучатель Тогда
		   
		    СчетаДтКтИАналитикаОдинаковая = Ложь;
			
		КонецЕсли;
		
		Если ТипЗнч(СтруктураШапкидокумента.Ссылка) = Тип("ДокументСсылка.КорректировкаПрочихЗатрат")
		   И СтрокаТабличнойЧасти.ОбъектСтроительства <> СтрокаТабличнойЧасти.ОбъектСтроительстваПолучатель Тогда
		   
		    СчетаДтКтИАналитикаОдинаковая = Ложь;
			
		КонецЕсли;
		
	Иначе
		Если СтрокаТабличнойЧасти.Субконто1 <> СтрокаТабличнойЧасти.СубконтоПолучатель1
		   ИЛИ СтрокаТабличнойЧасти.Субконто2 <> СтрокаТабличнойЧасти.СубконтоПолучатель2
		   ИЛИ СтрокаТабличнойЧасти.Субконто3 <> СтрокаТабличнойЧасти.СубконтоПолучатель3 Тогда
		   
		    СчетаДтКтИАналитикаОдинаковая = Ложь;
			
		КонецЕсли;
	КонецЕсли;
	
	Возврат СчетаДтКтИАналитикаОдинаковая;
	
КонецФункции

// Функция получает таблицу затрат для формирования движений по регистрам.
//
// Параметры:
//	ТаблицаДокумента - ТабличнаяЧасть, ТаблицаЗначений
//	СтруктураШапкиДокумента - Структура - Реквизиты документа
//
// Возвращаемое значение:
//	ТаблицаЗначений - Таблица затрат
//
Функция ПолучитьТаблицуЗатрат(
	ТаблицаДокумента,
	СтруктураШапкиДокумента
	)
	
	Если ТипЗнч(ТаблицаДокумента) <> Тип("ТаблицаЗначений") Тогда
		ТаблицаЗатрат = ТаблицаДокумента.Выгрузить();
	Иначе
		ТаблицаЗатрат = ТаблицаДокумента.Скопировать();
	КонецЕсли;
	
	//Смартис Лиманчук начало 08.04.2013
	Если СтруктураШапкиДокумента.Свойство("ПоИнвентаризации") Тогда
		Пока Истина Цикл
			НашлиЗП = Ложь;
			Для каждого Стр Из ТаблицаЗатрат Цикл
				Если Стр.СчетЗатрат = ПланыСчетов.Хозрасчетный.РасчетыПоЗаработнойПлате Тогда
					ТаблицаЗатрат.Удалить(Стр);
					НашлиЗП = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла; 
			Если НЕ НашлиЗП Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла; 
	КонецЕсли;
	//Смартис Лиманчук окончание 08.04.2013
	
	СтруктураКолонок = СформироватьСтруктуруКолонокТаблицыЗатрат();
	Для Каждого Колонка Из СтруктураКолонок Цикл
		
		Если ТаблицаЗатрат.Колонки.Найти(Колонка.Ключ) = Неопределено Тогда
			ТаблицаЗатрат.Колонки.Добавить(Колонка.Ключ, Колонка.Значение);
		КонецЕсли;
		
	КонецЦикла;
	
	Если ТаблицаЗатрат.Колонки.Найти("ПроектЗатрат") <> Неопределено Тогда
		МассивПроектов = ТаблицаЗатрат.ВыгрузитьКолонку("ПроектЗатрат");
		ТаблицаЗатрат.ЗагрузитьКолонку(МассивПроектов, "Проект");
	КонецЕсли;
	
	Если СтруктураШапкиДокумента.Свойство("Организация") Тогда
		ТаблицаЗатрат.ЗаполнитьЗначения(СтруктураШапкиДокумента.Организация, "Организация");
	КонецЕсли;
	
	//Смартис Лиманчук начало 12.02.2013
	Если СтруктураШапкиДокумента.ВидДокумента = "ОтражениеЗарплатыВРеглУчете" Тогда
		ТаблицаЗатрат.ЗаполнитьЗначения(0, "СуммаБух");
	КонецЕсли;
	//Смартис Лиманчук окончание 12.02.2013
	
	//Смартис Лиманчук начало 12.04.2013
	//Если Не ЗначениеЗаполнено(ТаблицаЗатрат[0].ВидДвижения) Тогда
	Если ТаблицаЗатрат.Количество()>0 И Не ЗначениеЗаполнено(ТаблицаЗатрат[0].ВидДвижения) Тогда
	//Смартис Лиманчук окончание 12.04.2013
		ТаблицаЗатрат.ЗаполнитьЗначения(ВидДвиженияНакопления.Приход, "ВидДвижения");
	КонецЕсли;
	
	Если ТипЗнч(СтруктураШапкиДокумента.Ссылка) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг")
	 ИЛИ ТипЗнч(СтруктураШапкиДокумента.Ссылка) = Тип("ДокументСсылка.ПоступлениеТоваровУслугВНТТ")
	 ИЛИ ТипЗнч(СтруктураШапкиДокумента.Ссылка) = Тип("ДокументСсылка.АвансовыйОтчет")
	 ИЛИ ТипЗнч(СтруктураШапкиДокумента.Ссылка) = Тип("ДокументСсылка.ПолучениеУслугПоПереработке")
	Тогда
		КодОперации = Перечисления.КодыОперацийЗатраты.УслугиКонтрагентаНаПостоянныеЗатраты;
		КодОперацииНЗП = Перечисления.КодыОперацийНезавершенноеПроизводство.УслугиКонтрагентаНаПроизводство;
	Иначе
		КодОперации = Перечисления.КодыОперацийЗатраты.ПрочиеПостоянныеЗатраты;
	КонецЕсли;
	ТаблицаЗатрат.ЗаполнитьЗначения(КодОперации, "КодОперации");
	
	УдаляемыеКолоноки = Новый Массив;
	
	Для Каждого Колонка Из ТаблицаЗатрат.Колонки Цикл
		Если Колонка.ТипЗначения.СодержитТип(Тип("Строка"))
		 ИЛИ Не ЗначениеЗаполнено(Колонка.ТипЗначения) 
		Тогда
			УдаляемыеКолоноки.Добавить(Колонка);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Колонка Из УдаляемыеКолоноки Цикл
		ТаблицаЗатрат.Колонки.Удалить(Колонка);
	КонецЦикла;
	
	Возврат ТаблицаЗатрат;
	
КонецФункции // ПолучитьТаблицуЗатрат()

// Процедура дополняет структуру шапки документа.
//
// Выходные параметры:
//	СтруктураШапкиДокумента - Структура - Реквизиты документа
//	
Процедура ДополнитьСтруктуруШапкиДокументаУчетЗатрат(СтруктураШапкиДокумента)
	
	Если Не СтруктураШапкиДокумента.Свойство("ОтражатьВУправленческомУчете") Тогда
		СтруктураШапкиДокумента.Вставить("ОтражатьВУправленческомУчете", Ложь);
	КонецЕсли;
	Если Не СтруктураШапкиДокумента.Свойство("ОтражатьВБухгалтерскомУчете") Тогда
		СтруктураШапкиДокумента.Вставить("ОтражатьВБухгалтерскомУчете", Ложь);
	КонецЕсли;
	СтруктураШапкиДокумента.Вставить("ОтражатьВНалоговомУчете", СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете);
	
	Если ТипЗнч(СтруктураШапкиДокумента.Ссылка) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг")
	 ИЛИ ТипЗнч(СтруктураШапкиДокумента.Ссылка) = Тип("ДокументСсылка.ПоступлениеТоваровУслугВНТТ")
	 ИЛИ ТипЗнч(СтруктураШапкиДокумента.Ссылка) = Тип("ДокументСсылка.АвансовыйОтчет")
	 ИЛИ ТипЗнч(СтруктураШапкиДокумента.Ссылка) = Тип("ДокументСсылка.ПолучениеУслугПоПереработке")
	Тогда
		КодОперации = Перечисления.КодыОперацийЗатраты.УслугиКонтрагентаНаПостоянныеЗатраты;
		КодОперацииНЗП = Перечисления.КодыОперацийНезавершенноеПроизводство.УслугиКонтрагентаНаПроизводство;
	Иначе
		КодОперации = Перечисления.КодыОперацийЗатраты.ПрочиеПостоянныеЗатраты;
		КодОперацииНЗП = "";
	КонецЕсли;
	СтруктураШапкиДокумента.Вставить("КодОперации", КодОперации);
	СтруктураШапкиДокумента.Вставить("КодОперацииНЗП", КодОперацииНЗП);
	
КонецПроцедуры // ДополнитьСтруктуруШапкиДокументаУчетЗатрат()

// Процедура формирует движения в регистры по прочим затратам по всем видам учета.
//
// Параметры:
//	СтруктураШапкиДокумента - Структура - Реквизиты документа
//	ТаблицаДокумента - Таблица документа для отражения затрат
//	ВидОтраженияВУчете - ПеречислениеСсылка.ВидыОтраженияВУчете - Вид отражения в учете
//
Процедура ДвиженияПоПрочимЗатратам(
	СтруктураШапкиДокумента, 
	ТаблицаДокумента,
	ВидОтраженияВУчете = Неопределено
	) Экспорт
	
	Если ТаблицаДокумента.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ДополнитьСтруктуруШапкиДокументаУчетЗатрат(СтруктураШапкиДокумента);
			
	ТаблицаЗатрат = ПолучитьТаблицуЗатрат(
		ТаблицаДокумента,
		СтруктураШапкиДокумента
	);
	УправлениеЗатратамиДвиженияПоРегистрам.СформироватьДвиженияПоОтражениюЗатрат(
		СтруктураШапкиДокумента,
		ТаблицаЗатрат,
		ВидОтраженияВУчете
	);
	
КонецПроцедуры // ДвиженияПоПрочимЗатратам()

// Процедура заполняет субконто учета затрат в проводке по бухгалтерскому учету.
//
// Параметры
//	НоваяПроводка - РегистрБухгалтерииЗапись - Текущая проводка
//	СтрокаТабличнойЧасти - СтрокаТабличнойЧасти - Текущая строка
//	СтруктураШапкиДокумента - Структура - Реквизиты документа
//	Суффикс - Строка - Суффикс заполнения субконто по дебету или по кредиту
//	ПолучитьПодразделениеИзСтрокиТаблицы - Булево - Признак получения значения подразделения из строки
//		таблицы.
//
Процедура ЗаполнитьСчетИСубконтоУчетаЗатрат(
	НоваяПроводка, 
	СтрокаТабличнойЧасти, 
	СтруктураШапкиДокумента, 
	Суффикс = "Дт",
	ПолучитьПодразделениеИзСтрокиТаблицы = Ложь
	) Экспорт

	СчетЗатрат = СтрокаТабличнойЧасти.СчетЗатрат;
	
	СчетДтКт = "Счет" + Суффикс;
	СубконтоДтКт = "Субконто" + Суффикс;
	
	ПроизводственныеРасходы = ПроверитьСчетЗатратНаПроизводственныеРасходы(СчетЗатрат);

	НоваяПроводка[СчетДтКт] = СчетЗатрат;
	Если ПроизводственныеРасходы Тогда
		Если ПолучитьПодразделениеИзСтрокиТаблицы Тогда
			ПодразделениеОрганизации = СтрокаТабличнойЧасти.ПодразделениеОрганизации;
		Иначе
			ПодразделениеОрганизации = СтруктураШапкиДокумента.ПодразделениеОрганизации;
		КонецЕсли;
		
		БухгалтерскийУчет.УстановитьСубконто(
			НоваяПроводка[СчетДтКт],
			НоваяПроводка[СубконтоДтКт],
			"СтатьиЗатрат",
			СтрокаТабличнойЧасти.СтатьяЗатрат
		);
		БухгалтерскийУчет.УстановитьСубконто(
			НоваяПроводка[СчетДтКт],
			НоваяПроводка[СубконтоДтКт],
			"Подразделения",
			ПодразделениеОрганизации
		);
		БухгалтерскийУчет.УстановитьСубконто(
			НоваяПроводка[СчетДтКт], 
			НоваяПроводка[СубконтоДтКт],
			"НоменклатурныеГруппы",
			СтрокаТабличнойЧасти.НоменклатурнаяГруппа
		);
		БухгалтерскийУчет.УстановитьСубконто(
			НоваяПроводка[СчетДтКт], 
			НоваяПроводка[СубконтоДтКт], 
			"ОбъектыСтроительства", 
			СтрокаТабличнойЧасти.ОбъектСтроительства
		);
	Иначе
		БухгалтерскийУчет.УстановитьСубконто(
			НоваяПроводка[СчетДтКт], 
			НоваяПроводка[СубконтоДтКт], 
			1, 
			СтрокаТабличнойЧасти.Субконто1
		);
		БухгалтерскийУчет.УстановитьСубконто(
			НоваяПроводка[СчетДтКт], 
			НоваяПроводка[СубконтоДтКт], 
			2, 
			СтрокаТабличнойЧасти.Субконто2
		);
		БухгалтерскийУчет.УстановитьСубконто(
			НоваяПроводка[СчетДтКт], 
			НоваяПроводка[СубконтоДтКт], 
			3, 
			СтрокаТабличнойЧасти.Субконто3
		);
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьСчетИСубконтоУчетаЗатрат()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ РАБОТЫ С АНАЛИТИКОЙ ЗАТРАТ

// Процедура устанавливает видимость ячеек для ввода аналитики в зависимости от указанной статьи затрат.
//
Процедура УстановитьВидимостьЯчеекАналитикиЗатрат(ХарактерЗатрат, Продукция, СчетЗатрат = НЕОПРЕДЕЛЕНО, ОформлениеСтроки, ОтражатьВУправленческомУчете, ОтражатьВБухгалтерскомУчете) Экспорт

	ХарактерЗатратБУ = УправлениеЗатратами.ПолучитьХарактерЗатратПоСчетуЗатрат(СчетЗатрат, Неопределено);
	
	
	ПустойХарактерЗатрат    = НЕ ЗначениеЗаполнено(ХарактерЗатрат);
	
	ПрочиеРасходы           = (ХарактерЗатрат = Перечисления.ХарактерЗатрат.Прочие ИЛИ ХарактерЗатрат = Перечисления.ХарактерЗатрат.ПрочиеОперационныеРасходы);
	
	ПрочиеРасходыБУ         = (ХарактерЗатратБУ = Перечисления.ХарактерЗатрат.Прочие ИЛИ ХарактерЗатратБУ = Перечисления.ХарактерЗатрат.ПрочиеОперационныеРасходы);
	
	
	ПрочиеРасходыРегл       = (ХарактерЗатратБУ = Перечисления.ХарактерЗатрат.Прочие) ИЛИ (ХарактерЗатратБУ = Перечисления.ХарактерЗатрат.ПрочиеОперационныеРасходы);
	
	ПроизводственныеРасходы = (ХарактерЗатрат = Перечисления.ХарактерЗатрат.ПроизводственныеРасходы) 
							   ИЛИ (ХарактерЗатратБУ = Перечисления.ХарактерЗатрат.ПроизводственныеРасходы);
	
	БракВПроизводстве       = (ХарактерЗатрат = Перечисления.ХарактерЗатрат.БракВПроизводстве)
							   ИЛИ (ХарактерЗатратБУ = Перечисления.ХарактерЗатрат.БракВПроизводстве);
	
	Строительство           = (ХарактерЗатрат = Перечисления.ХарактерЗатрат.ВложенияВоВнеоборотныеАктивы)
							   ИЛИ (ХарактерЗатратБУ = Перечисления.ХарактерЗатрат.ВложенияВоВнеоборотныеАктивы);

	НоменклатурнаяГруппаДоп = ЛОЖЬ;
	Если НЕ ОформлениеСтроки.Ячейки.Найти("НоменклатурнаяГруппаДоп") = НЕОПРЕДЕЛЕНО Тогда
	  	НоменклатурнаяГруппаДоп = ПроизводственныеРасходы;
	КонецЕсли;
	
	НоменклатурнаяГруппа = (Не ПустойХарактерЗатрат) 
							И НЕ ((ПрочиеРасходы И ПрочиеРасходыБУ ) ИЛИ Строительство ИЛИ НоменклатурнаяГруппаДоп);
							

	Если НЕ ОформлениеСтроки.Ячейки.Найти("Аналитика") = НЕОПРЕДЕЛЕНО Тогда
		ОформлениеСтроки.Ячейки.Аналитика.Видимость = Ложь;
	КонецЕсли;
	
	Если НЕ ОформлениеСтроки.Ячейки.Найти("НоменклатурнаяГруппаДоп") = НЕОПРЕДЕЛЕНО Тогда
		
		Если ОформлениеСтроки.Ячейки.НоменклатурнаяГруппаДоп.Видимость <> НоменклатурнаяГруппаДоп Тогда
			ОформлениеСтроки.Ячейки.НоменклатурнаяГруппаДоп.Видимость      = НоменклатурнаяГруппаДоп;
			ОформлениеСтроки.Ячейки.НоменклатурнаяГруппаДоп.ТолькоПросмотр = НЕ НоменклатурнаяГруппаДоп;
		КонецЕсли;
	  	
	КонецЕсли;
	
	
	Если ОформлениеСтроки.Ячейки.НоменклатурнаяГруппа.Видимость <> НоменклатурнаяГруппа Тогда
		ОформлениеСтроки.Ячейки.НоменклатурнаяГруппа.Видимость      = НоменклатурнаяГруппа;
		ОформлениеСтроки.Ячейки.НоменклатурнаяГруппа.ТолькоПросмотр = НЕ НоменклатурнаяГруппа;
	КонецЕсли;
	
	ЕстьСпособРаспределенияЗатратНаВыпуск = Ложь;
	Если НЕ ОформлениеСтроки.Ячейки.Найти("СпособРаспределенияЗатратНаВыпуск") = НЕОПРЕДЕЛЕНО Тогда
		ОформлениеСтроки.Ячейки.СпособРаспределенияЗатратНаВыпуск.Видимость      =    ПроизводственныеРасходы;
		ОформлениеСтроки.Ячейки.СпособРаспределенияЗатратНаВыпуск.ТолькоПросмотр = НЕ ПроизводственныеРасходы;
		ЕстьСпособРаспределенияЗатратНаВыпуск = Истина;
	КонецЕсли;
	
	Если НЕ ОформлениеСтроки.Ячейки.Найти("СпособРаспределенияЗатратНаВыпускПолучатель") = НЕОПРЕДЕЛЕНО Тогда
		ОформлениеСтроки.Ячейки.СпособРаспределенияЗатратНаВыпускПолучатель.Видимость      =    ПроизводственныеРасходы;
		ОформлениеСтроки.Ячейки.СпособРаспределенияЗатратНаВыпускПолучатель.ТолькоПросмотр = НЕ ПроизводственныеРасходы;
	КонецЕсли;

	Если НЕ ОформлениеСтроки.Ячейки.Найти("ВидАналитики") = НЕОПРЕДЕЛЕНО Тогда

		ОформлениеСтроки.Ячейки.ВидАналитики.Видимость          = НЕ ((ПрочиеРасходы И ПрочиеРасходыБУ) ИЛИ ПустойХарактерЗатрат);
		
		ОформлениеСтроки.Ячейки.ВидАналитики.ОтображатьТекст    = Истина;
		ОформлениеСтроки.Ячейки.ВидАналитикиДоп.ОтображатьТекст = Истина;

		Если Строительство Тогда
			ОформлениеСтроки.Ячейки.ВидАналитики.Текст = "Объект строительства";

		ИначеЕсли НоменклатурнаяГруппаДоп Тогда
			ОформлениеСтроки.Ячейки.ВидАналитики.Текст = "Номенклатурная группа"
					+ ?(ЕстьСпособРаспределенияЗатратНаВыпуск, " / Способ распределения", "");

		Иначе
			ОформлениеСтроки.Ячейки.ВидАналитики.Текст = "Номенклатурная группа";

		КонецЕсли;

		ОформлениеСтроки.Ячейки.ВидАналитикиДоп.Видимость = БракВПроизводстве;

		Если БракВПроизводстве Тогда
			ОформлениеСтроки.Ячейки.ВидАналитикиДоп.Текст = "Продукция";

		Иначе
			ОформлениеСтроки.Ячейки.ВидАналитикиДоп.Текст = "";

		КонецЕсли;

	КонецЕсли;
	
	Если ПрочиеРасходыБУ И ЗначениеЗаполнено(СчетЗатрат) Тогда
		КоличествоСубконто = СчетЗатрат.ВидыСубконто.Количество();
	Иначе
		КоличествоСубконто = 0;
	КонецЕсли;
	
	
	Если НЕ ОформлениеСтроки.Ячейки.Найти("ВидСубконто3") = НЕОПРЕДЕЛЕНО Тогда

		ОформлениеСтроки.Ячейки.ВидСубконто1.Видимость = ПрочиеРасходыБУ И ОтражатьВБухгалтерскомУчете И (КоличествоСубконто <> 0);
		ОформлениеСтроки.Ячейки.ВидСубконто2.Видимость = ПрочиеРасходыБУ И ОтражатьВБухгалтерскомУчете И (КоличествоСубконто <> 0);
		ОформлениеСтроки.Ячейки.ВидСубконто3.Видимость = ПрочиеРасходыБУ И ОтражатьВБухгалтерскомУчете И (КоличествоСубконто <> 0);

		ОформлениеСтроки.Ячейки.ВидСубконто1.ОтображатьТекст = Истина;
		ОформлениеСтроки.Ячейки.ВидСубконто2.ОтображатьТекст = Истина;
		ОформлениеСтроки.Ячейки.ВидСубконто3.ОтображатьТекст = Истина;

		Если ПрочиеРасходыБУ И ЗначениеЗаполнено(СчетЗатрат) Тогда

			Если КоличествоСубконто > 0 Тогда
				ОформлениеСтроки.Ячейки.ВидСубконто1.Текст = СчетЗатрат.ВидыСубконто.Получить(0).ВидСубконто;
			Иначе
				ОформлениеСтроки.Ячейки.ВидСубконто1.Текст = "";
			КонецЕсли;

			Если КоличествоСубконто > 1 Тогда
				ОформлениеСтроки.Ячейки.ВидСубконто2.Текст = СчетЗатрат.ВидыСубконто.Получить(1).ВидСубконто;
			Иначе
				ОформлениеСтроки.Ячейки.ВидСубконто2.Текст = "";
			КонецЕсли;

			Если КоличествоСубконто > 2 Тогда
				ОформлениеСтроки.Ячейки.ВидСубконто3.Текст = СчетЗатрат.ВидыСубконто.Получить(2).ВидСубконто;
			Иначе
				ОформлениеСтроки.Ячейки.ВидСубконто3.Текст = "";
			КонецЕсли;

		Иначе

			ОформлениеСтроки.Ячейки.ВидСубконто1.Текст = "";
			ОформлениеСтроки.Ячейки.ВидСубконто2.Текст = "";
			ОформлениеСтроки.Ячейки.ВидСубконто3.Текст = "";

		КонецЕсли;

	КонецЕсли;


	Если НЕ ОформлениеСтроки.Ячейки.Найти("Продукция") = НЕОПРЕДЕЛЕНО Тогда

		Если БракВПроизводстве
		   И Продукция.ВестиУчетПоХарактеристикам
		Тогда
			ВидимостьХарактеристикаПродукции = глЗначениеПеременной("ИспользоватьХарактеристикиНоменклатуры");
		Иначе
			ВидимостьХарактеристикаПродукции = Ложь;
		КонецЕсли;
		
		Если БракВПроизводстве
		   И Продукция.ВестиУчетПоСериям
		Тогда
			ВидимостьСерияПродукции = глЗначениеПеременной("ИспользоватьСерииНоменклатуры");
		Иначе
			ВидимостьСерияПродукции = Ложь;
		КонецЕсли;

		ОформлениеСтроки.Ячейки.Продукция.Видимость               = БракВПроизводстве;
		ОформлениеСтроки.Ячейки.ХарактеристикаПродукции.Видимость = ВидимостьХарактеристикаПродукции;
		ОформлениеСтроки.Ячейки.СерияПродукции.Видимость          = ВидимостьСерияПродукции;
	
		ОформлениеСтроки.Ячейки.Продукция.ТолькоПросмотр               = НЕ БракВПроизводстве;
		ОформлениеСтроки.Ячейки.ХарактеристикаПродукции.ТолькоПросмотр = НЕ ВидимостьХарактеристикаПродукции;
		ОформлениеСтроки.Ячейки.СерияПродукции.ТолькоПросмотр          = НЕ ВидимостьСерияПродукции;

	КонецЕсли;

	Если НЕ ОформлениеСтроки.Ячейки.Найти("ОбъектСтроительства") = НЕОПРЕДЕЛЕНО Тогда
		ОформлениеСтроки.Ячейки.ОбъектСтроительства.Видимость = Строительство;
		ОформлениеСтроки.Ячейки.ОбъектСтроительства.ТолькоПросмотр = НЕ Строительство;
	КонецЕсли;

	
	Если НЕ ОформлениеСтроки.Ячейки.Найти("Субконто1") = НЕОПРЕДЕЛЕНО Тогда

		ОформлениеСтроки.Ячейки.Субконто1.Видимость = ПрочиеРасходыБУ И ОтражатьВБухгалтерскомУчете И (КоличествоСубконто <> 0);
		ОформлениеСтроки.Ячейки.Субконто2.Видимость = ПрочиеРасходыБУ И ОтражатьВБухгалтерскомУчете И (КоличествоСубконто <> 0);
		ОформлениеСтроки.Ячейки.Субконто3.Видимость = ПрочиеРасходыБУ И ОтражатьВБухгалтерскомУчете И (КоличествоСубконто <> 0);

		ОформлениеСтроки.Ячейки.Субконто1.ТолькоПросмотр = (КоличествоСубконто < 1) ИЛИ НЕ (ПрочиеРасходыБУ И ОтражатьВБухгалтерскомУчете);
		ОформлениеСтроки.Ячейки.Субконто2.ТолькоПросмотр = (КоличествоСубконто < 2) ИЛИ НЕ (ПрочиеРасходыБУ И ОтражатьВБухгалтерскомУчете);
		ОформлениеСтроки.Ячейки.Субконто3.ТолькоПросмотр = (КоличествоСубконто < 3) ИЛИ НЕ (ПрочиеРасходыБУ И ОтражатьВБухгалтерскомУчете);

	КонецЕсли;
	

КонецПроцедуры // УстановитьВидимостьЯчеекАналитикиЗатрат()

// Процедура устанавливает статью затрат в субконто.
// Используется когда в документе есть статья затрат и счет
// с субконто вида "СтатьяЗатрат".
//
Процедура УстановитьСубконтоСтатьяЗатрат(Знач СтатЗатрат, Знач Счет, Субк1, Субк2, Субк3) Экспорт

	ВидСубк = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные["СтатьиЗатрат"];
	Если Счет.ВидыСубконто.Найти( ВидСубк) = Неопределено Тогда
		Возврат;
	КонецЕсли;

	НомерСубконто = Счет.ВидыСубконто.Найти( ВидСубк).НомерСтроки;

	Если НомерСубконто = 1 Тогда
		Субк1 = СтатЗатрат;

	ИначеЕсли НомерСубконто = 2 Тогда
		Субк2 = СтатЗатрат;

	ИначеЕсли НомерСубконто = 3 Тогда
		Субк3 = СтатЗатрат;

	КонецЕсли;

КонецПроцедуры // УстановитьСубконтоСтатьяЗатрат()

// Функция возвращает подразделение организации, соответствующее подразделению компании.
//
// Параметры:
//	Организация - СправочникСсылка.Организации
//	Подразделение - СправочникСсылка.Подразделения
//	ОтражатьВБухгалтерскомУчете - Булево - Признак отражения документа в бух. учете
//
// Возвращаемое значение:
//	СправочникСсылка.ПодразделенияОрганизаций - Подразделение организации
//
Функция ПолучитьПодразделениеОрганизации(
	Организация,
	Подразделение,
	ОтражатьВБухгалтерскомУчете
	) Экспорт
	
	ПодразделениеОрганизации = Справочники.ПодразделенияОрганизаций.ПустаяСсылка();
	
	Если ОтражатьВБухгалтерскомУчете Тогда
	
		ТекстЗапроса = "
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	СоответствиеПодразделений.ПодразделениеОрганизации
		|ИЗ
		|	РегистрСведений.СоответствиеПодразделенийИПодразделенийОрганизаций КАК СоответствиеПодразделений
		|
		|ГДЕ
		|	СоответствиеПодразделений.Подразделение = &Подразделение
		|	И СоответствиеПодразделений.Организация = &Организация
		|	И СоответствиеПодразделений.ПодразделениеОрганизации <> ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
		|";
					   
		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапроса;
		Запрос.УстановитьПараметр("Подразделение", Подразделение);
		Запрос.УстановитьПараметр("Организация", Организация);

		РезультатЗапроса = Запрос.Выполнить();
		
		Выборка = РезультатЗапроса.Выбрать();
		Если Выборка.Следующий() Тогда
			ПодразделениеОрганизации = Выборка.ПодразделениеОрганизации;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ПодразделениеОрганизации;
	
КонецФункции // ПолучитьПодразделениеОрганизации();

// Процедура заполняет номенклатурную группу и статью затрат в строке табличной части.
//
Процедура ЗаполнитьНоменклатурнуюГруппуИСтатьюЗатратВСтрокеТабличногоПоля(СтрокаТабличнойЧасти, ЕстьСтатьяЗатрат = Истина, ЕстьНоменклатурнаяГруппа = Истина) Экспорт

	Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Номенклатура) Тогда

		Если ЕстьСтатьяЗатрат Тогда
			СтрокаТабличнойЧасти.СтатьяЗатрат = СтрокаТабличнойЧасти.Номенклатура.СтатьяЗатрат;
		КонецЕсли;

		Если ЕстьНоменклатурнаяГруппа Тогда
			СтрокаТабличнойЧасти.НоменклатурнаяГруппа = СтрокаТабличнойЧасти.Номенклатура.НоменклатурнаяГруппаЗатрат;
		КонецЕсли;

	КонецЕсли;

КонецПроцедуры // ЗаполнитьНоменклатурнуюГруппуИСтатьюЗатратВСтрокеТабличногоПоля()

// Процедура заполняет значение счета затрат в табличной части документа в зависимости от статьи затрат.
//
Процедура ЗаполнитьСчетЗатратВСтрокеТабличногоПоля(СтрокаТабличнойЧасти, ПодразделениеОрганизации, СтатьяЗатрат, ИмяСчетаЗатрат = "СчетЗатрат", ЗаполнятьБУ = Истина) Экспорт

	СчетаУчета = УправлениеЗатратами.ПолучитьСчетаУчетаСтатьиЗатрат(ПодразделениеОрганизации, СтатьяЗатрат); 
    	
	Если ЗаполнятьБУ = Истина Тогда
		СтрокаТабличнойЧасти[ИмяСчетаЗатрат] = СчетаУчета.СчетУчетаБУ;
	ИначеЕсли ЗаполнятьБУ = Ложь Тогда
		СтрокаТабличнойЧасти[ИмяСчетаЗатрат] = ПланыСчетов.Хозрасчетный.ПустаяСсылка();
	КонецЕсли;

КонецПроцедуры // ЗаполнитьСчетЗатратВСтрокеТабличногоПоля()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ПРОВЕРКИ ЗАПОЛНЕНИЯ РЕКВИЗИТОВ

// Процедура проверяет соответствие подразделения организации и организации в документе.
//
// Параметры
//  ДокументОбъект - Документ, в котором проверяется соответствие
//  Отказ          - Значению параметра будет присвоено Истина, если соответствия нет
//  Заголовок      - Заголовок сообщения о том, что соответствия нет
Процедура ПроверитьПодразделениеОрганизации(ДокументОбъект, Отказ, Заголовок) Экспорт
	
	Если Не ДокументОбъект.ОтражатьВБухгалтерскомУчете Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДокументОбъект.Организация)
	   И ЗначениеЗаполнено(ДокументОбъект.ПодразделениеОрганизации)
	   И Не ДокументОбъект.ПодразделениеОрганизации.Владелец = ДокументОбъект.Организация Тогда

		ОбщегоНазначения.СообщитьОбОшибке("Указанное в документе подразделение не соответствует организации!", Отказ, Заголовок);

	КонецЕсли;

КонецПроцедуры // ПроверитьПодразделениеОрганизации()

// Процедура проверяет соответствие подразделения организации в строках табличной части документа
// и организации в шапке документа.
//
// Параметры
//  ДокументОбъект - Документ, в котором проверяется соответствие
//  ТабЧасть       - Табличная часть, в строках которой проверяется
//  ИмяТабЧасти    - Имя табличной части
//  ИмяРеквизитаТЧ - Имя реквизита с проверяемыми значениями
//  Отказ          - Значению параметра будет присвоено Истина, если соответствия нет
//  Заголовок      - Заголовок сообщения о том, что соответствия нет
Процедура ПроверитьПодразделениеОрганизацииВСтрокахТабЧасти(ДокументОбъект, ТабЧасть, ИмяТабЧасти, ИмяРеквизитаТЧ = "ПодразделениеОрганизации", Отказ, Заголовок) Экспорт
	
	Если Не ДокументОбъект.ОтражатьВБухгалтерскомУчете Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДокументОбъект.Организация) Тогда
		Возврат;
	КонецЕсли;
		
	Для Каждого СтрокаТабличнойЧасти из ТабЧасть Цикл
		   Если ЗначениеЗаполнено(СтрокаТабличнойЧасти[ИмяРеквизитаТЧ])
		   И Не СтрокаТабличнойЧасти[ИмяРеквизитаТЧ].Владелец = ДокументОбъект.Организация Тогда

			ОбщегоНазначения.СообщитьОбОшибке("Указанное подразделение не соответствует организации! Строка № " + СтрокаТабличнойЧасти.НомерСтроки + " (таб. часть """ + ИмяТабЧасти + """)", Отказ, Заголовок);
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры // ПроверитьПодразделениеОрганизацииВСтрокахТабЧасти()

// Процедура проверяет правильность заполнения субконто, связанных со счетом
//
Процедура ПроверитьЗаполнениеСубконто(ДокОбъект, СтруктураПараметров, ИмяТабЧасти = "", Отказ, Заголовок) Экспорт

	ИмяСчета    = "";
	ИмяСубконто = Новый Массив(3);
	Индекс      = 0;

	Для Каждого Параметр Из СтруктураПараметров Цикл

		Если Индекс = 0 Тогда
			ИмяСчета = Параметр.Ключ;
		Иначе
			ИмяСубконто[Индекс - 1] = Параметр.Ключ;
		КонецЕсли;

		Индекс = Индекс + 1;

	КонецЦикла;

	Если ПустаяСтрока(ИмяТабЧасти) Тогда

		Если ДокОбъект[ИмяСчета].Пустая() Тогда
			ОбщегоНазначения.СообщитьОбОшибке(СтруктураПараметров[ИмяСчета], Отказ, Заголовок);
			Возврат;
		КонецЕсли;

		Для К = 0 По ДокОбъект[ИмяСчета].ВидыСубконто.Количество() - 1 Цикл
			Если Не ДокОбъект[ИмяСчета].ВидыСубконто[К].ТолькоОбороты
			      И НЕ ЗначениеЗаполнено(ДокОбъект[ИмяСубконто[К]]) Тогда
				ОбщегоНазначения.СообщитьОбОшибке(СтруктураПараметров[ИмяСубконто[К]], Отказ, Заголовок);
			КонецЕсли;
		КонецЦикла;

	Иначе

		Для Каждого СтрокаТЧ Из ДокОбъект[ИмяТабЧасти] Цикл

			Если СтрокаТЧ[ИмяСчета].Пустая() Тогда
				ОбщегоНазначения.СообщитьОбОшибке(СтруктураПараметров[ИмяСчета] + " (строка № " + СтрокаТЧ.НомерСтроки + ")", Отказ, Заголовок);
				Продолжить;
			КонецЕсли;

			Для К = 0 По СтрокаТЧ[ИмяСчета].ВидыСубконто.Количество() - 1 Цикл
				Если Не СтрокаТЧ[ИмяСчета].ВидыСубконто[К].ТолькоОбороты
				      И НЕ ЗначениеЗаполнено(СтрокаТЧ[ИмяСубконто[К]]) Тогда
					ОбщегоНазначения.СообщитьОбОшибке(СтруктураПараметров[ИмяСубконто[К]] + " (строка № " + СтрокаТЧ.НомерСтроки + ")", Отказ, Заголовок);
				КонецЕсли;
			КонецЦикла;

		КонецЦикла;

	КонецЕсли;

КонецПроцедуры // ПроверитьЗаполнениеСубконто()

// Определяет налоговое назначение по заказу затрат, НГ, подразделению
//
// СтруктураПараметров - структура с ключами
//  	Заказ - заказ 
// 		НоменклатурнаяГруппа - номенклатурная группа 
// 		ПодразделениеОрганизации
//
// Возвращаемое значение:
//   НалоговоеНазначение - Справочники.НалоговыеНазначенияАктивовИЗатрат – налоговое назначение
//
Функция ОпределитьНалоговоеНазначение(СтруктураПараметров) Экспорт
	
	НалоговоеНазначение = Справочники.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка();
	
	Если СтруктураПараметров.Свойство("Заказ") И ЗначениеЗаполнено(СтруктураПараметров.Заказ) Тогда
		НалоговоеНазначение = СтруктураПараметров.Заказ.НалоговоеНазначение; 
	КонецЕсли; 
	
	Если НЕ ЗначениеЗаполнено(НалоговоеНазначение) И СтруктураПараметров.Свойство("НоменклатурнаяГруппа") И ЗначениеЗаполнено(СтруктураПараметров.НоменклатурнаяГруппа) Тогда
		НалоговоеНазначение = СтруктураПараметров.НоменклатурнаяГруппа.НалоговоеНазначение; 
	КонецЕсли; 
	
	Если НЕ ЗначениеЗаполнено(НалоговоеНазначение) И СтруктураПараметров.Свойство("ПодразделениеОрганизации") И ЗначениеЗаполнено(СтруктураПараметров.ПодразделениеОрганизации) Тогда
		НалоговоеНазначение = СтруктураПараметров.ПодразделениеОрганизации.НалоговоеНазначение; 
	КонецЕсли; 
	
	Возврат НалоговоеНазначение;
	
КонецФункции // ОпределитьНалоговоеНазначение()

Процедура УстановитьДоступностьНалоговогоНазначения(ОформлениеСтроки, Заказ, НоменклатурнаяГруппа, СчетЗатрат, ИмяКолонкиНалоговоеНазначение = "НалоговоеНазначение") Экспорт

	РазрешитьИзменение = Истина;
	
	Если ЗначениеЗаполнено(Заказ) Тогда
		НалоговоеНазначение = Заказ.НалоговоеНазначение; 
		Если ЗначениеЗаполнено(НалоговоеНазначение) Тогда
			РазрешитьИзменение = Ложь;
		КонецЕсли; 
	КонецЕсли; 
	
	Если НЕ ЗначениеЗаполнено(НалоговоеНазначение) И ЗначениеЗаполнено(НоменклатурнаяГруппа) Тогда
		НалоговоеНазначение = НоменклатурнаяГруппа.НалоговоеНазначение; 
		Если ЗначениеЗаполнено(НалоговоеНазначение) Тогда
			РазрешитьИзменение = Ложь;
		КонецЕсли; 
	КонецЕсли; 
	
	// Для брака разрешаем изменять НалоговоеНазначение в независимости от того определяется ли оно заказом или НГ
	Если ЗначениеЗаполнено(СчетЗатрат) Тогда
		Если ПолучитьХарактерЗатратПоСчетуЗатрат(СчетЗатрат, Неопределено) = Перечисления.ХарактерЗатрат.БракВПроизводстве Тогда
			РазрешитьИзменение = Истина;
		КонецЕсли; 	
	КонецЕсли; 
	
	ОформлениеСтроки.Ячейки[ИмяКолонкиНалоговоеНазначение].ТолькоПросмотр = НЕ РазрешитьИзменение;
	

КонецПроцедуры // УстановитьДоступностьНалоговогоНазначения

