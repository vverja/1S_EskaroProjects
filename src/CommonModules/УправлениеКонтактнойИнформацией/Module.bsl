// Этот модуль предназачен для реализации алгоритмов работы 
// с контактной информацией.

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

//процедура пытается заблокировать объект для изменения
Процедура ПроизвестиПопыткуЗаблокироватьОбъект(ЭтотОбъект, ЭтаФорма) Экспорт
	
	ЭтотОбъект.Заблокировать();
	ЭтаФорма.Модифицированность = Истина;
		
КонецПроцедуры

// Функция раскладывает номер телефона, по полям для записи в КИ объекта
//
// Параметры
//  
//  НомерТелефона - строка, номер телефона для преобразования
//
// Возвращаемое значение:
//   Список значений
//
Функция РазложитьТелефонПоПолям(НомерТелефона) Экспорт

	КодСтраны = "";
	НачалоКодаСтраны = Найти(НомерТелефона, "+");
	Если НачалоКодаСтраны > 0 Тогда
		Для а = (НачалоКодаСтраны + 1) По СтрДлина(НомерТелефона) Цикл
			Если Сред(НомерТелефона, а, 1) = " " Тогда
				Прервать;
			КонецЕсли; 
			КодСтраны = КодСтраны + Сред(НомерТелефона, а, 1);
		КонецЦикла; 
	КонецЕсли; 
	КодСтраны = СокрЛП(КодСтраны);

	КодГорода = "";
	Если СтрЧислоВхождений(НомерТелефона, "(") = 1 И СтрЧислоВхождений(НомерТелефона, ")") = 1 Тогда
		НачалоКодаГорода = Найти(НомерТелефона, "(");
		КонецКодаГорода = Найти(НомерТелефона, ")");
		Если КонецКодаГорода > НачалоКодаГорода Тогда
			КодГорода = Сред(НомерТелефона, (НачалоКодаГорода + 1), (КонецКодаГорода - НачалоКодаГорода - 1));
		КонецЕсли;
	КонецЕсли;
	КодГорода = СокрЛП(КодГорода);

	СамТелефон = НомерТелефона;
	Если НЕ ПустаяСтрока(КодСтраны) Тогда
		СамТелефон = СтрЗаменить(СамТелефон, ("+" + КодСтраны), "");
		СамТелефон = СокрЛП(СамТелефон);
	КонецЕсли; 
	Если НЕ ПустаяСтрока(КодГорода) Тогда
		СамТелефон = СтрЗаменить(СамТелефон, ("(" + КодГорода + ")"), "");
		СамТелефон = СокрЛП(СамТелефон);
	КонецЕсли;
	
	а = 1;
	Пока а <= СтрДлина(СамТелефон) Цикл
		Если (КодСимвола(Сред(СамТелефон, а, 1)) >= 48 И КодСимвола(Сред(СамТелефон, а, 1)) <= 57) ИЛИ КодСимвола(Сред(СамТелефон, а, 1)) = 32 Тогда
			а = а + 1;
			Продолжить;
		КонецЕсли;
		СамТелефон = Сред(СамТелефон, 1, (а - 1)) + Сред(СамТелефон, (а + 1));
	КонецЦикла; 

	Если НЕ ПустаяСтрока(КодСтраны) И Лев(СокрЛ(КодСтраны), 1) <> "+" Тогда
		КодСтраны = СокрЛП(КодСтраны);
		Пока Лев(КодСтраны, 1) = "0" Цикл
			КодСтраны = Сред(КодСтраны, 2);
		КонецЦикла;
		Если НЕ ПустаяСтрока(КодСтраны) Тогда
			КодСтраны = "+" + КодСтраны;
		КонецЕсли; 
	КонецЕсли; 
	
	СтруктураПолейТелефона = Новый Структура("КодСтраны,КодГорода,СамТелефон", КодСтраны, КодГорода, ПривестиНомерТелефонаКШаблону(СамТелефон));
	
	Возврат СтруктураПолейТелефона;

КонецФункции // ()

// Функция проверяет строку на наличие значимых символов
//
// Параметры
//  ВыбСтрока  – строка для проверки
// Возвращаемое значение:
//   Строка - пробел или пустое значение строки
//
Функция ПроверкаПустойСтроки(ВыбСтрока, ПризнакЗапятой=Истина) Экспорт
	
	Если ПустаяСтрока(ВыбСтрока) Тогда
		Возврат "";
	Иначе
		Возврат ?(ПризнакЗапятой,",","")+" ";
	КонецЕсли; 
	
КонецФункции // ПроверкаПустойСтроки()

// Процедура заполняет поля объекта редактирования адреса по структуре
Процедура ЗаполнитьОбъектРедактированияАдресаПоСтруктуре(Объект, СтруктураАдреса) Экспорт
	
	Если СтруктураАдреса = Неопределено Тогда
		
		Объект.Поле1 = "";
		Объект.Поле2 = "";
		Объект.Поле3 = "";
		Объект.Поле4 = "";
		Объект.Поле5 = "";
		Объект.Поле6 = "";
		Объект.Поле7 = "";
		Объект.Поле8 = "";
		Объект.Поле9 = "";
		Объект.Представление = "";
	
		Возврат;
	КонецЕсли;
	Если СтруктураАдреса.Свойство("ЗаПределамиУкраины") Тогда			
		Объект.Поле1 = СтруктураАдреса.Страна;
		Объект.Представление = СтруктураАдреса.Представление;		
	Иначе
		Объект.Поле1 = СтруктураАдреса.Индекс;
		Объект.Поле2 = СтруктураАдреса.Регион;
		Объект.Поле3 = СтруктураАдреса.Район;
		Объект.Поле4 = СтруктураАдреса.Город;
		Объект.Поле5 = СтруктураАдреса.НаселенныйПункт;
		Объект.Поле6 = СтруктураАдреса.Улица;
		Объект.Поле7 = СтруктураАдреса.Дом;
		Объект.Поле8 = СтруктураАдреса.Корпус;
		Объект.Поле9 = СтруктураАдреса.Квартира;
		
		//Определяем представление сразу же
		Объект.Представление = ПолучитьПредставлениеАдреса(Объект);
	КонецЕсли;
  
КонецПроцедуры

//Процедура дополняет представление адреса строкой 
Процедура ДополнитьПредставлениеАдреса(ТекущееПредставление, Дополнение, СтрокаКонкатенации)
	
	Если Дополнение <> "" Тогда
		ТекущееПредставление = ТекущееПредставление + СтрокаКонкатенации + Дополнение;
	КонецЕсли;
  	
КонецПроцедуры

// Функция возвращает строковое представление адреса (в формате через запятую) , включая незаполненные поля
Функция ПолучитьПолныйАдрес(Запись) Экспорт
	
	Результат = "";
	
	Если Запись = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	Если ОпределитьДляОбъектаУкраинскийАдрес(Запись) Тогда	
		Результат = "," + 
		СокрЛП(Запись.Поле1) + ", " +
		СокрЛП(Запись.Поле2) + ", " +
		СокрЛП(Запись.Поле3) + ", " +
		СокрЛП(Запись.Поле4) + ", " +
		СокрЛП(Запись.Поле5) + ", " +
		СокрЛП(Запись.Поле6) + ", " +
		СокрЛП(Запись.Поле7) + ", " +
		СокрЛП(Запись.Поле8) + ", " +
		СокрЛП(Запись.Поле9);
		
	Иначе
		Результат = Запись.Представление;
	КонецЕсли;
	
	Если ПустаяСтрока(СтрЗаменить(Результат, ",", "")) Тогда
		Результат = "";
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Функция выполняет "преобразование" строкового представления адреса в структуру
Функция ПолучитьСтруктуруАдресаИзСтроки(Знач СтроковыйАдрес) Экспорт
	
	Если ПустаяСтрока(СтроковыйАдрес) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ЧислоВхожденийЗапятойВАдрес = СтрЧислоВхождений(СтроковыйАдрес, ","); 
	
	Если ЧислоВхожденийЗапятойВАдрес = 9 Тогда
		
		СтруктураАдреса = РегламентированнаяОтчетность.РазложитьАдрес(СтроковыйАдрес);
		
	ИначеЕсли ЧислоВхожденийЗапятойВАдрес >= 1 Тогда
		
		СтруктураАдреса = Новый Структура;	
		СтруктураАдреса.Вставить("ЗаПределамиУкраины", Истина);
		СтруктураАдреса.Вставить("Страна", СокрЛП(Лев(СтроковыйАдрес, Найти(СтроковыйАдрес, ",") - 1)));
		СтруктураАдреса.Вставить("Представление", СтроковыйАдрес);			
		
	Иначе
		// вообще запятых нет в представлении два варианта,
		// либо задана только страна, но не Украина
		// либо необычный Украинский Адрес
		
		КлассификаторСтраны = Справочники.КлассификаторСтранМира.НайтиПоНаименованию(ВРег(СтроковыйАдрес), Истина);
		СтруктураАдреса = Новый Структура;	
		СтруктураАдреса.Вставить("ЗаПределамиУкраины", Истина);

		Если НЕ ЗначениеЗаполнено(КлассификаторСтраны) Тогда
			// необычный Украинский адрес
			СтруктураАдреса.Вставить("Страна", Справочники.КлассификаторСтранМира.Украина.Наименование);
			СтруктураАдреса.Вставить("Представление", СтроковыйАдрес);
		Иначе
			СтруктураАдреса.Вставить("Страна", СтроковыйАдрес);
			СтруктураАдреса.Вставить("Представление", "");
		КонецЕсли;
		
	КонецЕсли;
		
	Возврат СтруктураАдреса;	
	
КонецФункции


// функция определяет есть ли хотя бы одна не цифра в строке
Функция ЕстьНеЦифрыВСтроке(Знач СтрокаПроверки)
	
	Для а = 1 По СтрДлина(СтрокаПроверки) Цикл
		КодСимвола = КодСимвола(Сред(СтрокаПроверки, а, 1));
		Если НЕ (КодСимвола >= 48 И КодСимвола <= 57) Тогда
			Возврат Истина;
		КонецЕсли; 
	КонецЦикла; 

	Возврат Ложь;
	
КонецФункции

//Функция для объекта редактирования определяет Украинский это адрес или нет
Функция ОпределитьДляОбъектаУкраинскийАдрес(ОбъектРедактирования) Экспорт
	
	Если (НЕ ПустаяСтрока(ОбъектРедактирования.Поле1)) Тогда
		// если хотя бы один символ не цифра, то это точно не индекс
		МожетБытьИностарнныйАдрес = ЕстьНеЦифрыВСтроке(СокрЛП(ОбъектРедактирования.Поле1));
		// так, если есть хоть одна не цифра - это иностранный адрес, а если все цифры то Украинский
		Возврат НЕ МожетБытьИностарнныйАдрес;
	КонецЕсли;
	
	// Сравниваем представления
	ЭтоУкраинскийАдрес = (ПолучитьПредставлениеАдреса(ОбъектРедактирования) = ОбъектРедактирования.Представление);
	Возврат ЭтоУкраинскийАдрес;
		
КонецФункции

//Функция по структуре с адресом возвращает его представление
Функция ПолучитьПредставлениеАдресаПоСтруктуре(СтруктураАдреса, КодЯзыкаИБ=Неопределено)

	Если КодЯзыкаИБ = Неопределено Тогда
		КодЯзыкаИБ = Локализация.КодЯзыкаИнформационнойБазы();
	КонецЕсли;
	ТекПредставление = "";
	
	Если СтруктураАдреса = Неопределено Тогда
		Возврат ТекПредставление;
	КонецЕсли;
	АдресЗаПределамиУкраины = Неопределено;
	СтруктураАдреса.Свойство("ЗаПределамиУкраины", АдресЗаПределамиУкраины);
	Если АдресЗаПределамиУкраины = Неопределено Тогда
		АдресЗаПределамиУкраины = Ложь;
	КонецЕсли;
	
	ВПочтовомФормате = Неопределено;
	СтруктураАдреса.Свойство("ВПочтовомФормате", ВПочтовомФормате);
	Если ВПочтовомФормате = Неопределено Тогда
		ВПочтовомФормате = Ложь;
	КонецЕсли;
	
	Если Не АдресЗаПределамиУкраины Тогда
	
		Если ВПочтовомФормате Тогда
			
			ДополнитьПредставлениеАдреса(ТекПредставление, СокрЛП(СтруктураАдреса.Улица), ", ");
			ДополнитьПредставлениеАдреса(ТекПредставление, СокрЛП(СтруктураАдреса.Дом), 	НСтр("ru=', дом № ';uk=', будинок № '", КодЯзыкаИБ));
			ДополнитьПредставлениеАдреса(ТекПредставление, СокрЛП(СтруктураАдреса.Корпус), 	НСтр("ru=', корпус ';uk=', корпус '", 	КодЯзыкаИБ));
			ДополнитьПредставлениеАдреса(ТекПредставление, СокрЛП(СтруктураАдреса.Квартира),НСтр("ru=', кв.';uk=', кв.'", 			КодЯзыкаИБ));
			ДополнитьПредставлениеАдреса(ТекПредставление, СокрЛП(СтруктураАдреса.НаселенныйПункт), ", ");
			ДополнитьПредставлениеАдреса(ТекПредставление, СокрЛП(СтруктураАдреса.Город), ", ");
			ДополнитьПредставлениеАдреса(ТекПредставление, СокрЛП(СтруктураАдреса.Район), ", ");
			ДополнитьПредставлениеАдреса(ТекПредставление, СокрЛП(СтруктураАдреса.Регион), ", ");
			ДополнитьПредставлениеАдреса(ТекПредставление, СокрЛП(СтруктураАдреса.Индекс), ", ");
			
		Иначе	
		
			ДополнитьПредставлениеАдреса(ТекПредставление, СокрЛП(СтруктураАдреса.Индекс), ", ");
			ДополнитьПредставлениеАдреса(ТекПредставление, СокрЛП(СтруктураАдреса.Регион), ", ");
			ДополнитьПредставлениеАдреса(ТекПредставление, СокрЛП(СтруктураАдреса.Район), ", ");
			ДополнитьПредставлениеАдреса(ТекПредставление, СокрЛП(СтруктураАдреса.Город), ", ");
			ДополнитьПредставлениеАдреса(ТекПредставление, СокрЛП(СтруктураАдреса.НаселенныйПункт), ", ");
			ДополнитьПредставлениеАдреса(ТекПредставление, СокрЛП(СтруктураАдреса.Улица), ", ");
			ДополнитьПредставлениеАдреса(ТекПредставление, СокрЛП(СтруктураАдреса.Дом), 	НСтр("ru=', дом № ';uk=', будинок № '", КодЯзыкаИБ));
			ДополнитьПредставлениеАдреса(ТекПредставление, СокрЛП(СтруктураАдреса.Корпус), 	НСтр("ru=', корпус ';uk=', корпус '", 	КодЯзыкаИБ));
			ДополнитьПредставлениеАдреса(ТекПредставление, СокрЛП(СтруктураАдреса.Квартира),НСтр("ru=', кв.';uk=', кв.'", 			КодЯзыкаИБ));
			
		КонецЕсли;	
		
		Если СтрДлина(ТекПредставление) > 2 Тогда
			ТекПредставление = Сред(ТекПредставление, 3);
		КонецЕсли;
	
	Иначе
		
		ТекПредставление = ?(СтруктураАдреса.Представление = "", СтруктураАдреса.Страна, СтруктураАдреса.Представление);
	
	КонецЕсли;
	
	Возврат ТекПредставление;
	
КонецФункции

// Функция возвращает представление адреса собранного из полей значений Поле1 - Поле10
//
// Параметры
//  НЕТ
//
// Возвращаемое значение:
//   Строка
//
Функция ПолучитьПредставлениеАдреса(Запись, КодЯзыкаИБ=Неопределено) Экспорт

	Если КодЯзыкаИБ = Неопределено Тогда
		КодЯзыкаИБ = Локализация.КодЯзыкаИнформационнойБазы();
	КонецЕсли;

	ТекПредставление = "";
	
	ДополнитьПредставлениеАдреса(ТекПредставление, СокрЛП(Запись.Поле1), ", ");
	ДополнитьПредставлениеАдреса(ТекПредставление, СокрЛП(Запись.Поле2), ", ");
	ДополнитьПредставлениеАдреса(ТекПредставление, СокрЛП(Запись.Поле3), ", ");
	ДополнитьПредставлениеАдреса(ТекПредставление, СокрЛП(Запись.Поле4), ", ");
	ДополнитьПредставлениеАдреса(ТекПредставление, СокрЛП(Запись.Поле5), ", ");
	ДополнитьПредставлениеАдреса(ТекПредставление, СокрЛП(Запись.Поле6), ", ");
	ДополнитьПредставлениеАдреса(ТекПредставление, СокрЛП(Запись.Поле7), НСтр("ru=', дом № ';uk=', будинок № '",КодЯзыкаИБ));
	ДополнитьПредставлениеАдреса(ТекПредставление, СокрЛП(Запись.Поле8), НСтр("ru=', корпус ';uk=', корпус '", 	КодЯзыкаИБ));
	ДополнитьПредставлениеАдреса(ТекПредставление, СокрЛП(Запись.Поле9), НСтр("ru=', кв.';uk=', кв.'", 			КодЯзыкаИБ));
	
	Если СтрДлина(ТекПредставление) > 2 Тогда
		ТекПредставление = Сред(ТекПредставление, 3);
	КонецЕсли; 
	
	Возврат ТекПредставление;

КонецФункции

//Функция по строке адреса (разделенная через запятую) возвращает его представление
Функция ПолучитьПредставлениеАдресаПоСтрока(СтрокаАдреса) Экспорт
	
	СтруктураАдреса = ПолучитьСтруктуруАдресаИзСтроки(СтрокаАдреса);
	ПредставлениеАдреса = ПолучитьПредставлениеАдресаПоСтруктуре(СтруктураАдреса);
	Возврат ПредставлениеАдреса
	
КонецФункции

// Процедура формирует представление для Адреса за пределами Украины
Функция СформироватьПредставлениеАдресаЗаПределамиУкраины(Страна, Представление) Экспорт

	ТекущееПредставлениеАдреса = Представление;	
	Если ЗначениеЗаполнено(Страна) И Страна <> Справочники.КлассификаторСтранМира.Украина.Наименование Тогда
			
		ПозицияСтраныВАдресе = Найти(Врег(Представление), Врег(Страна));
		// добавляем страну в самое начало представления, если ее там еще нет
		Если ПозицияСтраныВАдресе <> 1 Тогда
			ТекущееПредставлениеАдреса = Страна + ?(ЗначениеЗаполнено(Представление), (", " + Представление), "");
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ТекущееПредставлениеАдреса;
	
КонецФункции

// Функция возвращает представление адреса за пределами Украины без Страны
Функция ПолучитьПредставлениеАдресаЗаПределамиУкраиныБезСтраны(Страна, Представление) Экспорт
	
	ТекущееПредставлениеАдреса = Представление;
	Если ЗначениеЗаполнено(Страна) И Страна <> Справочники.КлассификаторСтранМира.Украина.Наименование Тогда
		
		ИмяСтраны = Страна + ", ";
		ПозицияСтраныВАдресе = Найти(Врег(Представление), Врег(ИмяСтраны));
		// Если только страна в начале, то удаляем ее, иначе не трогаем
		Если ПозицияСтраныВАдресе = 1 Тогда
			ТекущееПредставлениеАдреса = Сред(Представление, СтрДлина(ИмяСтраны) + 1);	
		КонецЕсли;
		
	КонецЕсли;

	Возврат ТекущееПредставлениеАдреса;
	
КонецФункции


// Процедура формирует строковое представление адреса.
Процедура СформироватьПредставлениеТелефона(НаборПолей) Экспорт

	НаборПолей.Представление = НаборПолей.Поле1;
	НаборПолей.Представление = НаборПолей.Представление + ?((Не ПустаяСтрока(НаборПолей.Поле2)),(ПроверкаПустойСтроки(НаборПолей.Представление, Ложь)+"(" + НаборПолей.Поле2 + ")"),"");
	НаборПолей.Представление = НаборПолей.Представление + ?((Не ПустаяСтрока(НаборПолей.Поле3)),(ПроверкаПустойСтроки(НаборПолей.Представление, ПустаяСтрока(НаборПолей.Поле2)) + ПривестиНомерТелефонаКШаблону(НаборПолей.Поле3)),"");
	Если НЕ ПустаяСтрока(НаборПолей.Представление) Тогда
		НаборПолей.Представление = НаборПолей.Представление + ?((Не ПустаяСтрока(НаборПолей.Поле4)),(ПроверкаПустойСтроки(НаборПолей.Представление) + "доб. " + ПривестиНомерТелефонаКШаблону(НаборПолей.Поле4)),"");
	Иначе
		НаборПолей.Представление = ПривестиНомерТелефонаКШаблону(НаборПолей.Поле4);
	КонецЕсли; 

КонецПроцедуры // СформироватьПредставление()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОТОБРАЖЕНИЯ КИ В ФОРМАХ ОБЪЕКТОВ

// Процедура считывает контактную информацию из ИБ и возвращает ТЗ
//  Ссылка - Объект, по которому необходимо прочитать КИ
Функция ПрочитатьНаборЗаписейКонтактнойИнформации(Ссылка)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ * ИЗ РегистрСведений.КонтактнаяИнформация ГДЕ Объект = &Объект";
	Запрос.УстановитьПараметр("Объект", Ссылка);
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Процедура считывает контактную информацию из ИБ в набор записей регистра сведений.
//
// Параметры:
//  НаборЗаписей - набор записей регистра сведений
//  Ссылка - Объект, по которому необходимо заполнить КИ
//  СчитыватьНаборЗаписей - признак считывания набора записей
Процедура ПрочитатьКонтактнуюИнформацию(НаборЗаписей, Ссылка) Экспорт
	
	Если ТипЗнч(НаборЗаписей) <> Тип("РегистрСведенийНаборЗаписей.КонтактнаяИнформация") Тогда
		НаборЗаписей = РегистрыСведений.КонтактнаяИнформация.СоздатьНаборЗаписей();
	Иначе
		НаборЗаписей.Очистить();
	КонецЕсли; 
	
	НаборЗаписей.Отбор.Объект.Значение      = Ссылка;
	НаборЗаписей.Отбор.Объект.Использование = Истина;
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВложенныйЗапрос.Вид КАК Вид,
		|	ВложенныйЗапрос.Тип КАК Тип,
		|	ВложенныйЗапрос.Объект КАК Объект,
		|	МАКСИМУМ(ВложенныйЗапрос.Поле1) КАК Поле1,
		|	МАКСИМУМ(ВложенныйЗапрос.Поле2) КАК Поле2,
		|	МАКСИМУМ(ВложенныйЗапрос.Поле3) КАК Поле3,
		|	МАКСИМУМ(ВложенныйЗапрос.Поле4) КАК Поле4,
		|	МАКСИМУМ(ВложенныйЗапрос.Поле5) КАК Поле5,
		|	МАКСИМУМ(ВложенныйЗапрос.Поле6) КАК Поле6,
		|	МАКСИМУМ(ВложенныйЗапрос.Поле7) КАК Поле7,
		|	МАКСИМУМ(ВложенныйЗапрос.Поле8) КАК Поле8,
		|	МАКСИМУМ(ВложенныйЗапрос.Поле9) КАК Поле9,
		|	МАКСИМУМ(ВложенныйЗапрос.Поле10) КАК Поле10,
		|	МАКСИМУМ(ВложенныйЗапрос.Представление) КАК Представление,
		|	МАКСИМУМ(ВложенныйЗапрос.Комментарий) КАК Комментарий,
		|	МАКСИМУМ(ВложенныйЗапрос.ЗначениеПоУмолчанию) КАК ЗначениеПоУмолчанию
		|ИЗ
		|	(ВЫБРАТЬ
		|		&Объект КАК Объект,
		|		ВидыКонтактнойИнформации.Тип КАК Тип,
		|		ВидыКонтактнойИнформации.Ссылка КАК Вид,
		|		"""" КАК Представление,
		|		"""" КАК Поле1,
		|		"""" КАК Поле2,
		|		"""" КАК Поле3,
		|		"""" КАК Поле4,
		|		"""" КАК Поле5,
		|		"""" КАК Поле6,
		|		"""" КАК Поле7,
		|		"""" КАК Поле8,
		|		"""" КАК Поле9,
		|		"""" КАК Поле10,
		|		"""" КАК Комментарий,
		|		ЛОЖЬ КАК ЗначениеПоУмолчанию
		|	ИЗ
		|		Справочник.ВидыКонтактнойИнформации КАК ВидыКонтактнойИнформации
		|	ГДЕ
		|		(НЕ ВидыКонтактнойИнформации.ПометкаУдаления)
		|		И ВидыКонтактнойИнформации.ВидОбъектаКонтактнойИнформации = &ВидОбъектаКонтактнойИнформации
		|	
		|	ОБЪЕДИНИТЬ
		|	
		|	ВЫБРАТЬ
		|		РегистрСведенийКонтактнаяИнформация.Объект,
		|		РегистрСведенийКонтактнаяИнформация.Тип,
		|		РегистрСведенийКонтактнаяИнформация.Вид,
		|		ВЫРАЗИТЬ(РегистрСведенийКонтактнаяИнформация.Представление КАК СТРОКА(1000)),
		|		РегистрСведенийКонтактнаяИнформация.Поле1,
		|		РегистрСведенийКонтактнаяИнформация.Поле2,
		|		РегистрСведенийКонтактнаяИнформация.Поле3,
		|		РегистрСведенийКонтактнаяИнформация.Поле4,
		|		РегистрСведенийКонтактнаяИнформация.Поле5,
		|		РегистрСведенийКонтактнаяИнформация.Поле6,
		|		РегистрСведенийКонтактнаяИнформация.Поле7,
		|		РегистрСведенийКонтактнаяИнформация.Поле8,
		|		РегистрСведенийКонтактнаяИнформация.Поле9,
		|		РегистрСведенийКонтактнаяИнформация.Поле10,
		|		ВЫРАЗИТЬ(РегистрСведенийКонтактнаяИнформация.Комментарий КАК СТРОКА(1000)),
		|		РегистрСведенийКонтактнаяИнформация.ЗначениеПоУмолчанию
		|	ИЗ
		|		РегистрСведений.КонтактнаяИнформация КАК РегистрСведенийКонтактнаяИнформация
		|	ГДЕ
		|		РегистрСведенийКонтактнаяИнформация.Объект = &Объект) КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.Вид,
		|	ВложенныйЗапрос.Тип,
		|	ВложенныйЗапрос.Объект
		|
		|УПОРЯДОЧИТЬ ПО
		|	Представление УБЫВ,
		|	ВложенныйЗапрос.Тип.Порядок,
		|	Вид";
	
	Запрос.УстановитьПараметр("ВидОбъектаКонтактнойИнформации", ВидОбъектаКИ(Ссылка));
	Запрос.УстановитьПараметр("Объект", Ссылка);
	
	ТабЗн = Запрос.Выполнить().Выгрузить();
	Для Каждого стр Из ТабЗн Цикл
		стр.Представление = СокрП(стр.Представление);
		стр.Комментарий   = СокрП(стр.Комментарий);
	КонецЦикла;
	
	НаборЗаписей.Загрузить(ТабЗн);
	
КонецПроцедуры

// Процедура записывает контактную информацию в ИБ из набора записей регистра сведений.
//
// Параметры:
//  НаборЗаписей - набор записей регистра сведений
//  Ссылка - Объект, по которому необходимо записать КИ
//  Отказ - Булево
//
Процедура ЗаписатьКонтактнуюИнформацию(НаборЗаписей, Ссылка, Отказ) Экспорт
	
	Если НЕ Отказ Тогда
		
		ПервичныйНабор = НаборЗаписей.Выгрузить();
		
		КолвоЭлементовКоллекции = НаборЗаписей.Количество();
		Для ОбратныйИндекс = 1 По КолвоЭлементовКоллекции Цикл 
			ЭлементКоллекции = НаборЗаписей[КолвоЭлементовКоллекции - ОбратныйИндекс]; 
			Если НЕ ЗначениеЗаполнено(ЭлементКоллекции.Представление) Тогда 
				НаборЗаписей.Удалить(ЭлементКоллекции); 
			КонецЕсли; 
			ЭлементКоллекции.Объект = Ссылка;
		КонецЦикла;
		
		НаборЗаписей.Отбор.Объект.Значение      = Ссылка;
		НаборЗаписей.Отбор.Объект.Использование = Истина;
		
		Если Не НастройкаПравДоступа.СравнитьТаблицыНаборовЗаписей(ПрочитатьНаборЗаписейКонтактнойИнформации(Ссылка), НаборЗаписей.Выгрузить()) Тогда
			
			Попытка
				НаборЗаписей.Записать();
			Исключение
				ОбщегоНазначения.СообщитьОбОшибке(ОписаниеОшибки(),, ("Элемент """ + СокрЛП(Ссылка) + """ не записан. Не записана контактная информация."));
				// возвратим обратно данные набора
				НаборЗаписей.Загрузить(ПервичныйНабор);
				Отказ = Истина;
				Возврат;
			КонецПопытки;
			
			ПрочитатьКонтактнуюИнформацию(НаборЗаписей, Ссылка)
			
		Иначе
			НаборЗаписей.Загрузить(ПервичныйНабор);
		КонецЕсли; 
		
	КонецЕсли; 
	
КонецПроцедуры

#Если Клиент Тогда

// Процедура выполняется при активизации строки табличного поля, в котором
// отображается контактная информация объектов в их формах, и управляет
// доступностью кнопки командной пенели установки значения по умолчанию.
//
// Параметры:
//  Элемент - ТабличноеПоле
//  КнопкаУстановитьОсновным - Кнопка командной панели
//
Процедура КонтактнаяИнформацияПриАктивизацииСтрокиТаблицы(Элемент, КнопкаУстановитьОсновным) Экспорт

	Если Элемент.ТекущиеДанные <> Неопределено Тогда
		Если Элемент.ТекущиеДанные.ЗначениеПоУмолчанию Тогда
			КнопкаУстановитьОсновным.Пометка     = Истина;
			КнопкаУстановитьОсновным.Доступность = Истина;
		ИначеЕсли НЕ ЗначениеЗаполнено(Элемент.ТекущиеДанные.Представление) Тогда
			КнопкаУстановитьОсновным.Пометка     = Ложь;
			КнопкаУстановитьОсновным.Доступность = Ложь;
		Иначе
			КнопкаУстановитьОсновным.Пометка     = Ложь;
			КнопкаУстановитьОсновным.Доступность = Истина;
		КонецЕсли; 
	Иначе
		КнопкаУстановитьОсновным.Пометка     = Ложь;
		КнопкаУстановитьОсновным.Доступность = Ложь;
	КонецЕсли; 
	
КонецПроцедуры

// Процедура выполняется при выводе строки табличного поля, в котором
// отображается контактная информация объектов в их формах.
//
// Параметры:
//  Элемент - ТабличноеПоле
//  ОформлениеСтроки - Оформление строки табличного поля
//  ДанныеСтроки - данные строки табличного поля
//
Процедура КонтактнаяИнформацияСписокПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки) Экспорт
	
	// Если строкам не задана катринка, использовать стандартную
	Если Элемент.Колонки.Картинка.КартинкиСтрок.Вид = ВидКартинки.Пустая Тогда
		
		Элемент.Колонки.Картинка.ОтображатьСтандартнуюКартинку = Истина;
		Возврат;
		
	КонецЕсли;
	
	Элемент.Колонки.Картинка.ОтображатьСтандартнуюКартинку = Ложь;
	ОформлениеСтроки.Ячейки.Картинка.ОтображатьКартинку = Истина;
	
	Если ДанныеСтроки.ЗначениеПоУмолчанию Тогда
		ОформлениеСтроки.Шрифт = Новый Шрифт(,, Истина);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДанныеСтроки.Представление) Тогда
		ОформлениеСтроки.ЦветТекста = WebЦвета.Серый;
	ИначеЕсли ТипЗнч(ДанныеСтроки.Вид) = Тип("Строка") Тогда
		ОформлениеСтроки.ЦветТекста = ЦветаСтиля.ТекстИнформационнойНадписи;
	КонецЕсли;
	
	Если ДанныеСтроки.Тип = Перечисления.ТипыКонтактнойИнформации.Адрес Тогда
		Если ДанныеСтроки.Вид = Справочники.ВидыКонтактнойИнформации.ИнострАдресФизЛица Тогда
			Если ЗначениеЗаполнено(ДанныеСтроки.Представление) Тогда
				ОформлениеСтроки.Ячейки.Картинка.ИндексКартинки = 1;
			Иначе
				ОформлениеСтроки.Ячейки.Картинка.ИндексКартинки = 7;
			КонецЕсли;
		Иначе
			Если ОпределитьДляОбъектаУкраинскийАдрес(ДанныеСтроки) Тогда
				Если ЗначениеЗаполнено(ДанныеСтроки.Представление) Тогда
					ОформлениеСтроки.Ячейки.Картинка.ИндексКартинки = 2;
				Иначе
					ОформлениеСтроки.Ячейки.Картинка.ИндексКартинки = 8;
				КонецЕсли;
			Иначе
				Если ЗначениеЗаполнено(ДанныеСтроки.Представление) Тогда
					ОформлениеСтроки.Ячейки.Картинка.ИндексКартинки = 1;
				Иначе
					ОформлениеСтроки.Ячейки.Картинка.ИндексКартинки = 7;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ДанныеСтроки.Тип = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты Тогда
		Если ЗначениеЗаполнено(ДанныеСтроки.Представление) Тогда
			ОформлениеСтроки.Ячейки.Картинка.ИндексКартинки = 3;
		Иначе
			ОформлениеСтроки.Ячейки.Картинка.ИндексКартинки = 9;
		КонецЕсли;
	ИначеЕсли ДанныеСтроки.Тип = Перечисления.ТипыКонтактнойИнформации.ВебСтраница Тогда
		Если ЗначениеЗаполнено(ДанныеСтроки.Представление) Тогда
			ОформлениеСтроки.Ячейки.Картинка.ИндексКартинки = 4;
		Иначе
			ОформлениеСтроки.Ячейки.Картинка.ИндексКартинки = 10;
		КонецЕсли;
	ИначеЕсли ДанныеСтроки.Тип = Перечисления.ТипыКонтактнойИнформации.Другое Тогда
		Если ЗначениеЗаполнено(ДанныеСтроки.Представление) Тогда
			ОформлениеСтроки.Ячейки.Картинка.ИндексКартинки = 5;
		Иначе
			ОформлениеСтроки.Ячейки.Картинка.ИндексКартинки = 11;
		КонецЕсли;
	ИначеЕсли ДанныеСтроки.Тип = Перечисления.ТипыКонтактнойИнформации.Телефон Тогда
		Если ЗначениеЗаполнено(ДанныеСтроки.Представление) Тогда
			ОформлениеСтроки.Ячейки.Картинка.ИндексКартинки = 0;
		Иначе
			ОформлениеСтроки.Ячейки.Картинка.ИндексКартинки = 6;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // КонтактнаяИнформацияСписокПриВыводеСтроки()

// Процедура устанавливает запись контактной информации определенного типа и вида основной
// для объекта в пространстве одного типа.
//
// Параметры:
//  СтруктураПараметров - структура, параметры записи, для которой надо установить признак основной
//   Ключи:
//    Объект, СправочникСсылка, значение измерния Объект регистра сведений
//    Тип, ПеречислениеСсылка.ТипыКонтактнойИнформации
//    Вид, СправочникСсылка.ВидыКонтактнойИнформации
//
Процедура УстановитьЗаписьОсновной(НаборЗаписей, ТабличноеПоле, Кнопка) Экспорт

	Если ТабличноеПоле.ТекущиеДанные <> Неопределено
	   И ЗначениеЗаполнено(ТабличноеПоле.ТекущиеДанные.Представление) Тогда
	
		Если ТабличноеПоле.ТекущиеДанные.ЗначениеПоУмолчанию Тогда
		
			ТабличноеПоле.ТекущиеДанные.ЗначениеПоУмолчанию = Ложь;
			Кнопка.Пометка = Ложь;
			
		Иначе
			
			Для каждого ЗаписьНабора Из НаборЗаписей Цикл
				Если ЗаписьНабора.Тип = ТабличноеПоле.ТекущиеДанные.Тип Тогда
					ЗаписьНабора.ЗначениеПоУмолчанию = Ложь;
				КонецЕсли; 
			КонецЦикла;
			
			ТабличноеПоле.ТекущиеДанные.ЗначениеПоУмолчанию = Истина;
			Кнопка.Пометка = Истина;
		
		КонецЕсли; 
	
	КонецЕсли;
	
КонецПроцедуры

// Обраьботчик события "ПередУдалением" табличного поля набора записей.
//
// Параметры:
//  Элемент - Табличное поле
//  Отказ - Булево
//
Процедура УдалитьЗаписьКонтактнойИнформации(Элемент, Отказ, Форма = Неопределено) Экспорт

	Отказ = Истина;
	
	Если Элемент.ТекущиеДанные <> Неопределено И (ЗначениеЗаполнено(Элемент.ТекущиеДанные.Представление) ИЛИ ТипЗнч(Элемент.ТекущиеДанные.Вид) = Тип("Строка")) Тогда
	
		ОтветНаВопрос = Вопрос("Удалить запись?", РежимДиалогаВопрос.ДаНет);
		Если ОтветНаВопрос <> КодВозвратаДиалога.Да Тогда
			Возврат;
		КонецЕсли;
		
		Если ТипЗнч(Элемент.ТекущиеДанные.Вид) = Тип("Строка") Тогда
			Элемент.Значение.Удалить(Элемент.ТекущиеДанные);
		Иначе
			Элемент.ТекущиеДанные.Представление       = "";
			Элемент.ТекущиеДанные.ЗначениеПоУмолчанию = Ложь;
		КонецЕсли; 
		
		Если Форма <> Неопределено Тогда
			Форма.Модифицированность = Истина;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

// Функция формирует структуру записи регистра.
//
// Параметры:
//  Запись - запись набора записей регистра сведений
//
Функция ПолучитьСтруктуруЗаписиРегистра(Запись) Экспорт

	Если ТипЗнч(Запись) = Тип("РегистрСведенийЗапись.КонтактнаяИнформация")
	 ИЛИ ТипЗнч(Запись) = Тип("РегистрСведенийМенеджерЗаписи.КонтактнаяИнформация") Тогда
		СтруктураЗаписи = Новый Структура;
		СтруктураЗаписи.Вставить("Объект"       , Запись.Объект);
		СтруктураЗаписи.Вставить("Тип"          , Запись.Тип);
		СтруктураЗаписи.Вставить("Вид"          , Запись.Вид);
		СтруктураЗаписи.Вставить("Представление", Запись.Представление);
		СтруктураЗаписи.Вставить("Комментарий"  , Запись.Комментарий);
		Для а = 1 По 10 Цикл
			СтруктураЗаписи.Вставить("Поле" + Строка(а), Запись["Поле" + Строка(а)]);
		КонецЦикла;
		Возврат СтруктураЗаписи;
	Иначе
		Возврат Неопределено;
	КонецЕсли; 

КонецФункции

// Процедура копирует из строки источника адрес в строку приемника
Процедура СкопироватьДанныеАдреса(СтрокаИсточник, СтрокаПриемник)
	
	СтрокаПриемник.Представление	= СтрокаИсточник.Представление;
	СтрокаПриемник.Комментарий 		= СтрокаИсточник.Комментарий;
	Для а = 1 По 10 Цикл
		СтрокаПриемник["Поле" + Строка(а)] = СтрокаИсточник["Поле" + Строка(а)];
	КонецЦикла;
    	
КонецПроцедуры

// Процедура копирует КИ из Ффактич адреса в юридический и наоборот
Процедура СкопироватьАдресКонтактнойИнформации(СтрокаСАдресом, ТаблицаКИ) Экспорт
	
	Если (СтрокаСАдресом = Неопределено) Или (ТаблицаКИ = Неопределено) Тогда
		Возврат;
	КонецЕсли;
	
	Если СтрокаСАдресом.Вид = Справочники.ВидыКонтактнойИнформации.ФактАдресКонтрагента	Тогда 
		ВидПоиска = Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента;
	ИначеЕсли СтрокаСАдресом.Вид = Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента Тогда
		ВидПоиска = Справочники.ВидыКонтактнойИнформации.ФактАдресКонтрагента;
	Иначе
		Возврат;
    КонецЕсли;

	ЗадаватьВопросОбИзменении = Истина;
	
	// бежим по всем записям регистра и те которые нужно поменять - меняем
	Для Каждого СтрокаДляИзменения Из ТаблицаКИ Цикл
		
		// если представление не заполнено то только тогда можно предложить его поменять
		Если СтрокаДляИзменения.Вид = ВидПоиска И НЕ ЗначениеЗаполнено(СтрокаДляИзменения.Представление) Тогда
			
			Если ЗадаватьВопросОбИзменении Тогда
				
				// вопрос задаем только в том случае, если представление не заполнено, а когда оно заполнено, то ничего не спрашиваем и не меняем
				НужноМенятьАдрес = Вопрос("Изменился " + СтрокаСАдресом.Вид.Наименование + ".
											|Автоматически изменить " + СтрокаДляИзменения.Вид.Наименование + "?", РежимДиалогаВопрос.ДаНет);
				// если менять не нужно - то не меняем							
				Если НужноМенятьАдрес <> КодВозвратаДиалога.Да Тогда 
					Возврат;
				КонецЕсли;
				ЗадаватьВопросОбИзменении = Ложь;
			КонецЕсли;
			
			// изменяем адрес
			СкопироватьДанныеАдреса(СтрокаСАдресом, СтрокаДляИзменения);
			
		КонецЕсли;
		
	КонецЦикла;
		
КонецПроцедуры
              
// Процедура контролирует возможность редактирования текста в поле вводп представления КИ.
//
Процедура УстановитьВозможностьРедактированияТекстаКИ(Элемент) Экспорт

	Если Элемент.ТекущиеДанные.Тип = Перечисления.ТипыКонтактнойИнформации.Адрес
	   И ЗначениеЗаполнено(Элемент.ТекущиеДанные.Представление)
	   И (ОпределитьДляОбъектаУкраинскийАдрес(Элемент.ТекущиеДанные))
	   И Элемент.ТекущиеДанные.Вид <> Справочники.ВидыКонтактнойИнформации.ИнострАдресФизЛица Тогда
		Элемент.Колонки.Представление.ЭлементУправления.РедактированиеТекста = Ложь;
	Иначе
		Элемент.Колонки.Представление.ЭлементУправления.РедактированиеТекста = Истина;
	КонецЕсли; 

КонецПроцедуры

// Обработчик события "ПриНачалеРедактирования" табличного поля
// набора записей регистра сведений
//
Процедура КонтактнаяИнформацияПриНачалеРедактированияОбщая(Элемент, НоваяСтрока, ПоследнееЗначениеЭлементаПоискаПоСтрокеВидаКИ) Экспорт

	Если НоваяСтрока Тогда
		Если НЕ ЗначениеЗаполнено(Элемент.ТекущиеДанные.Тип) Тогда
			Элемент.ТекущиеДанные.Тип = Перечисления.ТипыКонтактнойИнформации.Адрес;
		КонецЕсли; 
		Если НЕ ЗначениеЗаполнено(Элемент.ТекущиеДанные.Вид) Тогда
			Элемент.ТекущиеДанные.Вид = Справочники.ВидыКонтактнойИнформации.ПустаяСсылка();
		КонецЕсли; 
	КонецЕсли; 
	
	ПоследнееЗначениеЭлементаПоискаПоСтрокеВидаКИ = Элемент.ТекущиеДанные.Вид;
	
КонецПроцедуры

// Обработчик события "ПередОкончаниемРедактирования" табличного поля
// набора записей регистра сведений
//
Процедура КонтактнаяИнформацияПередОкончаниемРедактированияОбщая(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ, мТекстПоискаПоСтрокеВидаКИ, мОбработкаПоискаПоСтрокеВидаКИ) Экспорт
	
	Если мОбработкаПоискаПоСтрокеВидаКИ И НЕ НоваяСтрока Тогда
		мОбработкаПоискаПоСтрокеВидаКИ = Ложь;
		Отказ = Истина;
		Элемент.ТекущаяКолонка = Элемент.Колонки.Вид;
		Элемент.Колонки.Вид.ЭлементУправления.ВыделенныйТекст = мТекстПоискаПоСтрокеВидаКИ;
		мТекстПоискаПоСтрокеВидаКИ = "";
	КонецЕсли; 
	
КонецПроцедуры

// Обработчик события "ПриИзменении" представления КИ табличного поля
// набора записей регистра сведений
//
Процедура КонтактнаяИнформацияПредставлениеПриИзмененииОбщая(Элемент, ТабличноеПоле) Экспорт

	Если ТабличноеПоле.ТекущиеДанные.Тип = Перечисления.ТипыКонтактнойИнформации.Телефон Тогда
		
		СтруктураПолей = РазложитьТелефонПоПолям(Элемент.Значение);
		ТабличноеПоле.ТекущиеДанные.Поле3 = СтруктураПолей.СамТелефон;
		ТабличноеПоле.ТекущиеДанные.Поле1 = СтруктураПолей.КодСтраны;
		ТабличноеПоле.ТекущиеДанные.Поле2 = СтруктураПолей.КодГорода;
		
		СформироватьПредставлениеТелефона(ТабличноеПоле.ТекущиеДанные);
		
	КонецЕсли; 
	
КонецПроцедуры

// Обработчик события "ПередНачаломДобавления" табличного поля
// набора записей регистра сведений
//
Процедура КонтактнаяИнформацияПередНачаломДобавленияОбщее(Элемент, Отказ, Копирование, мКнопкаРедактироватьКИВДиалоге, ТабличноеПоле, 
														  НаборКонтактнойИнформации, ДоступностьОбъекта = Истина, Форма = Неопределено) Экспорт
	
	Если Не Копирование Тогда
		Если мКнопкаРедактироватьКИВДиалоге.Пометка  Тогда
			Отказ = Истина;
			ОбработкаРедактирования = Обработки.РедактированиеКонтактнойИнформации.Создать();
			ОбработкаРедактирования.ДоступностьОбъекта = ДоступностьОбъекта;
			ОбработкаРедактирования.РедактироватьЗапись(НаборКонтактнойИнформации, , Форма);
		Иначе
			ТабличноеПоле.Колонки.Представление.ЭлементУправления.РедактированиеТекста = Истина;
		КонецЕсли;
	Иначе
		Если мКнопкаРедактироватьКИВДиалоге.Пометка Тогда
			Отказ = Истина;
			ОбработкаРедактирования = Обработки.РедактированиеКонтактнойИнформации.Создать();
			ОбработкаРедактирования.ДоступностьОбъекта = ДоступностьОбъекта;
			ОбработкаРедактирования.РедактироватьЗапись(НаборКонтактнойИнформации,, Форма, ПолучитьСтруктуруЗаписиРегистра(ТабличноеПоле.ТекущиеДанные));
		Иначе
			УстановитьВозможностьРедактированияТекстаКИ(ТабличноеПоле);
		КонецЕсли;
	КонецЕсли; 

КонецПроцедуры

// Обработчик события "ПередНачаломИзменения" табличного поля
// набора записей регистра сведений
//
Процедура КонтактнаяИнформацияПередНачаломИзмененияОбщее(Элемент, Отказ, мКнопкаРедактироватьКИВДиалоге, ДоступностьОбъекта = Истина, Форма = Неопределено) Экспорт
	
	Если мКнопкаРедактироватьКИВДиалоге.Пометка Тогда
		Отказ = Истина;
		ОбработкаРедактирования = Обработки.РедактированиеКонтактнойИнформации.Создать();
		ОбработкаРедактирования.ДоступностьОбъекта = ДоступностьОбъекта;
		ОбработкаРедактирования.РедактироватьЗапись(Элемент.ТекущиеДанные, , Форма);
	Иначе
		УстановитьВозможностьРедактированияТекстаКИ(Элемент);
	КонецЕсли; 

КонецПроцедуры

// Обработчик событие "Нажатия" кнопки выбора режима редактирования
// контаткной информации
Процедура КонтактнаяИнформацияРедактироватьВДиалоге(Кнопка)Экспорт
	
	РазрешеноВСписке = УправлениеДопПравамиПользователей.РазрешитьРедактированиеКИвСписке();
	
	Если РазрешеноВСписке Тогда
		Кнопка.Пометка = НЕ Кнопка.Пометка;
	Иначе
		Предупреждение("Вам разрешено редактировать контактную информацию только в диалоге!",5);
	КонецЕсли;
	
КонецПроцедуры

// Процедура устанавливает пометку для кнопки выбора режима редактирования
// контактной информации в форме
Процедура ИнициализацияКнопкиРедактированиеКИВДиалоге(Кнопка) Экспорт
	
	РазрешеноВСписке = УправлениеДопПравамиПользователей.РазрешитьРедактированиеКИвСписке();
	
	Если РазрешеноВСписке Тогда
		НажатиеКнопки = ВосстановитьЗначение("РедактироватьКИВДиалоге");
		Если ТипЗнч(НажатиеКнопки) = Тип("Булево") Тогда
			Кнопка.Пометка = НажатиеКнопки;
		КонецЕсли; 
	Иначе
		Кнопка.Пометка = Истина;
	КонецЕсли;
	
КонецПроцедуры

// Обработчик события "ПриИзменении" Типа КИ табличного поля
// набора записей регистра сведений
//
Процедура КонтактнаяИнформацияТипПриИзмененииОбщее(Элемент, ТабличноеПоле) Экспорт

	Если ТабличноеПоле.ТекущиеДанные <> Неопределено
	   И ЗначениеЗаполнено(ТабличноеПоле.ТекущиеДанные.Вид)
	   И ТипЗнч(ТабличноеПоле.ТекущиеДанные.Вид) = Тип("СправочникСсылка.ВидыКонтактнойИнформации")
	   И ТабличноеПоле.ТекущиеДанные.Вид.Тип <> Элемент.Значение Тогда
	
		ТабличноеПоле.ТекущиеДанные.Вид = Справочники.ВидыКонтактнойИнформации.ПустаяСсылка();
	
	КонецЕсли; 

КонецПроцедуры

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ РАБОТЫ С ФОРМАМИ МЕХАНИЗМА ХРАНЕНИЯ КОНТАКНОЙ ИНФОРМАЦИИ

// Процедура переносит строку из запроса в Структуру
Процедура ПеренестиСтрокуВыборкиВСтруктуру(РезультатЗапроса, СтрокаВыборки, СтруктураЭлемента)Экспорт 
	
	Если СтруктураЭлемента = Неопределено 
		ИЛИ РезультатЗапроса = Неопределено 
		ИЛИ СтрокаВыборки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураЭлемента.Очистить();
	Для Каждого Колонка Из РезультатЗапроса.Колонки Цикл
		СтруктураЭлемента.Вставить(Колонка.Имя, СтрокаВыборки[Колонка.Имя]);
	КонецЦикла;
	
КонецПроцедуры

// Функция создает структуру по строке выборки
Функция СоздатьСтруктуруПоСтрокеВыборки(РезультатЗапроса, СтрокаВыборки)
	
	РезультирующаяСтруктура = Новый Структура;
	ПеренестиСтрокуВыборкиВСтруктуру(РезультатЗапроса, СтрокаВыборки, РезультирующаяСтруктура);
	Возврат РезультирующаяСтруктура; 
	
КонецФункции

// Процедура переносит строку из таблицы в Структуру
Процедура ПеренестиСтрокуТаблицыВСтруктуру(Таблица, СтрокаТаблицы, СтруктураЭлемента)Экспорт 
	
	Если СтруктураЭлемента = Неопределено 
		ИЛИ СтрокаТаблицы = Неопределено 
		ИЛИ Таблица = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураЭлемента.Очистить();
	Для Каждого Колонка Из Таблица.Колонки Цикл
		СтруктураЭлемента.Вставить(Колонка.Имя, СтрокаТаблицы[Колонка.Имя]);
	КонецЦикла;
	
КонецПроцедуры


// Функция возвращает пустое значение определенного типа,
//  в соответствии с видом объекта контактной информации
//
// Параметры
//  ВидОбъектаКонтактнойИнформации - ПеречислениеСсылка.ВидыОбъектовКонтактнойИнформации
//
// Возвращаемое значение:
//   СправочникСсылка (пустая) или Неопределено
//
Функция ПолучитьПустоеЗначениеОбъектаКонтактнойИнформации(ВидОбъектаКонтактнойИнформации) Экспорт

	ИмяВидаОбъектаКонтактнойИнформации = Метаданные.Перечисления.ВидыОбъектовКонтактнойИнформации.ЗначенияПеречисления[Перечисления.ВидыОбъектовКонтактнойИнформации.Индекс(ВидОбъектаКонтактнойИнформации)].Имя;
	
	Если ВидОбъектаКонтактнойИнформации = Неопределено Тогда 
		Возврат Неопределено;
	Иначе                       
		Возврат Справочники[ИмяВидаОбъектаКонтактнойИнформации].ПустаяСсылка();
	КонецЕсли; 

КонецФункции // ()

// Функция определяет вид объекта контактной информации
//
// Параметры
//  ОбъектСсылка - Ссылка на объект контактной информации
// Возвращаемые значения
//  Возвращается ссылка на перечисление ВидыОбъектовКонтактнойИнформации,
//  соответствующее параметру ОбъектСсылка
Функция ВидОбъектаКИ(ОбъектСсылка) Экспорт

	Если ОбъектСсылка = Неопределено Тогда
		Возврат Перечисления.ВидыОбъектовКонтактнойИнформации.ПустаяСсылка();
	Иначе
		Попытка
			Возврат Перечисления.ВидыОбъектовКонтактнойИнформации[ОбъектСсылка.Метаданные().Имя];
		Исключение
			Возврат Перечисления.ВидыОбъектовКонтактнойИнформации.ПустаяСсылка();
		КонецПопытки;
	КонецЕсли; 
	
КонецФункции // ВидОбъектаКИ()

// Функция вызывается при начале выбора объекта контактной информации
//
// Параметры
//  ФормаЗаписи - форма записи регистра сведений КонтактнаяИнформация
//  Элемент - элемент формы записи регистра сведений КонтактнаяИнформация,
//  значение которого выбирается.
//
// Возвращаемое значение:
//   Булево - продолжить стандартную операцию выбора или нет.
//
Функция НачалоВыбораОбъектаКИ(ФормаЗаписи, Элемент, ТекущийПользователь) Экспорт

	Если Элемент.Значение <> Неопределено Тогда
		Возврат Истина;
	КонецЕсли; 
	
	СписокТиповОбъектов = Новый СписокЗначений;

	МассивТипов = ?(Элемент.ОграничениеТипа.Типы().Количество()>0, Элемент.ОграничениеТипа.Типы(), Элемент.ТипЗначения.Типы());
	Для каждого Тип Из МассивТипов Цикл
		НовыйТип = Новый(Тип);
		СписокТиповОбъектов.Добавить(НовыйТип.Ссылка.Метаданные().Имя, НовыйТип.Ссылка.Метаданные().Синоним);
	КонецЦикла;

	Если СписокТиповОбъектов.Количество() = 1 Тогда
		ВыбранноеЗначениеСписка = СписокТиповОбъектов[0];
	Иначе
		ВыбранноеЗначениеСписка = ФормаЗаписи.ВыбратьИзСписка(СписокТиповОбъектов, Элемент);
	КонецЕсли;
	
	Если ВыбранноеЗначениеСписка = Неопределено Тогда
		Возврат Ложь;
	Иначе
		Элемент.Значение = Справочники[ВыбранноеЗначениеСписка.Значение].ПустаяСсылка();
		Возврат Истина;
	КонецЕсли;

КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ РАБОТЫ С ФОРМАМИ МЕХАНИЗМА РАБОТЫ С АДРЕСНЫМ КЛАССИФИКАТОРОМ

// Функция по полному наименованию адресного элемента выдает отдельно его имя, и отдельно его адресное сокращение
Функция ПолучитьИмяИАдресноеСокращение(Знач СтрокаЭлемента, АдресноеСокращение) Экспорт
	
	Буфер = СокрП(СтрокаЭлемента);
	ПозицияПослПробела = Найти(Буфер, " ");

	Если ПозицияПослПробела = 0 Тогда
		Возврат СтрокаЭлемента;
	КонецЕсли;

	Пока  СтрЧислоВхождений(Буфер, " ") > 1 Цикл
		Буфер = Лев(Буфер, ПозицияПослПробела - 1) + "_" + Сред(Буфер, ПозицияПослПробела + 1);
		ПозицияПослПробела = Найти(Буфер, " ");
	КонецЦикла;

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Сокращение", Сред(СтрокаЭлемента, ПозицияПослПробела + 1));
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	АдресныеСокращения.Ссылка
	|ИЗ
	|	Справочник.АдресныеСокращения КАК АдресныеСокращения
	|
	|ГДЕ
	|	АдресныеСокращения.Сокращение = &Сокращение";

	Выборка = Запрос.Выполнить().Выбрать();
	АдресноеСокращение = "";

	Если Выборка.Следующий() Тогда
		Если ПустаяСтрока(СокрЛП(Лев(СтрокаЭлемента, ПозицияПослПробела - 1))) Тогда
			Возврат СтрокаЭлемента;
		Иначе
			АдресноеСокращение = Сред(СтрокаЭлемента, ПозицияПослПробела + 1);
			Возврат Лев(СтрокаЭлемента, ПозицияПослПробела - 1);
		КонецЕсли;
	Иначе
		Возврат СтрокаЭлемента;
	КонецЕсли;

	
КонецФункции

// Функция определяет является ли последнее слово в строке допустимым 
//  сокращением, и если является, то обрезает его из строки
//
// Параметры
//  СтрокаЭлемента - Строка в которой необходимо обрезать сокращение
// Возвращаемые значения
//  Обрезаную строку в случае, если есть сокращение и полученную строку в 
// обратном случае
Функция ОбрезатьСокращение(Знач СтрокаЭлемента) Экспорт

	АдресноеСокращение = "";
	ИмяЭлемента = ПолучитьИмяИАдресноеСокращение(СтрокаЭлемента, АдресноеСокращение);
	Возврат ИмяЭлемента;
	
КонецФункции

// Функция по для переданной строки определят необходимость дополнить её
// сокращением
//
// Параметры
//  Название - Строка .для которой определяется необходимость дополнения 
// сокращением
//  Уровень - уровень, для которго необходимо составить список сокращений
// Возвращаемые значения
//  Возвращается Неопределено, если строку дополнять не нужно или список 
// значений с сокращениями в обратном случае
Функция ПолучитьСписокСокращений(Знач Название, Уровень) Экспорт

	Название = СокрЛП(Название);

	Если ПустаяСтрока(Название) Тогда
		Возврат Неопределено;
	КонецЕсли;

	Если ОбрезатьСокращение(Название) <> Название Тогда // было что обрезать
		Возврат Неопределено; // для строки не требуется допонение сокращением
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Уровень", Уровень);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	АдресныеСокращения.Ссылка
	|ИЗ
	|	Справочник.АдресныеСокращения КАК АдресныеСокращения
	|
	|ГДЕ
	|	АдресныеСокращения.Уровень = &Уровень";

	Выборка = Запрос.Выполнить().Выбрать();

	СписокСокращений = Новый СписокЗначений();

	Пока Выборка.Следующий() Цикл
		СписокСокращений.Добавить(Выборка.Ссылка.Сокращение);
	КонецЦикла;

	Если СписокСокращений.Количество() = 0 Тогда
		Возврат Неопределено;
	Иначе
		Возврат СписокСокращений;
	КонецЕсли;

КонецФункции

// Функция формирует строку с названием адресного элемента,
// которое состоит из наименования и сокращения
//
// Параметры:
//  АдресныйЭлемент - элемент справочника Адресный классфикатор.
//
// Возвращаемое значение:
//  Название адресного элемента
//
Функция ПолучитьНазвание(АдресныйЭлемент) Экспорт

	Если АдресныйЭлемент.Код = 0 Тогда
		Возврат "";
	Иначе
		Возврат СокрЛП(АдресныйЭлемент.Наименование) + " " + СокрЛП(АдресныйЭлемент.Сокращение)
	КонецЕсли;

КонецФункции

// Процедура раскладывает код адресного элемента на составляющие:
// Код Региона, код района, код города, код населенного пункта, код улицы и код дома
Процедура РазложитькодАдресногоЭлементаНаСоставляющие(Знач Код, КодРегиона, КодРайона, КодГорода, КодНаселенногоПункта, КодУлицы, КодДома)
	
	КодЭлемента = Код;
	
	КодРегиона = Цел(КодЭлемента / МаскаРегиона());
	КодЭлемента = КодЭлемента % МаскаРегиона();

	КодРайона = Цел(КодЭлемента / МаскаРайона());
	КодЭлемента = КодЭлемента % МаскаРайона();

	КодГорода = Цел(КодЭлемента / МаскаГорода());
	КодЭлемента = КодЭлемента % МаскаГорода();

	КодНаселенногоПункта = Цел(КодЭлемента / МаскаНасПункта());
	КодЭлемента = КодЭлемента % МаскаНасПункта();

	КодУлицы = Цел(КодЭлемента / МаскаУлицы());
	КодЭлемента = КодЭлемента % МаскаУлицы();

	КодДома = Цел(КодЭлемента / МаскаДома());

КонецПроцедуры

// Функция формирует строку с полным названием адресного элемента,
// которое состоит из наименования и сокращения самого адресного
// элемента и его "родителей"
//
// Параметры:
//  КодЭлемента - Код адресного элемента.
//
// Возвращаемое значение:
//  Название адресного элемента и его родителей через запятую
//
Функция ПолучитьПолноеНазвание(Знач КодЭлемента, НачальныйУровеньДетализации = 1, КонечныйУровеньДетализации = 6) Экспорт
	
	Перем КодРегиона, КодРайона, КодГорода, КодНаселенногоПункта, КодУлицы, КодДома;

	РазложитькодАдресногоЭлементаНаСоставляющие(КодЭлемента, КодРегиона, КодРайона, КодГорода, КодНаселенногоПункта, КодУлицы, КодДома);
	
	Название = "";
	НовыйКод = 0;

	Если (КодРегиона > 0) Тогда
		НовыйКод = КодРегиона * МаскаРегиона();
		Если (1 >= НачальныйУровеньДетализации) и (КонечныйУровеньДетализации >= 1) Тогда
			Название = Название + ", " + ПолучитьНазвание(ПолучитьСтруктуруАдресногоЭлемента(НовыйКод));
		КонецЕсли;
	КонецЕсли;

	Если (КодРайона > 0) Тогда
		НовыйКод = НовыйКод + КодРайона * МаскаРайона();
		Если (2 >= НачальныйУровеньДетализации) и (КонечныйУровеньДетализации >= 2) Тогда
			Название = Название + ", " + ПолучитьНазвание(ПолучитьСтруктуруАдресногоЭлемента(НовыйКод));
		КонецЕсли;
	КонецЕсли;

	Если (КодГорода > 0) Тогда
		НовыйКод = НовыйКод + КодГорода * МаскаГорода();
		Если (3 >= НачальныйУровеньДетализации) и (КонечныйУровеньДетализации >= 3) Тогда
			Название = Название + ", " + ПолучитьНазвание(ПолучитьСтруктуруАдресногоЭлемента(НовыйКод));
		КонецЕсли;
	КонецЕсли;

	Если (КодНаселенногоПункта > 0) Тогда
		НовыйКод = НовыйКод + КодНаселенногоПункта * МаскаНасПункта();
		Если (4 >= НачальныйУровеньДетализации) и (КонечныйУровеньДетализации >= 4) Тогда
			Название = Название + ", " + ПолучитьНазвание(ПолучитьСтруктуруАдресногоЭлемента(НовыйКод));
		КонецЕсли;
	КонецЕсли;

	Если (КодУлицы > 0) Тогда
		НовыйКод = НовыйКод + КодУлицы * МаскаУлицы();
		Если (5 >= НачальныйУровеньДетализации) и (КонечныйУровеньДетализации >= 5) Тогда
			Название = Название + ", " + ПолучитьНазвание(ПолучитьСтруктуруАдресногоЭлемента(НовыйКод));
		КонецЕсли;
	КонецЕсли;

	Если (КодДома > 0) Тогда
		НовыйКод = НовыйКод + КодДома * МаскаДома();
		Если (6 >= НачальныйУровеньДетализации) и (КонечныйУровеньДетализации >= 6) Тогда
			Название = Название + ", " + ПолучитьНазвание(ПолучитьСтруктуруАдресногоЭлемента(НовыйКод));
		КонецЕсли;
	КонецЕсли;

	Если СтрДлина(Название) > 2 Тогда
		Название = Сред(Название, 3);
	КонецЕсли;

	Возврат Название;

КонецФункции

// Функция ставит в соответствие типу адресного элемента маску,
// через которую выделяется его значимый код
//
// Параметры:
//  ТипАдресногоЭлемента -  число - тип адресного элемента.
//
// Возвращаемое значение:
// Число - маска, при делении на которую выделяется значимый код
// адресного элемента
//
Функция ПолучитьМаскуПоТипу(ТипАдресногоЭлемента)

	Если ТипАдресногоЭлемента = 1 Тогда
		Возврат МаскаРегиона();

	ИначеЕсли ТипАдресногоЭлемента = 2 Тогда
		Возврат МаскаРайона();

	ИначеЕсли ТипАдресногоЭлемента = 3 Тогда
		Возврат МаскаГорода();

	ИначеЕсли ТипАдресногоЭлемента = 4 Тогда
		Возврат МаскаНасПункта();

	ИначеЕсли ТипАдресногоЭлемента = 5 Тогда
		Возврат МаскаУлицы();

	ИначеЕсли ТипАдресногоЭлемента = 6 Тогда
		Возврат МаскаДома();

	Иначе
		Возврат МаскаКвартиры();

	КонецЕсли;

КонецФункции

// Функция ставит в соответствие типу адресного элемента маску,
// через которую выделяется его значимый код
//
// Параметры:
//  ТипАдресногоЭлемента -  число - тип адресного элемента.
//
// Возвращаемое значение:
// Число - маска, при делении на которую выделяется значимый код
// адресного элемента
//
Функция ПолучитьТипАдресногоЭлемента(КодЭлемента) Экспорт
	
	Если КодЭлемента = 0 Тогда
		Возврат 0;
	КонецЕсли;
	
	Если КодЭлемента % МаскаРегиона() < 100  Тогда // регион 21 ноль и АА
		Возврат 1;
		
	ИначеЕсли КодЭлемента % МаскаРайона() < 100 Тогда // район 18 ноль и АА
		Возврат 2;
		
	ИначеЕсли КодЭлемента % МаскаГорода() < 100 Тогда // город 15 ноль и АА
		Возврат 3;
		
	ИначеЕсли КодЭлемента % МаскаНасПункта() < 100 Тогда // населнныйп ункт 12 ноль и АА
		Возврат 4;
		
	ИначеЕсли КодЭлемента % МаскаУлицы() < 100 Тогда // улица 8 ноль и АА
		Возврат 5;
		
	ИначеЕсли КодЭлемента % МаскаДома() < 100 Тогда // дом 4 ноль и АА
		Возврат 6;
		
	Иначе // квартира
		Возврат 7;
		
	КонецЕсли;
	
КонецФункции

Функция ПолучитьСтруктуруАдресногоЭлемента(КодЭлемента) Экспорт

	ТексЗапроса = "ВЫБРАТЬ ПЕРВЫЕ 1
	              |	АдресныйКлассификатор.Код,
	              |	АдресныйКлассификатор.КодРегионаВКоде,
	              |	АдресныйКлассификатор.Наименование,
	              |	АдресныйКлассификатор.Сокращение,
	              |	АдресныйКлассификатор.Индекс,
	              |	АдресныйКлассификатор.ТипАдресногоЭлемента,
	              |	АдресныйКлассификатор.КодРайонаВКоде,
	              |	АдресныйКлассификатор.КодГородаВКоде,
	              |	АдресныйКлассификатор.КодНаселенногоПунктаВКоде,
	              |	АдресныйКлассификатор.КодУлицыВКоде
	              |ИЗ
	              |	РегистрСведений.АдресныйКлассификатор КАК АдресныйКлассификатор
	              |
	              |ГДЕ
	              |	АдресныйКлассификатор.Код = &КодЭлемента";
	
	Запрос = Новый Запрос(ТексЗапроса);
	Запрос.УстановитьПараметр("КодЭлемента", КодЭлемента);
	
	РезультатЗапроса = Запрос.Выполнить(); 
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Структура = СоздатьСтруктуруПоСтрокеВыборки(РезультатЗапроса, Выборка);
	Иначе 
		Структура =  ПолучитьПустуюСтруктуруАдреса();
	КонецЕсли;
	
	Возврат Структура;
КонецФункции

// Функция возвращает структуру с набором полей как у записи регистра сведений
//  АдресныйКлассификатор с пустым набором значений
//
// Параметры:
// Нет.
//
// Возвращаемое значение:
// Струтктура - структуру с набором полей как у записи регистра сведений
//  АдресныйКлассификатор с пустым набором значений
//
Функция ПолучитьПустуюСтруктуруАдреса()
	
	Возврат Новый Структура("Код,Наименование,Сокращение,ТипАдресногоЭлемента,Индекс,КодРегионаВКоде,КодРайонаВКоде,КодГородаВКоде,КодНаселенногоПунктаВКоде,КодУлицыВКоде", 0, "", "", 0, "", 0, 0, 0, 0, 0);
	
КонецФункции

// Функция ищет требуемый адресный элемент по названию и типу 
// и возвращает первый найденный. В качестве дополнения может 
// быть задан элемент "родитель"
//
// Параметры:
//  НазваниеЭлемента - название адресного элемента ( с сокращением)
//  ТипЭлемента - тип искомого адресного элемента
//  ЭлементРодитель - элемент "родитель"
//
// Возвращаемое значение:
//  Найденный элемент справоника Адресный классификатор или пустая ссылка в случае неудачи
//
Функция ПолучитьАдресныйЭлемент(НазваниеЭлемента, ТипЭлемента, КодЭлементаРодителя = 0)

	Перем КодРегиона, КодРайона, КодГорода, КодНаселенногоПункта, КодУлицы, КодДома;
	
	Если (СокрЛП(НазваниеЭлемента) = "") ИЛИ (ТипЭлемента = 0) Тогда
		Возврат ПолучитьПустуюСтруктуруАдреса();
	КонецЕсли;
	
	// смотрим есть ли в имени адресное сокращение этого уровня
	// если есть, то ищем по наименованию и адресному сокращению
	АдресноеСокращение = "";
	НазваниеЭлемента = ПолучитьИмяИАдресноеСокращение(НазваниеЭлемента, АдресноеСокращение);

	Запрос = Новый Запрос();
	
	ОграничениеПоКоду = "";
	Если КодЭлементаРодителя > 0 Тогда // проверка на соответствие подчинению родителю
		ТипАдрЭлемента = ПолучитьТипАдресногоЭлемента(КодЭлементаРодителя);
		МаскаРодителя = ПолучитьМаскуПоТипу(ТипАдрЭлемента);
		
		Если ТипАдрЭлемента <= 5 Тогда
			
			КодЭлемента = КодЭлементаРодителя;
			
			РазложитькодАдресногоЭлементаНаСоставляющие(КодЭлемента, КодРегиона, КодРайона, КодГорода, КодНаселенногоПункта, КодУлицы, КодДома);

			Если КодРегиона <> 0 Тогда
				ОграничениеПоКоду = ОграничениеПоКоду + Символы.ПС + "  И (АдресныйКлассификатор.КодРегионаВКоде = &КодРегионаВКоде)";
				Запрос.УстановитьПараметр("КодРегионаВКоде", КодРегиона);
			КонецЕсли;
			
			Если КодРайона <> 0 Тогда
				ОграничениеПоКоду = ОграничениеПоКоду + Символы.ПС + "  И (АдресныйКлассификатор.КодРайонаВКоде = &КодРайонаВКоде)";
				Запрос.УстановитьПараметр("КодРайонаВКоде", КодРайона);
			КонецЕсли;
			
			Если КодГорода <> 0 Тогда
				ОграничениеПоКоду = ОграничениеПоКоду + Символы.ПС + "  И (АдресныйКлассификатор.КодГородаВКоде = &КодГородаВКоде)";
				Запрос.УстановитьПараметр("КодГородаВКоде", КодГорода);
			КонецЕсли;
			
			Если КодНаселенногоПункта <> 0 Тогда
				ОграничениеПоКоду = ОграничениеПоКоду + Символы.ПС + "  И (АдресныйКлассификатор.КодНаселенногоПунктаВКоде = &КодНаселенногоПунктаВКоде)";
				Запрос.УстановитьПараметр("КодНаселенногоПунктаВКоде", КодНаселенногоПункта);
			КонецЕсли;
			
			Если КодУлицы <> 0 Тогда
				ОграничениеПоКоду = ОграничениеПоКоду + Символы.ПС + "  И (АдресныйКлассификатор.КодУлицыВКоде = &КодУлицыВКоде)";
				Запрос.УстановитьПараметр("КодУлицыВКоде", КодУлицы);
			КонецЕсли;
		
		Иначе
			// ограничиваем по коду элемента
			КонечноеЗначениеКода = КодЭлементаРодителя + МаскаРодителя - 1; 
			
			ОграничениеПоКоду = Символы.ПС + "  И (АдресныйКлассификатор.Код Между &НачальноеЗанчениеКода И &КонечноеЗначениеКода)";
			Запрос.УстановитьПараметр("НачальноеЗанчениеКода", КодЭлементаРодителя);
			Запрос.УстановитьПараметр("КонечноеЗначениеКода", КонечноеЗначениеКода);
		КонецЕсли;
		
	КонецЕсли;
	
	// ограничение на адресное сокращение
	Если АдресноеСокращение <> "" Тогда
		ОграничениеПоКоду = ОграничениеПоКоду + Символы.ПС + "  И (АдресныйКлассификатор.Сокращение = &АдресноеСокращение)";
		Запрос.УстановитьПараметр("АдресноеСокращение", АдресноеСокращение);
	КонецЕсли;
	
    Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
				  |	АдресныйКлассификатор.Код,
	              |	АдресныйКлассификатор.КодРегионаВКоде,
	              |	АдресныйКлассификатор.Наименование,
	              |	АдресныйКлассификатор.Сокращение,
	              |	АдресныйКлассификатор.Индекс,
	              |	АдресныйКлассификатор.ТипАдресногоЭлемента,
	              |	АдресныйКлассификатор.КодРайонаВКоде,
	              |	АдресныйКлассификатор.КодГородаВКоде,
	              |	АдресныйКлассификатор.КодНаселенногоПунктаВКоде,
	              |	АдресныйКлассификатор.КодУлицыВКоде

	              |ИЗ
	              |	РегистрСведений.АдресныйКлассификатор КАК АдресныйКлассификатор
	              |
	              |ГДЕ
	              |	АдресныйКлассификатор.Наименование = &Наименование И
	              |	АдресныйКлассификатор.ТипАдресногоЭлемента = &ТипАдресногоЭлемента " +
				  ОграничениеПоКоду;
	
	Запрос.УстановитьПараметр("Наименование", НазваниеЭлемента);
	Запрос.УстановитьПараметр("ТипАдресногоЭлемента", ТипЭлемента);
		
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Структура = СоздатьСтруктуруПоСтрокеВыборки(РезультатЗапроса, Выборка);
	Иначе 
		Структура =  ПолучитьПустуюСтруктуруАдреса();
	КонецЕсли;
	
	Возврат Структура;
	
КонецФункции

// Функция определяет входимость числа в заданный диапазон
//  При этом диапазон задается как это описано в комментарии к функции ЧислоВИнтервале
//
// Параметры: 
//  Номер - (число, строка) номер дома с корпусом
//  Интервал - (строка), интервал чисел (домов)
//
// Возвращаемое значение:
//  Истина - если заданное число входит в диапазон,
//  Ложь   - если не входит
//
Функция ВходитВИнтервал(Знач Номер, Интервал)

	Если ПустаяСтрока("" + Номер) Тогда
		Возврат Ложь;
	КонецЕсли;

	Если НЕ ОбщегоНазначения.ТолькоЦифрыВСтроке(Номер, Ложь) Тогда
		Возврат СтрЗаменить("" + ВРег(Номер), " ", "") = СтрЗаменить("" + ВРег(Интервал), " ", "");
	КонецЕсли;

	Номер = Число(Номер);
	
	ТолькоЧислоИнтервал = ОбщегоНазначения.ТолькоЦифрыВСтроке(Интервал, Ложь);

	Если ТолькоЧислоИнтервал Тогда
		Если Номер = Число(Интервал) Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;

	Если Найти(Интервал,"Ч") > 0 Тогда // диапазон четных чисел
		Интервал      = СтрЗаменить(Интервал, "Ч", "");
		Четность = 2;
		
	ИначеЕсли Найти(Интервал,"Н") > 0 Тогда // диапазон нечетных чисел
		Интервал      = СтрЗаменить(Интервал, "Н", "");
		Четность = 1;
		
	ИначеЕсли (Найти(Интервал, "-") = 0) И ТолькоЧислоИнтервал Тогда 
		// интервал это дом представленный в виде строки
		Возврат Ложь;
		
	Иначе
		Четность=0;
		
	КонецЕсли;
	
	Интервал = СтрЗаменить(Интервал, ")", ""); // на всякий случай уберем скобки
	Интервал = СтрЗаменить(Интервал, "(", "");
	Позиция  = Найти(Интервал, "-");
	Попали = 0;
	
	Если Позиция <> 0 Тогда
		МинЗначение  = Число(Лев(Интервал, Позиция - 1));
		МаксЗначение = Число(Сред(Интервал, Позиция + 1));
		Если (Номер >= МинЗначение) И (Номер <= МаксЗначение) Тогда
			Попали = 1;
		КонецЕсли;
		
	ИначеЕсли ПустаяСтрока(Интервал) Тогда
		// случай когда интервал был равен Ч или Н
		Попали = 1;
		
	Иначе
		Если ОбщегоНазначения.ТолькоЦифрыВСтроке(Интервал, Ложь) Тогда
			Если Номер = Число(Интервал) Тогда
				Попали = 1;
			КонецЕсли;
		КонецЕсли;

	КонецЕсли;
	
	Если (Попали = 1) И (
	((Четность = 2) И (Номер % 2 = 0)) ИЛИ 
	((Четность = 1) И (Номер % 2 = 1)) ИЛИ 
	 (Четность = 0)) Тогда
		Возврат Истина;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции // ВходитВИнтервал()

// Функция определяет входимость:
//  1. Номера дома (представленного в виде строки)
//     где Номер дома, это строка вида:
//     <СтрокаДом> пример: 58А, 32/1, 50А/8
//
//  2. Номера дома и номера корпуса (строения), в виде строки:
//     <СтрокаДом>к<СтрокаКорпус>,   пример: 32к1   это дом 32 корпус 1, 
//     <СтрокаДом>/<СтрокаКорпус>,   пример: 32/1   это дом 32 корпус 1, 
//     <СтрокаДом>стр<СтрокаКорпус>, пример: 32стр1 это дом 32 корпус 1, 
//		
//  3. Числа (номера дома) в заданный интервал чисел
//     При этом интервал задается строкой вида:
//     <Диапазон>[,<Диапазон>]
//     где Диапазон, это строка вида:
//     [Ч/Н]<Число>[-<Число>]
//     Префикс Ч или Н диапазона обозначает четность и нечетность входящих в него чисел
//     Пример: в интервал Ч12-14,Н1-5,20-29 входят числа 1,3,5,12,14 и все с 20 по 29
//
// Параметры: 
//  Номер - (число, строка) номер дома с корпусом
//  Интервал - (строка), интервал чисел (домов)
//
//  Истина - если заданное число входит в диапазон,
//  Ложь   - если не входит
//
Функция НомерДомаВИнтервале(НомерДома, Знач Интервал) 
	
	Пока НЕ ПустаяСтрока(Интервал) Цикл

		Позиция = Найти(Интервал, ",");
		Если Позиция = 0 Тогда
			Возврат ВходитВИнтервал(НомерДома, Интервал);
		Иначе
			Если ВходитВИнтервал(НомерДома, СокрЛП(Лев(Интервал, Позиция - 1))) Тогда
				Возврат Истина;
			Иначе
				Интервал = Сред(Интервал, Позиция + 1);
			КонецЕсли;
		КонецЕсли;

	КонецЦикла;

	Возврат Ложь;

КонецФункции // НомерДомаВИнтервале()


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ РАБОТЫ С АДРЕСНЫМ КОДОМ

// Маска  СС РРР ГГГ ППП УУУУ ДДДД КККК АА
//
//        СС   - код региона
//        РРР  - код района
//        ГГГ  - код города
//        ППП  - код населенного пункта
//        УУУУ - код улицы
//        ДДДД - код дома
//        КККК - код квартиры
//        АА   - код актуальности

// Функция возвращает маску  для определения номера элемента в структуре кода 
//
// Параметры: 
// Нет
//
// Возвращаемое значение:
//  Число - маска с единица в значимых разрядах и нулями в остальных
//
Функция МаскаРегиона() Экспорт
	Возврат 100000000000000000000000;
КонецФункции

// Функция возвращает маску  для определения номера элемента в структуре кода 
//
// Параметры: 
// Нет
//
// Возвращаемое значение:
//  Число - маска с единица в значимых разрядах и нулями в остальных
//
Функция МаскаРайона() Экспорт
	Возврат 100000000000000000000;
КонецФункции

// Функция возвращает маску  для определения номера элемента в структуре кода 
//
// Параметры: 
// Нет
//
// Возвращаемое значение:
//  Число - маска с единица в значимых разрядах и нулями в остальных
//
Функция МаскаГорода() Экспорт
	Возврат 100000000000000000;
КонецФункции

// Функция возвращает маску  для определения номера элемента в структуре кода 
//
// Параметры: 
// Нет
//
// Возвращаемое значение:
//  Число - маска с единица в значимых разрядах и нулями в остальных
//
Функция МаскаНасПункта() Экспорт
	Возврат 100000000000000;
КонецФункции

// Функция возвращает маску  для определения номера элемента в структуре кода 
//
// Параметры: 
// Нет
//
// Возвращаемое значение:
//  Число - маска с единица в значимых разрядах и нулями в остальных
//
Функция МаскаУлицы() Экспорт
	Возврат 10000000000;
КонецФункции

// Функция возвращает маску  для определения номера элемента в структуре кода 
//
// Параметры: 
// Нет
//
// Возвращаемое значение:
//  Число - маска с единица в значимых разрядах и нулями в остальных
//
Функция МаскаДома() Экспорт
	Возврат 1000000;
КонецФункции

// Функция возвращает маску  для определения номера элемента в структуре кода 
//
// Параметры: 
// Нет
//
// Возвращаемое значение:
//  Число - маска с единица в значимых разрядах и нулями в остальных
//
Функция МаскаКвартиры()
	Возврат 100;
КонецФункции


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ - СТАНДАРТНЫЕ ОБРАБОТЧИКИ ПРИ РАБОТЕ С АДРЕСНЫМ КЛАССИФИКАТОРОМ

#Если Клиент Тогда
	

// Процедура устанавливает последние значения поиска по строке для адресныхЭлементов
Процедура УстановитьПоследниеЗначенияПоисковПоСтрокеДляАдресныхЭлементов(Регион, Район, Город, НаселенныйПункт, Улица,
							ПоследнееЗначениеРегион, ПоследнееЗначениеРайон, ПоследнееЗначениеГород, ПоследнееЗначениеНаселенныйПункт, ПоследнееЗначениеУлица) Экспорт
							
	ПоследнееЗначениеРегион = Регион;
	ПоследнееЗначениеРайон = Район;
	ПоследнееЗначениеГород = Город;
	ПоследнееЗначениеНаселенныйПункт = НаселенныйПункт;
	ПоследнееЗначениеУлица = Улица;
							
КонецПроцедуры							
	

// Стандартный обработчик "При Изменении" адресного элемента
Процедура АдресныйЭлементПриИзменении(Элемент, Поле1, Поле2, Поле3, Поле4, Поле5, Поле6, Поле7, Поле8, 
			ПоследнееЗначениеЭлементаПоискаПоСтроке) Экспорт
	
	ПоследнееЗначениеЭлементаПоискаПоСтроке = Элемент.Значение;
	
КонецПроцедуры

// Стандартный обработчик "Начало выбора из списка" для выпадающего списка сокращений адресного элемента
Процедура АдресныйЭлементПриВыбореИзСпискаСокращений(ЭтаФорма, Элемент, СтандартнаяОбработка, ПолеРедактирования, Уровень) Экспорт
	
	СтандартнаяОбработка = Ложь;
	НазваниеТерриториальнойЕдиницы = СокрЛП(ОбрезатьСокращение(ПолеРедактирования));
	СписокСокращений = ПолучитьСписокСокращений(НазваниеТерриториальнойЕдиницы, Уровень);
	
	Если СписокСокращений <> Неопределено Тогда
		Сокращение = ЭтаФорма.ВыбратьИзСписка(СписокСокращений, Элемент);
		Если Сокращение <> Неопределено Тогда
			ПолеРедактирования = НазваниеТерриториальнойЕдиницы + " " + СокрЛП(Сокращение.Значение);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

// Стандартный обработчик "Обработка выбора" адресного элемента
Процедура АдресныйЭлементОбработатьВыбор(Элемент, ВыбранноеЗначение, СтандартнаяОбработка, АдреснаяЗапись) Экспорт
	
	СтандартнаяОбработка = Ложь;
	АдреснаяЗапись = ПолучитьСтруктуруАдресногоЭлемента(ВыбранноеЗначение.Код);
	Элемент.Значение = СокрЛП(АдреснаяЗапись.Наименование) + " " + СокрЛП(АдреснаяЗапись.Сокращение);
	
КонецПроцедуры


	
											  
// Процедура модального редактирования адреса
Процедура РедактироватьМодальноЭлементАдреса(Элемент, ЭтаФорма = Неопределено, КлючФормы = Неопределено) Экспорт
	
	ОбработкаРедактированияАдреса = Обработки.РедактированиеКонтактнойИнформации.Создать();
	ОбработкаРедактированияАдреса.мВозвратСтруктуры = Истина;	
	ОбработкаРедактированияАдреса.РедактироватьМодальноЭлементАдресаРазделенногоЗапятыми(Элемент, ЭтаФорма, КлючФормы);
	
КонецПроцедуры



#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ДРУГИЕ ПРОЦЕДУРЫ И ФУНКЦИИ

//  Функция формирует сведения об указанном ЮрФизЛице. К сведениям относятся -
// наименование, адрес, номер телефона, банковские реквизиты.
//
// Параметры: 
//  ЮрФизЛицо   - организация или физическое лицо, о котором собираются сведения.
//  ДатаПериода - дата, на которую выбираются сведения о ЮрФизЛице.
//  ДляФизЛицаТолькоИнициалы - Для физ. лица выводить только инициалы имени и отчества.
//  БанковскийСчет - Банковский счет, если счет не основной.
//
// Возвращаемое значение:
//  Сведения - собранные сведения.
//
Функция СведенияОЮрФизЛице(ЮрФизЛицо, ДатаПериода, ДляФизЛицаТолькоИнициалы = Истина, БанковскийСчет = Неопределено, КодЯзыка = Неопределено) Экспорт
	
	Если КодЯзыка = Неопределено Тогда
		КодЯзыка = Локализация.КодЯзыкаИнформационнойБазы();
	КонецЕсли;

	Сведения = Новый Структура("Представление, ПолноеНаименование, КодПоЕДРПОУ, КодПоДРФО, ИНН, НомерСвидетельства, Телефоны, ЮридическийАдрес, ФактическийАдрес, Банк, МФО, КоррСчет, НомерСчета, АдресБанка, ПлательщикНалогаНаПрибыль");
	Запрос   = Новый Запрос;
	Данные   = Неопределено;

	Если НЕ ЗначениеЗаполнено(ЮрФизЛицо) Тогда
		Возврат Сведения;
	ИначеЕсли ТипЗнч(ЮрФизЛицо) = Тип("Строка") Тогда
		Сведения.Вставить("Представление",      ЮрФизЛицо);
		Сведения.Вставить("полноеНаименование", ЮрФизЛицо);
		Возврат Сведения;
	КонецЕсли;

	Если БанковскийСчет = Неопределено ИЛИ БанковскийСчет.Пустая() Тогда
		ТекБанковскийСчет = ЮрФизЛицо.ОсновнойБанковскийСчет;
	Иначе
		ТекБанковскийСчет = БанковскийСчет;
	КонецЕсли;

	// Выберем основные сведения о контрагенте и банковском счете
	ИмяРеквизитаБанка = "Банк";

	Если ТипЗнч(ЮрФизЛицо) = Тип("СправочникСсылка.Организации") Тогда
		ИмяСправочника = "Организации";
		ИмяРегистраСхемыНалогообложения = "УчетнаяПолитикаОрганизаций";
 		ИмяИзмерения = "Организация";
		ИмяТаблицыКодовОрганизации = "КодыОрганизации";
		
	ИначеЕсли ТипЗнч(ЮрФизЛицо) = Тип("СправочникСсылка.Контрагенты") Тогда
		ИмяСправочника = "Контрагенты";
 		ИмяРегистраСхемыНалогообложения = "СхемыНалогообложенияКонтрагентов";
 		ИмяИзмерения = "Контрагент";
		ИмяТаблицыКодовОрганизации = "Организации";
	Иначе
		Возврат Сведения;
	КонецЕсли;

	Запрос.УстановитьПараметр("ПарЮрФизЛицо", ЮрФизЛицо);
	Запрос.УстановитьПараметр("ПарБанковскийСчет", ТекБанковскийСчет);
 	Запрос.УстановитьПараметр("ДатаПериода", ДатаПериода);

	Если НЕ ЗначениеЗаполнено(ТекБанковскийСчет) Тогда
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Организации.Представление      КАК Наименование,
		|	Организации.НаименованиеПолное КАК ПолноеНаименование,
		|	"+ИмяТаблицыКодовОрганизации+".ИНН                КАК ИНН,
		|	"+ИмяТаблицыКодовОрганизации+".КодПоЕДРПОУ		   КАК КодПоЕДРПОУ,
		|	"+ИмяТаблицыКодовОрганизации+".НомерСвидетельства КАК НомерСвидетельства,";
		Запрос.Текст = Запрос.Текст + "
		|	""""                           КАК НомерСчета,
		|	""""                           КАК Банк,
		|	""""                           КАК МФО,
		|	""""                           КАК КоррСчет,
		|	""""                           КАК АдресБанка,
		|   РегистрСхемыНалогообложения.СхемаНалогообложения.НалогНаПрибыль   КАК ПлательщикНалогаНаПрибыль
		|ИЗ
		|	Справочник."+ИмяСправочника+" КАК Организации
		|
		|   ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений."+ИмяРегистраСхемыНалогообложения+".СрезПоследних(&ДатаПериода) КАК РегистрСхемыНалогообложения
		|	ПО РегистрСхемыНалогообложения."+ИмяИзмерения+" = Организации.Ссылка";
		
		
		Если ТипЗнч(ЮрФизЛицо) = Тип("СправочникСсылка.Организации") Тогда
			// У Организации регистрационные коды хранятся в регистре сведений
			Запрос.Текст = Запрос.Текст +"
		|   ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КодыОрганизации.СрезПоследних(&ДатаПериода) КАК КодыОрганизации
		|	ПО КодыОрганизации.Организация = Организации.Ссылка";
		КонецЕсли;
		
		Запрос.Текст = Запрос.Текст +"
		|ГДЕ
		|	Организации.Ссылка = &ПарЮрФизЛицо";
	Иначе
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Организации.Представление                      КАК Наименование,
		|	Организации.НаименованиеПолное                 КАК ПолноеНаименование,
		|	"+ИмяТаблицыКодовОрганизации+".ИНН                                КАК ИНН,
		|	"+ИмяТаблицыКодовОрганизации+".КодПоЕДРПОУ                        КАК КодПоЕДРПОУ,
		|	"+ИмяТаблицыКодовОрганизации+".НомерСвидетельства                 КАК НомерСвидетельства,";
		Запрос.Текст = Запрос.Текст + "
		|	БанковскиеСчета.НомерСчета                     КАК НомерСчета,
		|	БанковскиеСчета."+ИмяРеквизитаБанка+"          КАК Банк,
		|	БанковскиеСчета."+ИмяРеквизитаБанка+".Код      КАК МФО,
		|	БанковскиеСчета."+ИмяРеквизитаБанка+".КоррСчет КАК КоррСчет,
		|	БанковскиеСчета."+ИмяРеквизитаБанка+".Адрес    КАК АдресБанка,
		|   РегистрСхемыНалогообложения.СхемаНалогообложения.НалогНаПрибыль   КАК ПлательщикНалогаНаПрибыль
		|ИЗ
		|	Справочник."+ИмяСправочника+" КАК Организации
		|
		|   ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений."+ИмяРегистраСхемыНалогообложения+".СрезПоследних(&ДатаПериода) КАК РегистрСхемыНалогообложения
		|	ПО РегистрСхемыНалогообложения."+ИмяИзмерения+" = Организации.Ссылка";
		
		Если ТипЗнч(ЮрФизЛицо) = Тип("СправочникСсылка.Организации") Тогда
			// У Организации регистрационные коды хранятся в регистре сведений
			Запрос.Текст = Запрос.Текст +"
		|   ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КодыОрганизации.СрезПоследних(&ДатаПериода) КАК КодыОрганизации
		|	ПО КодыОрганизации.Организация = Организации.Ссылка";
		
		КонецЕсли;
		
		Запрос.Текст = Запрос.Текст +",
		|	Справочник.БанковскиеСчета    КАК БанковскиеСчета
		|
		|ГДЕ
		|	Организации.Ссылка       = &ПарЮрФизЛицо
		|	И БанковскиеСчета.Ссылка = &ПарБанковскийСчет";
	КонецЕсли;

	Данные = Запрос.Выполнить().Выбрать();
	Данные.Следующий();

	Если ЮрФизЛицо.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо Тогда
		Сведения.Вставить("КодПоЕДРПОУ", Данные.КодПоЕДРПОУ);
	Иначе
		Сведения.Вставить("КодПоДРФО", Данные.КодПоЕДРПОУ);
		//Если ИмяСправочника = "Организации" Тогда
		//	Сведения.Вставить("Свидетельство", "свидетельство " + Данные.СвидетельствоСерияНомер + " от " + Формат(Данные.СвидетельствоДатаВыдачи, "ДФ=dd.MM.yyyy"));
		//КонецЕсли;
	КонецЕсли;

	Сведения.Вставить("ПолноеНаименование", Данные.ПолноеНаименование);

	Если Данные <> Неопределено Тогда
		Сведения.Вставить("Представление",    Данные.Наименование);
		Сведения.Вставить("ИНН",              Данные.ИНН);
		Сведения.Вставить("НомерСвидетельства", Данные.НомерСвидетельства);
		Сведения.Вставить("Телефоны",         ПолучитьТелефонИзКонтактнойИнформации(ЮрФизЛицо));
		Сведения.Вставить("НомерСчета",       Данные.НомерСчета);
		Сведения.Вставить("Банк",             Данные.Банк);
		Сведения.Вставить("МФО",              Данные.МФО);
		Сведения.Вставить("АдресБанка",       Данные.АдресБанка);
		Сведения.Вставить("КоррСчет",         Данные.КоррСчет);
		Сведения.Вставить("ЮридическийАдрес", ПолучитьАдресИзКонтактнойИнформации(ЮрФизЛицо, "Юридический", КодЯзыка));
		Сведения.Вставить("ФактическийАдрес", ПолучитьАдресИзКонтактнойИнформации(ЮрФизЛицо, "Фактический", КодЯзыка));
		Сведения.Вставить("ЮридическийАдресПочтовый", 	ПолучитьАдресИзКонтактнойИнформации(ЮрФизЛицо, "Юридический", КодЯзыка, Истина));
		Сведения.Вставить("ФактическийАдресПочтовый", 	ПолучитьАдресИзКонтактнойИнформации(ЮрФизЛицо, "Фактический", КодЯзыка, Истина));

		Если (ИмяСправочника = "Контрагенты") И (Данные.ПлательщикНалогаНаПрибыль = Null) Тогда
			Сведения.Вставить("ПлательщикНалогаНаПрибыль", "");
		Иначе
			Сведения.Вставить("ПлательщикНалогаНаПрибыль", ?(Данные.ПлательщикНалогаНаПрибыль = Истина,НСтр("ru='Является ';uk='Є '",КодЯзыка) , НСтр("ru='Не является ';uk='Не є '",КодЯзыка)) 
															+ НСтр("ru='плательщиком налога на прибыль на общих основаниях';uk='платником податку на прибуток на загальних підставах'",КодЯзыка));
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Сведения.ПолноеНаименование) Тогда
			Сведения.ПолноеНаименование = Сведения.Представление;
		КонецЕсли;
	КонецЕсли;

	Возврат Сведения;

КонецФункции // СведенияОЮрФизЛице()

// Функция возвращает код организации (для юр/физ. лица)
//
// Параметры
//  СписокСведений - список значений со значениями параметров организации
//
// Возвращаемое значение:
//   Строка   – код ЕДРПОУ, если организация юр.лицо, код по ДРФО если физ.лицо
//
Функция ПолучитьКодОрганизации(СписокСведений) Экспорт

	Код = "";
	
	Если ТипЗнч(СписокСведений) = Тип("Структура") Тогда
		
		Если СписокСведений.Свойство("КодПоЕДРПОУ") Тогда
			
			Код = СписокСведений.КодПоЕДРПОУ;	
			
		КонецЕсли;
		
		// если в списке есть код по ДРФО, значит организация - физ.лицо.
		Если СписокСведений.Свойство("КодПоДРФО") Тогда
			
			Если НЕ ПустаяСтрока(СписокСведений.КодПоДРФО) Тогда
			
				Код = СписокСведений.КодПоДРФО;		
			
			КонецЕсли; 
			
		КонецЕсли;
		
	Иначе
		
		// странно, должна передаваться структура
		Код = СписокСведений;
		
	КонецЕсли;
	
	Возврат Код;
        
КонецФункции // ПолучитьКодОрганизации()

// Функция находит актуальное значение адреса в контакной информации.
//
// Параметры:
//  Объект - СправочникСсылка, объект контактной информации
//  ТипАдреса - тип контактной информации
//
// Возвращаемое значение
//  Строка - представление найденного адреса
//
Функция ПолучитьАдресИзКонтактнойИнформации(Объект, ТипАдреса = "", КодЯзыка = Неопределено, ВПочтовомФормате = Ложь) Экспорт

	ОбъектКонтактнойИнформации = Объект;
	
	Если НЕ ЗначениеЗаполнено(ТипАдреса) Тогда
		Возврат "";
	КонецЕсли;

	Если ТипАдреса="Фактический" Тогда
		Начало = "Факт";
	ИначеЕсли ТипАдреса="Регистрации"
		  ИЛИ ТипАдреса="Юридический" Тогда
		Начало = "Юр";
	Иначе
		Возврат "";
	КонецЕсли;
	
	Если ТипЗнч(Объект) = Тип("СправочникСсылка.Организации") Тогда
		Если Объект.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо Тогда
			ОбъектКонтактнойИнформации = Объект.ИндивидуальныйПредприниматель;
			Окончание = "ФизЛица";
		Иначе
			Окончание = "Организации";
		КонецЕсли;
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникСсылка.Контрагенты") Тогда
		Окончание = "Контрагента";
	КонецЕсли;
	
	ВидАдреса = Справочники.ВидыКонтактнойИнформации[Начало+"Адрес"+Окончание];

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Объект", ОбъектКонтактнойИнформации);
	Запрос.УстановитьПараметр("Тип"   , Перечисления.ТипыКонтактнойИнформации.Адрес);
	Запрос.УстановитьПараметр("Вид"   , ВидАдреса);
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
    |	*,
    |	КонтактнаяИнформация.Объект,
    |	КонтактнаяИнформация.Тип,
    |	КонтактнаяИнформация.Вид,
    |	КонтактнаяИнформация.Представление,
    |	КонтактнаяИнформация.Поле1 КАК Индекс,
    |	КонтактнаяИнформация.Поле2 КАК Регион,
    |	КонтактнаяИнформация.Поле3 КАК Район,
    |	КонтактнаяИнформация.Поле4 КАК Город,
    |	КонтактнаяИнформация.Поле5 КАК НаселенныйПункт,
    |	КонтактнаяИнформация.Поле6 КАК Улица,
    |	КонтактнаяИнформация.Поле7 КАК Дом,
    |	КонтактнаяИнформация.Поле8 КАК Корпус,
    |	КонтактнаяИнформация.Поле9 КАК Квартира,
    |	КонтактнаяИнформация.Поле10,
    |	КонтактнаяИнформация.Комментарий
	|ИЗ
	|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	|ГДЕ
	|	КонтактнаяИнформация.Объект = &Объект
	|	И
	|	КонтактнаяИнформация.Тип = &Тип
	|	И
	|	КонтактнаяИнформация.Вид = &Вид
	|";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат "";
	Иначе
		ТаблицаЗапроса = РезультатЗапроса.Выгрузить();
		СтруктураАдреса = Новый Структура();
		Для каждого Колонка Из ТаблицаЗапроса.Колонки Цикл
			СтруктураАдреса.Вставить(Колонка.Имя, ТаблицаЗапроса[0][Колонка.Имя]);
		КонецЦикла;
		СтруктураАдреса.Вставить("ЗаПределамиУкраины", НЕ УправлениеКонтактнойИнформацией.ОпределитьДляОбъектаУкраинскийАдрес(СтруктураАдреса));

		СтруктураАдреса.Вставить("ВПочтовомФормате", ВПочтовомФормате);
		
		Возврат ПолучитьПредставлениеАдресаПоСтруктуре(СтруктураАдреса, КодЯзыка);
		
	КонецЕсли;

КонецФункции // ПолучитьАдресИзКонтактнойИнформации()

// Функция находит актуальное значение телефона в контакной информации.
//
// Параметры:
//  Объект - СправочникСсылка, объект контактной информации
//
// Возвращаемое значение
//  Строка - представление найденного телефона
//
Функция ПолучитьТелефонИзКонтактнойИнформации(Объект) Экспорт

	Если ТипЗнч(Объект) = Тип("СправочникСсылка.Организации") Тогда
		Окончание = "Организации";
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникСсылка.Контрагенты") Тогда
		Окончание = "Контрагента";
	КонецЕсли;

	ВидТелефона = Справочники.ВидыКонтактнойИнформации["Телефон" + Окончание];

	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Объект", Объект);
	Запрос.УстановитьПараметр("Тип"   , Перечисления.ТипыКонтактнойИнформации.Телефон);
	Запрос.УстановитьПараметр("Вид"   , ВидТелефона);
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	КонтактнаяИнформация.Представление
	|ИЗ
	|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	|ГДЕ
	|	КонтактнаяИнформация.Объект = &Объект
	|	И
	|	КонтактнаяИнформация.Тип = &Тип
	|	И
	|	КонтактнаяИнформация.Вид = &Вид
	|";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат "";
	Иначе
		Возврат РезультатЗапроса.Выгрузить()[0].Представление;
	КонецЕсли;

КонецФункции // ПолучитьТелефонИзКонтактнойИнформации()

// Функция находит актуальное значение телефона в контакной информации.
//
// Параметры:
//  Объект - СправочникСсылка, объект контактной информации
//
// Возвращаемое значение
//  Строка - представление найденного телефона
//
Функция ПолучитьТелефонныйКодГородаИзКонтактнойИнформации(Объект) Экспорт

	Если ТипЗнч(Объект) = Тип("СправочникСсылка.Организации") Тогда
		Окончание = "Организации";
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникСсылка.Контрагенты") Тогда
		Окончание = "Контрагента";
	КонецЕсли;
	ВидТелефона = Справочники.ВидыКонтактнойИнформации["Телефон" + Окончание].ПолучитьОбъект().Ссылка;

	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Объект", Объект);
	Запрос.УстановитьПараметр("Тип"   , Перечисления.ТипыКонтактнойИнформации.Телефон);
	Запрос.УстановитьПараметр("Вид"   , ВидТелефона);
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1 
	|	КонтактнаяИнформация.Поле2
	|ИЗ
	|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	|ГДЕ
	|	КонтактнаяИнформация.Объект = &Объект
	|	И
	|	КонтактнаяИнформация.Тип = &Тип
	|	И
	|	КонтактнаяИнформация.Вид = &Вид
	|";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат "";
	Иначе
		Возврат РезультатЗапроса.Выгрузить()[0].Поле2;
	КонецЕсли;

КонецФункции // ПолучитьТелефонныйКодГородаИзКонтактнойИнформации()

// Функция находит актуальное значение факса в контакной информации.
//
// Параметры:
//  Объект - СправочникСсылка, объект контактной информации
//
// Возвращаемое значение
//  Строка - представление найденного факса
//
Функция ПолучитьФаксИзКонтактнойИнформации(Объект) Экспорт

	Если ТипЗнч(Объект) = Тип("СправочникСсылка.Организации") Тогда
		Окончание = "Организации";
	КонецЕсли;
	ВидТелефона = Справочники.ВидыКонтактнойИнформации["Факс" + Окончание].ПолучитьОбъект().Ссылка;

	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Объект", Объект);
	Запрос.УстановитьПараметр("Тип"   , Перечисления.ТипыКонтактнойИнформации.Телефон);
	Запрос.УстановитьПараметр("Вид"   , ВидТелефона);
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	КонтактнаяИнформация.Представление
	|ИЗ
	|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	|ГДЕ
	|	КонтактнаяИнформация.Объект = &Объект
	|	И
	|	КонтактнаяИнформация.Тип = &Тип
	|	И
	|	КонтактнаяИнформация.Вид = &Вид
	|";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат "";
	Иначе
		Возврат РезультатЗапроса.Выгрузить()[0].Представление;
	КонецЕсли;

КонецФункции // ПолучитьФаксИзКонтактнойИнформации()

// Функция приводит телефонный номер к одному из указанных в настройке шаблонов
//
// Параметры
//  НомерТЛФ – строка, номер телефона, который надо преобразовывать
//
// Возвращаемое значение:
//   Приведенный номер – строка, номер, приведенный к одному из шаблонов
//
Функция ПривестиНомерТелефонаКШаблону(НомерТЛФ) Экспорт
	
	ТолькоЦифрыНомера = "";
	КоличествоЦифрНомера = 0;
	
	Для а=1 По СтрДлина(НомерТЛФ) Цикл
		Если СтрЧислоВхождений("1234567890",Сред(НомерТЛФ,а,1)) > 0 Тогда
			КоличествоЦифрНомера = КоличествоЦифрНомера + 1;
			ТолькоЦифрыНомера = ТолькоЦифрыНомера + Сред(НомерТЛФ,а,1);
		КонецЕсли;
	КонецЦикла;
	
	Если КоличествоЦифрНомера = 0 Тогда
		Возврат НомерТЛФ;
	КонецЕсли;
	
	СтруктураШаблонов = Константы.ШаблоныТелефонныхНомеров.Получить().Получить();
	Если ТипЗнч(СтруктураШаблонов) <> Тип("Соответствие") Тогда
		Возврат НомерТЛФ;
	КонецЕсли; 
	ПолученныйШаблон = СтруктураШаблонов.Получить(КоличествоЦифрНомера);
	
	Если ПолученныйШаблон = Неопределено Тогда
		Возврат НомерТЛФ;
	КонецЕсли;
	
	ПриведенныйНомер = "";
	НомерЦифры = 0;
	
	Для а=1 По СтрДлина(ПолученныйШаблон) Цикл
		Если Сред(ПолученныйШаблон,а,1) = "9" Тогда
			НомерЦифры = НомерЦифры + 1;
			ПриведенныйНомер = ПриведенныйНомер + Сред(ТолькоЦифрыНомера,НомерЦифры,1);
		Иначе
			ПриведенныйНомер = ПриведенныйНомер + Сред(ПолученныйШаблон,а,1);
		КонецЕсли;
	КонецЦикла; 

	Возврат ПриведенныйНомер;
	
КонецФункции // ПривестиКШаблону()

// Функция находит актуальное значение адреса Email в контакной информации.
//
// Параметры:
//  Объект - СправочникСсылка, объект контактной информации
//
// Возвращаемое значение
//  Строка - представление найденного телефона
//
Функция ПолучитьEmailИзКонтактнойИнформации(Объект) Экспорт

	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Объект", Объект);
	Запрос.УстановитьПараметр("Тип"   , Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);

	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	КонтактнаяИнформация.Представление
	|ИЗ
	|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	|ГДЕ
	|	КонтактнаяИнформация.Объект = &Объект
	|	И
	|	КонтактнаяИнформация.Тип = &Тип
	|";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат "";
	Иначе
		Возврат РезультатЗапроса.Выгрузить()[0].Представление;
	КонецЕсли;

КонецФункции // ПолучитьEmailИзКонтактнойИнформации()

Функция ПолучитьРезидентНерезидентИзКонтактнойИнформации(Объект, ТипАдреса = "") Экспорт

	МетаданныеОбъекта = Объект.Метаданные();
	НеЯвляетсяРезидентом = ?(МетаданныеОбъекта.Реквизиты.Найти("НеЯвляетсяРезидентом") <> Неопределено, ?(Объект.НеЯвляетсяРезидентом, Истина, Ложь), Ложь);
	
	Возврат ?(НеЯвляетсяРезидентом, Ложь, Истина);

КонецФункции // ПолучитьРезидентНерезидентИзКонтактнойИнформации()

#Если Клиент Тогда
	
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ РАБОТЫ С ФОРМАМИ МЕХАНИЗМА ХРАНЕНИЯ КОНТАКНОЙ ИНФОРМАЦИИ

// Процедура открывает форму справочника ВидыКонтактнойИнформации для выбора.
//
// Параметры:
//  РеджимВыбора - булево, задает режим выбора для открываемой формы.
//  ВладелецФормы - задает владельца для открываемой формы.
//  ЗначениеОтбораПоТипу - задает значение отбора по типу конт.инф.
//  ЗначениеОтбораПоВидуОбъектаКИ - задает значение отбора по виду объекта конт.инф.
Процедура ОткрытьФормуВыбораВидаКИ(РежимВыбора, ВладелецФормы, ЗначениеОтбораПоТипу = Неопределено,
                                     ЗначениеОтбораПоВидуОбъектаКИ = Неопределено) Экспорт

	ВыбФорма = Справочники.ВидыКонтактнойИнформации.ПолучитьФорму("ФормаВыбора", ВладелецФормы);
	ВыбФорма.РежимВыбора = РежимВыбора;
	
	Если ЗначениеОтбораПоТипу <> Неопределено Тогда
		ВыбФорма.Отбор.Тип.Значение = ЗначениеОтбораПоТипу;
		ВыбФорма.Отбор.Тип.Использование = Истина;
		ВыбФорма.ЭлементыФормы.СправочникСписок.НастройкаОтбора.Тип.Доступность = Ложь;
	КонецЕсли; 
	
	Если ЗначениеОтбораПоВидуОбъектаКИ <> Неопределено Тогда
		ВыбФорма.Отбор.ВидОбъектаКонтактнойИнформации.Значение = ЗначениеОтбораПоВидуОбъектаКИ;
		ВыбФорма.Отбор.ВидОбъектаКонтактнойИнформации.Использование = Истина;
		ВыбФорма.ЭлементыФормы.СправочникСписок.НастройкаОтбора.ВидОбъектаКонтактнойИнформации.Доступность = Ложь;
	КонецЕсли;
	
	Если ТипЗнч(ВладелецФормы.Значение) = Тип("СправочникСсылка.ВидыКонтактнойИнформации")
	   И ЗначениеЗаполнено(ВладелецФормы.Значение) Тогда
		ВыбФорма.ПараметрТекущаяСтрока = ВладелецФормы.Значение;
	КонецЕсли; 
	
	ВыбФорма.Открыть();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ РАБОТЫ С ФОРМАМИ МЕХАНИЗМА ХРАНЕНИЯ КОНТАКНОЙ ИНФОРМАЦИИ

// Процедура вызывается по событию ПередЗаписью в форме записи регистра сведений КонтактнаяИнформация
//
// Параметры:
//  Отказ - Булево, флаг отказа от записи объекта
//  ЭтаФорма - Форма записи
//
Процедура ПередЗаписьюВФормеКонтактнойИнформации(Отказ, ЭтаФорма) Экспорт

	Если (ТипЗнч(ЭтаФорма.мСтруктураИзмерений) = Тип("Структура") И (ЭтаФорма.РегистрСведенийМенеджерЗаписи.Вид <> ЭтаФорма.мСтруктураИзмерений.Вид ИЛИ ЭтаФорма.РегистрСведенийМенеджерЗаписи.Объект <> ЭтаФорма.мСтруктураИзмерений.Объект)) ИЛИ ЭтаФорма.мСтруктураИзмерений = Неопределено Тогда
	
		НаборРегистра = РегистрыСведений.КонтактнаяИнформация.СоздатьНаборЗаписей();
		НаборРегистра.Отбор.Объект.Значение      = ЭтаФорма.РегистрСведенийМенеджерЗаписи.Объект;
		НаборРегистра.Отбор.Объект.Использование = Истина;
		НаборРегистра.Отбор.Вид.Значение         = ЭтаФорма.РегистрСведенийМенеджерЗаписи.Вид;
		НаборРегистра.Отбор.Вид.Использование    = Истина;
		
		НаборРегистра.Прочитать();
		
		Если НаборРегистра.Количество() > 0 Тогда
		
			ОтветНаВопрос = Вопрос("Запись с данными параметрами уже существует. Удалить старые значения?", РежимДиалогаВопрос.ОКОтмена);
			
			Если ОтветНаВопрос <> КодВозвратаДиалога.ОК Тогда
				Отказ = Истина;
				Возврат;
			КонецЕсли; 
			
			Попытка
				НаборРегистра.Очистить();
				НаборРегистра.Записать();
			Исключение
				ОбщегоНазначения.СообщитьОбОшибке(ОписаниеОшибки());
				Отказ = Истина;
			КонецПопытки; 
			
		КонецЕсли; 
		
	КонецЕсли; 
	
КонецПроцедуры

#КонецЕсли
