////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ 
// 

// Хранит признак ДокОбъекта "Указывать документ передачи"
Перем  мУказДокПередачи;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ 
// 

// Процедура перегружает значения отбора
// из реквизита обработки "Отбор"
// в отбор построителя запроса
// 
Процедура СкопироватьОтбор(ОтборИсточник, ОтборПриемник, ТолькоИспользуемые = Ложь) 
	
	// Очистка отбора приемника
	КвоЭлементов = ОтборПриемник.Количество() - 1;
	
	Для Н = 0 По КвоЭлементов Цикл
	
		ОтборПриемник.Удалить(КвоЭлементов - Н);	
	
	КонецЦикла;
	
	// Загрузка элементов отбора
	КвоЭлементов = ОтборИсточник.Количество() - 1;
	
	Для Н = 0 По КвоЭлементов Цикл
		
		ОбратныйИндекс = КвоЭлементов - Н;
		Элемент        = ОтборИсточник[ОбратныйИндекс];
		
		Если (НЕ ТолькоИспользуемые) ИЛИ Элемент.Использование Тогда
			
			Если ЗначениеЗаполнено(Элемент.ПутьКДанным) Тогда
				
				ЭлементПриемника = ОтборПриемник.Добавить(Элемент.ПутьКДанным);
				
			Иначе
				
				ЭлементПриемника = Неопределено;
				
			КонецЕсли;
			
			Если ЭлементПриемника <> Неопределено Тогда
				
				Попытка
				
					//ЗаполнитьЗначенияСвойств(ЭлементПриемника, Элемент);
					ЭлементПриемника.ВидСравнения 	= Элемент.ВидСравнения;
					ЭлементПриемника.Использование 	= Элемент.Использование;
					Если ТипЗнч(Элемент.Значение) = Тип("СписокЗначений") Тогда
						ЭлементПриемника.Значение.ЗагрузитьЗначения(Элемент.Значение.ВыгрузитьЗначения());	
					Иначе	
						ЭлементПриемника.Значение 	= Элемент.Значение;
					КонецЕсли;	
					ЭлементПриемника.ЗначениеС 		= Элемент.ЗначениеС;
					ЭлементПриемника.ЗначениеПо 	= Элемент.ЗначениеПо;
				
				Исключение
					
					ТекстОшибки = ОписаниеОшибки();
					Поз = Найти(ТекстОшибки, "):") + 3;
					ТекстОшибки = Сред(ТекстОшибки,	Поз);
					ОбщегоНазначения.СообщитьОбОшибке("Элемент отбора <" + Элемент + ">:" + Символы.ПС + Символы.Таб 
									 + ТекстОшибки
									 + Символы.ПС + Символы.Таб + "Элемент удален.");
					
					ОтборПриемник.Удалить(ОтборПриемник.Индекс(ЭлементПриемника));
					ОтборИсточник.Удалить(ОбратныйИндекс);
					
				КонецПопытки;
				
			Иначе
				
				ОбщегоНазначения.СообщитьОбОшибке("Элемент отбора <" + Элемент + "> не входит в число допустимых."
				                 + Символы.ПС + Символы.Таб + "Элемент удален.");
				ОтборИсточник.Удалить(ОбратныйИндекс);
				
			КонецЕсли;
			
		КонецЕсли;
	
	КонецЦикла;

КонецПроцедуры 

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ 
// 

Процедура ВыполнитьЗаполнение() Экспорт

	ЭтоДокПеремещение = (ТипЗнч(ДокОбъект) = Тип("ДокументОбъект.ПеремещениеМатериаловВЭксплуатации"));
	
	// Перегрузка значений из реквизита формы "Отбор"
	// в отбор построителя запроса
	СкопироватьОтбор(Отбор, ПостроительЗапроса.Отбор, Истина);
	
	Заголовок = "Заполнение табличной части """ + Метаданные.НайтиПоТипу(ТипЗнч(ТаблицаМатериалов)).Представление() 
	            + """ документа " + ДокОбъект + ".";
	
	ПостроительЗапроса.Выполнить();
	
	Если ПостроительЗапроса.Результат.Пустой() Тогда
		
		ОбщегоНазначения.СообщитьИнформацию("Нет данных для заполнения!", Заголовок);	
		                                                                              
	КонецЕсли;
	
	Выборка = ПостроительЗапроса.Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = ТаблицаМатериалов.Добавить();
		НоваяСтрока.Номенклатура               = Выборка.Номенклатура;
		НоваяСтрока.ХарактеристикаНоменклатуры = Выборка.ХарактеристикаНоменклатуры;
		НоваяСтрока.СерияНоменклатуры          = Выборка.СерияНоменклатуры;
		НоваяСтрока.ЕдиницаИзмерения	   	   = Выборка.ЕдиницаИзмерения;
		НоваяСтрока.ЕдиницаИзмеренияМест   	   = Выборка.ЕдиницаИзмерения;
		НоваяСтрока.Коэффициент     	   	   = Выборка.Коэффициент;
		
		
		Если Выборка.Количество < 0 Тогда
			
			НоваяСтрока.КоличествоМест = 0;
			НоваяСтрока.Количество 	   = 0;
			Сообщение = "Строка номер " + НоваяСтрока.НомерСтроки 
						+ Символы.ПС + УправлениеЗапасами.ПредставлениеНоменклатуры(Строка(Выборка.Номенклатура),
																 Строка(Выборка.ХарактеристикаНоменклатуры),
																 Строка(Выборка.СерияНоменклатуры))
						+ ", " + Выборка.СхемаНазначенияИспользования + ", " + Выборка.ФизЛицо + ", "
						+ Выборка.Качество + ":" + Символы.ПС + Символы.Таб + "Отрицательные остатки <" 
						+ Выборка.Количество + " " + Выборка.ЕдиницаИзмерения +">!";
			
			ОбщегоНазначения.СообщитьОбОшибке(Сообщение, , Заголовок);
			
		Иначе
			
			НоваяСтрока.КоличествоМест = Выборка.Количество;
			НоваяСтрока.Количество     = Выборка.Количество;
			
		КонецЕсли;
		
		НоваяСтрока.ФизЛицо 	   					= Выборка.ФизЛицо;
		НоваяСтрока.СхемаНазначенияИспользования 	= Выборка.СхемаНазначенияИспользования;
		НоваяСтрока.СпособОтраженияРасходов     	= Выборка.СпособОтраженияРасходов;
		НоваяСтрока.СрокПолезногоИспользования 		= Выборка.СрокПолезногоИспользования;
		НоваяСтрока.Качество                    	= Выборка.Качество;
		
		Если мУказДокПередачи Тогда
			
			НоваяСтрока.ДокументПередачи = Выборка.ДокументПередачи;
			
		КонецЕсли;
		
		Если ЭтоДокПеремещение Тогда
			
			НоваяСтрока.СхемаНазначенияИспользованияНовое 	= Выборка.СхемаНазначенияИспользования;
			НоваяСтрока.СпособОтраженияРасходовНовое 		= Выборка.СпособОтраженияРасходов;
			НоваяСтрока.СрокПолезногоИспользованияНовое 	= Выборка.СрокПолезногоИспользования;
			НоваяСтрока.КачествоНовое                		= Выборка.Качество;
			
		КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры // СформироватьОтчет()
                                                                                                    
// Процедура - заполняет начальные настройки заполнения
//
Процедура ЗаполнитьНачальныеНастройки(Отказ) Экспорт
		
	Если ДокОбъект = Неопределено Тогда
		
		ОбщегоНазначения.СообщитьОбОшибке("Данная обработка служебная и вызывается при необходимости
											|заполнить табличную часть документов по остаткам", Отказ);
		Возврат
		
	КонецЕсли;
	
	Если ДокОбъект.ЭтоНовый() Тогда
		
		ИспользоватьРасширеннуюАналитику = глЗначениеПеременной("ИспользоватьРасширеннуюАналитикуУчетаНоменклатурыИЗатрат");
		
	Иначе
		
		ИспользоватьРасширеннуюАналитику = глЗначениеПеременной("ИспользоватьРасширеннуюАналитикуУчетаНоменклатурыИЗатрат") 
			И (глЗначениеПеременной("ДатаНачалаИспользованияРасширеннойАналитикиУчетаНоменклатурыИЗатрат") <= ДокОбъект.Дата);
		
	КонецЕсли;
		
	Если ИспользоватьРасширеннуюАналитику Тогда
		ЗаполнитьНачальныеНастройки_РАУЗ(Отказ);
		Возврат;	
	КонецЕсли;	
	
	УУ = ДокОбъект.ОтражатьВУправленческомУчете;
	БУ = ДокОбъект.ОтражатьВБухгалтерскомУчете;
	мУказДокПередачи = ДокОбъект.УказыватьДокументПередачи;
	
	ПостроительЗапроса = Новый ПостроительЗапроса;
	
	Если ДокОбъект.ЭтоНовый() Тогда
		
		ПостроительЗапроса.Параметры.Вставить("НаДату", Неопределено);
		
	Иначе
		
		ПостроительЗапроса.Параметры.Вставить("НаДату", ДокОбъект.МоментВремени());
		
	КонецЕсли;
	
	ПостроительЗапроса.Параметры.Вставить("ДатаДокумента"           , ДокОбъект.Дата);
	ПостроительЗапроса.Параметры.Вставить("Подразделение"           , ДокОбъект.Подразделение);
	ПостроительЗапроса.Параметры.Вставить("ПодразделениеОрганизации", ДокОбъект.ПодразделениеОрганизации);

	ПостроительЗапроса.Текст = 
	"ВЫБРАТЬ
	|	Партии.Номенклатура                                     КАК Номенклатура,
	|	Партии.ХарактеристикаНоменклатуры                       КАК ХарактеристикаНоменклатуры,
	|	Партии.СерияНоменклатуры                                КАК СерияНоменклатуры,
	|	Партии.Номенклатура.ЕдиницаХраненияОстатков             КАК ЕдиницаИзмерения,
	|	Партии.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент КАК Коэффициент,
	|	Партии.ДокументПередачи                                 КАК ДокументПередачи,
	|	Партии.ФизЛицо                                          КАК ФизЛицо,
	|	Партии.СхемаНазначенияИспользования                     КАК СхемаНазначенияИспользования,
	|	Партии.СпособОтраженияРасходов                          КАК СпособОтраженияРасходов,
	|	Партии.СрокПолезногоИспользования                       КАК СрокПолезногоИспользования,
	|	Партии.Качество                                         КАК Качество,
	|	МАКСИМУМ(ЕСТЬNULL(Партии.Количество, 0))                КАК Количество
	|{ВЫБРАТЬ
	|	Номенклатура                                     КАК Номенклатура,
	|	ХарактеристикаНоменклатуры                       КАК ХарактеристикаНоменклатуры,
	|	СерияНоменклатуры                                КАК СерияНоменклатуры,
	|	Номенклатура.ЕдиницаХраненияОстатков             КАК ЕдиницаИзмерения,
	|	Номенклатура.ЕдиницаХраненияОстатков.Коэффициент КАК Коэффициент,
	|	ДокументПередачи                                 КАК ДокументПередачи,
	|	ФизЛицо                                          КАК ФизЛицо,
	|	СхемаНазначенияИспользования                     КАК СхемаНазначенияИспользования,
	|	СпособОтраженияРасходов                          КАК СпособОтраженияРасходов,
	|	СрокПолезногоИспользования                       КАК СрокПолезногоИспользования,
	|	Качество                                         КАК Качество,
	|	Количество                                       КАК Количество}
	|ИЗ
	|	(" + ?(УУ, "
	|	 ВЫБРАТЬ
	|		ПартииУУ.Номенклатура               КАК Номенклатура,
	|		ПартииУУ.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|		ПартииУУ.СерияНоменклатуры          КАК СерияНоменклатуры,
	|		ПартииУУ.ДокументПередачи           КАК ДокументПередачи,
	|		ПартииУУ.ФизЛицо                    КАК ФизЛицо,
	|		ПартииУУ.СхемаНазначенияИспользования 	КАК СхемаНазначенияИспользования,
	|		ПартииУУ.СпособОтраженияРасходов 		КАК СпособОтраженияРасходов,
	|		ПартииУУ.СрокПолезногоИспользования 	КАК СрокПолезногоИспользования,
	|		ПартииУУ.Качество                   КАК Качество,
	|		СУММА(ПартииУУ.КоличествоОстаток)   КАК Количество
	|	{ВЫБРАТЬ
	|		Номенклатура,
	|		ХарактеристикаНоменклатуры,
	|		СерияНоменклатуры,
	|		ДокументПередачи,
	|		ФизЛицо,
	|		СхемаНазначенияИспользования,
	|		СпособОтраженияРасходов,
	|		СрокПолезногоИспользования,
	|		Качество}
	|	ИЗ
	|		РегистрНакопления.ПартииМатериаловВЭксплуатации.Остатки(&НаДату, 
	|		                  Подразделение = &Подразделение
	|		                  {Номенклатура.*               КАК Номенклатура,
	|		                   ХарактеристикаНоменклатуры.* КАК ХарактеристикаНоменклатуры,
	|		                   СерияНоменклатуры.*          КАК СерияНоменклатуры,
	|		                   ДокументПередачи.*           КАК ДокументПередачи,
	|		                   ФизЛицо.*                    КАК ФизЛицо,
	|		                   СхемаНазначенияИспользования.*    КАК СхемаНазначенияИспользования,
	|		                   СпособОтраженияРасходов.*    КАК СпособОтраженияРасходов,
	|		                   СрокПолезногоИспользования.* КАК СрокПолезногоИспользования,
	|		                   Качество.*                   КАК Качество,
	|							ВЫБОР
	|								КОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ДокументПередачи.Дата,ДЕНЬ), МЕСЯЦ, СрокПолезногоИспользования) <= НАЧАЛОПЕРИОДА(&ДатаДокумента, ДЕНЬ)
	|								ТОГДА ИСТИНА
	|								ИНАЧЕ ЛОЖЬ
	|							КОНЕЦ КАК СИстекшимСроком,
	|		                   РАЗНОСТЬДАТ(ДокументПередачи.Дата, &ДатаДокумента, МЕСЯЦ) КАК СрокВЭксплуатации,
	|		                   РАЗНОСТЬДАТ(&ДатаДокумента, ДОБАВИТЬКДАТЕ(ДокументПередачи.Дата, МЕСЯЦ, СрокПолезногоИспользования), МЕСЯЦ) КАК МесяцевДоОкончанияСрокаЭксплуатации,
	|		                   РАЗНОСТЬДАТ(ДОБАВИТЬКДАТЕ(ДокументПередачи.Дата, МЕСЯЦ, СрокПолезногоИспользования), &ДатаДокумента, МЕСЯЦ) КАК МесяцевПослеОкончанияСрокаЭксплуатации,
	|		                   НАЧАЛОПЕРИОДА(ДокументПередачи.Дата, ДЕНЬ)                КАК ДатаПередачи,
	|		                   ДокументОприходования.*      КАК ДокументОприходования}) КАК ПартииУУ
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ПартииУУ.Номенклатура,
	|		ПартииУУ.ХарактеристикаНоменклатуры,
	|		ПартииУУ.СерияНоменклатуры,
	|		ПартииУУ.ДокументПередачи,
	|		ПартииУУ.ФизЛицо,
	|		ПартииУУ.СхемаНазначенияИспользования,
	|		ПартииУУ.СпособОтраженияРасходов,
	|		ПартииУУ.СрокПолезногоИспользования,
	|		ПартииУУ.Качество", "") + ?(УУ И БУ, "
	|	
	|	ОБЪЕДИНИТЬ ВСЕ", "") + ?(БУ, "
	|	
	|	ВЫБРАТЬ
	|		ПартииБУ.Номенклатура               КАК Номенклатура,
	|		ПартииБУ.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|		ПартииБУ.СерияНоменклатуры          КАК СерияНоменклатуры,
	|		ПартииБУ.ДокументПередачи           КАК ДокументПередачи,
	|		ПартииБУ.ФизЛицо                    КАК ФизЛицо,
	|		ПартииБУ.СхемаНазначенияИспользования 	КАК СхемаНазначенияИспользования,
	|		ПартииБУ.СпособОтраженияРасходов 		КАК СпособОтраженияРасходов,
	|		ПартииБУ.СрокПолезногоИспользования 	КАК СрокПолезногоИспользования,
	|		ПартииБУ.Качество                   КАК Качество,
	|		СУММА(ПартииБУ.КоличествоОстаток)   КАК Количество
	|	{ВЫБРАТЬ
	|		Номенклатура,
	|		ХарактеристикаНоменклатуры,
	|		СерияНоменклатуры,
	|		ДокументПередачи,
	|		ФизЛицо,
	|		СхемаНазначенияИспользования,
	|		СпособОтраженияРасходов,
	|		СрокПолезногоИспользования,
	|		Качество}
	|	ИЗ
	|		РегистрНакопления.ПартииМатериаловВЭксплуатацииБухгалтерскийУчет.Остатки(&НаДату, 
	|		                  Подразделение = &ПодразделениеОрганизации 
	|		                  {Номенклатура.*               КАК Номенклатура,
	|		                   ХарактеристикаНоменклатуры.* КАК ХарактеристикаНоменклатуры,
	|		                   СерияНоменклатуры.*          КАК СерияНоменклатуры,
	|		                   ДокументПередачи.*           КАК ДокументПередачи,
	|		                   ФизЛицо.*                    КАК ФизЛицо,
	|		                   СхемаНазначенияИспользования.*    КАК СхемаНазначенияИспользования,
	|		                   СпособОтраженияРасходов.*    КАК СпособОтраженияРасходов,
	|		                   СрокПолезногоИспользования.* КАК СрокПолезногоИспользования,
	|		                   Качество.*                   КАК Качество,
	|							ВЫБОР
	|								КОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ДокументПередачи.Дата,ДЕНЬ), МЕСЯЦ, СрокПолезногоИспользования) <= НАЧАЛОПЕРИОДА(&ДатаДокумента, ДЕНЬ)
	|								ТОГДА ИСТИНА
	|								ИНАЧЕ ЛОЖЬ
	|							КОНЕЦ КАК СИстекшимСроком,
	|		                   РАЗНОСТЬДАТ(ДокументПередачи.Дата, &ДатаДокумента, МЕСЯЦ) КАК СрокВЭксплуатации,
	|		                   РАЗНОСТЬДАТ(&ДатаДокумента, ДОБАВИТЬКДАТЕ(ДокументПередачи.Дата, МЕСЯЦ, СрокПолезногоИспользования), МЕСЯЦ) КАК МесяцевДоОкончанияСрокаЭксплуатации,
	|		                   РАЗНОСТЬДАТ(ДОБАВИТЬКДАТЕ(ДокументПередачи.Дата, МЕСЯЦ, СрокПолезногоИспользования), &ДатаДокумента, МЕСЯЦ) КАК МесяцевПослеОкончанияСрокаЭксплуатации,
	|		                   НАЧАЛОПЕРИОДА(ДокументПередачи.Дата, ДЕНЬ)                КАК ДатаПередачи,
	|		                   ДокументОприходования.*      КАК ДокументОприходования}) КАК ПартииБУ
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ПартииБУ.Номенклатура,
	|		ПартииБУ.ХарактеристикаНоменклатуры,
	|		ПартииБУ.СерияНоменклатуры,
	|		ПартииБУ.ДокументПередачи,
	|		ПартииБУ.ФизЛицо,
	|		ПартииБУ.СхемаНазначенияИспользования,
	|		ПартииБУ.СпособОтраженияРасходов,
	|		ПартииБУ.СрокПолезногоИспользования,
	|		ПартииБУ.Качество", "") + "
	|	
	|	) КАК Партии
	|
	|СГРУППИРОВАТЬ ПО
	|	Партии.ФизЛицо,
	|	Партии.ДокументПередачи,
	|	Партии.СерияНоменклатуры,
	|	Партии.ХарактеристикаНоменклатуры,
	|	Партии.Номенклатура,
	|	Партии.Номенклатура.ЕдиницаХраненияОстатков,
	|	Партии.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент,
	|	Партии.СхемаНазначенияИспользования,
	|	Партии.СпособОтраженияРасходов,
	|	Партии.СрокПолезногоИспользования,
	|	Партии.Качество
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номенклатура,
	|	ХарактеристикаНоменклатуры,
	|	СерияНоменклатуры,
	|	СхемаНазначенияИспользования,
	|	СпособОтраженияРасходов,
	|	СрокПолезногоИспользования,
	|	ФизЛицо,
	|	Качество
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	ДоступныеПоля = ПостроительЗапроса.ДоступныеПоля;
	
	// Заполнение пользовательских представлений для доступных полей
	СтруктураПредставлениеПолей = Новый Структура;
	СтруктураПредставлениеПолей.Вставить("ХарактеристикаНоменклатуры", "Характеристика номенклатуры");
	СтруктураПредставлениеПолей.Вставить("СерияНоменклатуры"         , "Серия номенклатуры");
	СтруктураПредставлениеПолей.Вставить("ДокументПередачи"          , "Документ передачи в эксплуатацию");
	СтруктураПредставлениеПолей.Вставить("ДатаПередачи"              , "Дата передачи в эксплуатацию");
	СтруктураПредставлениеПолей.Вставить("СрокВЭксплуатации"         , "Месяцев в эксплуатации на дату документа");
	СтруктураПредставлениеПолей.Вставить("ФизЛицо"                   , "Работник");
	СтруктураПредставлениеПолей.Вставить("СхемаНазначенияИспользования", 	"Схема назначения использования");
	СтруктураПредставлениеПолей.Вставить("СпособОтраженияРасходов"   , 		"Способ отражения расходов");
	СтруктураПредставлениеПолей.Вставить("СрокПолезногоИспользования", 		"Срок полезного использования");
	СтруктураПредставлениеПолей.Вставить("ДокументОприходования"     , "Документ оприходования на предприятие");
	
	СтруктураПредставлениеПолей.Вставить("МесяцевПослеОкончанияСрокаЭксплуатации" , "Кво месяцев после окончания срока эксплуатации");
	СтруктураПредставлениеПолей.Вставить("МесяцевДоОкончанияСрокаЭксплуатации" , "Кво месяцев до окончания срока эксплуатации");
	
	СтруктураПредставлениеПолей.Вставить("СИстекшимСроком"         , "С истёкшим сроком");
	
	Для каждого КлючЗначение Из СтруктураПредставлениеПолей Цикл
		
		Поле = ДоступныеПоля.Найти(КлючЗначение.Ключ);
		
		Если Поле <> Неопределено  Тогда
			
			Поле.Представление = КлючЗначение.Значение;			
			
		КонецЕсли;		
	
	КонецЦикла;
	
	Если НЕ мУказДокПередачи Тогда
		
		// Нужно удалить из выбраных полей ДокументПередачи
		ИзмерениеДокументПередачи = ПостроительЗапроса.ВыбранныеПоля.Найти("ДокументПередачи");
		
		Если ИзмерениеДокументПередачи <> Неопределено Тогда
			
			 ПостроительЗапроса.ВыбранныеПоля.Удалить(ИзмерениеДокументПередачи);
			
		КонецЕсли;
		
		
	КонецЕсли;
	
	// заполнение доступных полей для реквизита обработки
	Отбор.УстановитьДоступныеПоля(ДоступныеПоля);
	
	// поля по умолчанию                                                                
	Отбор.Добавить("Номенклатура");
	Отбор.Добавить("ФизЛицо");
	Отбор.Добавить("ДатаПередачи"							, , "Дата передачи в эксплуатацию");
	Отбор.Добавить("СрокВЭксплуатации"						, , "Месяцев в эксплуатации на дату документа");
	
	Отбор.Добавить("МесяцевДоОкончанияСрокаЭксплуатации"	, , "Кво месяцев до окончания срока эксплуатации");
	Отбор.Добавить("МесяцевПослеОкончанияСрокаЭксплуатации"	, , "Кво месяцев после окончания срока эксплуатации");
	
	Отбор.Добавить("СИстекшимСроком"                        , , "С истёкшим сроком");
	
	Отбор.Добавить("СрокПолезногоИспользования", , "Срок полезного использования");
	
	ПостроительЗапроса.ДобавлениеПредставлений = ТипДобавленияПредставлений.НеДобавлять;
	
КонецПроцедуры // ЗаполнитьНачальныеНастройки()

// Процедура - заполняет начальные настройки заполнения
//
Процедура ЗаполнитьНачальныеНастройки_РАУЗ(Отказ) Экспорт
		
	УУ = ДокОбъект.ОтражатьВУправленческомУчете;
	БУ = ДокОбъект.ОтражатьВБухгалтерскомУчете;
	мУказДокПередачи = ДокОбъект.УказыватьДокументПередачи;
	
	ПостроительЗапроса = Новый ПостроительЗапроса;
	
	Если ДокОбъект.ЭтоНовый() Тогда
		
		ПостроительЗапроса.Параметры.Вставить("НаДату", Неопределено);
		
	Иначе
		
		ПостроительЗапроса.Параметры.Вставить("НаДату", ДокОбъект.МоментВремени());
		
	КонецЕсли;
	
	ПостроительЗапроса.Параметры.Вставить("ДатаДокумента"           , ДокОбъект.Дата);
	ПостроительЗапроса.Параметры.Вставить("Подразделение"           , ДокОбъект.Подразделение);
	ПостроительЗапроса.Параметры.Вставить("ПодразделениеОрганизации", ДокОбъект.ПодразделениеОрганизации);

	ПостроительЗапроса.Текст = 
	"ВЫБРАТЬ
	|	Партии.Номенклатура                                     КАК Номенклатура,
	|	Партии.ХарактеристикаНоменклатуры                       КАК ХарактеристикаНоменклатуры,
	|	Партии.СерияНоменклатуры                                КАК СерияНоменклатуры,
	|	Партии.Номенклатура.ЕдиницаХраненияОстатков             КАК ЕдиницаИзмерения,
	|	Партии.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент КАК Коэффициент,
	|	Партии.ДокументПередачи                                 КАК ДокументПередачи,
	|	Партии.ФизЛицо                                          КАК ФизЛицо,
	|	Партии.СхемаНазначенияИспользования                     КАК СхемаНазначенияИспользования,
	|	Партии.СпособОтраженияРасходов                          КАК СпособОтраженияРасходов,
	|	Партии.СрокПолезногоИспользования                       КАК СрокПолезногоИспользования,
	|	ЗНАЧЕНИЕ(Справочник.Качество.Новый)                     КАК Качество,
	|	МАКСИМУМ(ЕСТЬNULL(Партии.Количество, 0))                КАК Количество
	|{ВЫБРАТЬ
	|	Номенклатура                                     КАК Номенклатура,
	|	ХарактеристикаНоменклатуры                       КАК ХарактеристикаНоменклатуры,
	|	СерияНоменклатуры                                КАК СерияНоменклатуры,
	|	Номенклатура.ЕдиницаХраненияОстатков             КАК ЕдиницаИзмерения,
	|	Номенклатура.ЕдиницаХраненияОстатков.Коэффициент КАК Коэффициент,
	|	ДокументПередачи                                 КАК ДокументПередачи,
	|	ФизЛицо                                          КАК ФизЛицо,
	|	СхемаНазначенияИспользования                     КАК СхемаНазначенияИспользования,
	|	СпособОтраженияРасходов                          КАК СпособОтраженияРасходов,
	|	СрокПолезногоИспользования                       КАК СрокПолезногоИспользования,
	|	Качество                                         КАК Качество,
	|	Количество                                       КАК Количество}
	|ИЗ
	|	(" + ?(УУ, "
	|	 ВЫБРАТЬ
	|		РегистрАналитикаУчетаЗатрат.Затрата               			КАК Номенклатура,
	|		РегистрАналитикаУчетаЗатрат.ХарактеристикаЗатраты 			КАК ХарактеристикаНоменклатуры,
	|		РегистрАналитикаУчетаЗатрат.СерияЗатраты          			КАК СерияНоменклатуры,
	|		РегистрАналитикаУчетаПартий.ДокументПередачи           		КАК ДокументПередачи,
	|		РегистрАналитикаУчетаПартий.ФизЛицо                    		КАК ФизЛицо,
	|		РегистрАналитикаУчетаПартий.СхемаНазначенияИспользования 	КАК СхемаНазначенияИспользования,
	|		РегистрАналитикаУчетаПартий.СпособОтраженияРасходов 		КАК СпособОтраженияРасходов,
	|		РегистрАналитикаУчетаПартий.СрокПолезногоИспользования 		КАК СрокПолезногоИспользования,
	|		РегистрАналитикаУчетаЗатрат.Качество                   		КАК Качество,
	|		СУММА(УчетЗатрат.КоличествоОстаток)   						КАК Количество
	|	{ВЫБРАТЬ
	|		Номенклатура,
	|		ХарактеристикаНоменклатуры,
	|		СерияНоменклатуры,
	|		ДокументПередачи,
	|		ФизЛицо,
	|		СхемаНазначенияИспользования,
	|		СпособОтраженияРасходов,
	|		СрокПолезногоИспользования,
	|		Качество}
	|	ИЗ
	|		РегистрНакопления.УчетЗатрат.Остатки(&НаДату,
	|		                   АналитикаВидаУчета В (
	|								ВЫБРАТЬ РАЗЛИЧНЫЕ
	|									РегистрАналитикаВидаУчета.Ссылка
	|								ИЗ
	|									РегистрСведений.АналитикаВидаУчета КАК РегистрАналитикаВидаУчета
	|								ГДЕ
	|									РегистрАналитикаВидаУчета.РазделУчета В (
	|										ЗНАЧЕНИЕ(Перечисление.РазделыУчета.МатериалыВЭксплуатации) )
	|										)
	|			) КАК УчетЗатрат
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаВидаУчета КАК РегистрАналитикаВидаУчета
	|	ПО
	|		УчетЗатрат.АналитикаВидаУчета = РегистрАналитикаВидаУчета.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаЗатрат КАК РегистрАналитикаУчетаЗатрат
	|	ПО
	|		УчетЗатрат.АналитикаУчетаЗатрат = РегистрАналитикаУчетаЗатрат.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПартий КАК РегистрАналитикаУчетаПартий
	|	ПО
	|		УчетЗатрат.АналитикаУчетаПартий = РегистрАналитикаУчетаПартий.Ссылка
	|	ГДЕ
	|		РегистрАналитикаУчетаПартий.ДокументПередачи <> ЗНАЧЕНИЕ(Документ.ПередачаМатериаловВЭксплуатацию.ПустаяСсылка)
	|		И РегистрАналитикаВидаУчета.Подразделение = &Подразделение
	|{ГДЕ 
	|	РегистрАналитикаУчетаЗатрат.Затрата.* 						КАК Номенклатура,
	|	РегистрАналитикаУчетаЗатрат.ХарактеристикаЗатраты.* 		КАК ХарактеристикаНоменклатуры,
	|	РегистрАналитикаУчетаЗатрат.СерияЗатраты.*          		КАК СерияНоменклатуры,
	|	РегистрАналитикаУчетаПартий.ДокументПередачи.*           	КАК ДокументПередачи,
	|	РегистрАналитикаУчетаПартий.ФизЛицо.*                    	КАК ФизЛицо,
	|	РегистрАналитикаУчетаПартий.СхемаНазначенияИспользования.*	КАК СхемаНазначенияИспользования,
	|	РегистрАналитикаУчетаПартий.СпособОтраженияРасходов.*    	КАК СпособОтраженияРасходов,
	|	РегистрАналитикаУчетаПартий.СрокПолезногоИспользования.* 	КАК СрокПолезногоИспользования,
	|	РегистрАналитикаУчетаЗатрат.Качество.*                   	КАК Качество,
	|	ВЫБОР
	|		КОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(РегистрАналитикаУчетаПартий.ДокументПередачи.Дата,ДЕНЬ), МЕСЯЦ, РегистрАналитикаУчетаПартий.СрокПолезногоИспользования) <= НАЧАЛОПЕРИОДА(&ДатаДокумента, ДЕНЬ)
	|		ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СИстекшимСроком,
	|	РАЗНОСТЬДАТ(РегистрАналитикаУчетаПартий.ДокументПередачи.Дата, &ДатаДокумента, МЕСЯЦ) КАК СрокВЭксплуатации,
	|	РАЗНОСТЬДАТ(&ДатаДокумента, ДОБАВИТЬКДАТЕ(РегистрАналитикаУчетаПартий.ДокументПередачи.Дата, МЕСЯЦ, РегистрАналитикаУчетаПартий.СрокПолезногоИспользования), МЕСЯЦ) КАК МесяцевДоОкончанияСрокаЭксплуатации,
	|	РАЗНОСТЬДАТ(ДОБАВИТЬКДАТЕ(РегистрАналитикаУчетаПартий.ДокументПередачи.Дата, МЕСЯЦ, РегистрАналитикаУчетаПартий.СрокПолезногоИспользования), &ДатаДокумента, МЕСЯЦ) КАК МесяцевПослеОкончанияСрокаЭксплуатации,
	|	НАЧАЛОПЕРИОДА(РегистрАналитикаУчетаПартий.ДокументПередачи.Дата, ДЕНЬ)                КАК ДатаПередачи
	|}
	|	
	|	СГРУППИРОВАТЬ ПО
	|		РегистрАналитикаУчетаЗатрат.Затрата,
	|		РегистрАналитикаУчетаЗатрат.ХарактеристикаЗатраты,
	|		РегистрАналитикаУчетаЗатрат.СерияЗатраты,
	|		РегистрАналитикаУчетаПартий.ДокументПередачи,
	|		РегистрАналитикаУчетаПартий.ФизЛицо,
	|		РегистрАналитикаУчетаПартий.СхемаНазначенияИспользования,
	|		РегистрАналитикаУчетаПартий.СпособОтраженияРасходов,
	|		РегистрАналитикаУчетаПартий.СрокПолезногоИспользования,
	|		РегистрАналитикаУчетаЗатрат.Качество", "") + ?(УУ И БУ, "
	|	
	|	ОБЪЕДИНИТЬ ВСЕ", "") + ?(БУ, "
	|	
	|	ВЫБРАТЬ
	|		РегистрАналитикаУчетаЗатрат.Затрата               			КАК Номенклатура,
	|		РегистрАналитикаУчетаЗатрат.ХарактеристикаЗатраты 			КАК ХарактеристикаНоменклатуры,
	|		РегистрАналитикаУчетаЗатрат.СерияЗатраты          			КАК СерияНоменклатуры,
	|		РегистрАналитикаУчетаПартий.ДокументПередачи           		КАК ДокументПередачи,
	|		РегистрАналитикаУчетаПартий.ФизЛицо                    		КАК ФизЛицо,
	|		РегистрАналитикаУчетаПартий.СхемаНазначенияИспользования 	КАК СхемаНазначенияИспользования,
	|		РегистрАналитикаУчетаПартий.СпособОтраженияРасходов 		КАК СпособОтраженияРасходов,
	|		РегистрАналитикаУчетаПартий.СрокПолезногоИспользования 		КАК СрокПолезногоИспользования,
	|		РегистрАналитикаУчетаЗатрат.Качество                   		КАК Качество,
	|		СУММА(УчетЗатрат.КоличествоОстаток)   						КАК Количество
	|	{ВЫБРАТЬ
	|		Номенклатура,
	|		ХарактеристикаНоменклатуры,
	|		СерияНоменклатуры,
	|		ДокументПередачи,
	|		ФизЛицо,
	|		СхемаНазначенияИспользования,
	|		СпособОтраженияРасходов,
	|		СрокПолезногоИспользования,
	|		Качество}
	|	ИЗ
	|		РегистрНакопления.УчетЗатратРегл.Остатки(&НаДату,
	|		                   АналитикаВидаУчета В (
	|								ВЫБРАТЬ РАЗЛИЧНЫЕ
	|									РегистрАналитикаВидаУчета.Ссылка
	|								ИЗ
	|									РегистрСведений.АналитикаВидаУчета КАК РегистрАналитикаВидаУчета
	|								ГДЕ
	|									РегистрАналитикаВидаУчета.РазделУчета В (
	|										ЗНАЧЕНИЕ(Перечисление.РазделыУчета.МатериалыВЭксплуатации) )
	|										)
	|			) КАК УчетЗатрат
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаВидаУчета КАК РегистрАналитикаВидаУчета
	|	ПО
	|		УчетЗатрат.АналитикаВидаУчета = РегистрАналитикаВидаУчета.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаЗатрат КАК РегистрАналитикаУчетаЗатрат
	|	ПО
	|		УчетЗатрат.АналитикаУчетаЗатрат = РегистрАналитикаУчетаЗатрат.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПартий КАК РегистрАналитикаУчетаПартий
	|	ПО
	|		УчетЗатрат.АналитикаУчетаПартий = РегистрАналитикаУчетаПартий.Ссылка
	|	ГДЕ
	|		РегистрАналитикаУчетаПартий.ДокументПередачи <> ЗНАЧЕНИЕ(Документ.ПередачаМатериаловВЭксплуатацию.ПустаяСсылка)
	|		И РегистрАналитикаВидаУчета.ПодразделениеОрганизации = &ПодразделениеОрганизации
	|{ГДЕ 
	|	РегистрАналитикаУчетаЗатрат.Затрата.* 						КАК Номенклатура,
	|	РегистрАналитикаУчетаЗатрат.ХарактеристикаЗатраты.* 		КАК ХарактеристикаНоменклатуры,
	|	РегистрАналитикаУчетаЗатрат.СерияЗатраты.*          		КАК СерияНоменклатуры,
	|	РегистрАналитикаУчетаПартий.ДокументПередачи.*           	КАК ДокументПередачи,
	|	РегистрАналитикаУчетаПартий.ФизЛицо.*                    	КАК ФизЛицо,
	|	РегистрАналитикаУчетаПартий.СхемаНазначенияИспользования.*	КАК СхемаНазначенияИспользования,
	|	РегистрАналитикаУчетаПартий.СпособОтраженияРасходов.*    	КАК СпособОтраженияРасходов,
	|	РегистрАналитикаУчетаПартий.СрокПолезногоИспользования.* 	КАК СрокПолезногоИспользования,
	|	РегистрАналитикаУчетаЗатрат.Качество.*                   	КАК Качество,
	|	ВЫБОР
	|		КОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(РегистрАналитикаУчетаПартий.ДокументПередачи.Дата,ДЕНЬ), МЕСЯЦ, РегистрАналитикаУчетаПартий.СрокПолезногоИспользования) <= НАЧАЛОПЕРИОДА(&ДатаДокумента, ДЕНЬ)
	|		ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СИстекшимСроком,
	|	РАЗНОСТЬДАТ(РегистрАналитикаУчетаПартий.ДокументПередачи.Дата, &ДатаДокумента, МЕСЯЦ) КАК СрокВЭксплуатации,
	|	РАЗНОСТЬДАТ(&ДатаДокумента, ДОБАВИТЬКДАТЕ(РегистрАналитикаУчетаПартий.ДокументПередачи.Дата, МЕСЯЦ, РегистрАналитикаУчетаПартий.СрокПолезногоИспользования), МЕСЯЦ) КАК МесяцевДоОкончанияСрокаЭксплуатации,
	|	РАЗНОСТЬДАТ(ДОБАВИТЬКДАТЕ(РегистрАналитикаУчетаПартий.ДокументПередачи.Дата, МЕСЯЦ, РегистрАналитикаУчетаПартий.СрокПолезногоИспользования), &ДатаДокумента, МЕСЯЦ) КАК МесяцевПослеОкончанияСрокаЭксплуатации,
	|	НАЧАЛОПЕРИОДА(РегистрАналитикаУчетаПартий.ДокументПередачи.Дата, ДЕНЬ) 	КАК ДатаПередачи
	|}
	|	
	|	СГРУППИРОВАТЬ ПО
	|		РегистрАналитикаУчетаЗатрат.Затрата,
	|		РегистрАналитикаУчетаЗатрат.ХарактеристикаЗатраты,
	|		РегистрАналитикаУчетаЗатрат.СерияЗатраты,
	|		РегистрАналитикаУчетаПартий.ДокументПередачи,
	|		РегистрАналитикаУчетаПартий.ФизЛицо,
	|		РегистрАналитикаУчетаПартий.СхемаНазначенияИспользования,
	|		РегистрАналитикаУчетаПартий.СпособОтраженияРасходов,
	|		РегистрАналитикаУчетаПартий.СрокПолезногоИспользования,
	|		РегистрАналитикаУчетаЗатрат.Качество", "") + "
	|	) КАК Партии
	|
	|СГРУППИРОВАТЬ ПО
	|	Партии.ФизЛицо,
	|	Партии.ДокументПередачи,
	|	Партии.СерияНоменклатуры,
	|	Партии.ХарактеристикаНоменклатуры,
	|	Партии.Номенклатура,
	|	Партии.Номенклатура.ЕдиницаХраненияОстатков,
	|	Партии.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент,
	|	Партии.СхемаНазначенияИспользования,
	|	Партии.СпособОтраженияРасходов,
	|	Партии.СрокПолезногоИспользования,
	|	Партии.Качество
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номенклатура,
	|	ХарактеристикаНоменклатуры,
	|	СерияНоменклатуры,
	|	СхемаНазначенияИспользования,
	|	СпособОтраженияРасходов,
	|	СрокПолезногоИспользования,
	|	ФизЛицо,
	|	Качество
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	ДоступныеПоля = ПостроительЗапроса.ДоступныеПоля;
	
	// Заполнение пользовательских представлений для доступных полей
	СтруктураПредставлениеПолей = Новый Структура;
	СтруктураПредставлениеПолей.Вставить("ХарактеристикаНоменклатуры", "Характеристика номенклатуры");
	СтруктураПредставлениеПолей.Вставить("СерияНоменклатуры"         , "Серия номенклатуры");
	СтруктураПредставлениеПолей.Вставить("ДокументПередачи"          , "Документ передачи в эксплуатацию");
	СтруктураПредставлениеПолей.Вставить("ДатаПередачи"              , "Дата передачи в эксплуатацию");
	СтруктураПредставлениеПолей.Вставить("СрокВЭксплуатации"         , "Месяцев в эксплуатации на дату документа");
	СтруктураПредставлениеПолей.Вставить("ФизЛицо"                   , "Работник");
	СтруктураПредставлениеПолей.Вставить("СхемаНазначенияИспользования", 	"Схема назначения использования");
	СтруктураПредставлениеПолей.Вставить("СпособОтраженияРасходов"   , 		"Способ отражения расходов");
	СтруктураПредставлениеПолей.Вставить("СрокПолезногоИспользования", 		"Срок полезного использования");
	СтруктураПредставлениеПолей.Вставить("ДокументОприходования"     , "Документ оприходования на предприятие");
	
	СтруктураПредставлениеПолей.Вставить("МесяцевПослеОкончанияСрокаЭксплуатации" , "Кво месяцев после окончания срока эксплуатации");
	СтруктураПредставлениеПолей.Вставить("МесяцевДоОкончанияСрокаЭксплуатации" , "Кво месяцев до окончания срока эксплуатации");
	
	СтруктураПредставлениеПолей.Вставить("СИстекшимСроком"         , "С истёкшим сроком");
	
	Для каждого КлючЗначение Из СтруктураПредставлениеПолей Цикл
		
		Поле = ДоступныеПоля.Найти(КлючЗначение.Ключ);
		
		Если Поле <> Неопределено  Тогда
			
			Поле.Представление = КлючЗначение.Значение;			
			
		КонецЕсли;		
	
	КонецЦикла;
	
	Если НЕ мУказДокПередачи Тогда
		
		// Нужно удалить из выбраных полей ДокументПередачи
		ИзмерениеДокументПередачи = ПостроительЗапроса.ВыбранныеПоля.Найти("ДокументПередачи");
		
		Если ИзмерениеДокументПередачи <> Неопределено Тогда
			
			 ПостроительЗапроса.ВыбранныеПоля.Удалить(ИзмерениеДокументПередачи);
			
		КонецЕсли;
		
		
	КонецЕсли;
	
	// заполнение доступных полей для реквизита обработки
	Отбор.УстановитьДоступныеПоля(ДоступныеПоля);
	
	// поля по умолчанию                                                                
	Отбор.Добавить("Номенклатура");
	Отбор.Добавить("ФизЛицо");
	Отбор.Добавить("ДатаПередачи"							, , "Дата передачи в эксплуатацию");
	Отбор.Добавить("СрокВЭксплуатации"						, , "Месяцев в эксплуатации на дату документа");
	
	Отбор.Добавить("МесяцевДоОкончанияСрокаЭксплуатации"	, , "Кво месяцев до окончания срока эксплуатации");
	Отбор.Добавить("МесяцевПослеОкончанияСрокаЭксплуатации"	, , "Кво месяцев после окончания срока эксплуатации");
	
	Отбор.Добавить("СИстекшимСроком"                        , , "С истёкшим сроком");
	
	Отбор.Добавить("СрокПолезногоИспользования", , "Срок полезного использования");
	
	ПостроительЗапроса.ДобавлениеПредставлений = ТипДобавленияПредставлений.НеДобавлять;
	
КонецПроцедуры // ЗаполнитьНачальныеНастройки()
