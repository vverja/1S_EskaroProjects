Перем мПодразделенияДокументы Экспорт;  // соответствие подразделений и документов перерасчета
Перем мРаботникиДокументы Экспорт; // соответствие работников и документов перерасчета

Функция СформироватьЗапрос(Организации = Неопределено, ТекущийПериодРегистрации = Неопределено, СписокПомеченныхДокументов = Неопределено, МаксимальноеКоличествоСобытий = 0) Экспорт
	
	Если Не ЗначениеЗаполнено(ТекущийПериодРегистрации) Тогда
		ТекущийПериодРегистрации = НачалоМесяца(ОбщегоНазначения.ПолучитьРабочуюДату())
	КонецЕсли;	
	
	ТекстПервыхЗаписей = ?(МаксимальноеКоличествоСобытий = 0, "", " ПЕРВЫЕ " + МаксимальноеКоличествоСобытий);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("ТекущийПериод",				ТекущийПериодРегистрации);
	Запрос.УстановитьПараметр("СписокПомеченныхДокументов",	СписокПомеченныхДокументов);
	Запрос.УстановитьПараметр("ПоВсемОрганизациям",			Не ЗначениеЗаполнено(Организации));
	Запрос.УстановитьПараметр("Организация",				Организации);
	
	// документы к перерасчету без отборов по периоду начисления и организации
	// ВТДокументыКПерерасчету
	Если Истина Тогда
		Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПерерасчетОсновныхНачислений.ОбъектПерерасчета КАК Документ
		|ПОМЕСТИТЬ ВТДокументыКПерерасчету
		|ИЗ
		|	РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций.ПерерасчетОсновныхНачислений КАК ПерерасчетОсновныхНачислений
		|ГДЕ
		|	(&ПоВсемОрганизациям
		|			ИЛИ ПерерасчетОсновныхНачислений.Организация В (&Организация))
		|	И (НЕ ПерерасчетОсновныхНачислений.ОбъектПерерасчета ССЫЛКА Документ.Сторнирование)
		|
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПерерасчетВзносовВФонды.ОбъектПерерасчета
		|ИЗ
		|	РегистрРасчета.ВзносыВФонды.ПерерасчетВзносовВФонды КАК ПерерасчетВзносовВФонды
		|ГДЕ
		|	(&ПоВсемОрганизациям
		|			ИЛИ ПерерасчетВзносовВФонды.Организация В (&Организация))
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПерерасчетСреднегоЗаработка.ОбъектПерерасчета
		|ИЗ
		|	РегистрРасчета.РасчетСреднегоЗаработка.ПерерасчетСреднегоЗаработка КАК ПерерасчетСреднегоЗаработка
		|ГДЕ
		|	(&ПоВсемОрганизациям
		|			ИЛИ ПерерасчетСреднегоЗаработка.Организация В (&Организация))
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЗаполнениеПлановыхНачислений.ОбъектЗаполнения
		|ИЗ
		|	РегистрСведений.ЗаполнениеПлановыхНачислений КАК ЗаполнениеПлановыхНачислений
		|ГДЕ
		|	ЗаполнениеПлановыхНачислений.ПериодРегистрации < &ТекущийПериод
		|	И (&ПоВсемОрганизациям
		|			ИЛИ ЗаполнениеПлановыхНачислений.Организация В (&Организация))
		|	И ЗаполнениеПлановыхНачислений.ПериодРегистрации <> ДАТАВРЕМЯ(1, 1, 1)
		|ИНДЕКСИРОВАТЬ ПО
		|	Документ
		|";
	КонецЕсли;
	
	//Если Запрос.Выполнить().Выгрузить()[0].Количество = 0 Тогда // перерассчитывать нечего - остальные запросы исполнять не будем
	//	
	//	Запрос.Текст =
	//	"ВЫБРАТЬ
	//	|	ПустаяТаблица.Документ,
	//	|	ДАТАВРЕМЯ(1, 1, 1) КАК ПериодРегистрации,
	//	|	"""" КАК НаименованиеОрганизации,
	//	|	"""" КАК Наименование,
	//	|	"""" КАК Ссылка,
	//	|	"""" КАК Организация
	//	|ИЗ
	//	|	ВТДокументыКПерерасчету КАК ПустаяТаблица";
	//	
	//	Возврат Запрос.Выполнить()
	//
	//КонецЕсли;
	Запрос.Выполнить();
	Запрос.Текст = "ВЫБРАТЬ * ИЗ ВТДокументыКПерерасчету";
	
	// однострочные документы к перерасчету предыдущих периодов с отбором по организации без исправленных документов (которых пересчитывать уже нельзя)
	// ВТНачислениеОтпуска
	Если Истина Тогда
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ " + ТекстПервыхЗаписей + "
		|	НачислениеОтпускаРаботникамОрганизаций.Ссылка КАК Документ,
		|	НачислениеОтпускаРаботникамОрганизаций.ПериодРегистрации,
		|	НачислениеОтпускаРаботникамОрганизаций.Организация
		|ПОМЕСТИТЬ ВТНачислениеОтпуска
		|ИЗ
		|	Документ.НачислениеОтпускаРаботникамОрганизаций КАК НачислениеОтпускаРаботникамОрганизаций
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.НачислениеОтпускаРаботникамОрганизаций КАК Исправления
		|		ПО НачислениеОтпускаРаботникамОрганизаций.Ссылка = Исправления.ПерерассчитываемыйДокумент
		|ГДЕ
		|	НачислениеОтпускаРаботникамОрганизаций.Ссылка В
		|			(ВЫБРАТЬ
		|				ДокументыКПерерасчету.Документ
		|			ИЗ
		|				ВТДокументыКПерерасчету КАК ДокументыКПерерасчету)
		|	И (&ПоВсемОрганизациям
		|			ИЛИ НачислениеОтпускаРаботникамОрганизаций.Организация В (&Организация))
		|	И НачислениеОтпускаРаботникамОрганизаций.ПериодРегистрации <= &ТекущийПериод
		|	И Исправления.Ссылка ЕСТЬ NULL 
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Документ";
		Запрос.Выполнить();
	КонецЕсли;
	
	// ВТНачислениеПоБольничномуЛисту
	Если Истина Тогда
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ " + ТекстПервыхЗаписей + "
		|	НачислениеПоБольничномуЛисту.Ссылка КАК Документ,
		|	НачислениеПоБольничномуЛисту.ПериодРегистрации,
		|	НачислениеПоБольничномуЛисту.Организация
		|ПОМЕСТИТЬ ВТНачислениеПоБольничномуЛисту
		|ИЗ
		|	Документ.НачислениеПоБольничномуЛисту КАК НачислениеПоБольничномуЛисту
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.НачислениеПоБольничномуЛисту КАК Исправления
		|		ПО НачислениеПоБольничномуЛисту.Ссылка = Исправления.ПерерассчитываемыйДокумент
		|ГДЕ
		|	НачислениеПоБольничномуЛисту.Ссылка В
		|			(ВЫБРАТЬ
		|				ДокументыКПерерасчету.Документ
		|			ИЗ
		|				ВТДокументыКПерерасчету КАК ДокументыКПерерасчету)
		|	И (&ПоВсемОрганизациям
		|			ИЛИ НачислениеПоБольничномуЛисту.Организация В (&Организация))
		|	И НачислениеПоБольничномуЛисту.ПериодРегистрации <= &ТекущийПериод
		|	И Исправления.Ссылка ЕСТЬ NULL 
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Документ";
		Запрос.Выполнить();
	КонецЕсли;
	
	// ВТОплатаПоСреднемуЗаработку
	Если Истина Тогда
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ " + ТекстПервыхЗаписей + "
		|	ОплатаПоСреднемуЗаработку.Ссылка КАК Документ,
		|	ОплатаПоСреднемуЗаработку.ПериодРегистрации,
		|	ОплатаПоСреднемуЗаработку.Организация
		|ПОМЕСТИТЬ ВТОплатаПоСреднемуЗаработку
		|ИЗ
		|	Документ.ОплатаПоСреднемуЗаработку КАК ОплатаПоСреднемуЗаработку
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОплатаПоСреднемуЗаработку КАК Исправления
		|		ПО ОплатаПоСреднемуЗаработку.Ссылка = Исправления.ПерерассчитываемыйДокумент
		|ГДЕ
		|	ОплатаПоСреднемуЗаработку.Ссылка В
		|			(ВЫБРАТЬ
		|				ДокументыКПерерасчету.Документ
		|			ИЗ
		|				ВТДокументыКПерерасчету КАК ДокументыКПерерасчету)
		|	И (&ПоВсемОрганизациям
		|			ИЛИ ОплатаПоСреднемуЗаработку.Организация В (&Организация))
		|	И ОплатаПоСреднемуЗаработку.ПериодРегистрации <= &ТекущийПериод
		|	И Исправления.Ссылка ЕСТЬ NULL 
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Документ";
		Запрос.Выполнить();
	КонецЕсли;
	
	// ВТРасчетПриУвольнении
	Если Истина Тогда
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ " + ТекстПервыхЗаписей + "
		|	РасчетПриУвольненииРаботникаОрганизаций.Ссылка КАК Документ,
		|	РасчетПриУвольненииРаботникаОрганизаций.ПериодРегистрации,
		|	РасчетПриУвольненииРаботникаОрганизаций.Организация
		|ПОМЕСТИТЬ ВТРасчетПриУвольнении
		|ИЗ
		|	Документ.РасчетПриУвольненииРаботникаОрганизаций КАК РасчетПриУвольненииРаботникаОрганизаций
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасчетПриУвольненииРаботникаОрганизаций КАК Исправления
		|		ПО РасчетПриУвольненииРаботникаОрганизаций.Ссылка = Исправления.ПерерассчитываемыйДокумент
		|ГДЕ
		|	РасчетПриУвольненииРаботникаОрганизаций.Ссылка В
		|			(ВЫБРАТЬ
		|				ДокументыКПерерасчету.Документ
		|			ИЗ
		|				ВТДокументыКПерерасчету КАК ДокументыКПерерасчету)
		|	И (&ПоВсемОрганизациям
		|			ИЛИ РасчетПриУвольненииРаботникаОрганизаций.Организация В (&Организация))
		|	И РасчетПриУвольненииРаботникаОрганизаций.ПериодРегистрации <= &ТекущийПериод
		|	И Исправления.Ссылка ЕСТЬ NULL 
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Документ";
		Запрос.Выполнить();
	КонецЕсли;
	
	// ВТРегистрацияПростоев
	Если Истина Тогда
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ " + ТекстПервыхЗаписей + "
		|	РегистрацияПростоевВОрганизациях.Ссылка КАК Документ,
		|	РегистрацияПростоевВОрганизациях.ПериодРегистрации,
		|	РегистрацияПростоевВОрганизациях.Организация
		|ПОМЕСТИТЬ ВТРегистрацияПростоев
		|ИЗ
		|	Документ.РегистрацияПростоевВОрганизациях КАК РегистрацияПростоевВОрганизациях
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РегистрацияПростоевВОрганизациях КАК Исправления
		|		ПО РегистрацияПростоевВОрганизациях.Ссылка = Исправления.ПерерассчитываемыйДокумент
		|ГДЕ
		|	РегистрацияПростоевВОрганизациях.Ссылка В
		|			(ВЫБРАТЬ
		|				ДокументыКПерерасчету.Документ
		|			ИЗ
		|				ВТДокументыКПерерасчету КАК ДокументыКПерерасчету)
		|	И (&ПоВсемОрганизациям
		|			ИЛИ РегистрацияПростоевВОрганизациях.Организация В (&Организация))
		|	И РегистрацияПростоевВОрганизациях.ПериодРегистрации <= &ТекущийПериод
		|	И Исправления.Ссылка ЕСТЬ NULL 
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Документ";
		Запрос.Выполнить();
	КонецЕсли;
	
	// документы к перерасчету предыдущих периодов с отбором по организации, но включая исправленные документы
	// ВТНачислениеЗарплаты
	Если Истина Тогда
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ " + ТекстПервыхЗаписей + "
		|	НачислениеЗарплатыРаботникамОрганизаций.Ссылка КАК Документ,
		|	НачислениеЗарплатыРаботникамОрганизаций.ПериодРегистрации КАК ПериодРегистрации,
		|	НачислениеЗарплатыРаботникамОрганизаций.Организация
		|ПОМЕСТИТЬ ВТНачислениеЗарплаты
		|ИЗ
		|	Документ.НачислениеЗарплатыРаботникамОрганизаций КАК НачислениеЗарплатыРаботникамОрганизаций
		|ГДЕ
		|	НачислениеЗарплатыРаботникамОрганизаций.Ссылка В
		|			(ВЫБРАТЬ
		|				ДокументыКПерерасчету.Документ
		|			ИЗ
		|				ВТДокументыКПерерасчету КАК ДокументыКПерерасчету)
		|	И (&ПоВсемОрганизациям
		|			ИЛИ НачислениеЗарплатыРаботникамОрганизаций.Организация В (&Организация))
		|	И НачислениеЗарплатыРаботникамОрганизаций.ПериодРегистрации <= &ТекущийПериод
		|	И (НачислениеЗарплатыРаботникамОрганизаций.Проведен
		|			ИЛИ НачислениеЗарплатыРаботникамОрганизаций.ПериодРегистрации = &ТекущийПериод)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Документ
		|";
		Запрос.Выполнить();
	КонецЕсли;
	Запрос.Текст = "ВЫБРАТЬ * ИЗ ВТНачислениеЗарплаты";
	
	// ВТОплатаПраздничных
	Если Истина Тогда
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ " + ТекстПервыхЗаписей + "
		|	ОплатаПраздничныхИВыходныхДнейОрганизаций.Ссылка КАК Документ,
		|	ОплатаПраздничныхИВыходныхДнейОрганизаций.ПериодРегистрации КАК ПериодРегистрации,
		|	ОплатаПраздничныхИВыходныхДнейОрганизаций.Организация
		|ПОМЕСТИТЬ ВТОплатаПраздничных
		|ИЗ
		|	Документ.ОплатаПраздничныхИВыходныхДнейОрганизаций КАК ОплатаПраздничныхИВыходныхДнейОрганизаций
		|ГДЕ
		|	ОплатаПраздничныхИВыходныхДнейОрганизаций.Ссылка В
		|			(ВЫБРАТЬ
		|				ДокументыКПерерасчету.Документ
		|			ИЗ
		|				ВТДокументыКПерерасчету КАК ДокументыКПерерасчету)
		|	И (&ПоВсемОрганизациям
		|			ИЛИ ОплатаПраздничныхИВыходныхДнейОрганизаций.Организация В (&Организация))
		|	И ОплатаПраздничныхИВыходныхДнейОрганизаций.ПериодРегистрации <= &ТекущийПериод
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Документ";
		Запрос.Выполнить();
	КонецЕсли;
	
	// ВТНевыходыВОрганизациях
	Если Истина Тогда
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ " + ТекстПервыхЗаписей + "
		|	НевыходыВОрганизациях.Ссылка КАК Документ,
		|	НевыходыВОрганизациях.ПериодРегистрации КАК ПериодРегистрации,
		|	НевыходыВОрганизациях.Организация
		|ПОМЕСТИТЬ ВТНевыходыВОрганизациях
		|ИЗ
		|	Документ.НевыходыВОрганизациях КАК НевыходыВОрганизациях
		|ГДЕ
		|	НевыходыВОрганизациях.Ссылка В
		|			(ВЫБРАТЬ
		|				ДокументыКПерерасчету.Документ
		|			ИЗ
		|				ВТДокументыКПерерасчету КАК ДокументыКПерерасчету)
		|	И (&ПоВсемОрганизациям
		|			ИЛИ НевыходыВОрганизациях.Организация В (&Организация))
		|	И НевыходыВОрганизациях.ПериодРегистрации <= &ТекущийПериод
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Документ";
		Запрос.Выполнить();
	КонецЕсли;
	
	// ВТОплатаСверхурочныхЧасов
	Если Истина Тогда
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ " + ТекстПервыхЗаписей + "
		|	ОплатаСверхурочныхЧасов.Ссылка КАК Документ,
		|	ОплатаСверхурочныхЧасов.ПериодРегистрации КАК ПериодРегистрации,
		|	ОплатаСверхурочныхЧасов.Организация
		|ПОМЕСТИТЬ ВТОплатаСверхурочныхЧасов
		|ИЗ
		|	Документ.ОплатаСверхурочныхЧасов КАК ОплатаСверхурочныхЧасов
		|ГДЕ
		|	ОплатаСверхурочныхЧасов.Ссылка В
		|			(ВЫБРАТЬ
		|				ДокументыКПерерасчету.Документ
		|			ИЗ
		|				ВТДокументыКПерерасчету КАК ДокументыКПерерасчету)
		|	И (&ПоВсемОрганизациям
		|			ИЛИ ОплатаСверхурочныхЧасов.Организация В (&Организация))
		|	И ОплатаСверхурочныхЧасов.ПериодРегистрации <= &ТекущийПериод
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Документ";
		Запрос.Выполнить();
	КонецЕсли;
	
	// ВТПремии
	Если Истина Тогда
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ " + ТекстПервыхЗаписей + "
		|	ПремииРаботниковОрганизаций.Ссылка КАК Документ,
		|	ПремииРаботниковОрганизаций.ПериодРегистрации КАК ПериодРегистрации,
		|	ПремииРаботниковОрганизаций.Организация
		|ПОМЕСТИТЬ ВТПремии
		|ИЗ
		|	Документ.ПремииРаботниковОрганизаций КАК ПремииРаботниковОрганизаций
		|ГДЕ
		|	ПремииРаботниковОрганизаций.Ссылка В
		|			(ВЫБРАТЬ
		|				ДокументыКПерерасчету.Документ
		|			ИЗ
		|				ВТДокументыКПерерасчету КАК ДокументыКПерерасчету)
		|	И (&ПоВсемОрганизациям
		|			ИЛИ ПремииРаботниковОрганизаций.Организация В (&Организация))
		|	И ПремииРаботниковОрганизаций.ПериодРегистрации <= &ТекущийПериод
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Документ";
		Запрос.Выполнить();
	КонецЕсли;
	
	// ВТРазовыеНачисления
	Если Истина Тогда
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ " + ТекстПервыхЗаписей + "
		|	РегистрацияРазовыхНачисленийРаботниковОрганизаций.Ссылка КАК Документ,
		|	РегистрацияРазовыхНачисленийРаботниковОрганизаций.ПериодРегистрации КАК ПериодРегистрации,
		|	РегистрацияРазовыхНачисленийРаботниковОрганизаций.Организация
		|ПОМЕСТИТЬ ВТРазовыеНачисления
		|ИЗ
		|	Документ.РегистрацияРазовыхНачисленийРаботниковОрганизаций КАК РегистрацияРазовыхНачисленийРаботниковОрганизаций
		|ГДЕ
		|	РегистрацияРазовыхНачисленийРаботниковОрганизаций.Ссылка В
		|			(ВЫБРАТЬ
		|				ДокументыКПерерасчету.Документ
		|			ИЗ
		|				ВТДокументыКПерерасчету КАК ДокументыКПерерасчету)
		|	И (&ПоВсемОрганизациям
		|			ИЛИ РегистрацияРазовыхНачисленийРаботниковОрганизаций.Организация В (&Организация))
		|	И РегистрацияРазовыхНачисленийРаботниковОрганизаций.ПериодРегистрации <= &ТекущийПериод
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Документ";
		Запрос.Выполнить();
	КонецЕсли;
	
	// документы-исправления выбранных ранее документов, нуждающихся в перерасчете - только многострочные
	// ВТДокументыИсправления
	Если Истина Тогда
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	НачислениеЗарплатыРаботникамОрганизаций.Ссылка КАК Документ,
		|	НачислениеЗарплатыРаботникамОрганизаций.ПерерассчитываемыйДокумент КАК ИсходныйДокумент
		|ПОМЕСТИТЬ ВТДокументыИсправления
		|ИЗ
		|	Документ.НачислениеЗарплатыРаботникамОрганизаций КАК НачислениеЗарплатыРаботникамОрганизаций
		|ГДЕ
		|	НачислениеЗарплатыРаботникамОрганизаций.ПерерассчитываемыйДокумент В
		|			(ВЫБРАТЬ
		|				ДокументыКПерерасчету.Документ
		|			ИЗ
		|				ВТНачислениеЗарплаты КАК ДокументыКПерерасчету)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ОплатаПраздничныхИВыходныхДнейОрганизаций.Ссылка,
		|	ОплатаПраздничныхИВыходныхДнейОрганизаций.ПерерассчитываемыйДокумент
		|ИЗ
		|	Документ.ОплатаПраздничныхИВыходныхДнейОрганизаций КАК ОплатаПраздничныхИВыходныхДнейОрганизаций
		|ГДЕ
		|	ОплатаПраздничныхИВыходныхДнейОрганизаций.ПерерассчитываемыйДокумент В
		|			(ВЫБРАТЬ
		|				ДокументыКПерерасчету.Документ
		|			ИЗ
		|				ВТОплатаПраздничных КАК ДокументыКПерерасчету)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	НевыходыВОрганизациях.Ссылка,
		|	НевыходыВОрганизациях.ПерерассчитываемыйДокумент
		|ИЗ
		|	Документ.НевыходыВОрганизациях КАК НевыходыВОрганизациях
		|ГДЕ
		|	НевыходыВОрганизациях.ПерерассчитываемыйДокумент В
		|			(ВЫБРАТЬ
		|				ДокументыКПерерасчету.Документ
		|			ИЗ
		|				ВТНевыходыВОрганизациях КАК ДокументыКПерерасчету)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ОплатаСверхурочныхЧасов.Ссылка,
		|	ОплатаСверхурочныхЧасов.ПерерассчитываемыйДокумент
		|ИЗ
		|	Документ.ОплатаСверхурочныхЧасов КАК ОплатаСверхурочныхЧасов
		|ГДЕ
		|	ОплатаСверхурочныхЧасов.ПерерассчитываемыйДокумент В
		|			(ВЫБРАТЬ
		|				ДокументыКПерерасчету.Документ
		|			ИЗ
		|				ВТОплатаСверхурочныхЧасов КАК ДокументыКПерерасчету)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПремииРаботниковОрганизаций.Ссылка,
		|	ПремииРаботниковОрганизаций.ПерерассчитываемыйДокумент
		|ИЗ
		|	Документ.ПремииРаботниковОрганизаций КАК ПремииРаботниковОрганизаций
		|ГДЕ
		|	ПремииРаботниковОрганизаций.ПерерассчитываемыйДокумент В
		|			(ВЫБРАТЬ
		|				ДокументыКПерерасчету.Документ
		|			ИЗ
		|				ВТПремии КАК ДокументыКПерерасчету)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РегистрацияРазовыхНачисленийРаботниковОрганизаций.Ссылка,
		|	РегистрацияРазовыхНачисленийРаботниковОрганизаций.ПерерассчитываемыйДокумент
		|ИЗ
		|	Документ.РегистрацияРазовыхНачисленийРаботниковОрганизаций КАК РегистрацияРазовыхНачисленийРаботниковОрганизаций
		|ГДЕ
		|	РегистрацияРазовыхНачисленийРаботниковОрганизаций.ПерерассчитываемыйДокумент В
		|			(ВЫБРАТЬ
		|				ДокументыКПерерасчету.Документ
		|			ИЗ
		|				ВТРазовыеНачисления КАК ДокументыКПерерасчету)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Документ
		|";
	КонецЕсли;
	
	
	Если Запрос.Выполнить().Выгрузить()[0].Количество = 0 Тогда // все многострочные документы оказались неисправленными, лишние запросы исполнять не будем 
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ДокументыПерерасчета.Документ,
		|	ДокументыПерерасчета.ПериодРегистрации КАК ПериодРегистрации,
		|	ВЫБОР
		|		КОГДА ДокументыПерерасчета.Документ В (&СписокПомеченныхДокументов)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Пометка,
		|	ПРЕДСТАВЛЕНИЕ(ДокументыПерерасчета.Документ) КАК Наименование,
		|	ДокументыПерерасчета.Организация.Наименование КАК НаименованиеОрганизации,
		|	ДокументыПерерасчета.Документ КАК Ссылка,
		|	ДокументыПерерасчета.Организация
		|ИЗ
		|	ВТНачислениеОтпуска КАК ДокументыПерерасчета
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДокументыПерерасчета.Документ,
		|	ДокументыПерерасчета.ПериодРегистрации,
		|	ВЫБОР
		|		КОГДА ДокументыПерерасчета.Документ В (&СписокПомеченныхДокументов)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ,
		|	ПРЕДСТАВЛЕНИЕ(ДокументыПерерасчета.Документ),
		|	ДокументыПерерасчета.Организация.Наименование,
		|	ДокументыПерерасчета.Документ,
		|	ДокументыПерерасчета.Организация
		|ИЗ
		|	ВТНачислениеПоБольничномуЛисту КАК ДокументыПерерасчета
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДокументыПерерасчета.Документ,
		|	ДокументыПерерасчета.ПериодРегистрации,
		|	ВЫБОР
		|		КОГДА ДокументыПерерасчета.Документ В (&СписокПомеченныхДокументов)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ,
		|	ПРЕДСТАВЛЕНИЕ(ДокументыПерерасчета.Документ),
		|	ДокументыПерерасчета.Организация.Наименование,
		|	ДокументыПерерасчета.Документ,
		|	ДокументыПерерасчета.Организация
		|ИЗ
		|	ВТОплатаПоСреднемуЗаработку КАК ДокументыПерерасчета
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДокументыПерерасчета.Документ,
		|	ДокументыПерерасчета.ПериодРегистрации,
		|	ВЫБОР
		|		КОГДА ДокументыПерерасчета.Документ В (&СписокПомеченныхДокументов)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ,
		|	ПРЕДСТАВЛЕНИЕ(ДокументыПерерасчета.Документ),
		|	ДокументыПерерасчета.Организация.Наименование,
		|	ДокументыПерерасчета.Документ,
		|	ДокументыПерерасчета.Организация
		|ИЗ
		|	ВТРасчетПриУвольнении КАК ДокументыПерерасчета
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДокументыПерерасчета.Документ,
		|	ДокументыПерерасчета.ПериодРегистрации,
		|	ВЫБОР
		|		КОГДА ДокументыПерерасчета.Документ В (&СписокПомеченныхДокументов)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ,
		|	ПРЕДСТАВЛЕНИЕ(ДокументыПерерасчета.Документ),
		|	ДокументыПерерасчета.Организация.Наименование,
		|	ДокументыПерерасчета.Документ,
		|	ДокументыПерерасчета.Организация
		|ИЗ
		|	ВТРегистрацияПростоев КАК ДокументыПерерасчета
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДокументыПерерасчета.Документ,
		|	ДокументыПерерасчета.ПериодРегистрации,
		|	ВЫБОР
		|		КОГДА ДокументыПерерасчета.Документ В (&СписокПомеченныхДокументов)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ,
		|	ПРЕДСТАВЛЕНИЕ(ДокументыПерерасчета.Документ),
		|	ДокументыПерерасчета.Организация.Наименование,
		|	ДокументыПерерасчета.Документ,
		|	ДокументыПерерасчета.Организация
		|ИЗ
		|	ВТНачислениеЗарплаты КАК ДокументыПерерасчета
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДокументыПерерасчета.Документ,
		|	ДокументыПерерасчета.ПериодРегистрации,
		|	ВЫБОР
		|		КОГДА ДокументыПерерасчета.Документ В (&СписокПомеченныхДокументов)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ,
		|	ПРЕДСТАВЛЕНИЕ(ДокументыПерерасчета.Документ),
		|	ДокументыПерерасчета.Организация.Наименование,
		|	ДокументыПерерасчета.Документ,
		|	ДокументыПерерасчета.Организация
		|ИЗ
		|	ВТОплатаПраздничных КАК ДокументыПерерасчета
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДокументыПерерасчета.Документ,
		|	ДокументыПерерасчета.ПериодРегистрации,
		|	ВЫБОР
		|		КОГДА ДокументыПерерасчета.Документ В (&СписокПомеченныхДокументов)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ,
		|	ПРЕДСТАВЛЕНИЕ(ДокументыПерерасчета.Документ),
		|	ДокументыПерерасчета.Организация.Наименование,
		|	ДокументыПерерасчета.Документ,
		|	ДокументыПерерасчета.Организация
		|ИЗ
		|	ВТНевыходыВОрганизациях КАК ДокументыПерерасчета
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДокументыПерерасчета.Документ,
		|	ДокументыПерерасчета.ПериодРегистрации,
		|	ВЫБОР
		|		КОГДА ДокументыПерерасчета.Документ В (&СписокПомеченныхДокументов)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ,
		|	ПРЕДСТАВЛЕНИЕ(ДокументыПерерасчета.Документ),
		|	ДокументыПерерасчета.Организация.Наименование,
		|	ДокументыПерерасчета.Документ,
		|	ДокументыПерерасчета.Организация
		|ИЗ
		|	ВТОплатаСверхурочныхЧасов КАК ДокументыПерерасчета
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДокументыПерерасчета.Документ,
		|	ДокументыПерерасчета.ПериодРегистрации,
		|	ВЫБОР
		|		КОГДА ДокументыПерерасчета.Документ В (&СписокПомеченныхДокументов)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ,
		|	ПРЕДСТАВЛЕНИЕ(ДокументыПерерасчета.Документ),
		|	ДокументыПерерасчета.Организация.Наименование,
		|	ДокументыПерерасчета.Документ,
		|	ДокументыПерерасчета.Организация
		|ИЗ
		|	ВТПремии КАК ДокументыПерерасчета
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДокументыПерерасчета.Документ,
		|	ДокументыПерерасчета.ПериодРегистрации,
		|	ВЫБОР
		|		КОГДА ДокументыПерерасчета.Документ В (&СписокПомеченныхДокументов)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ,
		|	ПРЕДСТАВЛЕНИЕ(ДокументыПерерасчета.Документ),
		|	ДокументыПерерасчета.Организация.Наименование,
		|	ДокументыПерерасчета.Документ,
		|	ДокументыПерерасчета.Организация
		|ИЗ
		|	ВТРазовыеНачисления КАК ДокументыПерерасчета
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПериодРегистрации";
		
	Иначе
		
		// ВТДокументыПерерасчетаСИсправленными
		Если Истина Тогда
			Запрос.Текст =
			"ВЫБРАТЬ
			|	ДокументыПерерасчета.Документ КАК Документ,
			|	ДокументыПерерасчета.ПериодРегистрации КАК ПериодРегистрации,
			|	ДокументыПерерасчета.Организация
			|ПОМЕСТИТЬ ВТДокументыПерерасчетаСИсправленными
			|ИЗ
			|	ВТНачислениеЗарплаты КАК ДокументыПерерасчета
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|ВЫБРАТЬ
			|	ДокументыПерерасчета.Документ,
			|	ДокументыПерерасчета.ПериодРегистрации,
			|	ДокументыПерерасчета.Организация
			|ИЗ ВТНачислениеПоБольничномуЛисту КАК ДокументыПерерасчета
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|ВЫБРАТЬ
			|	ДокументыПерерасчета.Документ,
			|	ДокументыПерерасчета.ПериодРегистрации,
			|	ДокументыПерерасчета.Организация
			|ИЗ ВТОплатаПоСреднемуЗаработку КАК ДокументыПерерасчета
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|ВЫБРАТЬ
			|	ДокументыПерерасчета.Документ,
			|	ДокументыПерерасчета.ПериодРегистрации,
			|	ДокументыПерерасчета.Организация
			|ИЗ ВТОплатаПраздничных КАК ДокументыПерерасчета
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|ВЫБРАТЬ
			|	ДокументыПерерасчета.Документ,
			|	ДокументыПерерасчета.ПериодРегистрации,
			|	ДокументыПерерасчета.Организация
			|ИЗ ВТНевыходыВОрганизациях КАК ДокументыПерерасчета
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|ВЫБРАТЬ
			|	ДокументыПерерасчета.Документ,
			|	ДокументыПерерасчета.ПериодРегистрации,
			|	ДокументыПерерасчета.Организация
			|ИЗ ВТОплатаСверхурочныхЧасов КАК ДокументыПерерасчета
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ДокументыПерерасчета.Документ,
			|	ДокументыПерерасчета.ПериодРегистрации,
			|	ДокументыПерерасчета.Организация
			|ИЗ
			|	ВТПремии КАК ДокументыПерерасчета
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ДокументыПерерасчета.Документ,
			|	ДокументыПерерасчета.ПериодРегистрации,
			|	ДокументыПерерасчета.Организация
			|ИЗ
			|	ВТРазовыеНачисления КАК ДокументыПерерасчета
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Документ";
			Запрос.Выполнить();
		КонецЕсли;
		
		// Сотрудники, чьи результаты расчетов устарели
		// ВТСотрудникиПерерасчета
		Если Истина Тогда
			Запрос.Текст =
			"ВЫБРАТЬ
			|	ОсновныеНачисленияРаботниковОрганизацийПерерасчетОсновныхНачислений.Сотрудник КАК Сотрудник,
			|	ОсновныеНачисленияРаботниковОрганизацийПерерасчетОсновныхНачислений.ОбъектПерерасчета КАК Ссылка
			|ПОМЕСТИТЬ ВТСотрудникиПерерасчета
			|ИЗ РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций.ПерерасчетОсновныхНачислений КАК ОсновныеНачисленияРаботниковОрганизацийПерерасчетОсновныхНачислений
			|ГДЕ ОсновныеНачисленияРаботниковОрганизацийПерерасчетОсновныхНачислений.ОбъектПерерасчета В
			|			(ВЫБРАТЬ
			|				ДокументыКПерерасчету.Документ
			|			ИЗ ВТДокументыПерерасчетаСИсправленными КАК ДокументыКПерерасчету)
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|ВЫБРАТЬ
			|	ПерерасчетВзносовВФонды.Сотрудник,
			|	ПерерасчетВзносовВФонды.ОбъектПерерасчета
			|ИЗ
			|	РегистрРасчета.ВзносыВФонды.ПерерасчетВзносовВФонды КАК ПерерасчетВзносовВФонды
			|ГДЕ
			|	ПерерасчетВзносовВФонды.ОбъектПерерасчета В
			|			(ВЫБРАТЬ
			|				ДокументыКПерерасчету.Документ
			|			ИЗ
			|				ВТДокументыПерерасчетаСИсправленными КАК ДокументыКПерерасчету)
			|
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|ВЫБРАТЬ
			|	РасчетСреднегоЗаработкаПерерасчетСреднегоЗаработка.Сотрудник,
			|	РасчетСреднегоЗаработкаПерерасчетСреднегоЗаработка.ОбъектПерерасчета
			|ИЗ
			|	РегистрРасчета.РасчетСреднегоЗаработка.ПерерасчетСреднегоЗаработка КАК РасчетСреднегоЗаработкаПерерасчетСреднегоЗаработка
			|ГДЕ
			|	РасчетСреднегоЗаработкаПерерасчетСреднегоЗаработка.ОбъектПерерасчета В
			|			(ВЫБРАТЬ
			|				ДокументыКПерерасчету.Документ
			|			ИЗ
			|				ВТДокументыПерерасчетаСИсправленными КАК ДокументыКПерерасчету)
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|ВЫБРАТЬ 
			|	ДанныеДокументы.Сотрудник,
			|	ЗаполнениеПлановыхНачислений.ОбъектЗаполнения
			|ИЗ
			|	РегистрСведений.ЗаполнениеПлановыхНачислений КАК ЗаполнениеПлановыхНачислений
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (
			|		ВЫБРАТЬ
			|			НачислениеЗарплатыРаботникамОрганизацийНачисления.Сотрудник КАК Сотрудник,
			|			НачислениеЗарплатыРаботникамОрганизацийНачисления.Ссылка КАК Ссылка
			|		ИЗ Документ.НачислениеЗарплатыРаботникамОрганизаций.Начисления КАК НачислениеЗарплатыРаботникамОрганизацийНачисления
			|		ГДЕ НачислениеЗарплатыРаботникамОрганизацийНачисления.Ссылка В
			|					(ВЫБРАТЬ
			|						ДокументыКПерерасчету.Документ
			|					ИЗ ВТДокументыПерерасчетаСИсправленными КАК ДокументыКПерерасчету
			|					ГДЕ ДокументыКПерерасчету.Документ ССЫЛКА Документ.НачислениеЗарплатыРаботникамОрганизаций)
			|		 
			|		ОБЪЕДИНИТЬ 
			|		ВЫБРАТЬ
			|			НачислениеПоБольничномуЛисту.Ссылка.Сотрудник КАК Сотрудник,
			|			НачислениеПоБольничномуЛисту.Ссылка КАК Ссылка
			|		ИЗ Документ.НачислениеПоБольничномуЛисту.РасчетСреднего КАК НачислениеПоБольничномуЛисту
			|		ГДЕ НачислениеПоБольничномуЛисту.ВидРасчета = ЗНАЧЕНИЕ(ПланВидовРасчета.СреднийЗаработок.ПоОкладу)
			|			И НачислениеПоБольничномуЛисту.Ссылка В
			|					(ВЫБРАТЬ
			|						ДокументыКПерерасчету.Документ
			|					ИЗ ВТДокументыПерерасчетаСИсправленными КАК ДокументыКПерерасчету
			|					ГДЕ ДокументыКПерерасчету.Документ ССЫЛКА Документ.НачислениеЗарплатыРаботникамОрганизаций)
			|		 
			|		ОБЪЕДИНИТЬ 
			|		ВЫБРАТЬ
			|			НачислениеОтпускаРаботникамОрганизаций.Ссылка.Сотрудник КАК Сотрудник,
			|			НачислениеОтпускаРаботникамОрганизаций.Ссылка КАК Ссылка
			|		ИЗ Документ.НачислениеОтпускаРаботникамОрганизаций.РасчетСреднего КАК НачислениеОтпускаРаботникамОрганизаций
			|		ГДЕ НачислениеОтпускаРаботникамОрганизаций.ВидРасчета = ЗНАЧЕНИЕ(ПланВидовРасчета.СреднийЗаработок.ПоОкладу)
			|			И НачислениеОтпускаРаботникамОрганизаций.Ссылка В
			|					(ВЫБРАТЬ
			|						ДокументыКПерерасчету.Документ
			|					ИЗ ВТДокументыПерерасчетаСИсправленными КАК ДокументыКПерерасчету
			|					ГДЕ ДокументыКПерерасчету.Документ ССЫЛКА Документ.НачислениеЗарплатыРаботникамОрганизаций)
			|		 
			|		ОБЪЕДИНИТЬ 
			|		ВЫБРАТЬ
			|			ОплатаПоСреднемуЗаработку.Ссылка.Сотрудник КАК Сотрудник,
			|			ОплатаПоСреднемуЗаработку.Ссылка КАК Ссылка
			|		ИЗ Документ.ОплатаПоСреднемуЗаработку.РасчетСреднего КАК ОплатаПоСреднемуЗаработку
			|		ГДЕ ОплатаПоСреднемуЗаработку.ВидРасчета = ЗНАЧЕНИЕ(ПланВидовРасчета.СреднийЗаработок.ПоОкладу)
			|			И ОплатаПоСреднемуЗаработку.Ссылка В
			|					(ВЫБРАТЬ
			|						ДокументыКПерерасчету.Документ
			|					ИЗ ВТДокументыПерерасчетаСИсправленными КАК ДокументыКПерерасчету
			|					ГДЕ ДокументыКПерерасчету.Документ ССЫЛКА Документ.НачислениеЗарплатыРаботникамОрганизаций)
			|		) КАК ДанныеДокументы
			|		ПО ЗаполнениеПлановыхНачислений.ОбъектЗаполнения = ДанныеДокументы.Ссылка
			|			И ЗаполнениеПлановыхНачислений.Сотрудник = ДанныеДокументы.Сотрудник
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Ссылка,
			|	Сотрудник";
			Запрос.Выполнить();
		КонецЕсли;
		
		// физлица, чьи результаты расчетов уже исправляли, 
		// содержащие только таких сотрудников документы не предлагаем к перерасчету
		// ВТФизлицаИсправлений
		Если Истина Тогда
			Запрос.Текст =
			"ВЫБРАТЬ
			|	ДокументыИсправления.ИсходныйДокумент КАК Ссылка,
			|	НачислениеЗарплатыРаботникамОрганизацийНачисления.Сотрудник КАК Сотрудник
			|ПОМЕСТИТЬ ВТФизлицаИсправлений
			|ИЗ
			|	Документ.НачислениеЗарплатыРаботникамОрганизаций.Начисления КАК НачислениеЗарплатыРаботникамОрганизацийНачисления
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДокументыИсправления КАК ДокументыИсправления
			|		ПО НачислениеЗарплатыРаботникамОрганизацийНачисления.Ссылка = ДокументыИсправления.Документ
			|			И НачислениеЗарплатыРаботникамОрганизацийНачисления.СторнируемыйДокумент = ДокументыИсправления.ИсходныйДокумент
			|ГДЕ
			|	НачислениеЗарплатыРаботникамОрганизацийНачисления.Ссылка В
			|			(ВЫБРАТЬ
			|				ДокументыКПерерасчету.Документ
			|			ИЗ
			|				ВТДокументыИсправления КАК ДокументыКПерерасчету
			|			ГДЕ
			|				ДокументыКПерерасчету.Документ ССЫЛКА Документ.НачислениеЗарплатыРаботникамОрганизаций)
			|	И НачислениеЗарплатыРаботникамОрганизацийНачисления.Сторно
			|
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ДокументыИсправления.ИсходныйДокумент,
			|	РегистрацияРазовыхНачисленийРаботниковОрганизацийОсновныеНачисления.Сотрудник
			|ИЗ
			|	Документ.РегистрацияРазовыхНачисленийРаботниковОрганизаций.ОсновныеНачисления КАК РегистрацияРазовыхНачисленийРаботниковОрганизацийОсновныеНачисления
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДокументыИсправления КАК ДокументыИсправления
			|		ПО РегистрацияРазовыхНачисленийРаботниковОрганизацийОсновныеНачисления.Ссылка = ДокументыИсправления.Документ
			|			И РегистрацияРазовыхНачисленийРаботниковОрганизацийОсновныеНачисления.СторнируемыйДокумент = ДокументыИсправления.ИсходныйДокумент
			|ГДЕ
			|	РегистрацияРазовыхНачисленийРаботниковОрганизацийОсновныеНачисления.Ссылка В
			|			(ВЫБРАТЬ
			|				ДокументыКПерерасчету.Документ
			|			ИЗ
			|				ВТДокументыИсправления КАК ДокументыКПерерасчету
			|			ГДЕ
			|				ДокументыКПерерасчету.Документ ССЫЛКА Документ.РегистрацияРазовыхНачисленийРаботниковОрганизаций)
			|	И РегистрацияРазовыхНачисленийРаботниковОрганизацийОсновныеНачисления.Сторно
			|
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ДокументыИсправления.ИсходныйДокумент,
			|	НевыходыВОрганизацияхНачисления.Сотрудник
			|ИЗ
			|	Документ.НевыходыВОрганизациях.Начисления КАК НевыходыВОрганизацияхНачисления
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДокументыИсправления КАК ДокументыИсправления
			|		ПО НевыходыВОрганизацияхНачисления.Ссылка = ДокументыИсправления.Документ
			|			И НевыходыВОрганизацияхНачисления.СторнируемыйДокумент = ДокументыИсправления.ИсходныйДокумент
			|ГДЕ
			|	НевыходыВОрганизацияхНачисления.Ссылка В
			|			(ВЫБРАТЬ
			|				ДокументыКПерерасчету.Документ
			|			ИЗ
			|				ВТДокументыИсправления КАК ДокументыКПерерасчету
			|			ГДЕ
			|				ДокументыКПерерасчету.Документ ССЫЛКА Документ.НевыходыВОрганизациях)
			|	И НевыходыВОрганизацияхНачисления.Сторно
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ПремииРаботниковОрганизацийНачисления.Ссылка.ПерерассчитываемыйДокумент,
			|	ПремииРаботниковОрганизацийНачисления.Сотрудник
			|ИЗ
			|	Документ.ПремииРаботниковОрганизаций.Начисления КАК ПремииРаботниковОрганизацийНачисления
			|ГДЕ
			|	ПремииРаботниковОрганизацийНачисления.Ссылка В
			|			(ВЫБРАТЬ
			|				ДокументыКПерерасчету.Документ
			|			ИЗ
			|				ВТДокументыИсправления КАК ДокументыКПерерасчету
			|			ГДЕ
			|				ДокументыКПерерасчету.Документ ССЫЛКА Документ.ПремииРаботниковОрганизаций)
			|	И ПремииРаботниковОрганизацийНачисления.Сторно
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ОплатаПраздничныхИВыходныхДнейОрганизацийНачисления.Ссылка.ПерерассчитываемыйДокумент,
			|	ОплатаПраздничныхИВыходныхДнейОрганизацийНачисления.Сотрудник
			|ИЗ
			|	Документ.ОплатаПраздничныхИВыходныхДнейОрганизаций.Начисления КАК ОплатаПраздничныхИВыходныхДнейОрганизацийНачисления
			|ГДЕ
			|	ОплатаПраздничныхИВыходныхДнейОрганизацийНачисления.Ссылка В
			|			(ВЫБРАТЬ
			|				ДокументыКПерерасчету.Документ
			|			ИЗ
			|				ВТДокументыИсправления КАК ДокументыКПерерасчету
			|			ГДЕ
			|				ДокументыКПерерасчету.Документ ССЫЛКА Документ.ОплатаПраздничныхИВыходныхДнейОрганизаций)
			|	И ОплатаПраздничныхИВыходныхДнейОрганизацийНачисления.Сторно
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ОплатаСверхурочныхЧасовНачисления.Ссылка.ПерерассчитываемыйДокумент,
			|	ОплатаСверхурочныхЧасовНачисления.Сотрудник
			|ИЗ
			|	Документ.ОплатаСверхурочныхЧасов.Начисления КАК ОплатаСверхурочныхЧасовНачисления
			|ГДЕ
			|	ОплатаСверхурочныхЧасовНачисления.Ссылка В
			|			(ВЫБРАТЬ
			|				ДокументыКПерерасчету.Документ
			|			ИЗ
			|				ВТДокументыИсправления КАК ДокументыКПерерасчету
			|			ГДЕ
			|				ДокументыКПерерасчету.Документ ССЫЛКА Документ.ОплатаСверхурочныхЧасов)
			|	И ОплатаСверхурочныхЧасовНачисления.Сторно
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Ссылка,
			|	Сотрудник";
			Запрос.Выполнить();
		КонецЕсли;
		
		Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДокументыПерерасчета.Документ,
		|	ДокументыПерерасчета.ПериодРегистрации КАК ПериодРегистрации,
		|	ВЫБОР
		|		КОГДА ДокументыПерерасчета.Документ В (&СписокПомеченныхДокументов)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Пометка,
		|	ПРЕДСТАВЛЕНИЕ(ДокументыПерерасчета.Документ) КАК Наименование,
		|	ДокументыПерерасчета.Организация.Наименование КАК НаименованиеОрганизации,
		|	ДокументыПерерасчета.Документ КАК Ссылка,
		|	ДокументыПерерасчета.Организация
		|ИЗ
		|	ВТНачислениеОтпуска КАК ДокументыПерерасчета
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДокументыПерерасчета.Документ,
		|	ДокументыПерерасчета.ПериодРегистрации,
		|	ВЫБОР
		|		КОГДА ДокументыПерерасчета.Документ В (&СписокПомеченныхДокументов)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ,
		|	ПРЕДСТАВЛЕНИЕ(ДокументыПерерасчета.Документ),
		|	ДокументыПерерасчета.Организация.Наименование,
		|	ДокументыПерерасчета.Документ,
		|	ДокументыПерерасчета.Организация
		|ИЗ
		|	ВТНачислениеПоБольничномуЛисту КАК ДокументыПерерасчета
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДокументыПерерасчета.Документ,
		|	ДокументыПерерасчета.ПериодРегистрации,
		|	ВЫБОР
		|		КОГДА ДокументыПерерасчета.Документ В (&СписокПомеченныхДокументов)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ,
		|	ПРЕДСТАВЛЕНИЕ(ДокументыПерерасчета.Документ),
		|	ДокументыПерерасчета.Организация.Наименование,
		|	ДокументыПерерасчета.Документ,
		|	ДокументыПерерасчета.Организация
		|ИЗ
		|	ВТОплатаПоСреднемуЗаработку КАК ДокументыПерерасчета
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДокументыПерерасчета.Документ,
		|	ДокументыПерерасчета.ПериодРегистрации,
		|	ВЫБОР
		|		КОГДА ДокументыПерерасчета.Документ В (&СписокПомеченныхДокументов)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ,
		|	ПРЕДСТАВЛЕНИЕ(ДокументыПерерасчета.Документ),
		|	ДокументыПерерасчета.Организация.Наименование,
		|	ДокументыПерерасчета.Документ,
		|	ДокументыПерерасчета.Организация
		|ИЗ
		|	ВТРасчетПриУвольнении КАК ДокументыПерерасчета
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДокументыПерерасчета.Документ,
		|	ДокументыПерерасчета.ПериодРегистрации,
		|	ВЫБОР
		|		КОГДА ДокументыПерерасчета.Документ В (&СписокПомеченныхДокументов)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ,
		|	ПРЕДСТАВЛЕНИЕ(ДокументыПерерасчета.Документ),
		|	ДокументыПерерасчета.Организация.Наименование,
		|	ДокументыПерерасчета.Документ,
		|	ДокументыПерерасчета.Организация
		|ИЗ
		|	ВТРегистрацияПростоев КАК ДокументыПерерасчета
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ " + ТекстПервыхЗаписей + "
		|	ДокументыПерерасчета.Документ,
		|	ДокументыПерерасчета.ПериодРегистрации,
		|	ВЫБОР
		|		КОГДА ДокументыПерерасчета.Документ В (&СписокПомеченныхДокументов)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ,
		|	ПРЕДСТАВЛЕНИЕ(ДокументыПерерасчета.Документ),
		|	ДокументыПерерасчета.Организация.Наименование,
		|	ДокументыПерерасчета.Документ,
		|	ДокументыПерерасчета.Организация
		|ИЗ
		|	ВТДокументыПерерасчетаСИсправленными КАК ДокументыПерерасчета
		|ГДЕ
		|	ДокументыПерерасчета.Документ В
		|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|				Документы.Ссылка КАК Ссылка
		|			ИЗ
		|				ВТСотрудникиПерерасчета КАК Документы 
		|					ЛЕВОЕ СОЕДИНЕНИЕ ВТФизлицаИсправлений КАК ДокументыИсправления
		|					ПО Документы.Ссылка = ДокументыИсправления.Ссылка
		|						И Документы.Сотрудник = ДокументыИсправления.Сотрудник
		|			ГДЕ
		|				ДокументыИсправления.Сотрудник ЕСТЬ NULL )
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПериодРегистрации";
		
	КонецЕсли;

	Возврат Запрос.Выполнить()
	
КонецФункции // СформироватьЗапрос()

// Заполняет список физлиц по которым необходимо пересчитать зарплату по заданной организации
// Параметры: 
//	Организация - ссылка на организацию
// Возвращаемое значение:
//	Нет
//
Процедура ЗаполнитьДанныеПоОрганизации() Экспорт
	
	мПодразделенияДокументы.Очистить();
	мРаботникиДокументы.Очистить();
	
	СписокПодразделений.Очистить();
	СписокРаботников.Очистить();
	
	// пока не выбрана организация ничего не делаем
	Если Организация.Пустая() Тогда
		СписокДокументов.Очистить();
		Возврат;
	КонецЕсли;
	
	//запомним какие документы отмечены в списке документов
	ТаблицаДокументов = СписокДокументов.Выгрузить();
	МассивСтрок = ТаблицаДокументов.НайтиСтроки(Новый Структура("Пометка", Истина));
	СписокПомеченныхДокументов = ТаблицаДокументов.Скопировать(МассивСтрок).ВыгрузитьКолонку("Документ");
	
	Результат = СформироватьЗапрос(Организация, ТекущийПериод, СписокПомеченныхДокументов);
	
	ТЗ = Результат.Выгрузить();
	ТЗ.Свернуть("Пометка, Документ, ПериодРегистрации"); 
	СписокДокументов.Загрузить(ТЗ);
	
КонецПроцедуры

// перерассчитать помеченные документы, в порядке от самого старого до самого нового.
// перебираются все документы (объекты перерасчета) и для них вызывается стандартная процедура  
//
// Возвращаемое значение:
//	Нет
Процедура Перерассчитать() Экспорт
	
	Заголовок = ОбщегоНазначения.КомментарийРасчета("Перерасчет документов");
	ИндексСтрокиТаблицы = СписокДокументов.Количество();
	Пока ИндексСтрокиТаблицы > 0 Цикл
		ИндексСтрокиТаблицы = ИндексСтрокиТаблицы - 1;
		
		СтрокаТаблицы = СписокДокументов[ИндексСтрокиТаблицы];
		Если СтрокаТаблицы.Пометка Тогда
			
			// проверим можно ли что-то делать с документом
			ОписаниеПричиныОтказа = "";
			Если ПроведениеРасчетов.ДокументНельзяИзменятьЗаднимЧислом(СтрокаТаблицы.Документ, ОписаниеПричиныОтказа) Тогда
				ОбщегоНазначения.КомментарийРасчета(СтрЗаменить(ОписаниеПричиныОтказа,"Документ ",Строка(СтрокаТаблицы.Документ)), , "Открыть", СтрокаТаблицы.Документ,Перечисления.ВидыСообщений.ВажнаяИнформация);
				Продолжить;	
			КонецЕсли;
			
			ДокументОбъект = СтрокаТаблицы.Документ.ПолучитьОбъект();
			
			Попытка
				ДокументОбъект.Перерассчитать();
				ОбщегоНазначения.КомментарийРасчета(Строка(ДокументОбъект) + " обработан успешно. ", Заголовок, "Открыть", СтрокаТаблицы.Документ);
			Исключение
				ОбщегоНазначения.КомментарийРасчета("Документ " + Строка(ДокументОбъект) + " не может быть перерассчитан!
				|" + ОписаниеОшибки(), , "Открыть", СтрокаТаблицы.Документ,Перечисления.ВидыСообщений.ВажнаяИнформация);
			КонецПопытки;
		КонецЕсли;
	КонецЦикла;
	ЗаполнитьДанныеПоОрганизации();
	
КонецПроцедуры

// удалить сведения о перерасчете помеченных документов
Процедура УдалитьСведенияОПерерасчете() Экспорт
	
	Для Каждого СтрокаТаблицы Из СписокДокументов.НайтиСтроки(Новый Структура("Пометка", Истина)) Цикл
		ПроведениеРасчетов.УдалитьСведенияОПерерасчетеДокумента(СтрокаТаблицы.Документ);
		ПроведениеРасчетов.УдалитьСведенияОПерезаполненииДокумента(СтрокаТаблицы.Документ);
	КонецЦикла;
	
	ЗаполнитьДанныеПоОрганизации();
	
КонецПроцедуры

// заполняет табличную часть СписокПодразделений и соответствие мПодразделенияДокументы 
// предназначенные для выполнения команды "отметить по подразделениеям"
Процедура ЗаполнитьСписокПодразделений() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("ТекущийПериод", ТекущийПериод);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	// ВТСписокСотрудников
	Если Истина Тогда
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Перерасчет.Сотрудник КАК Сотрудник,
		|	Перерасчет.ОбъектПерерасчета КАК Документ
		|ПОМЕСТИТЬ ВТСписокСотрудников
		|ИЗ
		|	РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций.ПерерасчетОсновныхНачислений КАК Перерасчет
		|ГДЕ
		|	Перерасчет.ОбъектПерерасчета.Организация = &Организация
		|	И Перерасчет.ОбъектПерерасчета.ПериодРегистрации < &ТекущийПериод
		|
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Перерасчет.Сотрудник,
		|	Перерасчет.ОбъектПерерасчета КАК Документ
		|ИЗ
		|	РегистрРасчета.ВзносыВФонды.ПерерасчетВзносовВФонды КАК Перерасчет
		|ГДЕ
		|	Перерасчет.ОбъектПерерасчета.Организация = &Организация
		|	И Перерасчет.ОбъектПерерасчета.ПериодРегистрации < &ТекущийПериод
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Перерасчет.Сотрудник,
		|	Перерасчет.ОбъектПерерасчета КАК Документ
		|ИЗ
		|	РегистрРасчета.РасчетСреднегоЗаработка.ПерерасчетСреднегоЗаработка КАК Перерасчет
		|ГДЕ
		|	Перерасчет.ОбъектПерерасчета.Организация = &Организация
		|	И Перерасчет.ОбъектПерерасчета.ПериодРегистрации < &ТекущийПериод
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЗаполнениеПлановыхНачислений.Сотрудник,
		|	ЗаполнениеПлановыхНачислений.ОбъектЗаполнения КАК Документ
		|ИЗ
		|	РегистрСведений.ЗаполнениеПлановыхНачислений КАК ЗаполнениеПлановыхНачислений
		|ГДЕ
		|	ЗаполнениеПлановыхНачислений.ПериодРегистрации < &ТекущийПериод
		|	И ЗаполнениеПлановыхНачислений.Организация = &Организация
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Сотрудник";
	КонецЕсли;
	
	Запрос.Выполнить();
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Перерасчеты.Документ КАК Документ,
	|	Работники.ПодразделениеОрганизации.Наименование КАК ПодразделениеНаименование,
	|	Работники.ПодразделениеОрганизации КАК Подразделение
	|ИЗ ВТСписокСотрудников КАК Перерасчеты
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ (
	|   // Все неуволенные на конец месяца
	|	ВЫБРАТЬ
	|		РаботникиОрганизаций.Сотрудник,
	|		РаботникиОрганизаций.ПодразделениеОрганизации
	|	ИЗ РегистрСведений.РаботникиОрганизаций.СрезПоследних( НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(&ТекущийПериод, МЕСЯЦ), ДЕНЬ)
	|			, Организация = &Организация	
	|			И Сотрудник В (ВЫБРАТЬ Сотрудник ИЗ ВТСписокСотрудников)
	|      ) КАК РаботникиОрганизаций
	|	ГДЕ РаботникиОрганизаций.ЗанимаемыхСтавок > 0
	|
	|   // + уволенные за месяц
	|   ОБЪЕДИНИТЬ 
	|	ВЫБРАТЬ
	|		РаботникиОрганизаций.Сотрудник,
	|		РаботникиОрганизаций.ПодразделениеОрганизации
	|	ИЗ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизаций
	|	ГДЕ РаботникиОрганизаций.ЗанимаемыхСтавок = 0
	|       И РаботникиОрганизаций.Сотрудник В (ВЫБРАТЬ Сотрудник ИЗ ВТСписокСотрудников)
	|       И РаботникиОрганизаций.Организация = &Организация
	|       И НАЧАЛОПЕРИОДА(РаботникиОрганизаций.Период, МЕСЯЦ) = &ТекущийПериод
	|
	|) КАК Работники
	|ПО Перерасчеты.Сотрудник = Работники.Сотрудник
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПодразделениеНаименование";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ДокументыМассив = мПодразделенияДокументы.Получить(Выборка.Подразделение);
		Если ДокументыМассив = НеОпределено Тогда
			ДокументыМассив = Новый Массив;
			ДокументыМассив.Добавить(Выборка.Документ);
			мПодразделенияДокументы[Выборка.Подразделение] = ДокументыМассив;
			Строка = СписокПодразделений.Добавить();
			Строка.Подразделение = Выборка.Подразделение;
			Строка.ПодразделениеНаименование = Выборка.ПодразделениеНаименование;
		Иначе
			ДокументыМассив.Добавить(Выборка.Документ);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// заполняет табличную часть СписокРаботников и соответсвие мРаботникиДокументы 
// предназначенные для выполнения команды "отметить по подразделениям"
Процедура ЗаполнитьСписокРаботников() Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ТекущийПериод", ТекущийПериод);
	Запрос.УстановитьПараметр("Организация", Организация);

	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Перерасчет.Сотрудник КАК Сотрудник,
	|	Перерасчет.Сотрудник.Наименование КАК СотрудникНаименование,
	|	Перерасчет.ОбъектПерерасчета КАК Документ
	|ИЗ
	|	РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций.ПерерасчетОсновныхНачислений КАК Перерасчет
	|ГДЕ
	|	Перерасчет.ОбъектПерерасчета.Организация = &Организация
	//*?|	И Перерасчет.ОбъектПерерасчета.ПериодРегистрации < &ТекущийПериод
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Перерасчет.Сотрудник,
	|	Перерасчет.Сотрудник.Наименование,
	|	Перерасчет.ОбъектПерерасчета
	|ИЗ
	|	РегистрРасчета.ВзносыВФонды.ПерерасчетВзносовВФонды КАК Перерасчет
	|ГДЕ
	|	Перерасчет.ОбъектПерерасчета.Организация = &Организация
	//*?|	И Перерасчет.ОбъектПерерасчета.ПериодРегистрации < &ТекущийПериод
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Перерасчет.Сотрудник,
	|	Перерасчет.Сотрудник.Наименование,
	|	Перерасчет.ОбъектПерерасчета
	|ИЗ
	|	РегистрРасчета.РасчетСреднегоЗаработка.ПерерасчетСреднегоЗаработка КАК Перерасчет
	|ГДЕ
	|	Перерасчет.ОбъектПерерасчета.Организация = &Организация
	//*?|	И Перерасчет.ОбъектПерерасчета.ПериодРегистрации < &ТекущийПериод
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗаполнениеПлановыхНачислений.Сотрудник,
	|	ЗаполнениеПлановыхНачислений.Сотрудник.Наименование,
	|	ЗаполнениеПлановыхНачислений.ОбъектЗаполнения
	|ИЗ
	|	РегистрСведений.ЗаполнениеПлановыхНачислений КАК ЗаполнениеПлановыхНачислений
	|ГДЕ
	|	ЗаполнениеПлановыхНачислений.ПериодРегистрации < &ТекущийПериод
	|	И ЗаполнениеПлановыхНачислений.Организация = &Организация
	|
	|УПОРЯДОЧИТЬ ПО
	|	СотрудникНаименование";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ДокументыМассив = мРаботникиДокументы.Получить(Выборка.Сотрудник);
		Если ДокументыМассив = НеОпределено Тогда
			ДокументыМассив = Новый Массив;
			ДокументыМассив.Добавить(Выборка.Документ);
			мРаботникиДокументы[Выборка.Сотрудник] = ДокументыМассив;
			Строка = СписокРаботников.Добавить();
			Строка.Работник = Выборка.Сотрудник;
			Строка.РаботникНаименование = Выборка.СотрудникНаименование;
		Иначе
			ДокументыМассив.Добавить(Выборка.Документ);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

мПодразделенияДокументы = Новый Соответствие;
мРаботникиДокументы = Новый Соответствие;
