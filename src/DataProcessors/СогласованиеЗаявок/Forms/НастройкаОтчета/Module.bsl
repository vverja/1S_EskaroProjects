&НаКлиенте
Процедура ЗаполнитьСписокДоступныхВариантов()
	
	Если Объект.ПроизвольныйОтчет.Пустая() Тогда
		Элементы.СохраненнаяНастройка.СписокВыбора.Очистить();
		Возврат;
	КонецЕсли; 
	
	СписокДоступныхВариантов = ТиповыеОтчеты.ПолучитьСписокДоступныхВариантов(Объект.ПроизвольныйОтчет);
	Элементы.СохраненнаяНастройка.СписокВыбора.ЗагрузитьЗначения(СписокДоступныхВариантов.ВыгрузитьЗначения());
	
	// Установим первый доступный вариант отчета, если он не заполнен
	Если Объект.СохраненнаяНастройка.Пустая() 
		И СписокДоступныхВариантов.Количество() <> 0 Тогда
		
		Объект.СохраненнаяНастройка = СписокДоступныхВариантов.Получить(0).Значение;
	КонецЕсли; 
	
КонецПроцедуры //

&НаКлиенте
Процедура ПроизвольныйОтчетПриИзменении(Элемент)
	
	ЗаполнитьСписокДоступныхВариантов();
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИЗакрыть(Команда)
	
	РезультатНастройки = Новый Структура;
	РезультатНастройки.Вставить("ПроизвольныйОтчет",    Объект.ПроизвольныйОтчет);
	РезультатНастройки.Вставить("СохраненнаяНастройка", Объект.СохраненнаяНастройка);
	
	Закрыть(РезультатНастройки);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Объект.ПроизвольныйОтчет = Параметры.ПроизвольныйОтчет;
	
	Если НЕ Объект.ПроизвольныйОтчет.Пустая() Тогда
		СписокДоступныхВариантов = ТиповыеОтчеты.ПолучитьСписокДоступныхВариантов(Объект.ПроизвольныйОтчет);
		Элементы.СохраненнаяНастройка.СписокВыбора.ЗагрузитьЗначения(СписокДоступныхВариантов.ВыгрузитьЗначения());
	КонецЕсли; 
	
	Объект.СохраненнаяНастройка = Параметры.СохраненнаяНастройка;

КонецПроцедуры
