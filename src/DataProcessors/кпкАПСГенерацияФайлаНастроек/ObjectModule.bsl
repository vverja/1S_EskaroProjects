#Если Клиент Тогда
	
// Функция ПроверкаНастроекАгентСервера
//
// Параметры: 
//  ПапкаОбмена  - папка обмена Агент Плюс СОД
//  АдресСервера - адрес  Агент Плюс СОД
//  НомерПорта   - порт Агент Плюс СОД
//
// Описание:
//  Функция проверяет корректность формирования параметров для файла настроек
//
// Возвращаемое значение:
//  Булево
//   
Функция ПроверкаНастроекАгентСервера(ПапкаОбмена, АдресСервера, НомерПорта) Экспорт
	
	ЕстьОшибки = Ложь;
	
	Если ПустаяСтрока(АдресСервера) Тогда
		ЕстьОшибки = Истина;
		ОбщегоНазначения.СообщитьОбОшибке("Не указано значение константы ""Адрес сервера""!");
	КонецЕсли;
	
	Если Найти(АдресСервера, " ") <> 0 Тогда
		ЕстьОшибки = Истина;
		ОбщегоНазначения.СообщитьОбОшибке("Неправильно указан адрес сервера в константе ""Адрес сервера"" (в адресе есть символы ""пробел"")!");
	КонецЕсли;
	
	Поз = Найти(АдресСервера, ":");
	Если Поз = 0 Тогда
		ЕстьОшибки = Истина;
		ОбщегоНазначения.СообщитьОбОшибке("Неправильно указан адрес сервера в константе ""Адрес сервера"" (в адресе нет символа "":"" после которого указывается номер Интернет-порта)!");
	КонецЕсли;
	
	ОшибкаПорта = Ложь;
	Попытка
		НомерПорта = Число(Сред(АдресСервера,Поз+1));
		Если НомерПорта <= 0 Тогда
			ОшибкаПорта = Истина;
		КонецЕсли;
	Исключение
		ОшибкаПорта = Истина;
	КонецПопытки;
	
	Если ОшибкаПорта Тогда
		ЕстьОшибки = Истина;
		ОбщегоНазначения.СообщитьОбОшибке("Неправильно указан номер порта в адресе сервера (константа ""Адрес сервера"")! Номер порта должен быть больше нуля!");
	КонецЕсли;
	
	Если СтрДлина(ПапкаОбмена) = 1 Тогда
		ЕстьОшибки = Истина;
		ОбщегоНазначения.СообщитьОбОшибке("Неправильно указано значение в константе ""Адрес сервера"")! Слишком короткий путь к папке обмена данными!");
	КонецЕсли;
	
	Если ПустаяСтрока(ПапкаОбмена) Тогда
		ЕстьОшибки = Истина;
		ОбщегоНазначения.СообщитьОбОшибке("Не указано значение константы ""Папка обмена""!");
	КонецЕсли;
	
	КаталогНаДиске = Новый Файл(ПапкаОбмена);
	Если Не КаталогНаДиске.Существует() Тогда
		ЕстьОшибки = Истина;
		ОбщегоНазначения.СообщитьОбОшибке("Не существует каталога, указанного в константе ""Папка обмена""!");
	КонецЕсли;   	
	
	Если (Прав(ПапкаОбмена, 1) = "\") И (ЕстьОшибки = 0) Тогда
	    ПапкаОбмена = Лев(ПапкаОбмена, СтрДлина(ПапкаОбмена) - 1);
	КонецЕсли;
	
	КаталогОбмена = Новый Файл(ПапкаОбмена);
	Если (НЕ КаталогОбмена.Существует()) И (ЕстьОшибки = 0) Тогда
		ЕстьОшибки = Истина;
		ОбщегоНазначения.СообщитьОбОшибке("Неправильно указано значение в константе ""Адрес сервера"")! Папка не существует!");
	КонецЕсли;
	
	Возврат ЕстьОшибки;
	
КонецФункции //ПроверкаНастроек()

// Процедура ГенерацияФайлаНастроек
//
// Параметры: 
//	 Нет
//
// Описание:
//  Процедура генерирует файл настроек Агент Плюс СОД согласно введенным настройкам в системе
//
Процедура ГенерацияФайлаНастроек() Экспорт
		
	ПапкаОбмена  = СокрЛП(Константы.кпкАПСПапкаОбмена.Получить());
	АдресСервера = СокрЛП(Константы.кпкАПССервер.Получить());
	
	НомерПорта = "";
	
	Если ПроверкаНастроекАгентСервера(ПапкаОбмена, АдресСервера, НомерПорта) Тогда		
	    Предупреждение("Обнаружены ошибки в значениях констант! Создание фала настроек невозможно!", 30);
		Возврат;
	КонецЕсли;
 	
	Сообщить("Значение констант успешно проверено!", СтатусСообщения.Информация);
	
	//Создаем XML-файл через обычный объект "Текст" (просто более компактный код, чем через V7Plus.dll).
	
	Текст = Новый ТекстовыйДокумент;
	Текст.ДобавитьСтроку("<?xml version=""1.0"" encoding=""UTF-16""?>");
	Текст.ДобавитьСтроку("<APlusServer VERSION=""2.5"">");
	Текст.ДобавитьСтроку("<ServerConfig PortExternal=""" + Строка(Формат(НомерПорта, "ЧГ=0"))+""" />");
	Текст.ДобавитьСтроку("<!-- PortExternal - номер порта для запросов от КПК -->");
	Текст.ДобавитьСтроку("<!-- Образец описания настроек для КПК: -->");
	Текст.ДобавитьСтроку("<!-- PPC DEVICE_ID=""00000000-0000-0000-0000-000000000000"" PSEUDONIM=""Agent1"" DIRECTORY=""PPC1"" -->");
    
	//заносим в файл настроек идентификаторы задействованых КПК и их привязку к агентам
	КолПользователей = 0;   	
		
	Выборка = РегистрыСведений.кпкСведенияАгента.Выбрать();
	
	Пока Выборка.Следующий() Цикл		
		НаимАгента 	= СтрЗаменить(СокрЛП(Выборка.Объект.Наименование),"""","'");
		ПапкаАгента = СокрЛП(Выборка.АПСПапка);
		Если Выборка.КПК.Пустая() Тогда
		    ОбщегоНазначения.СообщитьОбОшибке("Пропущен агент " + НаимАгента + ": не указано мобильное устройство!");
			Продолжить;
		Иначе
			ИДКПК = СокрЛП(Выборка.КПК.Идентификатор);
		КонецЕсли;
		Если ПустаяСтрока(ПапкаАгента) Тогда
		    ОбщегоНазначения.СообщитьОбОшибке("Пропущен агент " + НаимАгента + ": не указана папка обмена!");
			Продолжить;
		КонецЕсли;
		Если ПустаяСтрока(ИДКПК) Тогда
		    ОбщегоНазначения.СообщитьОбОшибке("Пропущен агент " + НаимАгента + ": не указан идентификатор мобильного устройства!");
			Продолжить;
		КонецЕсли;
		Если СтрДлина(ИДКПК) <> 36 Тогда
		    ОбщегоНазначения.СообщитьОбОшибке("Пропущен агент " + НаимАгента + ": неправильно указан идентификатор мобильного устройства (длина <> 36 символов)!");
			Продолжить;
		КонецЕсли;
		
		КолПользователей = КолПользователей + 1;
		Текст.ДобавитьСтроку("<PPC DEVICE_ID=""" + ИДКПК + """ PSEUDONIM=""" + Лев(НаимАгента, 60) + """ DIRECTORY=""" + ПапкаАгента + """ />");
		Сообщить(Строка(КолПользователей) + ". Выгружены данные для агента: " + НаимАгента);
	КонецЦикла;
	
	Сообщить("**************************************", СтатусСообщения.Информация);	
	Сообщить("Выгружено пользователей: " + Строка(КолПользователей), СтатусСообщения.Информация);	
	
	Если КолПользователей <> 0 Тогда
		
		Текст.ДобавитьСтроку("</APlusServer>");
		
		ИмяФайла = ПапкаОбмена + "\config.xml";
		
		Если Вопрос("Записать новый файл настроек """ + ИмяФайла + """ ?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
			
			Текст.Записать(ИмяФайла, КодировкаТекста.UTF16);
			
			Сообщить("Записан файл настроек: " + ИмяФайла, СтатусСообщения.Информация);
			
			ТекстВопроса = "Необходимо перезапустить службу ""Агент Плюс СОД"", если она была до этого запущена! Попытаться сделать это сейчас?"; 
			
			Если Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
				
				Попытка
					КомандаСистемы("net stop ""Agent Plus Service""");
					КомандаСистемы("net start ""Agent Plus Service""");
				Исключение
					ОбщегоНазначения.СообщитьОбОшибке("Не удалось выполнить перезапуск службы!");
					ОбщегоНазначения.СообщитьОбОшибке("Причина: " + ОписаниеОшибки());					
				КонецПопытки;				
				
				Предупреждение("Внимание! Если обработка обмена данными была запущена, то для вступления в силу изменений, необходимо ее перезапустить!", 60);
				
			КонецЕсли;	
			
		КонецЕсли;
		
	Иначе
		
		Сообщить("Файл настроек не создан!", СтатусСообщения.Внимание);	
		Предупреждение("Не указаны настройки для работы с сервером ни для одного торгового агента! Файл настроек не создан!", 30);	
		
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли
