&НаСервере
Процедура ПеренестиНастройкиОтборов(Источник, Получатель)
	
	Если Не Источник = Неопределено Тогда
		
		// Загружаем данные об отборе
		Получатель.Отбор.Элементы.Очистить();
		ДанныеОтбора = Источник.Отбор;
		
		Для каждого ЭлементОтбораСписка Из ДанныеОтбора.Элементы Цикл
			Если ЭлементОтбораСписка.Использование
			  И ЭлементОтбораСписка.РежимОтображения <> РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный Тогда
				НовыйЭлементОтбора = Получатель.Отбор.Элементы.Добавить(ТипЗнч(ЭлементОтбораСписка));	
				ЗаполнитьЗначенияСвойств(НовыйЭлементОтбора, ЭлементОтбораСписка);
			КонецЕсли;	
		КонецЦикла;
		
		// Данные о порядке и условном оформлении не храним и не загружаем
КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПриСохраненииДанныхВНастройкахНаСервере(Настройки)
	
	//Создадим КомпоновщикНастроекКомпоновкиДанных и инициализируем его настройками по умолчанию (из СКД)
	СхемаКомпоновкиДанных = Справочники.НастройкиФормированияДокументовПоОрдерам.ПолучитьМакет("Настройка"); 
	КомпоновщикНастроекКомпоновкиДанных = Новый КомпоновщикНастроекКомпоновкиДанных;
	КомпоновщикНастроекКомпоновкиДанных.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных));
	КомпоновщикНастроекКомпоновкиДанных.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	
	//Перенесем в КомпоновщикНастроекКомпоновкиДанных отборы из динамического списка
	ПеренестиНастройкиОтборов(Список, КомпоновщикНастроекКомпоновкиДанных.Настройки);
	
	//Не будем сохранять отборы, программно добавленные при формировании регулярных документов
	СчетчикЦикла = 0;
	Пока СчетчикЦикла < КомпоновщикНастроекКомпоновкиДанных.Настройки.Отбор.Элементы.Количество() Цикл
		ТекущийОтбор = КомпоновщикНастроекКомпоновкиДанных.Настройки.Отбор.Элементы[СчетчикЦикла];
		Если ТекущийОтбор.Представление = "Обработанные документы"
		  ИЛИ ТекущийОтбор.Представление = "Не обработанные документы" Тогда
			КомпоновщикНастроекКомпоновкиДанных.Настройки.Отбор.Элементы.Удалить(ТекущийОтбор);
		Иначе
			СчетчикЦикла = СчетчикЦикла + 1;
		КонецЕсли;	
	КонецЦикла;	
	
	Настройки.Вставить("НастройкиКомпоновщика",КомпоновщикНастроекКомпоновкиДанных.ПолучитьНастройки());
	
КонецПроцедуры


&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	ТекущаяНастройка = Настройки["ТекущаяНастройка"];
	КлючТекущейНастройки = Настройки["КлючТекущейНастройки"];
	
	Если Настройки["НастройкиКомпоновщика"] <> Неопределено Тогда
		КомпоновщикНастроекКомпоновкиДанных = Новый КомпоновщикНастроекКомпоновкиДанных();
		КомпоновщикНастроекКомпоновкиДанных.ЗагрузитьНастройки(Настройки["НастройкиКомпоновщика"]);
		ПеренестиНастройкиОтборов(КомпоновщикНастроекКомпоновкиДанных.Настройки, Список)
	КонецЕсли;
КонецПроцедуры

