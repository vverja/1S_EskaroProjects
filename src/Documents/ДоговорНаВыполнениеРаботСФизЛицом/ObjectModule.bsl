////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

Перем мДлинаСуток;


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Формирует запрос по документу
//
// Параметры: 
//  Режим - режим проведения
//
// Возвращаемое значение:
//  Результат запроса
//
Функция СформироватьЗапросДляПечати()

	Запрос = Новый Запрос;
    Запрос.УстановитьПараметр("Ссылка",				Ссылка);
    Запрос.УстановитьПараметр("Адрес",				Перечисления.ТипыКонтактнойИнформации.Адрес);
    Запрос.УстановитьПараметр("АдресЮридический",	Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации);
    Запрос.УстановитьПараметр("ФизЛицо",			Сотрудник.ФизЛицо);
    Запрос.УстановитьПараметр("Дата",				Дата);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДоговорНаВыполнениеРаботСФизЛицом.Номер,
	|	ДоговорНаВыполнениеРаботСФизЛицом.Дата,
	|	ДоговорНаВыполнениеРаботСФизЛицом.Организация.НаименованиеПолное КАК НазваниеОрганизации,
	|	ДоговорНаВыполнениеРаботСФизЛицом.ДатаНачала,
	|	ДоговорНаВыполнениеРаботСФизЛицом.ДатаОкончания,
	|	ДоговорНаВыполнениеРаботСФизЛицом.ХарактерОплаты КАК ЧастотаВыплат,
	|	ЕСТЬNULL(ФИОФизЛицСрезПоследних.Фамилия + "" "" + ФИОФизЛицСрезПоследних.Имя + "" "" + ФИОФизЛицСрезПоследних.Отчество, ДоговорНаВыполнениеРаботСФизЛицом.Сотрудник.Наименование) КАК РаботникНаименование,
	|	ДоговорНаВыполнениеРаботСФизЛицом.СуммаЗаРаботу,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ДоговорНаВыполнениеРаботСФизЛицом.ВалютаДокумента.Код, ""980"") = ""980""
	|			ТОГДА ""грн.""
	|		ИНАЧЕ ДоговорНаВыполнениеРаботСФизЛицом.ВалютаДокумента.Наименование
	|	КОНЕЦ КАК ВалютаДокумента,
	|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументВид.Наименование КАК ДокументВид,
	|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументСерия,
	|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументНомер,
	|	КонтактнаяИнформация.Представление КАК АдресОрганизации,
	|	ЕСТЬNULL(ДоговорНаВыполнениеРаботСФизЛицом.Организация.ОсновнойБанковскийСчет.НомерСчета, ""__________________"") КАК НомерСчета,
	|	ЕСТЬNULL(ДоговорНаВыполнениеРаботСФизЛицом.Организация.ОсновнойБанковскийСчет.Банк.Наименование, ""_______________________________________________________________________________________"") КАК Банк
	|ИЗ
	|	Документ.ДоговорНаВыполнениеРаботСФизЛицом КАК ДоговорНаВыполнениеРаботСФизЛицом
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(&Дата, ФизЛицо = &ФизЛицо) КАК ФИОФизЛицСрезПоследних
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПаспортныеДанныеФизЛиц.СрезПоследних(&Дата, ФизЛицо = &ФизЛицо) КАК ПаспортныеДанныеФизЛицСрезПоследних
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	|		ПО ДоговорНаВыполнениеРаботСФизЛицом.Организация = КонтактнаяИнформация.Объект
	|			И (КонтактнаяИнформация.Тип = &Адрес)
	|			И (КонтактнаяИнформация.Вид = &АдресЮридический)
	|ГДЕ
	|	ДоговорНаВыполнениеРаботСФизЛицом.Ссылка = &Ссылка";
	
	Запрос.Текст = ТекстЗапроса;
	Возврат Запрос.Выполнить();
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

#Если Клиент Тогда

// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходмое количество копий.
//
//  Название макета печати передается в качестве параметра,
// по переданному названию находим имя макета в соответствии.
//
// Параметры:
//  НазваниеМакета - строка, название макета.
//
Функция Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь) Экспорт
	
	Если ИмяМакета = "Договор" Тогда
		
		ТабДокумент = Новый ТабличныйДокумент;
		ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ДоговорНаВыполнениеРаботСФизЛицом_Договор";
		
		// получаем макет
		Макет = ПолучитьМакет("Договор");
		
		// печать производится на языке, указанном в настройках пользователя
		КодЯзыкаПечать = Локализация.ПолучитьЯзыкФормированияПечатныхФорм(УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "РежимФормированияПечатныхФорм"));
		Макет.КодЯзыкаМакета = КодЯзыкаПечать;
		
		// получаем данные для печати
		Выборка = СформироватьЗапросДляПечати().Выбрать();
		Если Выборка.Следующий() Тогда
			Макет.Параметры.Заполнить(Выборка);
		КонецЕсли;
		
		Если Выборка.ЧастотаВыплат = Перечисления.ХарактерВыплатыПоДоговору.Ежемесячно Тогда
			Макет.Параметры.ЧастотаВыплат =  НСтр("ru='ежемесячно';uk='щомісячно'",КодЯзыкаПечать);
		ИначеЕсли Выборка.ЧастотаВыплат = Перечисления.ХарактерВыплатыПоДоговору.ОднократноВКонцеСрока Тогда
			Макет.Параметры.ЧастотаВыплат =  НСтр("ru='однократно в конце срока';uk='однократно у кінці строку'",КодЯзыкаПечать);
		ИначеЕсли Выборка.ЧастотаВыплат = Перечисления.ХарактерВыплатыПоДоговору.ПоСдельномуНаряду Тогда
			Макет.Параметры.ЧастотаВыплат =  НСтр("ru='по сдельному наряду';uk='за нарядом'",КодЯзыкаПечать);		
		КонецЕсли;	
		
		ТабДокумент.Вывести(Макет);
		
	КонецЕсли;
	
	УниверсальныеМеханизмы.НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер,ОбщегоНазначения.СформироватьЗаголовокДокумента(ЭтотОбъект,"Договор "));
	
КонецФункции // Печать

#КонецЕсли


// Возвращает доступные варианты печати документа
//
// Вовращаемое значение:
//  Струткура, каждая строка которой соответствует одному из вариантов печати
//  
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	
	Возврат Новый Структура("Договор","Договор");

КонецФункции // ПолучитьСтруктуруПечатныхФорм()


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

// Проверяет правильность заполнения шапки документа.
// Если какой-то из реквизтов шапки, влияющий на проведение, не заполнен или
// заполнен некорректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса по шапке,
// все проверяемые реквизиты должны быть включены в выборку по шапке.
//
// Параметры: 
//  Отказ 					- флаг отказа в проведении.
//	Заголовок				- Заголовок для сообщений об ошибках проведения
//
Процедура ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ, Заголовок)

	//  Организация
	Если Не ЗначениеЗаполнено(Организация) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указана организация!", Отказ, Заголовок);
	КонецЕсли;
	
	// Сотрудник
	Если Не ЗначениеЗаполнено(Сотрудник) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указан сотрудник, с которым заключается договор!", Отказ, Заголовок);
	Иначе 
		Если ВыборкаПоШапкеДокумента.ОшибкаНеСоответствиеСотрудникаИОрганизации Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Указан сотрудник другой организации!", Отказ, Заголовок);
		КонецЕсли;
		
		Если ВыборкаПоШапкеДокумента.ОшибкаСотрудникНеДоговорник Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Указан сотрудник, не являющийся договорником!", Отказ, Заголовок);
		КонецЕсли;
	КонецЕсли;
	
		
	// ВидРасчета
	Если НЕ ЗначениеЗаполнено(ВидРасчета) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указано начисление!", Отказ, Заголовок);
	КонецЕсли;
	
	// ДатаНачала
	Если Не ЗначениеЗаполнено(ДатаНачала) или НЕ ЗначениеЗаполнено(ДатаОкончания) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указаны даты договора!", Отказ, Заголовок);
	ИначеЕсли ДатаНачала > ДатаОкончания Тогда	
		ОбщегоНазначения.СообщитьОбОшибке("Неверно указаны даты договора!", Отказ, Заголовок);
	КонецЕсли;
	
КонецПроцедуры // ПроверитьЗаполнениеШапки()

Процедура ДвиженияРегистров()
	
	Движение = Движения.ПлановыеНачисленияРаботниковОрганизаций.Добавить();
	
	Если ХарактерОплаты = Перечисления.ХарактерВыплатыПоДоговору.Ежемесячно
		ИЛИ ХарактерОплаты = Перечисления.ХарактерВыплатыПоДоговору.ПоСдельномуНаряду
		Тогда
		Движение.Период			= ДатаНачала;
	Иначе
		Движение.Период			= Макс(НачалоМесяца(ДатаОкончания), ДатаНачала);   
	КонецЕсли;
	
	// Измерения
	Движение.Сотрудник		= Сотрудник;
	Движение.Организация	= Организация;
	Движение.ДокументОснование = Ссылка;
	Движение.ВидРасчетаИзмерение = ВидРасчета;
	
	// Ресурсы
	Движение.ВидРасчета		= ВидРасчета;
	Движение.Актуальность	= Истина;
	Движение.Показатель1	= СуммаЗаРаботу;
	Если Не ПроцедурыУправленияПерсоналом.ЗначениеУчетнойПолитикиПоПерсоналуОрганизации(глЗначениеПеременной("глУчетнаяПолитикаПоПерсоналуОрганизации"), Организация, "ИспользуютсяНачисленияВВалюте") Тогда
		Движение.Валюта1			= глЗначениеПеременной("ВалютаРегламентированногоУчета");
	Иначе
		Движение.Валюта1			= ВалютаДокумента;
	КонецЕсли;
	// Реквизиты
	
	Если ЗначениеЗаполнено(ДатаОкончания) Тогда
		Движение = Движения.ПлановыеНачисленияРаботниковОрганизаций.Добавить();
		Движение.Период 	= КонецДня(ДатаОкончания);
		
		// Измерения
		Движение.Сотрудник		= Сотрудник;
		Движение.Организация	= Организация;
		Движение.ДокументОснование = Ссылка;
		Движение.ВидРасчетаИзмерение = ВидРасчета;

		Движение.ВидРасчета		= ВидРасчета;
		Движение.Актуальность	= Ложь;
	КонецЕсли;	
	
	//взносы в фонды
	Если НазначитьВзносы Тогда
		ЗапросВзносы = Новый Запрос;
		ЗапросВзносы.УстановитьПараметр("Сотрудники",	?(ЗначениеЗаполнено(Сотрудник.ОсновноеНазначение),Сотрудник.ОсновноеНазначение,Сотрудник));
		ЗапросВзносы.УстановитьПараметр("Организация",	Организация);
		ЗапросВзносы.УстановитьПараметр("Дата",			ДатаНачала);
		ЗапросВзносы.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
		Если ЗначениеЗаполнено(Сотрудник.ГруппаВзносов) Тогда
			ЗапросВзносы.УстановитьПараметр("Группа", Сотрудник.ГруппаВзносов);
		Иначе
			ЗапросВзносы.УстановитьПараметр("Группа", Справочники.ГруппыВзносовВФонды.Договорники);
		КонецЕсли;	
		
		ЗапросВзносы.Текст = 
		"ВЫБРАТЬ
		|	   Взносы.Сотрудник,
		|	   Взносы.Налог,
		|	   ВЫБОР 
		|	    КОГДА МАКСИМУМ(Взносы.Было) = ЛОЖЬ И МАКСИМУМ(Взносы.Стало)=ИСТИНА
		|	     ТОГДА ЗНАЧЕНИЕ (Перечисление.ВидыДействияСНачислением.Начать)
		|	    КОГДА МАКСИМУМ(Взносы.Было)  = ИСТИНА И МАКСИМУМ(Взносы.Стало) = ЛОЖЬ
		|	     ТОГДА ЗНАЧЕНИЕ (Перечисление.ВидыДействияСНачислением.НеИзменять)
		|	   ИНАЧЕ ЗНАЧЕНИЕ (Перечисление.ВидыДействияСНачислением.НеИзменять)  
		|	   КОНЕЦ КАК Действие,
		|	   &Дата КАК ДатаДействия
	    |
		|	ИЗ
		|	(ВЫБРАТЬ	
		|		ВзносыВФондыРаботниковОрганизацийСрезПоследних.Сотрудник КАК Сотрудник,
		|		ВзносыВФондыРаботниковОрганизацийСрезПоследних.Налог КАК Налог,
		|		Истина КАК Было,
		|		Ложь КАК Стало
		|	 ИЗ
		|		РегистрСведений.ВзносыВФондыРаботниковОрганизаций.СрезПоследних(&Дата,Сотрудник В (&Сотрудники) И Налог.Родитель = &ФОТ) КАК ВзносыВФондыРаботниковОрганизацийСрезПоследних
		|	 ГДЕ ВзносыВФондыРаботниковОрганизацийСрезПоследних.Актуальность
		|		
		|	 ОБЪЕДИНИТЬ	
	    |
		|	 ВЫБРАТЬ
		|		Сотрудники.Ссылка,
		|		ВзносыВФондыПоГруппам.Налог,
		|		Ложь,
		|		Истина
		|	 ИЗ
		|	    Справочник.СотрудникиОрганизаций КАК Сотрудники
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВзносыВФондыПоГруппам.СрезПоследних(&Дата) КАК ВзносыВФондыПоГруппам
		|		ПО Истина
		|	 ГДЕ ВзносыВФондыПоГруппам.ГруппаВзносов = &Группа
		|		И ВзносыВФондыПоГруппам.Налог.Родитель = &ФОТ
		|		И ВзносыВФондыПоГруппам.Действует
		|		И Сотрудники.Ссылка В (&Сотрудники)
		|	 ) КАК Взносы
		|   ГДЕ НЕ Взносы.Было
		|
		|	СГРУППИРОВАТЬ ПО Сотрудник, Налог";
		
		ЗапросВзносы.УстановитьПараметр("ФОТ", Справочники.Налоги.Взносы);
		ВыборкаПоВзносам = ЗапросВзносы.Выполнить().Выбрать();
		Пока ВыборкаПоВзносам.Следующий() Цикл
			Если ВыборкаПоВзносам.Действие = Перечисления.ВидыДействияСНачислением.Начать тогда
			
				Движение = Движения.ВзносыВФондыРаботниковОрганизаций.Добавить();
				
				Движение.Период       = ВыборкаПоВзносам.ДатаДействия;
				// Измерения
				Движение.Организация  = Организация;
				Движение.Сотрудник    = ВыборкаПоВзносам.Сотрудник;
				Движение.Налог		  = ВыборкаПоВзносам.Налог;
				Движение.Актуальность	= Истина;
			ИначеЕсли ВыборкаПоВзносам.Действие = Перечисления.ВидыДействияСНачислением.Прекратить тогда
			
				Движение = Движения.ВзносыВФондыРаботниковОрганизаций.Добавить();
				
				Движение.Период       = ВыборкаПоВзносам.ДатаДействия-1;
				// Измерения
				Движение.Организация  = Организация;
				Движение.Сотрудник    = ВыборкаПоВзносам.Сотрудник;
				Движение.Налог		  = ВыборкаПоВзносам.Налог;
				Движение.Актуальность = Ложь;	
			ИначеЕсли ВыборкаПоВзносам.Действие = Перечисления.ВидыДействияСНачислением.НеИзменять тогда
			    Сообщить("На дату начала договора " + Ссылка + " сотруднику уже назначено удержание взноса " + ВыборкаПоВзносам.Налог.Наименование );
   			КонецЕсли;	
		КонецЦикла;	
		
		ЗапросВзносы.УстановитьПараметр("ФОТ", Справочники.Налоги.ВзносыФОТ);
		ВыборкаПоВзносам = ЗапросВзносы.Выполнить().Выбрать();
		Пока ВыборкаПоВзносам.Следующий() Цикл
			Если ВыборкаПоВзносам.Действие = Перечисления.ВидыДействияСНачислением.Начать тогда
			
				Движение = Движения.ВзносыВФондыРаботниковОрганизаций.Добавить();
				
				Движение.Период       = ВыборкаПоВзносам.ДатаДействия;
				// Измерения
				Движение.Организация  = Организация;
				Движение.Сотрудник    = ВыборкаПоВзносам.Сотрудник;
				Движение.Налог		  = ВыборкаПоВзносам.Налог;
				Движение.Актуальность	= Истина;
				
			ИначеЕсли ВыборкаПоВзносам.Действие = Перечисления.ВидыДействияСНачислением.Прекратить тогда
			
				Движение = Движения.ВзносыВФондыРаботниковОрганизаций.Добавить();
				
				Движение.Период       = ВыборкаПоВзносам.ДатаДействия-1;
				// Измерения
				Движение.Организация  = Организация;
				Движение.Сотрудник    = ВыборкаПоВзносам.Сотрудник;
				Движение.Налог		  = ВыборкаПоВзносам.Налог;
				Движение.Актуальность = Ложь;	
	
			ИначеЕсли ВыборкаПоВзносам.Действие = Перечисления.ВидыДействияСНачислением.НеИзменять тогда
			    Сообщить("На дату начала договора сотруднику уже назначено удержание взноса " + ВыборкаПоВзносам.Налог.Наименование );
   			КонецЕсли;	
		КонецЦикла;
	КонецЕсли;
	
	// регистрируем способы отражения в учете
	Если НЕ ЗначениеЗаполнено(СпособОтраженияВБухучете) Тогда
		//не задан способ отражения, не регистрируем в регистре сведений
		Возврат;
	КонецЕсли;
	
	//Движения по регистру "УчетПлановыхНачисленийРаботниковОрганизаций"
	Движение = Движения.УчетПлановыхНачисленийРаботниковОрганизаций.Добавить();
	
	Если ХарактерОплаты = Перечисления.ХарактерВыплатыПоДоговору.Ежемесячно
		ИЛИ ХарактерОплаты = Перечисления.ХарактерВыплатыПоДоговору.ПоСдельномуНаряду
		Тогда
		Движение.Период	= ДатаНачала;
	Иначе
		Движение.Период	= НачалоМесяца(ДатаОкончания);
	КонецЕсли;
	
	// Измерения
	Движение.Сотрудник			= Сотрудник;
	Движение.Организация		= Организация;
	Движение.ВидРасчета			= ВидРасчета;
	Движение.ДокументОснование 	= Ссылка;
	
	// Ресурсы
	Движение.СпособОтраженияВБухучете			= СпособОтраженияВБухучете;
	
КонецПроцедуры


// Формирует запрос по шапке документа
//
// Параметры:
//	Режим - режим проведения
//
// Возвращаемое значение:
//	Результат запроса
Функция СформироватьЗапросПоШапке()
	
	Запрос = Новый Запрос;
	
	// Установим параметры запроса
	Запрос.УстановитьПараметр("парамГоловнаяОрганизация",	Организация);
	Запрос.УстановитьПараметр("парамСсылка",	Ссылка);
	
	СписокДоговоров = Новый Массив;
	СписокДоговоров.Добавить(Перечисления.ВидыДоговоровСФизЛицами.Авторский);
	СписокДоговоров.Добавить(Перечисления.ВидыДоговоровСФизЛицами.Подряда);
	Запрос.УстановитьПараметр("парамСписокДоговоров",	СписокДоговоров);
	
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА Договор.Сотрудник.Организация = &парамГоловнаяОрганизация
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ОшибкаНеСоответствиеСотрудникаИОрганизации,
	|	ВЫБОР
	|		КОГДА Договор.Сотрудник.ВидДоговора В (&парамСписокДоговоров)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ОшибкаСотрудникНеДоговорник
	|ИЗ
	|	Документ.ДоговорНаВыполнениеРаботСФизЛицом КАК Договор
	|ГДЕ
	|	Договор.Ссылка = &парамСсылка";
	
	Возврат Запрос.Выполнить();
	
КонецФункции // СформироватьЗапросПоШапке()


///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПроведения(Отказ, Режим)
	
	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка);
	
	РезультатЗапросаПоШапке = СформироватьЗапросПоШапке();
	
	// Получим реквизиты шапки из запроса
	ВыборкаПоШапкеДокумента = РезультатЗапросаПоШапке.Выбрать();
	
	Если ВыборкаПоШапкеДокумента.Следующий() Тогда
		//Надо позвать проверку заполнения реквизитов шапки
		ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ, Заголовок);
		// Движения стоит добавлять, если в проведении еще не отказано (отказ =ложь)
		Если НЕ Отказ Тогда
			
			ДвиженияРегистров()
			
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(Основание)
	
	ТипОснования = ТипЗнч(Основание);	
	Если ТипОснования = Тип("СправочникСсылка.СотрудникиОрганизаций") Тогда
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Основание, "Организация, ВидДоговора, ФизЛицо");
		
		Если ВидДоговора = Перечисления.ВидыДоговоровСФизЛицами.ТрудовойДоговор Тогда
			
			Организация = ""; ВидДоговора = ""; ФизЛицо = "";
			
			Возврат
		Иначе
			
			Сотрудник = Основание;
			
			Если ВидДоговора = Перечисления.ВидыДоговоровСФизЛицами.Подряда
				И ЗначениеЗаполнено(Сотрудник.ОсновноеНазначение)
				Тогда
				ВидРасчета = ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.ДоговорПодряда;
			ИначеЕсли ВидДоговора = Перечисления.ВидыДоговоровСФизЛицами.Подряда
				Тогда
				ВидРасчета = ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.ДоговорПодрядаСПД;
			ИначеЕсли ВидДоговора = Перечисления.ВидыДоговоровСФизЛицами.Авторский Тогда
				ВидРасчета = ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.АвторскоеВознаграждение;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ПроцедурыУправленияПерсоналом.ЗаполнитьФизЛицо(ЭтотОбъект);
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

мДлинаСуток = 86400;