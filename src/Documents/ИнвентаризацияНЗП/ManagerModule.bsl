// Сформировать печатные формы объектов
//
// ВХОДЯЩИЕ:
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
//   ОшибкиПечати          - Список значений  - Ошибки печати  (значение - ссылка на объект, представление - текст ошибки)
//   ОбъектыПечати         - Список значений  - Объекты печати (значение - ссылка на объект, представление - имя области в которой был выведен объект)
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ИнвентаризацияБух") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ИнвентаризацияБух", "Инвентаризация (регл.)", ПечатьИнвентаризация(МассивОбъектов, ОбъектыПечати, "Бух"));
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ИнвентаризацияУпр") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ИнвентаризацияУпр", "Инвентаризация (упр.)", ПечатьИнвентаризация(МассивОбъектов, ОбъектыПечати, ""));
	КонецЕсли;

	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ИнвентаризационнаяОпись") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ИнвентаризационнаяОпись", "Инвентаризационная опись", ПечатьИнвентаризационнаяОпись(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "АктИнвентаризацииНЗП") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "АктИнвентаризацииНЗП", "Акт инвентаризации НЗП", ПечатьАктИнвентаризацииНЗП(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;

КонецПроцедуры

// Процедура формирует печатную форму "Акт инвентаризации НЗП"
//
Функция ПечатьИнвентаризация(МассивОбъектов, ОбъектыПечати, ТипУчета)
	
	Суффикс = ?(ТипУчета = "Бух", "Бух", "");
	
	Если ТипУчета = "Бух" Тогда
		ТекстПодразделение = "ПодразделениеОрганизации";
	Иначе
		ТекстПодразделение = "Подразделение";
	КонецЕсли;
	
	ТекстЗапросаШапка = "
	|ВЫБРАТЬ
	|	ИнвентаризацияНЗП.Организация,
	|	ИнвентаризацияНЗП." + ТекстПодразделение + " КАК Подразделение,
	|	ИнвентаризацияНЗП." + ТекстПодразделение + ".Представление КАК ПодразделениеПредставление,
	|	ИнвентаризацияНЗП.Номер,
	|	ИнвентаризацияНЗП.Дата
	|ИЗ
	|	Документ.ИнвентаризацияНЗП КАК ИнвентаризацияНЗП
	|ГДЕ
	|	ИнвентаризацияНЗП.Ссылка = &ТекДок
	|";
	
	ТекстЗапросаМатериалы = "
	|ВЫБРАТЬ
	|	НомерСтроки КАК НомерСтроки,
	|	Номенклатура,
	|	ХарактеристикаНоменклатуры КАК Характеристика,
	|	СерияНоменклатуры КАК Серия,
	|	ВЫРАЗИТЬ(Номенклатура.НаименованиеПолное КАК Строка(1000)) КАК НоменклатураПредставление,
	|	СтатьяЗатрат,
	|	НоменклатурнаяГруппа,
	|	Заказ,
	|	СчетЗатрат,
	|	ПРЕДСТАВЛЕНИЕ(ИнвентаризацияНЗП.СтатьяЗатрат) 			КАК СтатьяЗатратПредставление,
	|	ПРЕДСТАВЛЕНИЕ(ИнвентаризацияНЗП.НоменклатурнаяГруппа) 	КАК НоменклатурнаяГруппаПредставление,
	|	ПРЕДСТАВЛЕНИЕ(ИнвентаризацияНЗП.Заказ) 					КАК ЗаказПредставление,
	|	ПРЕДСТАВЛЕНИЕ(ИнвентаризацияНЗП.СчетЗатрат) 			КАК СчетЗатратПредставление,
	|	Количество,
	|	ЕдиницаИзмерения
	|ИЗ
	|	Документ.ИнвентаризацияНЗП.Материалы КАК ИнвентаризацияНЗП
	|ГДЕ
	|	ИнвентаризацияНЗП.Ссылка = &ТекДок
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Если ТипУчета = "Бух" Тогда
		ТекстСумма = "СуммаРегл";
	Иначе
		ТекстСумма = "Сумма";
	КонецЕсли;

	ТекстЗапросаПрочиеЗатраты = "
	|ВЫБРАТЬ
	|	ИнвентаризацияНЗП.СтатьяЗатрат,
	|	ИнвентаризацияНЗП.НоменклатурнаяГруппа,
	|	ИнвентаризацияНЗП.Заказ,
	|	ИнвентаризацияНЗП.СчетЗатрат,
	|	ПРЕДСТАВЛЕНИЕ(ИнвентаризацияНЗП.СтатьяЗатрат) 			КАК СтатьяЗатратПредставление,
	|	ПРЕДСТАВЛЕНИЕ(ИнвентаризацияНЗП.НоменклатурнаяГруппа) 	КАК НоменклатурнаяГруппаПредставление,
	|	ПРЕДСТАВЛЕНИЕ(ИнвентаризацияНЗП.Заказ) 					КАК ЗаказПредставление,
	|	ПРЕДСТАВЛЕНИЕ(ИнвентаризацияНЗП.СчетЗатрат) 			КАК СчетЗатратПредставление,
	|	ИнвентаризацияНЗП." + ТекстСумма + " КАК Сумма,
	|	ИнвентаризацияНЗП.СуммаРегл,
	|	ИнвентаризацияНЗП.НомерСтроки
	|ИЗ
	|	Документ.ИнвентаризацияНЗП.ПрочиеЗатраты КАК ИнвентаризацияНЗП
	|ГДЕ
	|	ИнвентаризацияНЗП.Ссылка = &ТекДок
	|
	|УПОРЯДОЧИТЬ ПО
	|	ИнвентаризацияНЗП.НомерСтроки";

	ТабДокумент  = Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ИнвентаризацияНЗП_Инвентаризация";
	ТабДокумент.ОриентацияСтраницы  = ОриентацияСтраницы.Ландшафт;
	Макет   = ПолучитьМакет("Инвентаризация");

	ПервыйДокумент = Истина;
	
	Для Каждого Ссылка Из МассивОбъектов Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабДокумент.ВысотаТаблицы + 1;

		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапросаШапка;
		
		Запрос.УстановитьПараметр("ТекДок", Ссылка);
		
		РезультатЗапроса = Запрос.Выполнить();
		Шапка = РезультатЗапроса.Выбрать();
		Шапка.Следующий();
	
		ИнфоОрг = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(Шапка.Организация, Ссылка.Дата);
		
		Область = Макет.ПолучитьОбласть("Шапка");
		Область.Параметры.ТекстЗаголовок   = ОбщегоНазначения.СформироватьЗаголовокДокумента(Шапка, "Инвентаризация");
		Область.Параметры.ПечОрганизация   = ИнфоОрг.ПолноеНаименование;
		Область.Параметры.ПечПодразделение = Шапка.ПодразделениеПредставление;
		Область.Параметры.Организация      = Шапка.Организация;
		Область.Параметры.Подразделение    = Шапка.Подразделение;
		
		ТабДокумент.Вывести(Область);
	
		// Вывод данных о материалах
		Область = Макет.ПолучитьОбласть("ТабШапка" + Суффикс);
		ТабДокумент.Вывести(Область);
		
		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапросаМатериалы;
		Запрос.УстановитьПараметр("ТекДок", Ссылка);
			
		РезультатЗапроса = Запрос.Выполнить();
		Обход = РезультатЗапроса.Выбрать();
		Пока Обход.Следующий() Цикл
			
			Область  = Макет.ПолучитьОбласть("Строка" + Суффикс);
			Область.Параметры.Заполнить(Обход);
			Область.Параметры.НоменклатураПредставление = СокрЛП(Обход.НоменклатураПредставление) + ФормированиеПечатныхФормСервер.ПредставлениеСерий(Обход);
			
			ТабДокумент.Вывести(Область);
			
		КонецЦикла;
	
		Если Ссылка.ПрочиеЗатраты.Количество() > 0 Тогда
			
			
			Область = Макет.ПолучитьОбласть("Низ");
			ТабДокумент.Вывести( Область);
			
			Область = Макет.ПолучитьОбласть("ТабЗатраты" + Суффикс);
			ТабДокумент.Вывести( Область);
			
			Область = Макет.ПолучитьОбласть("СтрокаЗатраты" + Суффикс);
			
			// Вывод данных о прочих затратах
			
			Запрос = Новый Запрос;
			Запрос.Текст = ТекстЗапросаПрочиеЗатраты;
			Запрос.УстановитьПараметр( "ТекДок", Ссылка);
			
			РезультатЗапроса = Запрос.Выполнить();
			Обход = РезультатЗапроса.Выбрать();
			Пока Обход.Следующий() Цикл
				
				Область.Параметры.Заполнить(Обход);
				
				ТабДокумент.Вывести( Область);
				
			КонецЦикла;
		
		КонецЕсли;
	
		Область = Макет.ПолучитьОбласть("Подвал");
		ТабДокумент.Вывести( Область);
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДокумент, НомерСтрокиНачало, ОбъектыПечати, Ссылка);

	КонецЦикла;

	Возврат ТабДокумент;
	
КонецФункции // ПечатьИнвентаризация()

// Процедура формирует печатную форму "Акт инвентаризации НЗП"
//
Функция ПечатьАктИнвентаризацииНЗП(МассивОбъектов, ОбъектыПечати)
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ИнвентаризацияНЗП.Организация,
	|	ИнвентаризацияНЗП.Подразделение,
	|	ИнвентаризацияНЗП.Ссылка,
	|	ИнвентаризацияНЗП.Номер,
	|	ИнвентаризацияНЗП.Дата,
	|	ИнвентаризацияНЗП.Заказ,
	|	ИнвентаризацияНЗП.Материалы.(
	|		Ссылка,
	|		НомерСтроки КАК НомерСтроки,
	|		ЕдиницаИзмеренияМест,
	|		Заказ,
	|		КоличествоМест,
	|		Коэффициент,
	|		Номенклатура,
	|		НоменклатурнаяГруппа,
	|		СерияНоменклатуры,
	|		Количество,
	|		ХарактеристикаНоменклатуры,
	|		СчетЗатрат,
	|		ЕдиницаИзмерения,
	|		Номенклатура.Представление КАК НоменклатураПредставление,
	|		Номенклатура.БазоваяЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияКод
	|	),
	|	ИнвентаризацияНЗП.ВводитьЗаказыПоСтрокам,
	|	ИнвентаризацияНЗП.Заказ.Представление КАК ЗаказПредставление,
	|	ИнвентаризацияНЗП.Организация.Представление КАК ОрганизацияПредставление,
	|	ИнвентаризацияНЗП.Подразделение.Представление КАК ПодразделениеПредставление
	|ИЗ
	|	Документ.ИнвентаризацияНЗП КАК ИнвентаризацияНЗП
	|ГДЕ
	|	ИнвентаризацияНЗП.Ссылка = &ТекДок
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	ТабДокумент  = Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ИнвентаризацияНЗП_АктИнвентаризацииНЗП";
	ТабДокумент.ОриентацияСтраницы  = ОриентацияСтраницы.Ландшафт;
	Макет   = ПолучитьМакет("АктИнвентаризацииНЗП");
	
	ПервыйДокумент = Истина;
	
	Для Каждого Ссылка Из МассивОбъектов Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабДокумент.ВысотаТаблицы + 1;

		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапроса;
	
		Запрос.УстановитьПараметр( "ТекДок", Ссылка);
	
		РезультатЗапроса = Запрос.Выполнить();
		Шапка = РезультатЗапроса.Выбрать();
		Шапка.Следующий();
	
		ИнфоОрг = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице( Шапка.Организация, Ссылка.Дата);
		
		Область = Макет.ПолучитьОбласть( "Шапка");
		Область.Параметры.ПечНомерДок      = Шапка.Номер;
		Область.Параметры.ПечДатаДок       = Шапка.Дата;
		Область.Параметры.ПечОрганизация   = ИнфоОрг.ПолноеНаименование;
		Область.Параметры.ПечПодразделение = Шапка.ПодразделениеПредставление;
		Область.Параметры.КодЕДРПОУ 	   = УправлениеКонтактнойИнформацией.ПолучитьКодОрганизации(ИнфоОрг);	
		Область.Параметры.Организация      = Шапка.Организация;
		Область.Параметры.Подразделение    = Шапка.Подразделение;
		
		ТабДокумент.Вывести(Область);
		
		ВсегоКолФакт = 0;
		Обход        = Шапка.Материалы.Выбрать();
		Пока Обход.Следующий() Цикл
			
			Область  = Макет.ПолучитьОбласть("Строка");
			Область.Параметры.ПечНоменклатура = Обход.НоменклатураПредставление;
			Область.Параметры.Номенклатура    = Обход.Номенклатура;
			Область.Параметры.ПечКодТМЦ       = Обход.Номенклатура.Код;
			Область.Параметры.ПечКодЕИ        = Обход.ЕдиницаИзмеренияКод;
			Область.Параметры.ПечЕИ           = Обход.ЕдиницаИзмерения.Наименование;
			Область.Параметры.ПечКолФакт      = Формат( Обход.Количество, "ЧЦ=15; ЧДЦ=3");
			ТабДокумент.Вывести(Область);
			
			ВсегоКолФакт = ВсегоКолФакт + Обход.Количество;
			
		КонецЦикла;
	
		Область = Макет.ПолучитьОбласть("Подвал");
		ТабДокумент.Вывести( Область);
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДокумент, НомерСтрокиНачало, ОбъектыПечати, Ссылка);

	КонецЦикла;

	Возврат ТабДокумент;
	
КонецФункции // ПечатьАктИнвентаризацииНЗП()

// Процедура формирует печатную форму "Инвентаризационная опись"
//
Функция ПечатьИнвентаризационнаяОпись(МассивОбъектов, ОбъектыПечати)
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ИнвентаризацияНЗП.Организация,
	|	ИнвентаризацияНЗП.Подразделение,
	|	ИнвентаризацияНЗП.Ссылка,
	|	ИнвентаризацияНЗП.Номер,
	|	ИнвентаризацияНЗП.Дата,
	|	ИнвентаризацияНЗП.Заказ,
	|	ИнвентаризацияНЗП.Материалы.(
	|		Ссылка,
	|		НомерСтроки КАК НомерСтроки,
	|		ЕдиницаИзмеренияМест,
	|		Заказ,
	|		КоличествоМест,
	|		Коэффициент,
	|		Номенклатура,
	|		НоменклатурнаяГруппа,
	|		СерияНоменклатуры,
	|		Количество,
	|		ХарактеристикаНоменклатуры,
	|		СчетЗатрат,
	|		ЕдиницаИзмерения,
	|		Номенклатура.Представление КАК НоменклатураПредставление,
	|		Номенклатура.БазоваяЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияКод
	|	),
	|	ИнвентаризацияНЗП.ВводитьЗаказыПоСтрокам,
	|	ИнвентаризацияНЗП.Заказ.Представление КАК ЗаказПредставление,
	|	ИнвентаризацияНЗП.Организация.Представление КАК ОрганизацияПредставление,
	|	ИнвентаризацияНЗП.Подразделение.Представление КАК ПодразделениеПредставление
	|ИЗ
	|	Документ.ИнвентаризацияНЗП КАК ИнвентаризацияНЗП
	|ГДЕ
	|	ИнвентаризацияНЗП.Ссылка = &ТекДок
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	ТабДокумент  = Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ИнвентаризацияНЗП_ИнвентаризационнаяОпись";
	ТабДокумент.ОриентацияСтраницы  = ОриентацияСтраницы.Ландшафт;
	Макет   = ПолучитьМакет("ИнвентаризационнаяОпись");
	
	ПервыйДокумент = Истина;
	
	Для Каждого Ссылка Из МассивОбъектов Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабДокумент.ВысотаТаблицы + 1;

		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапроса;
		
		Запрос.УстановитьПараметр( "ТекДок", Ссылка);
		
		РезультатЗапроса = Запрос.Выполнить();
		Шапка = РезультатЗапроса.Выбрать();
		Шапка.Следующий();
	
		ИнфоОрг = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице( Шапка.Организация, Ссылка.Дата);
		
		Область = Макет.ПолучитьОбласть( "Шапка");
		Область.Параметры.ПечНомерДок      = Шапка.Номер;
		Область.Параметры.ПечДатаДок       = Шапка.Дата;
		Область.Параметры.ПечОрганизация   = ИнфоОрг.ПолноеНаименование;
		Область.Параметры.ПечПодразделение = Шапка.ПодразделениеПредставление;
		Если НЕ Шапка.ВводитьЗаказыПоСтрокам И ЗначениеЗаполнено(Ссылка.Заказ)Тогда
			Область.Параметры.ПечЗаказ     = Шапка.ЗаказПредставление;
			Область.Параметры.Заказ        = Шапка.ЗаказПредставление;
		КонецЕсли;
		Область.Параметры.КодЕДРПОУ 	   = УправлениеКонтактнойИнформацией.ПолучитьКодОрганизации(ИнфоОрг);	
		Область.Параметры.Организация      = Шапка.Организация;
		Область.Параметры.Подразделение    = Шапка.Подразделение;
		
		ТабДокумент.Вывести(Область);
		ОбластьТабШапка = Макет.ПолучитьОбласть("ТабШапка");
		ТабДокумент.Вывести(ОбластьТабШапка);
		
		Индекс       = 0;
		ВсегоТМЦ     = 0;
		ВсегоКолФакт = 0;
		КолФактСтр   = 0;
		СтрокНаЛисте = 10;
		Обход        = Шапка.Материалы.Выбрать();
		Пока Обход.Следующий() Цикл
			
			Если Индекс >= СтрокНаЛисте Тогда
				ОбластьПодвал = Макет.ПолучитьОбласть("ПодвалСтраницы");
				ОбластьПодвал.Параметры.КолНомСтр  = Индекс;
				ОбластьПодвал.Параметры.ИтогКолСтр = КолФактСтр;
				ТабДокумент.Вывести( ОбластьПодвал);
				Индекс     = 0;
				КолФактСтр = 0;
				ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ТабДокумент.Вывести(ОбластьТабШапка);
				СтрокНаЛисте = 31; // На первом листе 9 строк, на остальных 30
			КонецЕсли;
			
			Индекс   = Индекс + 1;
			ВсегоТМЦ = ВсегоТМЦ + 1;
			Область  = Макет.ПолучитьОбласть("Строка");
			Область.Параметры.ПечНомер        = Формат( Индекс, "ЧЦ=6; ЧН=");
			Область.Параметры.ПечНоменклатура = Обход.НоменклатураПредставление;
			Область.Параметры.Номенклатура    = Обход.Номенклатура;
			Область.Параметры.ПечКодТМЦ       = Обход.Номенклатура.Код;
			Область.Параметры.ПечКодЕИ        = Обход.ЕдиницаИзмеренияКод;
			Область.Параметры.ПечЕИ           = Обход.ЕдиницаИзмерения.Наименование;
			Область.Параметры.ПечКолФакт      = Формат( Обход.Количество, "ЧЦ=15; ЧДЦ=3");
			ТабДокумент.Вывести(Область);
			
			КолФактСтр   = КолФактСтр   + Обход.Количество;
			ВсегоКолФакт = ВсегоКолФакт + Обход.Количество;
			
		КонецЦикла;
		
		Область = Макет.ПолучитьОбласть("Итого");
		Область.Параметры.ПечКолФактИтого = Формат( ВсегоКолФакт, "ЧЦ=15; ЧДЦ=3");
		ТабДокумент.Вывести( Область);
		Если Индекс > 0 Тогда
			ОбластьПодвал = Макет.ПолучитьОбласть("ПодвалСтраницы");
			ОбластьПодвал.Параметры.КолНомСтр  = Индекс;
			ОбластьПодвал.Параметры.ИтогКолСтр = КолФактСтр;
			ТабДокумент.Вывести( ОбластьПодвал);
		КонецЕсли;
		
		Область = Макет.ПолучитьОбласть("Подвал");
		Область.Параметры.ВсегоТМЦ      = ВсегоТМЦ;
		Область.Параметры.ИтогоКолОпись = ВсегоКолФакт;
		ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		ТабДокумент.Вывести( Область);
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДокумент, НомерСтрокиНачало, ОбъектыПечати, Ссылка);

	КонецЦикла;

	Возврат ТабДокумент;
	
КонецФункции // ПечатьИнвентаризационнаяОпись()
	
// В функции описано, какие данные следует сохранять в шаблоне
Функция СтруктураДополнительныхДанныхФормы() Экспорт
	
	Возврат ХранилищаНастроек.ДанныеФорм.СформироватьСтруктуруДополнительныхДанных("Материалы, ПрочиеЗатраты");
	
КонецФункции
