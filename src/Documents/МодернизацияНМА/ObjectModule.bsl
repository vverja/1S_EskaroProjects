Перем мУдалятьДвижения;

// Строки, хранят реквизиты имеющие смысл только для бух. учета и упр. соответственно
// в случае если документ не отражается в каком-то виде учета, делаются невидимыми

Перем мСтрокаРеквизитыБухУчета Экспорт; // (Регл)
Перем мСтрокаРеквизитыУпрУчета Экспорт; // (Упр)
Перем мСтрокаРеквизитыНалУчета Экспорт; // (Регл)

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА

// Процедура заполняет структуры именами реквизитов, которые имеют смысл
// только для определенного вида учета
//
Процедура ЗаполнитьСписокРеквизитовЗависимыхОтТиповУчета() Экспорт

	ЗаполнитьСписокРеквизитовЗависимыхОтТиповУчетаУпр();
	ЗаполнитьСписокРеквизитовЗависимыхОтТиповУчетаРегл();

КонецПроцедуры // ЗаполнитьСписокРеквизитовЗависимыхОтТиповУчета()

// Процедура заполняет структуры именами реквизитов, которые имеют смысл
// только для управленческого учета
//
Процедура ЗаполнитьСписокРеквизитовЗависимыхОтТиповУчетаУпр()

	мСтрокаРеквизитыУпрУчета =  "Стоимость,
								|НМА.СрокПолезногоИспользованияУУ,
								|НМА.ОбъемПродукцииРаботУУ,
								|НМА.СтоимостьУУ,
								|НМА.СуммаМодернизацииУУ,
								|НМА.ЛиквидационнаяСтоимостьУУ";

КонецПроцедуры // ЗаполнитьСписокРеквизитовЗависимыхОтТиповУчетаУпр()

// Процедура заполняет структуры именами реквизитов, которые имеют смысл
// только для регламентированного учета
//
Процедура ЗаполнитьСписокРеквизитовЗависимыхОтТиповУчетаРегл()

	мСтрокаРеквизитыБухУчета =  "СтоимостьБУ,
								|СчетУчетаБУВнеоборотногоАктива,
								|НМА.СрокПолезногоИспользованияБУ,
								|НМА.СрокПолезногоИспользованияНУ,
								|НМА.ОбъемПродукцииРаботБУ,
								|НМА.СтоимостьБУ,
								|НМА.СтоимостьНУ,
								|НМА.СуммаМодернизацииБУ,
								|НМА.СуммаМодернизацииНУ,
								|НМА.ЛиквидационнаяСтоимостьБУ";

	мСтрокаРеквизитыНалУчета =  "";

КонецПроцедуры // ЗаполнитьСписокРеквизитовЗависимыхОтТиповУчетаРегл()

// Функция заполняет список значений доступных видов событий
// только для регламентированного учета
//
Функция ПолучитьСписокЗначенийВидыСобытий() Экспорт
	
	ВидыСобытий = Новый СписокЗначений;
	
	ВидыСобытий.Добавить(Перечисления.ВидыСобытийОС.Модернизация);
	
	Возврат ВидыСобытий;
	
 КонецФункции


////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ

#Если Клиент Тогда

// Функция формирует табличный документ унифицированной формы ОЗ-2
//
// Параметры: 
//  Нет.
//
// Возвращаемое значение:
//  Табличный документ по форме ОЗ-2.
//
Функция ПечатьОЗ2(ПечатьПоДаннымУпрУчета = Истина)

	Макет       = ПолучитьОбщийМакет("ОЗ2");
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_Форма_ОЗ2";
	
	Если Не ПечатьПоДаннымУпрУчета Тогда
		
		Запрос       = Новый Запрос;
		Запрос.УстановитьПараметр("Период",         МоментВремени());
		Запрос.УстановитьПараметр("ТекОрганизация", Организация);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(КодыОрганизацииСрезПоследних.Организация.НаименованиеПолное
		|	                                      КАК СТРОКА(1000))     КАК Организация,
		|	КодыОрганизацииСрезПоследних.КодПоЕДРПОУ                    КАК ЕДРПОУ
		|ИЗ
		|	РегистрСведений.КодыОрганизации.СрезПоследних(&Период, Организация = &ТекОрганизация)
		|	               КАК КодыОрганизацииСрезПоследних";
		ВыборкаПоШапке = Запрос.Выполнить().Выбрать();
		ВыборкаПоШапке.Следующий();
		
	КонецЕсли;

	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Ссылка",         Ссылка);
	Запрос.УстановитьПараметр("Период",         МоментВремени());
	Запрос.УстановитьПараметр("ТекОрганизация", Организация);
	СписокОС = НематериальныеАктивы.ВыгрузитьКолонку("ОсновноеСредство");
	Запрос.УстановитьПараметр("СписокОС",       СписокОС);
	
	Если ПечатьПоДаннымУпрУчета Тогда
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	МодернизацияОСОС.Ссылка.Номер                        КАК НомерАкта,
		|	МодернизацияОСОС.Ссылка.Дата                         КАК ДатаАкта,
		|	МодернизацияОСОС.Ссылка.Событие                      КАК ВидОперации,
		|	МодернизацияОСОС.ОсновноеСредство.Код                КАК ИнвентарныйНомер,
		|	МодернизацияОСОС.ОсновноеСредство.ЗаводскойНомер     КАК ЗаводскойНомер,
		|	МодернизацияОСОС.ОсновноеСредство.НаименованиеПолное КАК ОсновноеСредствоНаименование,
		|	МодернизацияОСОС.СуммаМодернизацииУУ                 КАК СуммаМодернизации,
		|	МодернизацияОСОС.ЛиквидационнаяСтоимостьУУ           КАК ЛиквидационнаяСтоимость,
		|	МодернизацияОСОС.СрокПолезногоИспользованияУУ        КАК СрокПолезногоИспользования,
		|	МодернизацияОСОС.ОбъемПродукцииРаботУУ               КАК ОбъемПродукцииРабот,
		|	МодернизацияОСОС.Ссылка.Сдал       					 КАК Сдал,
		|	МодернизацияОСОС.Ссылка.Принял       				 КАК Принял,
		|	МодернизацияОСОС.Ссылка.ДатаНачалаМодернизации       КАК ДатаНачала,
		|	МодернизацияОСОС.Ссылка.ДатаОкончанияМодернизации    КАК ДатаОкончания,
		|	МодернизацияОСОС.Ссылка.ЧтоНеВыполнено 				 КАК ЧтоНеВыполнено,
		|	МодернизацияОСОС.Ссылка.ЧтоИзменилось                КАК ЧтоИзменилось,
		|	РАЗНОСТЬДАТ(МодернизацияОСОС.Ссылка.ДатаНачалаМодернизации,
		|	            МодернизацияОСОС.Ссылка.ДатаОкончанияМодернизации,
		|	            ДЕНЬ)                                    КАК КоличествоДней,
		|	МестонахождениеОС.МОЛ.Код                            КАК КодМОЛа,
		|	МестонахождениеОС.Местонахождение.Наименование       КАК Подразделение
		|ИЗ
		|	Документ.МодернизацияОС.ОС КАК МодернизацияОСОС
		|		ЛЕВОЕ СОЕДИНЕНИЕ 
		|			РегистрСведений.МестонахождениеОС.СрезПоследних КАК МестонахождениеОС
		|		ПО МодернизацияОСОС.ОсновноеСредство = МестонахождениеОС.ОсновноеСредство
		|ГДЕ
		|	МодернизацияОСОС.Ссылка = &Ссылка";
	Иначе
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	МодернизацияОСОС.Ссылка.Номер                          КАК НомерАкта,
		|	МодернизацияОСОС.Ссылка.Дата                           КАК ДатаАкта,
		|	МодернизацияОСОС.Ссылка.СобытиеРегл                    КАК ВидОперации,
		|	МодернизацияОСОС.ОсновноеСредство.ЗаводскойНомер       КАК ЗаводскойНомер,
		|	МодернизацияОСОС.ОсновноеСредство.НаименованиеПолное   КАК ОсновноеСредствоНаименование,
		|	МодернизацияОСОС.СуммаМодернизацииБУ                   КАК СуммаМодернизации,
		|	МодернизацияОСОС.ЛиквидационнаяСтоимостьБУ             КАК ЛиквидационнаяСтоимость,
		|	МодернизацияОСОС.СрокПолезногоИспользованияБУ          КАК СрокПолезногоИспользования,
		|	МодернизацияОСОС.ОбъемПродукцииРаботБУ                 КАК ОбъемПродукцииРабот,
		|	МодернизацияОСОС.Ссылка.СчетУчетаБУВнеоборотногоАктива КАК СчетКт,
		|	МодернизацияОСОС.ОсновноеСредство.Код                  КАК КодАналитикиДт,
		|	МодернизацияОСОС.Ссылка.ОбъектСтроительства.Код        КАК КодАналитикиКт,
		|	МодернизацияОСОС.Ссылка.Сдал       					   КАК Сдал,
		|	МодернизацияОСОС.Ссылка.Принял       				   КАК Принял,
		|	МодернизацияОСОС.Ссылка.ДатаНачалаМодернизации 		   КАК ДатаНачала,
		|	МодернизацияОСОС.Ссылка.ДатаОкончанияМодернизации	   КАК ДатаОкончания,
		|	МодернизацияОСОС.Ссылка.ЧтоНеВыполнено 				   КАК ЧтоНеВыполнено,
		|	МодернизацияОСОС.Ссылка.ЧтоИзменилось 				   КАК ЧтоИзменилось,
		|	РАЗНОСТЬДАТ(МодернизацияОСОС.Ссылка.ДатаНачалаМодернизации,
		|	            МодернизацияОСОС.Ссылка.ДатаОкончанияМодернизации,
		|	            ДЕНЬ)                                      КАК КоличествоДней,
		|	СчетаУчетаОСБУ.СчетУчета                               КАК СчетДт,
		|	ПервоначальныеСведенияОСБУ.ИнвентарныйНомер            КАК ИнвентарныйНомер,
		|	МестонахождениеОС.МОЛ.Код                              КАК КодМОЛа,
		|	МестонахождениеОС.Местонахождение.Наименование         КАК Подразделение
		|ИЗ
		|	Документ.МодернизацияОС.ОС КАК МодернизацияОСОС
		|		ЛЕВОЕ СОЕДИНЕНИЕ 
		|			РегистрСведений.СчетаБухгалтерскогоУчетаОС.СрезПоследних(
		|		                    &Период,
		|		                    Организация = &ТекОрганизация
		|		                    И ОсновноеСредство В (&СписокОС)) КАК СчетаУчетаОСБУ
		|		ПО МодернизацияОСОС.ОсновноеСредство = СчетаУчетаОСБУ.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ 
		|			РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет.СрезПоследних(
		|		                    &Период,
		|		                    Организация = &ТекОрганизация
		|		                    И ОсновноеСредство В (&СписокОС)) КАК ПервоначальныеСведенияОСБУ
		|		ПО МодернизацияОСОС.ОсновноеСредство = ПервоначальныеСведенияОСБУ.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ 
		|			РегистрСведений.МестонахождениеОСБухгалтерскийУчет.СрезПоследних(
		|		                    &Период,
		|		                    Организация = &ТекОрганизация
		|		                    И ОсновноеСредство В (&СписокОС)) КАК МестонахождениеОС
		|		ПО МодернизацияОСОС.ОсновноеСредство = МестонахождениеОС.ОсновноеСредство
		|ГДЕ
		|	МодернизацияОСОС.Ссылка = &Ссылка";
	КонецЕсли;
	
		
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	НеПервая = Ложь;
	
	Пока Выборка.Следующий() Цикл
		
		Если НеПервая Тогда
			
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			
		КонецЕсли;
		
		ОбластьМакета = Макет.ПолучитьОбласть("ОЗ2");
		
		Если Не ПечатьПоДаннымУпрУчета Тогда
			
			ОбластьМакета.Параметры.Заполнить(ВыборкаПоШапке);
			ОбластьМакета.Параметры.Организация = СокрП(ВыборкаПоШапке.Организация);
			
		Иначе
			
			ОбластьМакета.Параметры.Организация = "Управлінський облік";
			
		КонецЕсли;
		
		ОбластьМакета.Параметры.Заполнить(Выборка);
		//ЧтоИзменилось = "";
		//
		//Если ЗначениеЗаполнено(Выборка.СрокПолезногоИспользования) Тогда
		//	
		//	ЧтоИзменилось = "Строк корисного використання (в місяцях): " + Выборка.СрокПолезногоИспользования + "; ";
		//	
		//КонецЕсли;
		//
		//Если ЗначениеЗаполнено(Выборка.ОбъемПродукцииРабот) Тогда
		//	
		//	ЧтоИзменилось = ЧтоИзменилось + "Очікуваний об'єм продукції (в натуральних од.): " + Выборка.ОбъемПродукцииРабот + "; ";
		//	
		//КонецЕсли;
		//
		//Если ЗначениеЗаполнено(Выборка.ЛиквидационнаяСтоимость) Тогда
		//	
		//	ЧтоИзменилось = ЧтоИзменилось + "Ліквідаційна вартість (в " + глЗначениеПеременной("ВалютаРегламентированногоУчета") + "): " + Выборка.ЛиквидационнаяСтоимость + "; ";
		//	
		//КонецЕсли;
		//	
		//ОбластьМакета.Параметры.ЧтоИзменилось = ЧтоИзменилось;
		ТабДокумент.Вывести(ОбластьМакета);
		НеПервая = Истина;
	
	КонецЦикла;
	
	Возврат ТабДокумент;

КонецФункции // ПечатьОЗ2()

// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
//
//  Название макета печати передается в качестве параметра,
// по переданному названию находим имя макета в соответствии.
//
// Параметры:
//  НазваниеМакета - строка, название макета.
//
Процедура Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь) Экспорт

	Если ЭтоНовый() Тогда
		Предупреждение("Документ можно распечатать только после его записи");
		Возврат;
	ИначеЕсли Не УправлениеДопПравамиПользователей.РазрешитьПечатьНепроведенныхДокументов(Проведен) Тогда
		Предупреждение("Недостаточно полномочий для печати непроведенного документа!");
		Возврат;
	КонецЕсли;

	Если Не РаботаСДиалогами.ПроверитьМодифицированность(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;

	// Получить экземпляр документа на печать
	Если ИмяМакета = "ОЗ2упр" тогда
		ТабДокумент = ПечатьОЗ2();
	ИначеЕсли ИмяМакета = "ОЗ2бух" тогда
		ТабДокумент = ПечатьОЗ2(Ложь);
    ИначеЕсли ТипЗнч(ИмяМакета) = Тип("ДвоичныеДанные") Тогда

		ТабДокумент = УниверсальныеМеханизмы.НапечататьВнешнююФорму(Ссылка, ИмяМакета);
		
		Если ТабДокумент = Неопределено Тогда
			Возврат
		КонецЕсли; 
              
	КонецЕсли;
	УниверсальныеМеханизмы.НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, ОбщегоНазначения.СформироватьЗаголовокДокумента(ЭтотОбъект, "Модернизация ОС"));

КонецПроцедуры // Печать

#КонецЕсли

// Возвращает доступные варианты печати документа
//
// Возвращаемое значение:
//  Струткура, каждая строка которой соответствует одному из вариантов печати
//  
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	СтруктураПечатныхФорм = новый Структура;
	//Если  ОтражатьВУправленческомУчете тогда 
	//	СтруктураПечатныхФорм.Вставить("ОЗ2упр","Форма ОЗ-2(упр. учет)");
	//КонецЕсли;
	//Если  ОтражатьВБухгалтерскомУчете тогда 
	//	СтруктураПечатныхФорм.Вставить("ОЗ2бух","Форма ОЗ-2(бух. учет)");
	//КонецЕсли;
	
	Возврат СтруктураПечатныхФорм;

КонецФункции // ПолучитьСтруктуруПечатныхФорм()


///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА
///////////////////////////////////////////////////////////////////////////////

// Процедура определяет параметры учетной политики
//
Процедура ПодготовитьПараметрыУчетнойПолитики(СтруктураШапкиДокумента, Отказ, Заголовок)

	ПодготовитьПараметрыУчетнойПолитикиРегл(СтруктураШапкиДокумента, Отказ, Заголовок);
	
КонецПроцедуры // ПодготовитьПараметрыУчетнойПолитики()

// Процедура определяет параметры регл. учетной политики
//
Процедура ПодготовитьПараметрыУчетнойПолитикиРегл(СтруктураШапкиДокумента, Отказ, Заголовок)
	
	Если ОтражатьВБухгалтерскомУчете Тогда
		
		// Прежде всего, проверим заполнение реквизита Организация в шапке документа
		СтруктураОбязательныхПолей = Новый Структура("Организация");
		// Теперь позовем общую процедуру проверки
		ЗаполнениеДокументов.ПроверитьЗаполнениеШапкиДокумента(ЭтотОбъект, СтруктураОбязательныхПолей, Отказ, Заголовок);
        // Организация не заполнена, получать учетную политику нет смысла
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
 	
	Если ОтражатьВБухгалтерскомУчете Тогда
		
		мУчетнаяПолитикаРегл = ОбщегоНазначения.ПолучитьПараметрыУчетнойПолитикиРегл(КонецМесяца(Дата), Организация);
		
		Если НЕ ЗначениеЗаполнено(мУчетнаяПолитикаРегл) Тогда
			Отказ = Истина;
		КонецЕсли;
		
		Если НЕ Отказ Тогда
			
			СтруктураШапкиДокумента.Вставить("ЕстьНалогНаПрибыль" , мУчетнаяПолитикаРегл.ЕстьНалогНаПрибыль);
			СтруктураШапкиДокумента.Вставить("ЕстьНДС"            , мУчетнаяПолитикаРегл.ЕстьНДС);
			
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры // ПодготовитьПараметрыУчетнойПолитикиРегл()

///////////////////////////////////////////////////////////////////////////////
// ДВИЖЕНИЯ ПО РЕГИСТРАМ

// Выполняет движения по регистрам 
//
Процедура ДвиженияПоРегистрам(СтруктураШапкиДокумента,ТаблицаПоНМА, ТаблицаПервоначальныхДанных, Отказ, Заголовок)

	ДвиженияПоРегистрамУпр(СтруктураШапкиДокумента, ТаблицаПоНМА, ТаблицаПервоначальныхДанных, Отказ, Заголовок);
	ДвиженияПоРегистрамРегл(СтруктураШапкиДокумента, ТаблицаПоНМА, ТаблицаПервоначальныхДанных, Отказ, Заголовок);
	
КонецПроцедуры // ДвиженияПоРегистрам

Процедура ДвиженияПоРегистрамУпр(СтруктураШапкиДокумента, ТаблицаПоНМА, ТаблицаПервоначальныхДанных, Отказ, Заголовок)

	Если НЕ СтруктураШапкиДокумента.ОтражатьВУправленческомУчете Тогда
		Возврат;
	КонецЕсли;

	// Проверка на наличие НМА
	Для каждого СтрокаТЧ Из ТаблицаПоНМА Цикл

		ТекНМА = СтрокаТЧ.НематериальныйАктив;
		ТекПервоначальныеДанные 	= ТаблицаПервоначальныхДанных.Найти(СтрокаТЧ.НематериальныйАктив,"НМА_УУ");
		
		Если ТекПервоначальныеДанные = Неопределено Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Строка " + (ТаблицаПоНМА.Индекс(СтрокаТЧ) + 1) + "." + Символы.ПС + Символы.Таб
							 + "Упр. учет: По нематериальному активу """ + ТекНМА + """ нет информации"
							 + Символы.ПС + Символы.Таб + "в регистре сведений:"
							 + Символы.ПС + Символы.Таб + """Первоначальные сведения о НМА"""
							 + Символы.ПС + Символы.Таб + "Возможно, оно не принято к учету.", Отказ, Заголовок);
							 
			Отказ = Истина;			
		КонецЕсли;	
		
		
	КонецЦикла;	
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	
	ДатаДока = Дата;

	АмортизацияНМА         = Движения.СтоимостьНМА;
	ПервоначальныеСведенияНМА= Движения.ПервоначальныеСведенияНМА;
	НазваниеДокумента      = Метаданные().Представление();

	ТаблицаДвиженийАмортизация = АмортизацияНМА.Выгрузить();
	
	Для каждого СтрокаТЧ Из ТаблицаПоНМА Цикл

		ТекНМА = СтрокаТЧ.НематериальныйАктив;

		//// Движения по регистру СобытияОС
		//Движение = СобытиеОС.Добавить();
		//Движение.Период               = ДатаДока;
		//Движение.ОсновноеСредство     = ТекОС;
		//Движение.Событие              = СтруктураШапкиДокумента.Событие;
		//Движение.НазваниеДокумента    = НазваниеДокумента;
		//Движение.НомерДокумента       = Номер;
		//Движение.СуммаЗатрат          = СтрокаТЧ.СуммаМодернизацииУУ;
		
		ТекПервоначальныеДанные 	= ТаблицаПервоначальныхДанных.Найти(СтрокаТЧ.НематериальныйАктив,"НМА_УУ");

		// Движения по регистру ПервоначальныеСведенияНМА
		Движение = ПервоначальныеСведенияНМА.Добавить();
		Движение.Период                  = ДатаДока;
		Движение.НематериальныйАктив        = ТекНМА;
		Движение.СрокПолезногоИспользования                  = СтрокаТЧ.СрокПолезногоИспользованияУУ;
		//Движение.СрокПолезногоИспользования   = Макс(0, СтрокаТЧ.СрокПолезногоИспользованияУУ - СтрокаТЧ.ФактСрокИспользованияУУ);
		

		Движение.ПервоначальнаяСтоимость           = Макс(0, СтрокаТЧ.СтоимостьУУ + СтрокаТЧ.СуммаМодернизацииУУ); 
		                                                            //   - СтрокаТЧ.АмортизацияУУ - СтрокаТЧ.АмортизацияЗаМесяцУУ);
		Движение.НачислятьАмортизацию 			= ТекПервоначальныеДанные.НачислятьАмортизацию_УУ;
		Движение.СпособНачисленияАмортизации 	= ТекПервоначальныеДанные.СпособНачисленияАмортизации_УУ;
		
		Движение.ЛиквидационнаяСтоимость                     = СтрокаТЧ.ЛиквидационнаяСтоимостьУУ;
		Движение.ОбъемПродукцииРаботДляВычисленияАмортизации = СтрокаТЧ.ОбъемПродукцииРаботУУ;
		//Движение.ПрименитьВТекущемМесяце = Ложь;
		
		// Движения по регистру СтоимостьНМА
		Движение = ТаблицаДвиженийАмортизация.Добавить();
		Движение.НематериальныйАктив 	= ТекНМА;
		Движение.Амортизация            = 0;
		Движение.Стоимость              = СтрокаТЧ.СуммаМодернизацииУУ;


	КонецЦикла;

	АмортизацияНМА.мПериод          = ДатаДока;
	АмортизацияНМА.мТаблицаДвижений = ТаблицаДвиженийАмортизация;
	Движения.СтоимостьНМА.ВыполнитьПриход();
	
	// Движения по регистру Строительство
	СтроительствоОС       = Движения.СтроительствоОбъектовОсновныхСредств;
	ТаблицаСтроительство = СтроительствоОС.Выгрузить();
	Движение = ТаблицаСтроительство.Добавить();
	Движение.ОбъектСтроительства        = СтруктураШапкиДокумента.ОбъектСтроительства;
	Движение.Сумма                      = ТаблицаПоНМА.Итог("СуммаМодернизацииУУ");
	
	СтроительствоОС.мПериод          = ДатаДока;
	СтроительствоОС.мТаблицаДвижений = ТаблицаСтроительство;
	Движения.СтроительствоОбъектовОсновныхСредств.ВыполнитьРасход();


КонецПроцедуры

Процедура ДвиженияПоРегистрамРегл(СтруктураШапкиДокумента, ТаблицаПоНМА, ТаблицаПервоначальныхДанных, Отказ, Заголовок)


	Если (НЕ СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете) Тогда
		Возврат;
	КонецЕсли;

	ДатаДока       = Дата;
	ТекОрганизация = СтруктураШапкиДокумента.Организация;
	СписокОС = ТаблицаПоНМА.ВыгрузитьКолонку("НематериальныйАктив");
	
	УправлениеВнеоборотнымиАктивами.ДополнитьТабличнуюЧастьСведениямиОбНМАБухНалогРегл(МоментВремени(), ТекОрганизация, ТаблицаПоНМА,
													  СтруктураШапкиДокумента, 
													  Отказ, Заголовок);
	
	Если Отказ Тогда
		
		Возврат
		
	КонецЕсли;

	ТЗНалоговыеНазначения = Неопределено;
	
	Если СтруктураШапкиДокумента.ЕстьНДС Тогда
		
		// Сформируем движения по НДС до отражения в бухгалтерском учете, 
		// поскольку бухгалтерская стоимость зависит от права на налоговый кредит
		ТЗНалоговыеНазначения = ТаблицаПоНМА.Скопировать();
		ФормированиеДвиженийПоПодсистемеНДС(СтруктураШапкиДокумента, ТЗНалоговыеНазначения, Отказ, Заголовок);
		
	КонецЕсли; 

	ТаблицаПоНМА.Колонки.Добавить("СуммаНалоговогоКредита"); // в том числе
	ТаблицаПоНМА.ЗаполнитьЗначения(0,"СуммаНалоговогоКредита");
	ТаблицаПоНМА.Колонки.Добавить("СуммаНалоговогоКредитаНУ"); // в том числе
	ТаблицаПоНМА.ЗаполнитьЗначения(0,"СуммаНалоговогоКредитаНУ");
	
	Если СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете Тогда
		
		Если (ТЗНалоговыеНазначения <> Неопределено) и (ТЗНалоговыеНазначения.Количество() > 0) Тогда
			// Распределим по всем основным средствам пропорционально корректировки по налоговому кредиту
			
			Для Каждого СтрокаТЗ Из ТЗНалоговыеНазначения Цикл
				
				СтруктураВидовДеятельности = Новый Структура("НалоговоеНазначение_НМА");
				СтруктураВидовДеятельности.НалоговоеНазначение_НМА = СтрокаТЗ.НалоговоеНазначениеПоФакту;
				
				ОбщаяСуммаМодернизацииБУ = 0;
				ОбщаяСуммаМодернизацииНУ = 0;
				
				МассивСтрок = ТаблицаПоНМА.НайтиСтроки(СтруктураВидовДеятельности);
				КвоСтрокВМассиве = МассивСтрок.ВГраница();
				
				Для Н = 0 По КвоСтрокВМассиве Цикл
				
					ОбщаяСуммаМодернизацииБУ = ОбщаяСуммаМодернизацииБУ + МассивСтрок[Н].СуммаМодернизацииБУ;
					ОбщаяСуммаМодернизацииНУ = ОбщаяСуммаМодернизацииНУ + МассивСтрок[Н].СуммаМодернизацииНУ;
				
				КонецЦикла;
				
				Если ОбщаяСуммаМодернизацииБУ = 0 И ОбщаяСуммаМодернизацииНУ = 0 Тогда
					
					Продолжить;
					
				КонецЕсли;
				                       
				Если СтрокаТЗ.СуммаКорректировкиНалоговогоКредита > 0 Тогда
					Коэф		= СтрокаТЗ.СуммаКорректировкиНалоговогоКредита / ОбщаяСуммаМодернизацииБУ;	
					КоэфНУ		= СтрокаТЗ.СуммаКорректировкиНалоговогоКредита / ОбщаяСуммаМодернизацииНУ;
				Иначе
					Коэф		= 0;
					КоэфНУ		= 0;
				КонецЕсли; 
				
				Погрешность	= 0;
				ПогрешностьНУ	= 0;
				
				Для  Н = 0 По КвоСтрокВМассиве Цикл
					
					СтрокаОС = МассивСтрок[Н];
					СтрокаОС.СуммаНалоговогоКредита	 = ОбщегоНазначения.ОкруглитьСУчетомПогрешности(Коэф*СтрокаОС.СуммаМодернизацииБУ, 2, Погрешность);
					
					СтрокаОС.СуммаНалоговогоКредитаНУ = ОбщегоНазначения.ОкруглитьСУчетомПогрешности(КоэфНУ*СтрокаОС.СуммаМодернизацииНУ, 2, ПогрешностьНУ);
					
				КонецЦикла;
				
			КонецЦикла;
			
		КонецЕсли;
		
		//СобытиеОС            = Движения.СобытияОСОрганизаций;
		АмортизацияНМА          		= Движения.СтоимостьНМАБухгалтерскийУчет;
		ПервоначальныеСведенияНМА 		= Движения.ПервоначальныеСведенияНМАБухгалтерскийУчет;
		ПервоначальныеСведенияНМА_НУ 	= Движения.ПервоначальныеСведенияНМАНалоговыйУчет;
		ПроводкиБУ             			= Движения.Хозрасчетный;
		НазваниеДокумента = Метаданные().Представление();

		ТаблицаДвиженийАмортизация = АмортизацияНМА.Выгрузить();

		Для каждого СтрокаТЧ Из ТаблицаПоНМА Цикл

			ТекНМА = СтрокаТЧ.НематериальныйАктив;
			
			СчетУчетаСтоимости  = СтрокаТЧ.СчетУчетаБУ;
			ТекПервоначальныеДанные 	= ТаблицаПервоначальныхДанных.Найти(СтрокаТЧ.НематериальныйАктив,"НМА_БУ");
			
			
			// Движения по регистру СтоимостьНМА
			Движение = ТаблицаДвиженийАмортизация.Добавить();
			Движение.НематериальныйАктив 	= ТекНМА;
			Движение.Организация 			= ТекОрганизация;
			Движение.Амортизация 			= 0;
			Движение.Стоимость 				= СтрокаТЧ.СуммаМодернизацииБУ - СтрокаТЧ.СуммаНалоговогоКредита;
			Движение.АмортизацияНУ 			= 0;
			Движение.СтоимостьНУ 			= СтрокаТЧ.СуммаМодернизацииНУ - СтрокаТЧ.СуммаНалоговогоКредитаНУ;

			// Движения по регистру ПервоначальныеСведенияНМАБухгалтерскийУчет
			Движение = ПервоначальныеСведенияНМА.Добавить();
			Движение.Период 				= ДатаДока;
			Движение.НематериальныйАктив 	= ТекНМА;
			Движение.Организация      		= ТекОрганизация;
			Движение.СрокПолезногоИспользования = СтрокаТЧ.СрокПолезногоИспользованияБУ;
																		   
			Движение.ПервоначальнаяСтоимость = Макс(0, СтрокаТЧ.СтоимостьБУ + СтрокаТЧ.СуммаМодернизацииБУ - СтрокаТЧ.СуммаНалоговогоКредита); 
			
			Движение.НачислятьАмортизацию 			= ТекПервоначальныеДанные.НачислятьАмортизацию_БУ;
			Движение.СпособНачисленияАмортизации 	= ТекПервоначальныеДанные.СпособНачисленияАмортизации_БУ;
			Движение.ОбъемПродукцииРаботДляВычисленияАмортизации = СтрокаТЧ.ОбъемПродукцииРаботБУ;
			Движение.ЛиквидационнаяСтоимость                     = СтрокаТЧ.ЛиквидационнаяСтоимостьБУ;

			
			// Движения по регистру ПервоначальныеСведенияНМАНалоговыйУчет
			Движение = ПервоначальныеСведенияНМА_НУ.Добавить();
			Движение.Период           		= ДатаДока;
			Движение.НематериальныйАктив 	= ТекНМА;
			Движение.Организация      		= ТекОрганизация;
			Движение.ПервоначальнаяСтоимость 	= Макс(0, СтрокаТЧ.СтоимостьНУ + СтрокаТЧ.СуммаМодернизацииНУ - СтрокаТЧ.СуммаНалоговогоКредитаНУ); 
			Движение.СрокПолезногоИспользования = СтрокаТЧ.СрокПолезногоИспользованияНУ;
			Движение.НачислятьАмортизацию 		= ТекПервоначальныеДанные.НачислятьАмортизацию_НУ;
			Движение.НалоговоеНазначение 		= СтрокаТЧ.НалоговоеНазначение_НМА;
			Движение.НалоговаяГруппаОС 			= СтрокаТЧ.НалоговаяГруппаОС;
			
			
			// Корректировка налогового кредита
			СуммаПроводки 	=  -СтрокаТЧ.СуммаНалоговогоКредита;
			СуммаПроводкиНУ =  -СтрокаТЧ.СуммаНалоговогоКредитаНУ;
			
			Если СуммаПроводки <> 0 ИЛИ СуммаПроводкиНУ <> 0 Тогда
				
				Проводка = ПроводкиБУ.Добавить();
				Проводка.Период       = ДатаДока;
				Проводка.Организация  = ТекОрганизация;
				Проводка.НомерЖурнала = "НМА";
				Проводка.Содержание   = "Корректировка налогового кредита";
				Проводка.Сумма        = СуммаПроводки;
				
				Если СтруктураШапкиДокумента.ЕстьНалогНаПрибыль Тогда
					
					Проводка.НалоговоеНазначениеДт  = СтруктураШапкиДокумента.НалоговоеНазначениеОбъектаСтроительства;
					Проводка.СуммаНУДт 				= НалоговыйУчет.ОпределитьСтоимостьНУ(Проводка.НалоговоеНазначениеДт, СуммаПроводкиНУ);
					
				КонецЕсли;
				
				Проводка.СчетДт       = СтруктураШапкиДокумента.СчетУчетаБУВнеоборотногоАктива;
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "ОбъектыСтроительства", СтруктураШапкиДокумента.ОбъектСтроительства);
				
				Проводка.СчетКт       = ПланыСчетов.Хозрасчетный.КорректировкиНалоговогоКредита;
				
			КонецЕсли;
			
			// Проводки по модернизации Д СчетУчетаСтоимости К СчетУчетаБУВнеоборотногоАктива
			СуммаПроводки 	= СтрокаТЧ.СуммаМодернизацииБУ - СтрокаТЧ.СуммаНалоговогоКредита;
			СуммаПроводкиНУ = СтрокаТЧ.СуммаМодернизацииНУ - СтрокаТЧ.СуммаНалоговогоКредитаНУ;

			Если СуммаПроводки <> 0 ИЛИ СуммаПроводкиНУ <> 0 Тогда

				Проводка = ПроводкиБУ.Добавить();

				Проводка.Период       = ДатаДока;
				Проводка.Организация  = ТекОрганизация;
				Проводка.Содержание   = "Модернизация";
				Проводка.НомерЖурнала = "НМА";
				Проводка.Сумма        = СуммаПроводки;
				
				Если СтруктураШапкиДокумента.ЕстьНалогНаПрибыль Тогда
					
					Проводка.НалоговоеНазначениеДт  = СтрокаТЧ.НалоговоеНазначение_НМА;
				    Проводка.СуммаНУДт 				= СуммаПроводкиНУ;
					
				КонецЕсли;
				
				Проводка.СчетДт       = СчетУчетаСтоимости;
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "НематериальныеАктивы", ТекНМА);
				
				Если СтруктураШапкиДокумента.ЕстьНалогНаПрибыль Тогда
					
					Проводка.НалоговоеНазначениеКт  = СтруктураШапкиДокумента.НалоговоеНазначениеОбъектаСтроительства;
				    Проводка.СуммаНУКт 				= СуммаПроводкиНУ;
					
				КонецЕсли;
				
				Проводка.СчетКт       = СтруктураШапкиДокумента.СчетУчетаБУВнеоборотногоАктива;
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "ОбъектыСтроительства", СтруктураШапкиДокумента.ОбъектСтроительства);

			КонецЕсли;

		КонецЦикла;

		АмортизацияНМА.мПериод          = ДатаДока;
		АмортизацияНМА.мТаблицаДвижений = ТаблицаДвиженийАмортизация;
		Движения.СтоимостьНМАБухгалтерскийУчет.ВыполнитьПриход();
		
	КонецЕсли;

КонецПроцедуры

// Процедура формирования движений по регистрам подсистемы учета НДС
//
// Параметры:
//		СтруктураШапкиДокумента
//		ТЗНалоговыеНазначения	- таблица с бухгалтерскими данными по основным средствам, в которую будет добавлена 
// 				информация о корректировке налогового кредита
Процедура ФормированиеДвиженийПоПодсистемеНДС(СтруктураШапкиДокумента, ТЗНалоговыеНазначения, Отказ, Заголовок)
	
	ДатаДока = Дата;

	// Объекты строительства
	
	// Спишем накопленный НДС из регистра. 
	// Суммы списываем в той пропорции, в какой стоимость принимаемых к учету объектов 
	// находится к стоимости всего остатка по налоговому учету
	
	ТекОрганизация         = СтруктураШапкиДокумента.Организация;
	ТекОбъектСтроительства = СтруктураШапкиДокумента.ОбъектСтроительства;
	ТекСтоимостьНУ         = СтруктураШапкиДокумента.СтоимостьНУ;
	ТекСтоимостьБУ         = СтруктураШапкиДокумента.СтоимостьБУ;
	
	Если ТекСтоимостьНУ = 0 И ТекСтоимостьБУ = 0 Тогда
		
		//Проверять нечего.
		ТЗНалоговыеНазначения = Неопределено;
		Возврат
		
	КонецЕсли;
	
	// Получим остатки из регистра НДС
	ВыборкаЗаписей = РегистрыНакопления.СтроительствоОбъектовОсновныхСредствБухгалтерскийУчет.Остатки(Новый Граница (Дата, ВидГраницы.Включая),
		             Новый Структура("Организация,ОбъектСтроительства, СчетУчета", ТекОрганизация, ТекОбъектСтроительства, СтруктураШапкиДокумента.СчетУчетаБУВнеоборотногоАктива));
					 
	ОстатокСтоимостиНУ = ВыборкаЗаписей.Итог("СтоимостьНУ");
	ОстатокСтоимостиБУ = ВыборкаЗаписей.Итог("Стоимость");
	
	Если ОстатокСтоимостиНУ = 0 И ОстатокСтоимостиБУ = 0 Тогда	
		
		ТЗНалоговыеНазначения = Неопределено;
		ОбщегоНазначения.СообщитьОбОшибке("Остаточная стоимость по налоговому учету объекта строительства """ + ТекОбъектСтроительства + """ равна 0",Отказ, Заголовок);
		Возврат
		
	ИначеЕсли ОстатокСтоимостиНУ < ТекСтоимостьНУ Тогда
		
		ТЗНалоговыеНазначения = Неопределено;
		ОбщегоНазначения.СообщитьОбОшибке("Остаточная стоимость по налоговому учету объекта строительства """ + ТекОбъектСтроительства + """ равна "
		                    + Формат(ОстатокСтоимостиНУ,"ЧЦ=15; ЧДЦ=2") + " что меньше, указанной в документе "
							+ Формат(ТекСтоимостьНУ,"ЧЦ=15; ЧДЦ=2"), Отказ, Заголовок);
		Возврат
		
	ИначеЕсли ОстатокСтоимостиБУ < ТекСтоимостьБУ Тогда
		
		ТЗНалоговыеНазначения = Неопределено;
		ОбщегоНазначения.СообщитьОбОшибке("Остаточная стоимость по бухгалтерскому учету объекта строительства """ + ТекОбъектСтроительства + """ равна "
		                    + Формат(ОстатокСтоимостиБУ,"ЧЦ=15; ЧДЦ=2") + " что меньше, указанной в документе "
							+ Формат(ТекСтоимостьБУ,"ЧЦ=15; ЧДЦ=2"), Отказ, Заголовок);
		Возврат
		
	КонецЕсли;
	
	//Получим суммы модернизации в разрезе видов налоговой деятельности и видов деятеятельности по НДС
	ТЗНалоговыеНазначения.Свернуть("НалоговоеНазначение_НМА", "СуммаМодернизацииНУ, СуммаМодернизацииБУ");
	
	Для каждого СтрокаТЗ Из ТЗНалоговыеНазначения Цикл
		
		//Определим часть списания приходящуюся на комбинацию: ВидНалоговойДеятельности_НМА, ВидДеятельностиНДС_НМА 
		Коэф 	= ?(ОстатокСтоимостиНУ = 0, 0, СтрокаТЗ.СуммаМодернизацииНУ / ОстатокСтоимостиНУ);
		КоэфБУ 	= ?(ОстатокСтоимостиБУ = 0, 0, СтрокаТЗ.СуммаМодернизацииБУ / ОстатокСтоимостиБУ);
		
		Если Коэф = 0 И КоэфБУ = 0 Тогда
			
			Продолжить
			
		КонецЕсли;
		
		//Распределяем с учетом погрешности
		Погрешность            = 0;
		ПогрешностьНДСВходящий = 0;
		ПогрешностьНДСКредит   = 0;
		
		Для каждого ЗаписьПоОстаткуНДС Из ВыборкаЗаписей Цикл
			
			Движение = Движения.СтроительствоОбъектовОсновныхСредствБухгалтерскийУчет.ДобавитьРасход();
			
			Движение.Период							= ДатаДока;
			Движение.ОбъектСтроительства			= ТекОбъектСтроительства;
			Движение.Организация					= ТекОрганизация;
			Движение.НалоговоеНазначение     		= ЗаписьПоОстаткуНДС.НалоговоеНазначение;
			
			Движение.СчетУчета						= СтруктураШапкиДокумента.СчетУчетаБУВнеоборотногоАктива;
			Движение.Стоимость						= КоэфБУ * ЗаписьПоОстаткуНДС.Стоимость;
			
			// Ресурсы списываем с учетом коэффициента и погрешности в пределах комбинации: ВидНалоговойДеятельности_НМА, ВидДеятельностиНДС_НМА
			Движение.СтоимостьНУ					= ОбщегоНазначения.ОкруглитьСУчетомПогрешности(Коэф * ЗаписьПоОстаткуНДС.СтоимостьНУ, 2, Погрешность);
			Движение.НДСВходящий					= ОбщегоНазначения.ОкруглитьСУчетомПогрешности(Коэф * ЗаписьПоОстаткуНДС.НДСВходящий, 2, ПогрешностьНДСВходящий);
			Движение.НДСКредит						= ОбщегоНазначения.ОкруглитьСУчетомПогрешности(Коэф * ЗаписьПоОстаткуНДС.НДСКредит,   2, ПогрешностьНДСКредит);
			
			// Устанавливаем реквизиты фактической принадлежности объекта строительства
			// к облагаемой деятельности
			//Движение.НалоговоеНазначениеПоФакту      	= СтруктураШапкиДокумента.НалоговоеНазначениеОбъектаСтроительства;
			Движение.НалоговоеНазначениеПоФакту      	= СтрокаТЗ.НалоговоеНазначение_НМА;
			
		КонецЦикла; 
		
	КонецЦикла;
	
	// Обработаем корретировки налогового кредита, к которым привело такое фактическое использование объектов строительства
	ТаблицаСписанияНДС = Движения.СтроительствоОбъектовОсновныхСредствБухгалтерскийУчет.Выгрузить();
	НалоговыйУчет.ПолучитьТаблицуКорректировокНалоговогоКредита(ТаблицаСписанияНДС);
	
	Для каждого СтрокаСписанияНДС Из ТаблицаСписанияНДС Цикл
	
		Если СтрокаСписанияНДС.СуммаКорректировкиНалоговогоКредита = 0 Тогда
		
			Продолжить;
		
		КонецЕсли; 
		
		// Зафиксируем в регистре необходимость корректировки
		Движение = Движения.КорректировкиСтроительствоОбъектовОсновныхСредствНалоговыйУчет.ДобавитьПриход();
		
		Движение.Период							 = ДатаДока;
		Движение.Организация					 = СтрокаСписанияНДС.Организация;
		Движение.ОбъектСтроительства			 = СтрокаСписанияНДС.ОбъектСтроительства;
 		Движение.НалоговоеНазначение		 	 = СтрокаСписанияНДС.НалоговоеНазначение;
	 	Движение.НалоговоеНазначениеПоФакту 	 = СтрокаСписанияНДС.НалоговоеНазначениеПоФакту;
		
		Движение.НДСКредит						 = СтрокаСписанияНДС.НДСКредит;
		Движение.НДСКредитПоФакту				 = СтрокаСписанияНДС.НДСКредит + СтрокаСписанияНДС.СуммаКорректировкиНалоговогоКредита;
		
		Движение.КодОперации = Перечисления.КодыОперацийКорректировкиИспользованияНалоговыйУчет.ПредполагаемаяКорректрировка;
		
	КонецЦикла; 
	
	// Изменения  стоимости основных средств из-за потери/получения права на налоговый кредит в резрезе НалоговоеНазначениеПоФакту
	ТаблицаСписанияНДС.Свернуть("НалоговоеНазначениеПоФакту", "СуммаКорректировкиНалоговогоКредита");
	ТЗНалоговыеНазначения = ТаблицаСписанияНДС;
	
КонецПроцедуры // ФормированиеДвиженийПоПодсистемеНДС()

////////////////////////////////////////////////////////////////////////////////
// ПРОВЕРКИ ПРАВИЛЬНОСТИ ЗАПОЛНЕНИЯ

// Формирует структуру полей, обязательных для заполнения при отражении фактического
// движения средств по банку.
//
// Возвращаемое значение:
//   СтруктураОбязательныхПолей   – структура для проверки
//
Функция СтруктураОбязательныхПолейШапки(СтруктураШапкиДокумента)

	СтруктураПолей = Новый Структура;

	СтруктураПолей.Вставить("ОбъектСтроительства");
	
	Если СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете И (СтруктураШапкиДокумента.ЕстьНалогНаПрибыль ИЛИ СтруктураШапкиДокумента.ЕстьНДС)Тогда
		
		СтруктураПолей.Вставить("НалоговоеНазначениеОбъектаСтроительства");
		
	КонецЕсли;
	
	Возврат СтруктураПолей;

КонецФункции // СтруктураОбязательныхПолейОплата()

// Процедура проверяет правильность заполнения реквизитов документа
//
Процедура ПроверкаРеквизитовТЧ(РежимПроведения, ТаблицаПоНМА, Отказ, Заголовок) Экспорт

	РеквизитыТабНМА = "НематериальныйАктив"; 
	ЗаполнениеДокументов.ПроверитьЗаполнениеТабличнойЧасти(ЭтотОбъект, "НематериальныеАктивы", Новый Структура(РеквизитыТабНМА), Отказ, Заголовок);
	
	Если РежимПроведения = РежимПроведенияДокумента.Оперативный Тогда
		// Проверим возможность изменения состояния ОС
		Для каждого СтрокаОС из ТаблицаПоНМА Цикл
			Если ОтражатьВБухгалтерскомУчете тогда
				//УправлениеВнеоборотнымиАктивами.ПроверитьВозможностьИзмененияСостоянияОС(СтрокаОС.ОсновноеСредство,Дата,СобытиеРегл,Отказ,Организация);
			КонецЕсли;
			Если ОтражатьВУправленческомУчете тогда
				//УправлениеВнеоборотнымиАктивами.ПроверитьВозможностьИзмененияСостоянияОС(СтрокаОС.ОсновноеСредство,Дата,Событие,Отказ);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	
КонецПроцедуры // ПроверкаРеквизитов()

// Проверяет заполнение табличной части документа
// для управленческого учета
Процедура ПроверитьЗаполнениеТЧУпр(СтруктураШапкиДокумента, ТаблицаПоНМА, Отказ, Заголовок)
	
	// Проверим что суммы модернизации относимые на каждый объект ОС в сумме равны данным из шапки документа
	Если ТаблицаПоНМА.Итог("СуммаМодернизацииУУ")<> СтруктураШапкиДокумента.СтоимостьУУ Тогда
		
		ОбщегоНазначения.СообщитьОбОшибке("По управленческому учету общая сумма модернизации, указанная в шапке документа, не соответствует в итоге суммам, отнесенным на нематериальные активы!", Отказ, Заголовок);		
		
	КонецЕсли;
	
КонецПроцедуры // ПроверитьЗаполнениеТЧУпр

// Проверяет заполнение табличной части документа
// для бухгалтерского и налогового учета
Процедура ПроверитьЗаполнениеТЧРегл(СтруктураШапкиДокумента, ТаблицаПоНМА, Отказ, Заголовок)
	
	Если СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете Тогда
		
		// Проверим что суммы модернизации относимые на каждый объект ОС в сумме равны данным из шапки документа
		Если ТаблицаПоНМА.Итог("СуммаМодернизацииБУ")<> СтруктураШапкиДокумента.СтоимостьБУ Тогда
			
			ОбщегоНазначения.СообщитьОбОшибке("По бухгалтерскому учету общая сумма модернизации, указанная в шапке документа, не соответствует в итоге суммам, отнесенным на нематериальные активы!", Отказ, Заголовок);
			
		КонецЕсли;
		
		Если СтруктураШапкиДокумента.ЕстьНалогНаПрибыль Тогда
			
			// Проверим что суммы модернизации относимые на каждый объект ОС в сумме равны данным из шапки документа
			Если ТаблицаПоНМА.Итог("СуммаМодернизацииНУ")<> СтруктураШапкиДокумента.СтоимостьНУ Тогда
				
				ОбщегоНазначения.СообщитьОбОшибке("По налоговому учету общая сумма модернизации, указанная в шапке документа, не соответствует в итоге суммам, отнесенным на нематериальные активы!", Отказ, Заголовок);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	
КонецПроцедуры // ПроверитьЗаполнениеТЧ

// Процедура проверяет правильность заполнения реквизитов документа
//
Процедура ПроверитьЗаполнениеДокумента(РежимПроведения, СтруктураШапкиДокумента, ТаблицаПоНМА, Отказ, Заголовок)

	// Документ должен принадлежать хотя бы к одному виду учета (управленческий, бухгалтерский, налоговый)
	ОбщегоНазначения.ПроверитьПринадлежностьКВидамУчета(СтруктураШапкиДокумента, Отказ, Заголовок);
	
	// Теперь позовем общую процедуру проверки.
	ЗаполнениеДокументов.ПроверитьЗаполнениеШапкиДокумента(ЭтотОбъект, СтруктураОбязательныхПолейШапки(СтруктураШапкиДокумента), Отказ, Заголовок);
	
	ПроверкаРеквизитовТЧ(РежимПроведения, ТаблицаПоНМА, Отказ, Заголовок);
	
КонецПроцедуры

// Процедура проверяет правильность заполнения реквизитов документа
// для управленческого учета
Процедура ПроверитьЗаполнениеДокументаУпр(РежимПроведения, СтруктураШапкиДокумента, ТаблицаПоНМА, Отказ, Заголовок)

	Если (НЕ СтруктураШапкиДокумента.ОтражатьВУправленческомУчете) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если РежимПроведения = РежимПроведенияДокумента.Оперативный Тогда
		
		ВыборкаЗаписей = РегистрыНакопления.СтроительствоОбъектовОсновныхСредств.Остатки(Новый Граница (Дата, ВидГраницы.Включая),Новый Структура("ОбъектСтроительства",СтруктураШапкиДокумента.ОбъектСтроительства));
		ОстатокСтоимости = ВыборкаЗаписей.Итог("Сумма");
		
		Требуется = СтруктураШапкиДокумента.СтоимостьУУ;
		
		Если Требуется > ОстатокСтоимости Тогда
			
			ОбщегоНазначения.СообщитьОбОшибке("По управленческому учету для объекта строительства остаток суммы меньше суммы модернизации"+ Символы.ПС + Символы.Таб +
			                    "Не хватает " + ?(ОстатокСтоимости > 0 , Требуется - ОстатокСтоимости, Требуется ) +
			                    "; Остаток " + ОстатокСтоимости +
			                    "; Требуется " + Требуется , Отказ,Заголовок);
			
		КонецЕсли;
		
	КонецЕсли;
	
	СтруктураПолей = Новый Структура;

	//СтруктураПолей.Вставить("Событие");
	
	// Теперь позовем общую процедуру проверки.
	ЗаполнениеДокументов.ПроверитьЗаполнениеШапкиДокумента(ЭтотОбъект, СтруктураПолей, Отказ, Заголовок);
	
	//// Проверим чем заполнено событие
	//ВидыСобытий = ПолучитьСписокЗначенийВидыСобытий();
	//ПредставлениеРеквизита = ЭтотОбъект.Метаданные().Реквизиты.Событие.Представление();
	//УправлениеВнеоборотнымиАктивами.ПроверкаЗаполненияСобытий(СтруктураШапкиДокумента.Событие.ВидСобытияОС,
	//						  ВидыСобытий,
	//						  ПредставлениеРеквизита,Отказ);
	
	ПроверитьЗаполнениеТЧУпр(СтруктураШапкиДокумента, ТаблицаПоНМА, Отказ, Заголовок);
	
КонецПроцедуры

// Процедура проверяет правильность заполнения реквизитов документа
// для бухгалтерского и налогового учета
Процедура ПроверитьЗаполнениеДокументаРегл( СтруктураШапкиДокумента, ТаблицаПоНМА, Отказ, Заголовок)

	Если (НЕ СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете) Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураПолей = Новый Структура;

	СтруктураПолей.Вставить("Организация");
	//СтруктураПолей.Вставить("СобытиеРегл");
	СтруктураПолей.Вставить("СчетУчетаБУВнеоборотногоАктива");
	
	// Теперь позовем общую процедуру проверки.
	ЗаполнениеДокументов.ПроверитьЗаполнениеШапкиДокумента(ЭтотОбъект, СтруктураПолей, Отказ, Заголовок);
	
	// Проверим чем заполнено событие
	//ВидыСобытий = ПолучитьСписокЗначенийВидыСобытий();
	//ПредставлениеРеквизита = ЭтотОбъект.Метаданные().Реквизиты.СобытиеРегл.Представление();
	//УправлениеВнеоборотнымиАктивами.ПроверкаЗаполненияСобытий(СтруктураШапкиДокумента.СобытиеРегл.ВидСобытияОС,
	//						  ВидыСобытий,
	//						  ПредставлениеРеквизита,Отказ);
	
	ПроверитьЗаполнениеТЧРегл(СтруктураШапкиДокумента, ТаблицаПоНМА, Отказ, Заголовок);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	Перем Заголовок, СтруктураШапкиДокумента, ТаблицаПоНМА;
	
	Если мУдалятьДвижения Тогда
		ОбщегоНазначения.УдалитьДвиженияРегистратора(ЭтотОбъект, Отказ);
	КонецЕсли;

	// Сформируем структуру реквизитов шапки документа
	СтруктураШапкиДокумента = ОбщегоНазначения.СформироватьСтруктуруШапкиДокумента(ЭтотОбъект);
	СтруктураШапкиДокумента.Вставить("ВидОперацийСОсновнымиСредствами", Перечисления.ВидыСобытийОС.Ремонт);
	
	Заголовок = ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка);
		
	// Получим данные учетной политики
	ПодготовитьПараметрыУчетнойПолитики(СтруктураШапкиДокумента, Отказ, Заголовок);

	Если Отказ Тогда
		
		Возврат;
		
	КонецЕсли;

	СтруктураПолей = Новый Структура;

	СтруктураПолей.Вставить("НематериальныйАктив",         "НематериальныйАктив");
	
	
	СтруктураПолей.Вставить("СтоимостьУУ",          "СтоимостьУУ");
	СтруктураПолей.Вставить("СуммаМодернизацииУУ",  "СуммаМодернизацииУУ");
	СтруктураПолей.Вставить("АмортизацияУУ",        "АмортизацияУУ");
	СтруктураПолей.Вставить("АмортизацияЗаМесяцУУ", "АмортизацияЗаМесяцУУ");
	
	СтруктураПолей.Вставить("СрокПолезногоИспользованияУУ", "СрокПолезногоИспользованияУУ");
	//СтруктураПолей.Вставить("ФактСрокИспользованияУУ",      "ФактСрокИспользованияУУ");
	СтруктураПолей.Вставить("ОбъемПродукцииРаботУУ",        "ОбъемПродукцииРаботУУ");
	СтруктураПолей.Вставить("ЛиквидационнаяСтоимостьУУ",    "ЛиквидационнаяСтоимостьУУ");
	
	//СтруктураПолей.Вставить("ФактОбъемПродукцииРаботУУ",    "ФактОбъемПродукцииРаботУУ");

	СтруктураПолей.Вставить("СтоимостьБУ",                  "СтоимостьБУ");
	СтруктураПолей.Вставить("СуммаМодернизацииБУ",          "СуммаМодернизацииБУ");
	СтруктураПолей.Вставить("АмортизацияБУ",                "АмортизацияБУ");
	СтруктураПолей.Вставить("АмортизацияЗаМесяцБУ",         "АмортизацияЗаМесяцБУ");
	СтруктураПолей.Вставить("ЛиквидационнаяСтоимостьБУ",    "ЛиквидационнаяСтоимостьБУ");
	
	СтруктураПолей.Вставить("СрокПолезногоИспользованияБУ", "СрокПолезногоИспользованияБУ");
	//СтруктураПолей.Вставить("ФактСрокИспользованияБУ",      "ФактСрокИспользованияБУ");
	СтруктураПолей.Вставить("ОбъемПродукцииРаботБУ",        "ОбъемПродукцииРаботБУ");
	
	//СтруктураПолей.Вставить("ФактОбъемПродукцииРаботБУ",    "ФактОбъемПродукцииРаботБУ");
	
	СтруктураПолей.Вставить("СуммаМодернизацииНУ",          "СуммаМодернизацииНУ");
	СтруктураПолей.Вставить("СтоимостьНУ",                  "СтоимостьНУ");
	СтруктураПолей.Вставить("СрокПолезногоИспользованияНУ", "СрокПолезногоИспользованияНУ");
	

	РезультатЗапросаПоНМА = УправлениеЗапасами.СформироватьЗапросПоТабличнойЧасти(ЭтотОбъект, "НематериальныеАктивы", СтруктураПолей);
	ТаблицаПоНМА          = РезультатЗапросаПоНМА.Выгрузить();
	
	ПроверитьЗаполнениеДокумента(РежимПроведения, СтруктураШапкиДокумента, ТаблицаПоНМА, Отказ, Заголовок);
	ПроверитьЗаполнениеДокументаУпр(РежимПроведения, СтруктураШапкиДокумента, ТаблицаПоНМА, Отказ, Заголовок);
	ПроверитьЗаполнениеДокументаРегл(СтруктураШапкиДокумента, ТаблицаПоНМА, Отказ, Заголовок);
	
	
	// подготовка первоначальной таблицы
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТекОрганизация", СтруктураШапкиДокумента.Организация);
	Запрос.УстановитьПараметр("ТекПериод",      МоментВремени());
	СписокНМА = ТаблицаПоНМА.ВыгрузитьКолонку("НематериальныйАктив");
	Запрос.УстановитьПараметр("СписокНМА",       СписокНМА);
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПервоначальныеСведенияНМА_УУ.НематериальныйАктив 			КАК НМА_УУ,
	               |	ПервоначальныеСведенияНМА_УУ.НачислятьАмортизацию 			КАК НачислятьАмортизацию_УУ,
	               |	ПервоначальныеСведенияНМА_УУ.СпособНачисленияАмортизации 	КАК СпособНачисленияАмортизации_УУ,
	               |	ПервоначальныеСведенияНМА_БУ.НематериальныйАктив 			КАК НМА_БУ,
	               |	ПервоначальныеСведенияНМА_БУ.НачислятьАмортизацию 			КАК НачислятьАмортизацию_БУ,
	               |	ПервоначальныеСведенияНМА_БУ.СпособНачисленияАмортизации 	КАК СпособНачисленияАмортизации_БУ,
	               |	ПервоначальныеСведенияНМА_НУ.НематериальныйАктив 			КАК НМА_НУ,
	               |	ПервоначальныеСведенияНМА_НУ.НачислятьАмортизацию 			КАК НачислятьАмортизацию_НУ,
	               |	ПервоначальныеСведенияНМА_НУ.СрокПолезногоИспользования 	КАК СрокПолезногоИспользования_НУ,
	               |	ПервоначальныеСведенияНМА_НУ.ПервоначальнаяСтоимость	 	КАК ПервоначальнаяСтоимость_НУ
	               |ИЗ
	               |	РегистрСведений.ПервоначальныеСведенияНМАБухгалтерскийУчет.СрезПоследних(
				   |	                &ТекПериод, НематериальныйАктив В (&СписокНМА) И Организация = &ТекОрганизация)
				   |	                КАК ПервоначальныеСведенияНМА_БУ
	               |		ПОЛНОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияНМА.СрезПоследних(
				   |	                                      &ТекПериод, НематериальныйАктив В (&СписокНМА))
				   |	                                      КАК ПервоначальныеСведенияНМА_УУ
	               |		ПО ПервоначальныеСведенияНМА_БУ.НематериальныйАктив = ПервоначальныеСведенияНМА_УУ.НематериальныйАктив
	               |		ПОЛНОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияНМАНалоговыйУчет.СрезПоследних(
				   |	                                      &ТекПериод, НематериальныйАктив В (&СписокНМА))
				   |	                                      КАК ПервоначальныеСведенияНМА_НУ
	               |		ПО ПервоначальныеСведенияНМА_НУ.НематериальныйАктив = ПервоначальныеСведенияНМА_УУ.НематериальныйАктив
				   |";
	ТаблицаПервоначальныхДанных = Запрос.Выполнить().Выгрузить();
	
	Если Не Отказ Тогда
		ДвиженияПоРегистрам(СтруктураШапкиДокумента,ТаблицаПоНМА, ТаблицаПервоначальныхДанных, Отказ, Заголовок);
	КонецЕсли;

КонецПроцедуры // ОбработкаПроведения()
                 
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка  Тогда
		Возврат;
	КонецЕсли;

	мУдалятьДвижения = НЕ ЭтоНовый();

КонецПроцедуры // ПередЗаписью

Процедура ПриЗаписи(Отказ)
	Если ОбменДанными.Загрузка  Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры

