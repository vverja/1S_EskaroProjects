Перем мНеСоздаватьДокумент Экспорт ;



////////////////////////////////////////////////////////////////////////////////
// ПЕЧАТЬ
//
#Если Клиент Тогда

// Функция формирует табличный документ с печатной формой "П6",
//
// Возвращаемое значение:
//  Табличный документ - печатная форма
//
Функция ПечатьП6() Экспорт
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	// ВЫВОД ДАННЫХ В ОТЧЕТ
	
	Макет = ПолучитьМакет("П6_от_09_10_1995");
	
	// вывод лицевой стороны
	СекцияФорма = Макет.ПолучитьОбласть("Форма");
	
	ТабДокумент.Вывести(СекцияФорма);
	
	Возврат ТабДокумент;
	
КонецФункции // ПечатьП6

Функция ПечатьСредняя() Экспорт
	ТабельныйНомер = Сотрудник.Код;

	ТабДокумент = Новый ТабличныйДокумент;
	Макет = ПолучитьМакет("Средняя");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("парамСсылка", Ссылка);
	ТекстЗапроса_ПериодОтпуска = "
	|ВЫБРАТЬ
	|	МИНИМУМ(Отпуска.ДатаНачала) КАК ДатаНачала,
	|	МАКСИМУМ(Отпуска.ДатаОкончания) КАК ДатаОкончания
	|ИЗ
	|	Документ.НачислениеОтпускаРаботникамОрганизаций.Отпуска КАК Отпуска
	|ГДЕ
	|	Отпуска.Ссылка.Ссылка = &парамСсылка
	|";
	Запрос.Текст = ТекстЗапроса_ПериодОтпуска;
	ВыборкаПериодОтпуска = Запрос.Выполнить().Выбрать();
	ДатаНачала = "";
	ДатаОкончания = "";
	Если ВыборкаПериодОтпуска.Следующий() Тогда
		ДатаНачала = ВыборкаПериодОтпуска.ДатаНачала;
		ДатаОкончания = ВыборкаПериодОтпуска.ДатаОкончания;
	КонецЕсли;
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьШапка.Параметры.Заполнить(ЭтотОбъект);
	ОбластьШапка.Параметры.Организация = Организация.НаименованиеПолное;
	ОбластьШапка.Параметры.ТабельныйНомер = ТабельныйНомер;
	ОбластьШапка.Параметры.Дата = Формат(Дата,"ДФ=dd.MM.yyyy");
	ОбластьШапка.Параметры.ДатаНачала = Формат(ДатаНачала,"ДФ=dd.MM.yyyy");
	ОбластьШапка.Параметры.ДатаОкончания = Формат(ДатаОкончания,"ДФ=dd.MM.yyyy");
	ОбластьШапка.Параметры.ПериодРасчетаСреднегоНачало = Формат(ПериодРасчетаСреднегоНачало,"ДФ=dd.MM.yyyy");
	ОбластьШапка.Параметры.ПериодРасчетаСреднегоКонец = Формат(ПериодРасчетаСреднегоКонец,"ДФ=dd.MM.yyyy");
	ТабДокумент.Вывести(ОбластьШапка);
	ОбластьСтроки = Макет.ПолучитьОбласть("СтрокаСреднего");
	
	табСредняяКоэф = РасчетСреднего.Выгрузить();
    табСредняяКоэф.Свернуть("БазовыйПериодНачало,БазовыйПериодКонец","КоэффициентПовышенияОкладов");
    табСредняяКоэф.Сортировать("БазовыйПериодНачало Убыв");
	Коэф = КоэффициентПовышенияОкладов;
	КоэфТек = 1;
	Для Каждого СтрКоэф Из табСредняяКоэф Цикл
		Коэф = Коэф * КоэфТек;
		КоэфТек = СтрКоэф.КоэффициентПовышенияОкладов;
		СтрКоэф.КоэффициентПовышенияОкладов = Коэф;
	КонецЦикла;
    табСредняяКоэф.Сортировать("БазовыйПериодНачало");
	
	ТекстЗапроса_Средняя = "
	|ВЫБРАТЬ
	|	НачислениеОтпускаРаботникамОрганизацийРасчетСреднего.БазовыйПериодНачало КАК БазовыйПериодНачало,
	|	НачислениеОтпускаРаботникамОрганизацийРасчетСреднего.ВидРасчета КАК ВидРасчета,
	|	СУММА(НачислениеОтпускаРаботникамОрганизацийРасчетСреднего.Результат) КАК Результат,
	|	СУММА(НачислениеОтпускаРаботникамОрганизацийРасчетСреднего.КалендарныеДни) КАК КалендарныеДни,
	|	СУММА(НачислениеОтпускаРаботникамОрганизацийРасчетСреднего.ОтработаноДней) КАК ОтработаноДней,
	|	СУММА(НачислениеОтпускаРаботникамОрганизацийРасчетСреднего.ОтработаноЧасов) КАК ОтработаноЧасов
	|ИЗ
	|	Документ.НачислениеОтпускаРаботникамОрганизаций.РасчетСреднего КАК НачислениеОтпускаРаботникамОрганизацийРасчетСреднего
	|ГДЕ
	|	НачислениеОтпускаРаботникамОрганизацийРасчетСреднего.Ссылка.Ссылка = &парамСсылка
	|
	|СГРУППИРОВАТЬ ПО
	|	НачислениеОтпускаРаботникамОрганизацийРасчетСреднего.ВидРасчета,
	|	НачислениеОтпускаРаботникамОрганизацийРасчетСреднего.БазовыйПериодНачало
	|УПОРЯДОЧИТЬ ПО
	|	НачислениеОтпускаРаботникамОрганизацийРасчетСреднего.ВидРасчета,
	|	НачислениеОтпускаРаботникамОрганизацийРасчетСреднего.БазовыйПериодНачало
 	|
	|ИТОГИ ПО
	|   ОБЩИЕ,
	|	БазовыйПериодНачало,
	|	ВидРасчета
	|";
	
	Запрос.Текст = ТекстЗапроса_Средняя;
	табСредняя = Запрос.Выполнить().Выгрузить();
	Рез = Запрос.Выполнить();
	Результат = Рез.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ВидРасчета");
	
	ОбластьШапкаРасетСреднего_Начало 	 = Макет.ПолучитьОбласть("ШапкаСредней|Начало");
	ОбластьШапкаРасетСреднего_ВидРасчета = Макет.ПолучитьОбласть("ШапкаСредней|ВидРасчета");
	ОбластьШапкаРасетСреднего_Конец 	 = Макет.ПолучитьОбласть("ШапкаСредней|КонецСредней");
	
	СтрокаСреднего_Начало				 = Макет.ПолучитьОбласть("СтрокаСреднего|Начало");
	СтрокаСреднего_ВидРасчета            = Макет.ПолучитьОбласть("СтрокаСреднего|ВидРасчета");
	СтрокаСреднего_Конец                 = Макет.ПолучитьОбласть("СтрокаСреднего|Конец");
	
	ТабДокумент.Вывести(ОбластьШапкаРасетСреднего_Начало);
	
	КоличествоВставляемыхОбластей = 0;
	Пока Результат.Следующий() Цикл
		ОбластьШапкаРасетСреднего_ВидРасчета.Параметры.ВидРасчета = Результат.ВидРасчета;
		ТабДокумент.Присоединить(ОбластьШапкаРасетСреднего_ВидРасчета);
		КоличествоВставляемыхОбластей = КоличествоВставляемыхОбластей + 1;
	КонецЦикла;
	ОбластьДляОбъединения = ТабДокумент.Область(15,4,15,4+КоличествоВставляемыхОбластей-1);
	ОбластьДляОбъединения.Объединить();
	
	ТабДокумент.Присоединить(ОбластьШапкаРасетСреднего_Конец);
	
	ИндКоэф = 0;
	ВыборкаПериод = Рез.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "БазовыйПериодНачало");
	Пока ВыборкаПериод.Следующий() Цикл
		СтрКоєф = табСредняяКоэф[ИндКоэф];
		СтрокаСреднего_Начало.Параметры.Год   =	Формат(Год(ВыборкаПериод.БазовыйПериодНачало),"ЧГ=0");	
		СтрокаДатыМесяца = "";
		Если СтрКоєф.БазовыйПериодНачало <> НачалоМесяца(СтрКоєф.БазовыйПериодНачало)
			ИЛИ СтрКоєф.БазовыйПериодКонец <> НачалоДня(КонецМесяца(СтрКоєф.БазовыйПериодКонец)) Тогда
			СтрокаДатыМесяца = "
			|("+День(СтрКоєф.БазовыйПериодНачало)+"-"+День(СтрКоєф.БазовыйПериодКонец)+")";
		КонецЕсли;
		СтрокаСреднего_Начало.Параметры.Месяц = Формат(СтрКоєф.БазовыйПериодНачало, "ДФ='ММММ'")+СтрокаДатыМесяца;
		ТабДокумент.Вывести(СтрокаСреднего_Начало);
		
		ВыборкаВидРасчета = ВыборкаПериод.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ВидРасчета");
		Пока ВыборкаВидРасчета.Следующий() Цикл
			
			СтрокаСреднего_ВидРасчета.Параметры.Результат = ВыборкаВидРасчета.Результат;
			ТабДокумент.Присоединить(СтрокаСреднего_ВидРасчета);
			
			СтрокаСреднего_Конец.Параметры.КоэффициентПовышенияОкладов = СтрКоєф.КоэффициентПовышенияОкладов;
			
		КонецЦикла;
		
		СтрокаСреднего_Конец.Параметры.ОтработаноДней = ВыборкаПериод.ОтработаноДней;	
		СтрокаСреднего_Конец.Параметры.ОтработаноЧасов = ВыборкаПериод.ОтработаноЧасов;	
		СтрокаСреднего_Конец.Параметры.КалендарныеДни = ВыборкаПериод.КалендарныеДни;	
		
		ТабДокумент.Присоединить(СтрокаСреднего_Конец);	
		ИндКоэф = ИндКоэф + 1;
	КонецЦикла;
	
	ОбластьИтогиСреднего_Начало								= Макет.ПолучитьОбласть("ИтогиСреднего|Начало");
	ОбластьИтогиСреднего_ВидРасчета							= Макет.ПолучитьОбласть("ИтогиСреднего|ВидРасчета");
	ОбластьИтогиСреднего_Конец								= Макет.ПолучитьОбласть("ИтогиСреднего|Конец");
	
	ОбластьИтогиСреднего_ВидРасчета.Параметры.Результат	= табСредняя.Итог("Результат");
	
	ТабДокумент.Вывести(ОбластьИтогиСреднего_Начало);
	Результат.Сбросить();
	ВыборкаВидРасчета = Результат;
    Пока ВыборкаВидРасчета.Следующий() Цикл
		ОбластьИтогиСреднего_ВидРасчета.Параметры.Результат = ВыборкаВидРасчета.Результат;
		ТабДокумент.Присоединить(ОбластьИтогиСреднего_ВидРасчета);
	КонецЦикла;
	
	Выборка = Рез.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ОБЩИЕ");
	Пока Выборка.Следующий() Цикл
		ОбластьИтогиСреднего_Конец.Параметры.ОтработаноДней		= Выборка.ОтработаноДней;
		ОбластьИтогиСреднего_Конец.Параметры.ОтработаноЧасов	= Выборка.ОтработаноЧасов;
		ОбластьИтогиСреднего_Конец.Параметры.КалендарныеДни		= Выборка.КалендарныеДни;
	КонецЦикла;

	ТабДокумент.Присоединить(ОбластьИтогиСреднего_Конец);
	
	ОбластьШапка2 = Макет.ПолучитьОбласть("Шапка2");
	Если ВидУчетаВремениДляСредней = Перечисления.ВидыУчетаВремениДляСредней.ПоРабочимДням Тогда
		ОбластьШапка2.Параметры.СредняяТекст = "Среднечасовая (по рабочим дням):";
	ИначеЕсли ВидУчетаВремениДляСредней = Перечисления.ВидыУчетаВремениДляСредней.ПоРабочимЧасам Тогда
		ОбластьШапка2.Параметры.СредняяТекст = "Среднечасовая (по рабочим часам):";
	Иначе
		ОбластьШапка2.Параметры.СредняяТекст = "Среднедневная (по календарным):";
	КонецЕсли;
	ОбластьШапка2.Параметры.СуммаСредней = СуммаСредней;
	ТабДокумент.Вывести(ОбластьШапка2);
	
	ОбластьСтроки2 = Макет.ПолучитьОбласть("СтрокаНачисления");
	
	Для Каждого СтрокаТЧ Из Начисления Цикл
		ОбластьСтроки2.Параметры.Заполнить(СтрокаТЧ);
		ТабДокумент.Вывести(ОбластьСтроки2);
	КонецЦикла;
	
	ОбластьИтогиНачислений = Макет.ПолучитьОбласть("ИтогиНачислений");
	ОбластьИтогиНачислений.Параметры.НормаДней = Начисления.Итог("НормаДней");
	ОбластьИтогиНачислений.Параметры.НормаЧасов = Начисления.Итог("НормаЧасов");
	ОбластьИтогиНачислений.Параметры.КалендарныеДни = Начисления.Итог("КалендарныеДни");
	ОбластьИтогиНачислений.Параметры.Результат = Начисления.Итог("Результат");
    ТабДокумент.Вывести(ОбластьИтогиНачислений);

	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	ОбластьПодвал.Параметры.Ответственный = Ответственный.ФизЛицо.Наименование;
	ТабДокумент.Вывести(ОбластьПодвал);	
	
	
	Возврат ТабДокумент;	
КонецФункции

// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходмое количество копий.
//
//  Название макета печати передается в качестве параметра,
// по переданному названию находим имя макета в соответствии.
//
// Параметры:
//  НазваниеМакета - строка, название макета.
//
Функция Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь) Экспорт

	// Получить экземпляр документа на печать
	Если  ИмяМакета = "П6_от_09_10_1995" Тогда
		ТабДокумент = ПечатьП6()
	ИначеЕсли ИмяМакета = "Средняя" Тогда
		ТабДокумент = ПечатьСредняя()
	КонецЕсли;
	
	УниверсальныеМеханизмы.НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер,ОбщегоНазначения.СформироватьЗаголовокДокумента(ЭтотОбъект,"Начисление отпуска "));

КонецФункции // Печать

#КонецЕсли

// Возвращает доступные варианты печати документа
//
// Вовращаемое значение:
//  Струткура, каждая строка которой соответствует одному из вариантов печати
//  
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	Возврат Новый Структура("П6_от_09_10_1995, Средняя","Форма П6 от 09.10.1995 (бланк)", "Расчет средней");
КонецФункции // ПолучитьСтруктуруПечатныхФорм()


////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
//

Процедура ОбработкаЗаполненияПоСотруднику() Экспорт
	
	мНеСоздаватьДокумент = Ложь;
	
	Если ПриказОтпускОрганизации = Неопределено Тогда
		Возврат;
	Иначе
		ОбработкаЗаполнения(ПриказОтпускОрганизации);
	КонецЕсли;
	
КонецПроцедуры

Процедура СформироватьДвиженияПоКоменсации(ВыборкаПоШапкеДокумента, НаборНачисления) Экспорт
	//
	//// календарные дни компенсации отпуска
	//Если ВыборкаПоШапкеДокумента.ДнейКомпенсацииУдержанияОтпуска > 0 Тогда
	//	ДнейМесяцевКОплате = ВыборкаПоШапкеДокумента.ДнейКомпенсацииУдержанияОтпуска;
	//ИначеЕсли ВыборкаПоШапкеДокумента.ДнейКомпенсацииУдержанияОтпуска < 0 Тогда
	//	ДнейМесяцевКОплате = - ВыборкаПоШапкеДокумента.ДнейКомпенсацииУдержанияОтпуска;
	//Иначе
	//	Возврат;
	//КонецЕсли; 
	//
	//// компенсация отпуска или удержание за неотработанные дни отпуска
	//ВидРасчета = ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.КомпенсацияОтпуска;
	//
	//Движение = НаборНачисления.Добавить();
	//
	//Движение.ПодразделениеОрганизации			= ВыборкаПоШапкеДокумента.ПодразделениеОрганизации;
	//
	//Движение.ПериодРегистрации          		= ПериодРегистрации;
	//Движение.ВидРасчета				    		= ВидРасчета;
	//	Движение.ПериодДействияНачало				= НачалоМесяца( Дата );
	//	Движение.ПериодДействияКонец				= КонецМесяца( Дата );
	//
	//Движение.ФизЛицо                   		 	= ФизЛицо;
	//Движение.Приказ								= Приказ;
	//Движение.Организация						= ВыборкаПоШапкеДокумента.ГоловнаяОрганизация;
	//
	//Движение.Размер								= ДнейМесяцевКОплате;
	//Движение.ГрафикРаботы					 	= ВыборкаПоШапкеДокумента.ГрафикРаботы;
	//Движение.ДатаНачалаСобытия					= ВыборкаПоШапкеДокумента.ДатаНачалаОсновногоОтпуска;
	//
	//КоличествоМесяцевРасчета 					= ВидРасчета.ЧислоМесяцев;

	//Движение.ПериодРасчетаСреднегоЗаработкаНачало	 = ВыборкаПоШапкеДокумента.ПериодРасчетаСреднегоЗаработкаНачало;
	//Движение.ПериодРасчетаСреднегоЗаработкаКонец	 = ВыборкаПоШапкеДокумента.ПериодРасчетаСреднегоЗаработкаКонец;
	//
	//ПереписатьНачисленияВТабличнуюЧастьДокумента(Движение);
	
КонецПроцедуры

Функция ПолучитьДатуНачалаСобытия(ДатаОкончанияСобытия = 0) Экспорт

	Если Отпуска.Количество() = 0 Тогда
		Возврат ПериодРегистрации;
	КонецЕсли;
		
	тбОтпуска = Отпуска.Выгрузить();
	тбОтпуска.Сортировать("ДатаНачала");
	ДатаОкончанияСобытия = тбОтпуска[тбОтпуска.Количество()-1].ДатаОкончания;
	
	Возврат тбОтпуска[0].ДатаНачала;

КонецФункции // ()


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ТБЛИЧНОЙ ЧАСТИ РасчетСреднего

// Расчитывает сумму средней по значениям в ТЧ РасчетСреднего 
//
Функция РасчитатьСреднюю() Экспорт
	
	СуммаСредней = 0;
	ОплачиваемоеВремя = 0;
	
	Если ВидУчетаВремениДляСредней = Перечисления.ВидыУчетаВремениДляСредней.ПоРабочимДням Тогда
		ОплачиваемоеВремя = РасчетСреднего.Итог("ОтработаноДней");
		
	ИначеЕсли ВидУчетаВремениДляСредней = Перечисления.ВидыУчетаВремениДляСредней.ПоРабочимЧасам Тогда
		ОплачиваемоеВремя = РасчетСреднего.Итог("ОтработаноЧасов");
		
	Иначе
		ОплачиваемоеВремя = РасчетСреднего.Итог("КалендарныеДни");
		
	КонецЕсли;
	
	СуммаСредней = ?(ОплачиваемоеВремя = 0 , 0, РасчетСреднего.Итог("Результат") / ОплачиваемоеВремя);
	
КонецФункции  // РасчитатьСреднюю()

Функция АвтозаполнениеРасчетСреднего() Экспорт
	Перем ДатаОкончанияСобытия;
	
	//	1. Проверка если данных для расчета средней ещ нет (человек мало проработал, начисляем по окладу)
	//
	Если  НачалоДня(ДобавитьМесяц(ПериодРасчетаСреднегоНачало,1)-1) > ПериодРасчетаСреднегоКонец Тогда
		
		// проработал меньше целого месяца
		// если есть за что платить, пользователю прийдется заполнить эти суммы вручную
		//
		СоздатьСтрокуРасчетСреднего( ПланыВидовРасчета.СреднийЗаработок.ПоОкладу, 1, НачалоМесяца(ПериодРасчетаСреднегоНачало), КонецМесяца(ПериодРасчетаСреднегоНачало));
	
		Записать();
	
		Возврат Истина;
	КонецЕсли;

	
	// Сначала определим коэффициент увеличения оклада 
	// если он вдруг поменялся за время пока за человеком 
	// сохранялась средняя ЗП
	Запрос = Новый Запрос;
	ДатаНачалаСобытия = ПолучитьДатуНачалаСобытия(ДатаОкончанияСобытия);
	Запрос.УстановитьПараметр( "парамСотрудник",	Сотрудник );
	Запрос.УстановитьПараметр( "парамНачало",		КонецДня(ПериодРасчетаСреднегоКонец) + 1 );
	Запрос.УстановитьПараметр( "парамКонец",		ДатаОкончанияСобытия );
	ТекстЗапроса = " 
	|ВЫБРАТЬ
	|	КоэффициентПовышения.Период,
	|	КоэффициентПовышения.Коэффициент
	|ИЗ РегистрСведений.КоэффициентыПовышенияОкладов КАК	КоэффициентПовышения		
	|ГДЕ	КоэффициентПовышения.Сотрудник	= &парамСотрудник
	|	И	КоэффициентПовышения.Период >= &парамНачало
	|	И	КоэффициентПовышения.Период <= &парамКонец
	|";
	Запрос.Текст = ТекстЗапроса;
	Выборка = Запрос.Выполнить().Выбрать();
	Коэф = 1;
	Пока Выборка.Следующий() Цикл
		Коэф = Коэф * Выборка.Коэффициент;
	КонецЦикла;
	КоэффициентПовышенияОкладов = Коэф;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр( "парамСотрудник",	Сотрудник );
	Запрос.УстановитьПараметр( "парамНачало",		ПериодРасчетаСреднегоНачало );
	Запрос.УстановитьПараметр( "парамКонец",		 ПериодРасчетаСреднегоКонец);
	
	//	ВТВремя
	Если Истина Тогда
		//	Описание:
		// 		таблица периодов для расчета средней
		//		
		Начало = ПериодРасчетаСреднегоНачало;
		Конец = ПериодРасчетаСреднегоКонец;
		Запрос.УстановитьПараметр( "парамНачало1", Начало );
		Запрос.УстановитьПараметр( "парамКонец1", Мин( КонецМесяца(Начало), Конец ) );
		ТекстЗапросаВТ = " 
		|	ВЫБРАТЬ
		|		&парамНачало1	КАК БазовыйПериодНачало,
		|		&парамКонец1	КАК БазовыйПериодКонец
		|ПОМЕСТИТЬ ВТВремя	
		|";
		
		Начало = ДобавитьМесяц(НачалоМесяца(Начало),1);
		Инд = 2;
		Пока Начало <= Конец Цикл
			
			Запрос.УстановитьПараметр( "парамНачало"+Инд    , Начало );
			Запрос.УстановитьПараметр( "парамКонец"+Инд    , Мин( КонецМесяца(Начало), Конец ) );
			ТекстЗапросаВТ = ТекстЗапросаВТ + " 
			|	ОБЪЕДИНИТЬ	  
			|	ВЫБРАТЬ
			|		&парамНачало"+Инд+"	КАК БазовыйПериодНачало,
			|		&парамКонец"+Инд+"	КАК БазовыйПериодКонец
			|";	  
			Инд = Инд + 1;
			Начало = ДобавитьМесяц(НачалоМесяца(Начало),1);
			
		КонецЦикла;
		
		Запрос.Текст = ТекстЗапросаВТ;
		Запрос.Выполнить();
		ТекстЗапросаВТВремя = "ВТВремя";
	КонецЕсли;
	
	//	ВТКоэффициент
	Если Истина Тогда
		//	Описание:
		// 		таблица с коффициентами для оклада и периодами (нижняя граница)
		//		
		ТекстЗапросаВТ = " 
		|	ВЫБРАТЬ
		|		КоэффициентПовышения.Период			КАК ПериодНачало,
		|		КоэффициентПовышения.Коэффициент	КАК Коэффициент,
		|		МИНИМУМ(
		|			ВЫБОР	КОГДА КоэффициентПовышения1.Период ЕСТЬ NULL 
		|						ТОГДА &парамКонец 	
		|					ИНАЧЕ ДОБАВИТЬКДАТЕ(КоэффициентПовышения1.Период,СЕКУНДА,-1) 
		|			КОНЕЦ ) КАК ПериодКонец
		|ПОМЕСТИТЬ ВТКоэффициент	
		|	ИЗ (
		|			ВЫБРАТЬ
		|				&парамНачало	КАК Период,
		|				1	КАК Коэффициент
		|	
		|	    	ОБЪЕДИНИТЬ 
		|			ВЫБРАТЬ
		|				КоэффициентПовышения.Период,
		|				КоэффициентПовышения.Коэффициент
		|			ИЗ РегистрСведений.КоэффициентыПовышенияОкладов КАК	КоэффициентПовышения		
		|			ГДЕ		КоэффициентПовышения.Сотрудник	= &парамСотрудник
		|				И	КоэффициентПовышения.Период > &парамНачало
		|	)КАК КоэффициентПовышения
		|	
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ  РегистрСведений.КоэффициентыПовышенияОкладов 	КАК КоэффициентПовышения1
		|	ПО		КоэффициентПовышения1.Сотрудник	= &парамСотрудник
		|		И	КоэффициентПовышения1.Период > КоэффициентПовышения.Период
		|	
		|	СГРУППИРОВАТЬ ПО
		|		КоэффициентПовышения.Период,
		|		КоэффициентПовышения.Коэффициент
		|";
		Запрос.Текст = ТекстЗапросаВТ;
		Запрос.Выполнить();
		ТекстЗапросаВТКоэффициент = "ВТКоэффициент";
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ВЫБОР КОГДА КоэффициентПериоды.ПериодНачало >= ВсеПериоды.БазовыйПериодНачало И КоэффициентПериоды.ПериодНачало <= ВсеПериоды.БазовыйПериодКонец ТОГДА КоэффициентПериоды.Коэффициент ИНАЧЕ 1 КОНЕЦ	КАК Коэффициент,
	|	ВЫБОР КОГДА КоэффициентПериоды.ПериодНачало <= ВсеПериоды.БазовыйПериодНачало ТОГДА ВсеПериоды.БазовыйПериодНачало ИНАЧЕ КоэффициентПериоды.ПериодНачало КОНЕЦ	КАК БазовыйПериодНачало,
	|	ВЫБОР КОГДА КоэффициентПериоды.ПериодКонец <= ВсеПериоды.БазовыйПериодКонец ТОГДА КоэффициентПериоды.ПериодКонец ИНАЧЕ ВсеПериоды.БазовыйПериодКонец КОНЕЦ	КАК БазовыйПериодКонец
	|ИЗ ВТВремя КАК ВсеПериоды
	|	
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТКоэффициент КАК КоэффициентПериоды
	|ПО		ВсеПериоды.БазовыйПериодНачало <= КоэффициентПериоды.ПериодКонец
	|И		КоэффициентПериоды.ПериодНачало <= ВсеПериоды.БазовыйПериодКонец	
	|	
	|УПОРЯДОЧИТЬ ПО
	|	ВЫБОР	КОГДА КоэффициентПериоды.ПериодНачало <= ВсеПериоды.БазовыйПериодНачало 
	|				ТОГДА ВсеПериоды.БазовыйПериодНачало
	|			ИНАЧЕ КоэффициентПериоды.ПериодНачало КОНЕЦ УБЫВ
	|";			
	Запрос.Текст = ТекстЗапроса;
	
	мЕстьПремии = РегистрыСведений.ПараметрыРасчетаЗарплатыОрганизаций.Получить(Новый Структура ("Организация", Организация)).ЕстьПремии;
	мЕстьГодоваяПремия = РегистрыСведений.ПараметрыРасчетаЗарплатыОрганизаций.Получить(Новый Структура ("Организация", Организация)).ЕстьГодоваяПремия;
	Коэффициент = 1;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		СоздатьСтрокуРасчетСреднего(ПланыВидовРасчета.СреднийЗаработок.ПоЗаработкуДляОтпуска, Выборка.Коэффициент, Выборка.БазовыйПериодНачало, Выборка.БазовыйПериодКонец);
		Если мЕстьПремии  Тогда
			СоздатьСтрокуРасчетСреднего(ПланыВидовРасчета.СреднийЗаработок.ПоПремиямОтпуска, 0, Выборка.БазовыйПериодНачало, Выборка.БазовыйПериодКонец);
			СоздатьСтрокуРасчетСреднего(ПланыВидовРасчета.СреднийЗаработок.ПоФиксПремиямОтпуска, 0, Выборка.БазовыйПериодНачало, Выборка.БазовыйПериодКонец);
		КонецЕсли;
		Если мЕстьГодоваяПремия Тогда
			СоздатьСтрокуРасчетСреднего(ПланыВидовРасчета.СреднийЗаработок.ПоГодовойПремии, 0, Выборка.БазовыйПериодНачало, Выборка.БазовыйПериодКонец);
		КонецЕсли;
		
	КонецЦикла;
	
	Записать();
	
КонецФункции  // АвтозаполнениеРасчетСреднего()

Функция РассчитатьРасчетСреднего(КомментироватьРасчет = Ложь) Экспорт
	
	// очистить расчитываемые поля в ТЧ РасчетСреднего
	Для каждого ТекущаяСтрока Из РасчетСреднего Цикл
		
		Если ТекущаяСтрока.Авторасчет Тогда
			ТекущаяСтрока.Результат = 0;
			ТекущаяСтрока.РезультатПолный = 0;
			
			ТекущаяСтрока.ОтработаноДней = 0;
			ТекущаяСтрока.ОтработаноЧасов = 0;
			ТекущаяСтрока.КалендарныеДни = 0;
			ТекущаяСтрока.НормаДней = 0;
			ТекущаяСтрока.НормаЧасов = 0;
		КонецЕсли;
		
	КонецЦикла;
	Записать();
	
	// 1. Если расчет по Окладу
	//
	Если  РасчетСреднего.Количество() > 0 И РасчетСреднего[0].ВидРасчета = ПланыВидовРасчета.СреднийЗаработок.ПоОкладу Тогда
		
		ПараметрыОклада = ПроведениеРасчетов.ПолучитьПараметыОклада(ПериодРасчетаСреднегоКонец, Сотрудник);
		РасчетСреднего[0].Результат			= ПараметрыОклада["Оклад"];
		РасчетСреднего[0].ОтработаноДней	= ПараметрыОклада["НормаДней"];
		РасчетСреднего[0].ОтработаноЧасов	= ПараметрыОклада["НормаЧасов"];
		РасчетСреднего[0].КалендарныеДни	= ПараметрыОклада["КалендарныеДни"];
		
		РасчитатьСреднюю();
		Записать();
		Возврат Истина;
		
	КонецЕсли;
	
	// 2. Стандартный расчет
    //
	НачатьТранзакцию();
	Прочитать();
	ВыборкаПоСредним = СформироватьЗапросПоРасчетСреднего().Выбрать();
	ЗафиксироватьТранзакцию();
	
	// Создадим рабочие наборы записей
	Отказ = Ложь;
	НаборСредних = РегистрыРасчета.РасчетСреднегоЗаработка.СоздатьНаборЗаписей();
	НаборСредних.Отбор.Регистратор.Значение = Ссылка;
	Пока ВыборкаПоСредним.Следующий() Цикл
		
		ПроверитьЗаполнениеСтрокиРасчетСреднего(ВыборкаПоСредним, Отказ);
		Если НЕ Отказ Тогда
			// Заполним записи в наборах записей регистров
			ДобавитьСтрокуРасчетаСреднего(ВыборкаПоСредним, НаборСредних);
		КонецЕсли;
		
	КонецЦикла;
	НаборСредних.Записать();
	
	Если Отказ Тогда
		
		// Если есть какие-то проблемы - удаляем движения (тут нет транзакции) 
		// Удаляем движения
		НаборСредних.Очистить();
		НаборСредних.Записать();
		
		Возврат Не Отказ;
		
	КонецЕсли;
	
	ОсновнойВидРасчета = ПланыВидовРасчета.СреднийЗаработок.ПоЗаработкуДляОтпуска;
	тКоэффициентПовышенияОкладов = КоэффициентПовышенияОкладов;
	
	Выборка = ПроведениеРасчетов.РассчитатьСреднююПорядок100(Ссылка).Выбрать();
	Пока Выборка.Следующий() Цикл
		
		СтрокаТабличнойЧасти = РасчетСреднего.Получить( Выборка.НомерСтроки - 1 );
		
		Если НЕ СтрокаТабличнойЧасти.Авторасчет Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаТабличнойЧасти.Результат				= Окр(Выборка.Результат,2) * тКоэффициентПовышенияОкладов;
		СтрокаТабличнойЧасти.РезультатПолный		= Выборка.РезультатПолный;
		
		СтрокаТабличнойЧасти.ОтработаноДней			= Выборка.ОтработаноДней;
		СтрокаТабличнойЧасти.ОтработаноЧасов		= Выборка.ОтработаноЧасов;
		Если ВидУчетаВремениДляСредней = Перечисления.ВидыУчетаВремениДляСредней.ПоКалендарнымДнямСПраздниками
			И СтрокаТабличнойЧасти.ВидРасчета = ПланыВидовРасчета.СреднийЗаработок.ПоЗаработкуДляОтпуска Тогда
			СтрокаТабличнойЧасти.КалендарныеДни		= 1 + День(Выборка.БазовыйПериодКонец) - День(Выборка.БазовыйПериодНачало);
		Иначе
			СтрокаТабличнойЧасти.КалендарныеДни		= Выборка.КалендарныеДни;
		КонецЕсли;
		СтрокаТабличнойЧасти.НормаДней				= Выборка.НормаДней;
		СтрокаТабличнойЧасти.НормаЧасов				= Выборка.НормаЧасов;
		
		Если Выборка.ВидРасчета = ОсновнойВидРасчета Тогда
			тКоэффициентПовышенияОкладов = тКоэффициентПовышенияОкладов * Выборка.КоэффициентПовышенияОкладов;;
		КонецЕсли;
		
	КонецЦикла;
	
	НаборСредних.Очистить();
	НаборСредних.Записать();
	
	РасчитатьСреднюю();
	Записать();
	
КонецФункции  // РассчитатьРасчетСреднего()

Функция СоздатьСтрокуРасчетСреднего( ВидРасч, Коэффициент, БазовыйПериодНачало, БазовыйПериодКонец) 
	
	// строку втавляем на первую позицию, потому что коэффициент считали в обратном порядке
	СтрокаТабличнойЧасти =  РасчетСреднего.Вставить(0);
	
	СтрокаТабличнойЧасти.Авторасчет 			= Истина;
	СтрокаТабличнойЧасти.ВидРасчета 			= ВидРасч;
	СтрокаТабличнойЧасти.БазовыйПериодНачало 	= БазовыйПериодНачало;
	СтрокаТабличнойЧасти.БазовыйПериодКонец 	= БазовыйПериодКонец;
	СтрокаТабличнойЧасти.КоэффициентПовышенияОкладов = Коэффициент;
	
КонецФункции  // СоздатьСтрокуРасчетСреднего()


Процедура ПолучитьПериодПоРаботнику() Экспорт
	Перем ДатаОкончанияСобытия, ДатаПриемаПервыйРабочийДень;
	
	Если НЕ ЗначениеЗаполнено(Сотрудник) Тогда
		Возврат;
	КонецЕсли;	
	
	// -1- работает дольше 12-и месяцев
	// -2- работает меньше 12 месяцев, но дольше 1-го месяца
	// -3- работает меньше целого месяца, но в отпуск не в первый рабочий день - заполняеи реальный период с даты приема
	// -4- в отпуск в первый рабочий день, нет начисленной зарплаты
	
	ДатаНачалаСобытия = ПолучитьДатуНачалаСобытия(ДатаОкончанияСобытия);
	ДатаПриема = ПолучитьДанныеОПриеме(ДатаПриемаПервыйРабочийДень);
	НеПроработалЦелогоМесяца = Ложь;
	
	Если НЕ ЗначениеЗаполнено(ДатаПриема) Тогда
		Возврат;
	КонецЕсли;	
	
	Если ДобавитьМесяц(НачалоМесяца(ДатаНачалаСобытия), -ЧислоМесяцев)>= ДатаПриема Тогда
		// -1- работает дольше 12-и месяцев
		ПериодРасчетаСреднегоНачало	= ДобавитьМесяц(НачалоМесяца(ДатаНачалаСобытия), -ЧислоМесяцев);
		ПериодРасчетаСреднегоКонец	= НачалоДня(НачалоМесяца(ДатаНачалаСобытия)-1);
		
	ИначеЕсли КонецМесяца(ДобавитьМесяц(ДатаНачалаСобытия, -1)) >= ДатаПриема Тогда
		//-2-3-
		Если ДатаПриемаПервыйРабочийДень Тогда
			// -2- работает меньше 12 месяцев, но дольше 1-го месяца
			// если дата приема первый рабочий день месяца, то месяц считаем целым
			ПериодРасчетаСреднегоНачало	= ДатаПриема;	
			ПериодРасчетаСреднегоКонец	= НачалоДня(НачалоМесяца(ДатаНачалаСобытия)-1);
		ИначеЕсли ДобавитьМесяц(НачалоМесяца(ДатаНачалаСобытия), -1) > ДатаПриема Тогда
			// -2- работает меньше 12 месяцев, но дольше 1-го месяца
			// если дата приема не первый рабочий день месяца, но есть целые отработанные месяцы
			ПериодРасчетаСреднегоНачало	= ДобавитьМесяц(НачалоМесяца(ДатаПриема),1);
			ПериодРасчетаСреднегоКонец	= НачалоДня(НачалоМесяца(ДатаНачалаСобытия)-1);
		Иначе
			// -3- работает меньше целого месяца, но в отпуск не в первый рабочий день 
			//    - заполняеи реальный период с даты приема
			ПериодРасчетаСреднегоНачало	= ДатаПриема;	
			ПериодРасчетаСреднегоКонец	= НачалоДня(ДатаНачалаСобытия-1);
			НеПроработалЦелогоМесяца = Истина;
		КонецЕсли;
		
	ИначеЕсли ДатаНачалаСобытия = ДатаПриема Тогда
		// -4- в отпуск в первый рабочий день, нет начисленной зарплаты
		ПериодРасчетаСреднегоНачало	= ДатаПриема;
		ПериодРасчетаСреднегоКонец	= ДатаПриема;
		НеПроработалЦелогоМесяца = Истина;
		
	Иначе
		// -3- работает меньше целого месяца, но в отпуск не в первый рабочий день 
		//    - заполняеи реальный период с даты приема
		// прием в месяце ухода в отпуск
		ПериодРасчетаСреднегоНачало	= ДатаПриема;	
		ПериодРасчетаСреднегоКонец	= НачалоДня(ДатаНачалаСобытия-1);
		НеПроработалЦелогоМесяца = Истина;
		
	КонецЕсли;

КонецПроцедуры // ПолучитьПериодПоРаботнику()

Функция		ПолучитьДанныеОПриеме(ДатаПриемаПервыйРабочийДень)
	
	ЭтоПервыйРабочийДень = Ложь;
	ДатаПриема = Дата('00010101');
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("парамСотрудник",		Сотрудник );
	ТекстЗапроса = "
	|	ВЫБРАТЬ
	|       ПриемРаботников.ДатаПриема КАК ДатаПриема,
	|		СУММА(ЕСТЬNULL(Графики.ОсновноеЗначение,0)) КАК ОтработаноДнейСНачалаМесяца
	|   ИЗ (
	|		ВЫБРАТЬ 
	|          	МИНИМУМ(Работники.Период) КАК ДатаПриема
	|		ИЗ РегистрСведений.РаботникиОрганизаций КАК Работники
	|   	ГДЕ Работники.Сотрудник = &парамСотрудник
	|       ) КАК ПриемРаботников
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК Работники 
	|   	ПО		Работники.Сотрудник = &парамСотрудник
	|			И	Работники.Период = ПриемРаботников.ДатаПриема
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикиРаботыПоВидамВремени КАК Графики
	|   	ПО  	Графики.Дата >= НАЧАЛОПЕРИОДА(Работники.Период, МЕСЯЦ)
	|			И	Графики.Дата < Работники.Период
	|			И	Графики.ВидУчетаВремени = ЗНАЧЕНИЕ(Перечисление.ВидыУчетаВремени.ПоДням)
	|			// индивидуальные графики и табели не анализируем, так как нас интерисует только норма времени
	|			И	ВЫБОР КОГДА Работники.ГрафикРаботы.ГрафикПолногоРабочегоВремени <> ЗНАЧЕНИЕ(Справочник.ГрафикиРаботы.ПустаяСсылка)
	|						ТОГДА  Работники.ГрафикРаботы.ГрафикПолногоРабочегоВремени
	|                     ИНАЧЕ Работники.ГрафикРаботы
	|				КОНЕЦ = Графики.ГрафикРаботы
	|    СГРУППИРОВАТЬ ПО
	|       ПриемРаботников.ДатаПриема
	|";
	Запрос.Текст = ТекстЗапроса;
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ДатаПриема = Выборка.ДатаПриема;
		ДатаПриемаПервыйРабочийДень = Выборка.ОтработаноДнейСНачалаМесяца = 0;
	КонецЕсли;

	Возврат ДатаПриема;
	
КонецФункции  // ПолучитьДанныеОПриеме()



////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ТБЛИЧНОЙ ЧАСТИ Начисления

Процедура АвтозаполнениеНачисленияОтпуск() Экспорт
	Перем ДатаОкончанияСобытия;
	
	ДатаНачалаСобытия = ПолучитьДатуНачалаСобытия(ДатаОкончанияСобытия);
	
	// данные нормы календаря помесячно
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("парамРегистратор",	Ссылка);
	Запрос.УстановитьПараметр("ДатаНачалаСобытия",	ДатаНачалаСобытия);
	Запрос.УстановитьПараметр("парамСотрудник",		Сотрудник);
	
	//	ВТВремя
	Если Истина Тогда
		//	Описание:
		// 		таблица периодов по месяцам
		//		
		Начало = ДатаНачалаСобытия;
		Конец = ДатаОкончанияСобытия;
		Запрос.УстановитьПараметр( "парамНачало1", Начало );
		ТекстЗапросаВТ = " 
		|	ВЫБРАТЬ
		|		&парамНачало1	КАК ДатаМесяца
		|ПОМЕСТИТЬ ВТВремя	
		|";
		
		Начало = ДобавитьМесяц(НачалоМесяца(Начало),1);
		Инд = 2;
		Пока Начало <= Конец Цикл
			Запрос.УстановитьПараметр( "парамНачало"+Инд    , Начало );
			ТекстЗапросаВТ = ТекстЗапросаВТ + " 
			|	ОБЪЕДИНИТЬ	  
			|	ВЫБРАТЬ
			|		&парамНачало"+Инд+"	КАК ДатаМесяца
			|";	  
			Инд = Инд + 1;
			Начало = ДобавитьМесяц(НачалоМесяца(Начало),1);
		КонецЦикла;
		
		Запрос.Текст = ТекстЗапросаВТ;
		Запрос.Выполнить();
		ТекстЗапросаВТВремя = "ВТВремя";
	КонецЕсли;
	
	// запрос по всем назначениям, включая внутреннее совместительство и замены
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Основной.Дата,
	|	Основной.ПериодРегистрации,
	|	Основной.Организация,
	|	Основной.Ссылка,
	|	Основной.Сотрудник,
	|	Основной.СуммаСредней,
	|	Основной.ВидУчетаВремениДляСредней,
	|  	Основной.ПериодРасчетаСреднегоНачало	КАК ПериодРасчетаСреднегоНачало,
	|  	Основной.ПериодРасчетаСреднегоКонец		КАК ПериодРасчетаСреднегоКонец,
	|
	|	Работники.Сотрудник 					КАК Назначение,
	|	Работники.ПодразделениеОрганизации		КАК ПодразделениеОрганизации,
	|	Время.ДатаМесяца         				КАК ДатаМесяца,
	|
	|   ТЧОтпуска.ВидРасчета,
	|	ВЫБОР КОГДА Время.ДатаМесяца > ТЧОтпуска.ДатаНачала		ТОГДА Время.ДатаМесяца ИНАЧЕ ТЧОтпуска.ДатаНачала КОНЕЦ КАК ДатаНачала,
	|	ВЫБОР КОГДА КОНЕЦПЕРИОДА(Время.ДатаМесяца, МЕСЯЦ) < ТЧОтпуска.ДатаОкончания	ТОГДА КОНЕЦПЕРИОДА(Время.ДатаМесяца, МЕСЯЦ) ИНАЧЕ ТЧОтпуска.ДатаОкончания КОНЕЦ КАК ДатаОкончания
	|
	|ИЗ Документ."+Ссылка.Метаданные().Имя+" КАК Основной
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций.СрезПоследних(&ДатаНачалаСобытия,
	|	Сотрудник = &парамСотрудник ИЛИ Сотрудник.ОсновноеНазначение = &парамСотрудник ) КАК Работники
	|ПО	ИСТИНА
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ "+ТекстЗапросаВТВремя+" КАК Время
	|ПО	ИСТИНА
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ	Документ.НачислениеОтпускаРаботникамОрганизаций.Отпуска КАК ТЧОтпуска
	|ПО		ТЧОтпуска.Ссылка = &парамРегистратор
	|	И	Время.ДатаМесяца >= НАЧАЛОПЕРИОДА(ТЧОтпуска.ДатаНачала, МЕСЯЦ)
	|	И	Время.ДатаМесяца <= КОНЕЦПЕРИОДА(ТЧОтпуска.ДатаОкончания, МЕСЯЦ)
	|
	|ГДЕ Основной.Ссылка = &парамРегистратор	
	|	И  ЕСТЬNULL(Работники.ЗанимаемыхСтавок,0) > 0
	|
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	Основной.Дата,
	|	Основной.ПериодРегистрации,
	|	Основной.Организация,
	|	Основной.Ссылка,
	|	Основной.Сотрудник,
	|	Основной.СуммаСредней,
	|	Основной.ВидУчетаВремениДляСредней,
	|  	Основной.ПериодРасчетаСреднегоНачало	КАК ПериодРасчетаСреднегоНачало,
	|  	Основной.ПериодРасчетаСреднегоКонец		КАК ПериодРасчетаСреднегоКонец,
	|
	|	ЗаменыОрганизаций.ЗаменаСотрудник 					КАК Назначение,
	|	ЗаменыОрганизаций.ЗаменаПодразделениеОрганизации		КАК ПодразделениеОрганизации,
	|	Время.ДатаМесяца         				КАК ДатаМесяца,
	|
	|   ТЧОтпуска.ВидРасчета,
	|   ВЫБОР
	|		КОГДА Время.ДатаМесяца > ТЧОтпуска.ДатаНачала И Время.ДатаМесяца  > ЗаменыОрганизаций.ДатаНачала	
	|			ТОГДА Время.ДатаМесяца 
	|		КОГДА ТЧОтпуска.ДатаНачала > Время.ДатаМесяца И ТЧОтпуска.ДатаНачала  > ЗаменыОрганизаций.ДатаНачала	
	|			ТОГДА ТЧОтпуска.ДатаНачала 
	|		ИНАЧЕ ЗаменыОрганизаций.ДатаНачала 
	|	КОНЕЦ КАК ДатаНачала,
	|
	|	ВЫБОР 
	|		КОГДА КОНЕЦПЕРИОДА(Время.ДатаМесяца, МЕСЯЦ) < ТЧОтпуска.ДатаОкончания И КОНЕЦПЕРИОДА(Время.ДатаМесяца, МЕСЯЦ) < ЗаменыОрганизаций.ДатаОкончания	
	|			ТОГДА КОНЕЦПЕРИОДА(Время.ДатаМесяца, МЕСЯЦ) 
	|		КОГДА ТЧОтпуска.ДатаОкончания < КОНЕЦПЕРИОДА(Время.ДатаМесяца, МЕСЯЦ) И ТЧОтпуска.ДатаОкончания < ЗаменыОрганизаций.ДатаОкончания	
	|			ТОГДА ТЧОтпуска.ДатаОкончания 
	|		ИНАЧЕ ЗаменыОрганизаций.ДатаОкончания 
	|	КОНЕЦ КАК ДатаОкончания
	|
	|
	|
	|ИЗ Документ."+Ссылка.Метаданные().Имя+" КАК Основной
	|
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ "+ТекстЗапросаВТВремя+" КАК Время
	|ПО	ИСТИНА
	|
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ	Документ.НачислениеОтпускаРаботникамОрганизаций.Отпуска КАК ТЧОтпуска
	|ПО		ТЧОтпуска.Ссылка = &парамРегистратор
	|	И	Время.ДатаМесяца >= НАЧАЛОПЕРИОДА(ТЧОтпуска.ДатаНачала, МЕСЯЦ)
	|	И	Время.ДатаМесяца <= КОНЕЦПЕРИОДА(ТЧОтпуска.ДатаОкончания, МЕСЯЦ)
	|
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаменыОрганизаций КАК ЗаменыОрганизаций
	|ПО		ЗаменыОрганизаций.Сотрудник = Основной.Сотрудник 
	|	И	ЗаменыОрганизаций.ДатаНачала <= ТЧОтпуска.ДатаОкончания
	|	И	ТЧОтпуска.ДатаНачала <= ЗаменыОрганизаций.ДатаОкончания
	|	И	Время.ДатаМесяца >= НАЧАЛОПЕРИОДА(ЗаменыОрганизаций.ДатаНачала, МЕСЯЦ)
	|	И	Время.ДатаМесяца <= КОНЕЦПЕРИОДА(ЗаменыОрганизаций.ДатаОкончания, МЕСЯЦ)
	|
	|ГДЕ Основной.Ссылка = &парамРегистратор	
	|";
	
	Запрос.Текст = ТекстЗапроса;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		// строка движений
		НоваяСтрока = Начисления.Добавить();
		
		// Свойства
		НоваяСтрока.Авторасчет			= Истина;
		НоваяСтрока.ДатаНачала			= Выборка.ДатаНачала;
		НоваяСтрока.ДатаОкончания		= Выборка.ДатаОкончания;
		НоваяСтрока.БазовыйПериодНачало	= Выборка.ДатаНачала;
		НоваяСтрока.БазовыйПериодКонец	= Выборка.ДатаОкончания;
		НоваяСтрока.ВидРасчета			= Выборка.ВидРасчета;
		
		// Измерения
		НоваяСтрока.Сотрудник	= Выборка.Сотрудник;
		НоваяСтрока.Назначение	= Выборка.Назначение;
		
		// Реквизиты
		ПроведениеРасчетов.ПроставитьДанныеСтроки(Сотрудник, Выборка.Назначение, НоваяСтрока.ДатаНачала, ПериодРегистрации, НоваяСтрока.ВидРасчета, НоваяСтрока);
		НоваяСтрока.Показатель1					= ?(Сотрудник = Выборка.Назначение, Выборка.СуммаСредней, 0);
		НоваяСтрока.ВидУчетаВремениДляСредней	= Выборка.ВидУчетаВремениДляСредней;
		
	КонецЦикла;
	Записать();
	
КонецПроцедуры  // АвтозаполнениеНачисленияОтпуск()

Процедура АвтозаполнениеНачисленияКомпенсацияОтпуска() Экспорт
	
	// данные нормы календаря помесячно
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("парамРегистратор",	Ссылка);
	Запрос.УстановитьПараметр("ДатаНачалаСобытия",	ПериодРегистрации);
	Запрос.УстановитьПараметр("парамСотрудник",		Сотрудник);
	
	// запрос только по соновному назначению, включая внутреннее совместительство
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Основной.Дата,
	|	Основной.ПериодРегистрации,
	|	Основной.Организация,
	|	Основной.Ссылка,
	|
	|   ЗНАЧЕНИЕ(ПланВидовРасчета.ОсновныеНачисленияОрганизаций.КомпенсацияОтпуска) КАК ВидРасчета,
	|	ИСТИНА 						КАК Авторасчет,
	|	Основной.Сотрудник 			КАК Сотрудник,
	|	Основной.ПериодРегистрации		КАК ДатаНачала,
	|	Основной.ПериодРегистрации		КАК ДатаОкончания,
	|	Основной.ПериодРегистрации		КАК БазовыйПериодНачало,
	|	Основной.ПериодРегистрации		КАК БазовыйПериодКонец,
	|
	|	Основной.СуммаСредней,
	|	Основной.ВидУчетаВремениДляСредней,
	|  	Основной.ПериодРасчетаСреднегоНачало	КАК ПериодРасчетаСреднегоНачало,
	|  	Основной.ПериодРасчетаСреднегоКонец		КАК ПериодРасчетаСреднегоКонец,
	|
	|	Работники.Сотрудник 						КАК Назначение,
	|	Работники.ПодразделениеОрганизации			КАК ПодразделениеОрганизации,
	|	Работники.ГрафикРаботы 						КАК ГрафикРаботы
	|
	|ИЗ Документ."+Ссылка.Метаданные().Имя+" КАК Основной
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций.СрезПоследних(&ДатаНачалаСобытия, Сотрудник = &парамСотрудник ) КАК Работники
	|ПО		ИСТИНА
	|
	|ГДЕ Основной.Ссылка = &парамРегистратор	
	|";
	
	Запрос.Текст = ТекстЗапроса;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = Начисления.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		
		ПроведениеРасчетов.ПроставитьДанныеСтроки(Сотрудник, Выборка.Назначение, НоваяСтрока.ДатаНачала, ПериодРегистрации, НоваяСтрока.ВидРасчета, НоваяСтрока);
		НоваяСтрока.Показатель1	= Выборка.СуммаСредней;
		НоваяСтрока.Показатель2	= ДнейЧасовКомпенсацииОтпуска;
		
	КонецЦикла;
	
	Записать();
	
КонецПроцедуры  // АвтозаполнениеНачисленияКомпенсацияОтпуска()


Процедура АвтозаполнениеНачисленияОтзывИзОтпуска() Экспорт
	
	// данные нормы календаря помесячно
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("парамРегистратор",	Ссылка);
	Запрос.УстановитьПараметр("ПервичныйОтпуск",	ПервичныйОтпуск);
	Запрос.УстановитьПараметр("парамСотрудник",		Сотрудник);
	
	
	// запрос по всем назначениям, включая внутреннее совместительство
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Истина КАК Авторасчет,
	|	Основной.Дата,
	|	Основной.ПериодРегистрации,
	|	Основной.Организация,
	|	Основной.Ссылка,
	|	Основной.Сотрудник,
	|	Основной.СуммаСредней,
	|  	Основной.ПериодРасчетаСреднегоНачало	КАК ПериодРасчетаСреднегоНачало,
	|  	Основной.ПериодРасчетаСреднегоКонец		КАК ПериодРасчетаСреднегоКонец,
	|
	|	ИСТИНА		КАК Сторно,
	|	НачисленияПервичныйОтпуск.Назначение	КАК Назначение,
	|  	НачисленияПервичныйОтпуск.БазовыйПериодНачало	КАК БазовыйПериодНачало,
	|  	НачисленияПервичныйОтпуск.БазовыйПериодКонец	КАК БазовыйПериодКонец,
	|	НачисленияПервичныйОтпуск.ГрафикРаботы,
	|	НачисленияПервичныйОтпуск.НормаДнейЗаМесяц,
	|	НачисленияПервичныйОтпуск.НормаЧасовЗаМесяц,
	|	НачисленияПервичныйОтпуск.Показатель1,
	|	НачисленияПервичныйОтпуск.Показатель2,
	|	НачисленияПервичныйОтпуск.Показатель3,
	|	НачисленияПервичныйОтпуск.Показатель4,
	|	НачисленияПервичныйОтпуск.ВидУчетаВремениДляСредней,
	|	НачисленияПервичныйОтпуск.ПодразделениеОрганизации	КАК ПодразделениеОрганизации,
	|
	|   НачисленияПервичныйОтпуск.ВидРасчета,
	|	ВЫБОР КОГДА НачисленияПервичныйОтпуск.ПериодДействияНачало > ТЧОтпуска.ДатаНачала	ТОГДА НачисленияПервичныйОтпуск.ПериодДействияНачало ИНАЧЕ ТЧОтпуска.ДатаНачала КОНЕЦ КАК ДатаНачала,
	|	ВЫБОР КОГДА НачисленияПервичныйОтпуск.ПериодДействияКонец < ТЧОтпуска.ДатаОкончания	ТОГДА НачисленияПервичныйОтпуск.ПериодДействияКонец ИНАЧЕ ТЧОтпуска.ДатаОкончания КОНЕЦ КАК ДатаОкончания
	|
	|ИЗ Документ."+Ссылка.Метаданные().Имя+" КАК Основной
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций КАК НачисленияПервичныйОтпуск
	|ПО	НачисленияПервичныйОтпуск.Регистратор = &ПервичныйОтпуск
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ	Документ.НачислениеОтпускаРаботникамОрганизаций.Отпуска КАК ТЧОтпуска
	|ПО		ТЧОтпуска.Ссылка = &парамРегистратор
	|	И	НачисленияПервичныйОтпуск.ПериодДействияНачало <= ТЧОтпуска.ДатаОкончания
	|	И	ТЧОтпуска.ДатаНачала <= НачисленияПервичныйОтпуск.ПериодДействияКонец
	|
	|ГДЕ Основной.Ссылка = &парамРегистратор	
	|	И НЕ НачисленияПервичныйОтпуск.Сторно	
	|
	|
	|// Добавим начисления, которые были вытеснены олад и т.п.
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	Истина,
	|	Основной.Дата,
	|	Основной.ПериодРегистрации,
	|	Основной.Организация,
	|	Основной.Ссылка,
	|	Основной.Сотрудник,
	|	Основной.СуммаСредней,
	|  	Основной.ПериодРасчетаСреднегоНачало	КАК ПериодРасчетаСреднегоНачало,
	|  	Основной.ПериодРасчетаСреднегоКонец		КАК ПериодРасчетаСреднегоКонец,
	|
	|	ЛОЖЬ		КАК Сторно,
	|	ОсновныеНачисления.Назначение	КАК Назначение,
	|  	ОсновныеНачисления.БазовыйПериодНачало	КАК БазовыйПериодНачало,
	|  	ОсновныеНачисления.БазовыйПериодКонец	КАК БазовыйПериодКонец,
	|	ОсновныеНачисления.ГрафикРаботы,
	|	ОсновныеНачисления.НормаДнейЗаМесяц,
	|	ОсновныеНачисления.НормаЧасовЗаМесяц,
	|	ОсновныеНачисления.Показатель1,
	|	ОсновныеНачисления.Показатель2,
	|	ОсновныеНачисления.Показатель3,
	|	ОсновныеНачисления.Показатель4,
	|	ОсновныеНачисления.ВидУчетаВремениДляСредней,
	|	ОсновныеНачисления.ПодразделениеОрганизации	КАК ПодразделениеОрганизации,
	|	
	|   ОсновныеНачисления.ВидРасчета,
	|	ВЫБОР 
	|		КОГДА НачисленияПервичныйОтпуск.ПериодДействияНачало > ТЧОтпуска.ДатаНачала	
	|			И НачисленияПервичныйОтпуск.ПериодДействияНачало > ОсновныеНачисления.ПериодДействияНачало
	|			ТОГДА НачисленияПервичныйОтпуск.ПериодДействияНачало 
	|		КОГДА ТЧОтпуска.ДатаНачала > НачисленияПервичныйОтпуск.ПериодДействияНачало	
	|			И ТЧОтпуска.ДатаНачала > ОсновныеНачисления.ПериодДействияНачало
	|			ТОГДА ТЧОтпуска.ДатаНачала 
	|		ИНАЧЕ ОсновныеНачисления.ПериодДействияНачало 
	|	КОНЕЦ КАК ДатаНачала,
	|	ВЫБОР 
	|		КОГДА НачисленияПервичныйОтпуск.ПериодДействияКонец < ТЧОтпуска.ДатаОкончания	
	|			И НачисленияПервичныйОтпуск.ПериодДействияКонец < ОсновныеНачисления.ПериодДействияКонец
	|			ТОГДА НачисленияПервичныйОтпуск.ПериодДействияКонец 
	|		КОГДА ТЧОтпуска.ДатаОкончания < НачисленияПервичныйОтпуск.ПериодДействияКонец	
	|			И ТЧОтпуска.ДатаОкончания < ОсновныеНачисления.ПериодДействияКонец
	|			ТОГДА ТЧОтпуска.ДатаОкончания 
	|		ИНАЧЕ ОсновныеНачисления.ПериодДействияКонец 
	|	КОНЕЦ КАК ДатаОкончания
	|
	|ИЗ Документ.НачислениеОтпускаРаботникамОрганизаций КАК Основной
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций КАК НачисленияПервичныйОтпуск
	|ПО	НачисленияПервичныйОтпуск.Регистратор = &ПервичныйОтпуск
	|
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ	Документ.НачислениеОтпускаРаботникамОрганизаций.Отпуска КАК ТЧОтпуска
	|ПО		ТЧОтпуска.Ссылка = &парамРегистратор
	|	И	НачисленияПервичныйОтпуск.ПериодДействияНачало <= ТЧОтпуска.ДатаОкончания
	|	И	ТЧОтпуска.ДатаНачала <= НачисленияПервичныйОтпуск.ПериодДействияКонец
	|
	|// начисления, которые вытеснял отпуск
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций КАК ОсновныеНачисления
	|ПО	ОсновныеНачисления.Регистратор <> &ПервичныйОтпуск
	|	И	ОсновныеНачисления.Сотрудник = &парамСотрудник
	|	И	ОсновныеНачисления.Назначение = НачисленияПервичныйОтпуск.Назначение
	|	И	ОсновныеНачисления.ПериодДействияНачало 
	|		<= ВЫБОР КОГДА НачисленияПервичныйОтпуск.ПериодДействияКонец < ТЧОтпуска.ДатаОкончания	ТОГДА НачисленияПервичныйОтпуск.ПериодДействияКонец ИНАЧЕ ТЧОтпуска.ДатаОкончания КОНЕЦ
	|	И	ВЫБОР КОГДА НачисленияПервичныйОтпуск.ПериодДействияНачало > ТЧОтпуска.ДатаНачала	ТОГДА НачисленияПервичныйОтпуск.ПериодДействияНачало ИНАЧЕ ТЧОтпуска.ДатаНачала КОНЕЦ 
	|		<= ОсновныеНачисления.ПериодДействияКонец
	|
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовРасчета.ОсновныеНачисленияОрганизаций.ВытесняющиеВидыРасчета КАК ВытесняющиеВР
	|ПО		ВытесняющиеВР.Ссылка = ОсновныеНачисления.ВидРасчета
	|	И	ВытесняющиеВР.ВидРасчета = НачисленияПервичныйОтпуск.ВидРасчета
	|
	|ГДЕ Основной.Ссылка = &парамРегистратор	
	|	И НЕ НачисленияПервичныйОтпуск.Сторно	
	|";
	
	Запрос.Текст = ТекстЗапроса;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Начисления.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
	КонецЦикла;
	Записать();
	
КонецПроцедуры  // АвтозаполнениеНачисленияОтзывИзОтпуска()


Процедура АвтозаполнениеНачисления() Экспорт
	
	Если ВидДокументаОтпуск = Перечисления.ВидыДокументаОтпуск.Отпускные Тогда
		АвтозаполнениеНачисленияОтпуск();	
	ИначеЕсли ВидДокументаОтпуск = Перечисления.ВидыДокументаОтпуск.КомпенсацияОтпуска Тогда
		АвтозаполнениеНачисленияКомпенсацияОтпуска();	
	ИначеЕсли ВидДокументаОтпуск = Перечисления.ВидыДокументаОтпуск.ОтзывИзОтпуска Тогда
		АвтозаполнениеНачисленияОтзывИзОтпуска();	
	Иначе	
	КонецЕсли;
	
КонецПроцедуры  // АвтозаполнениеНачисления()

Процедура РассчитатьНачисления(Сотрудники, КомментироватьРасчет = Ложь ) Экспорт
	
	//массивы для хранения индексов строк табличных частей,
	//используются для расчета по одному сотруднику
	МассивИндексыСтрокНачисления = Новый Массив();
	
	Отказ = Ложь;
	// Перечитаем объект и соберем данные для заполнения наборов записей регистров
	НачатьТранзакцию();
	Прочитать();
	ВыборкаПоШапкеДокумента = СформироватьЗапросПоШапке().Выбрать();
	
	// позиционируем выборку
	ВыборкаПоШапкеДокумента.Следующий();
	ВыборкаПоНачислениям = СформироватьЗапросПоНачислениям().Выбрать();
	ЗафиксироватьТранзакцию();
	
	// Создадим рабочие наборы записей
	НаборОсновныеНачисления = РегистрыРасчета.ОсновныеНачисленияРаботниковОрганизаций.СоздатьНаборЗаписей();
	НаборОсновныеНачисления.Отбор.Регистратор.Значение = Ссылка;
	
	// Если встретим почасовое отклонение, то записываем движения в регистр РабочееВремяРаботниковОрганизаций
	НаборЗаписейРабочееВремя = РегистрыНакопления.РабочееВремяРаботниковОрганизаций.СоздатьНаборЗаписей();	
	НаборЗаписейРабочееВремя.Отбор.Регистратор.Значение = Ссылка;
	
	Пока ВыборкаПоНачислениям.Следующий() Цикл
		
		ПроверитьЗаполнениеСтрокиНачисления(ВыборкаПоШапкеДокумента,ВыборкаПоНачислениям, Отказ);
		Если НЕ Отказ Тогда
			// Заполним записи в наборах записей регистров
			ДобавитьСтрокуОсновныхНачислений(ВыборкаПоНачислениям, НаборОсновныеНачисления, Ложь);
			ДобавитьСтрокуРабочегоВремени(ВыборкаПоНачислениям, НаборЗаписейРабочееВремя);

			МассивИндексыСтрокНачисления.Добавить(ВыборкаПоНачислениям.НомерСтроки-1);
			
		КонецЕсли;
		
	КонецЦикла;
	НаборОсновныеНачисления.Записать();
	НаборЗаписейРабочееВремя.Записать();
	
	Если Отказ Тогда
		
		// Если есть какие-то проблемы - удаляем движения (тут нет транзакции) 
		// Удаляем движения
		НаборОсновныеНачисления.Очистить();
		НаборОсновныеНачисления.Записать();
		
		НаборЗаписейРабочееВремя.Очистить();
		НаборЗаписейРабочееВремя.Записать();
		
		Возврат;
		
	КонецЕсли;
	
	// Получим дополнительные записи, имеющие признак сторно, которые необходимо добавить 
	// в текущий набор для того, чтобы в результате сохранения получился максимальный 
	// фактический период действия
	ТаблицаСторноЗаписей = НаборОсновныеНачисления.ПолучитьДополнение();
	ТаблицаСторноЗаписей.Колонки.Удалить("ПериодРегистрации");
	ТаблицаСторноЗаписей.Колонки.ПериодРегистрацииСторно.Имя = "ПериодРегистрации";
	ТаблицаСторноЗаписей.Колонки.Удалить("ПериодДействияНачало");
	ТаблицаСторноЗаписей.Колонки.ПериодДействияНачалоСторно.Имя = "ПериодДействияНачало";
	ТаблицаСторноЗаписей.Колонки.Удалить("ПериодДействияКонец");
	ТаблицаСторноЗаписей.Колонки.ПериодДействияКонецСторно.Имя = "ПериодДействияКонец";
	Для каждого СтрокаСторно Из ТаблицаСторноЗаписей Цикл
		
		// Заполним записи в наборе записей регистра
		Движение = НаборОсновныеНачисления.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, СтрокаСторно);
		Движение.Сторно						= Истина;
		Движение.Авторасчет					= Истина;
		Движение.СторнируемыйДокумент = СтрокаСторно.Регистратор;

		// и в табличной части
		Движение = Начисления.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, СтрокаСторно);
		Движение.ДатаНачала		= СтрокаСторно.ПериодДействияНачало;   
		Движение.ДатаОкончания	= СтрокаСторно.ПериодДействияКонец; 
		Движение.Сторно						= Истина;
		Движение.Авторасчет					= Истина;
		Движение.СторнируемыйДокумент = СтрокаСторно.Регистратор;

		МассивИндексыСтрокНачисления.Добавить(Движение.НомерСтроки-1);
		
	КонецЦикла;
	НаборОсновныеНачисления.Записать();
		
	// рассчитываем записи
	// при этом передаем не только набор записей регистра расчета, но и набор записей регисра 
	// накопления со сведениями об отработанном времени
	ПроведениеРасчетов.РассчитатьЗаписиРегистраРасчета("ОсновныеНачисленияРаботниковОрганизаций", 
														НаборОсновныеНачисления, 
														Начисления, 
														МассивИндексыСтрокНачисления, 
														Сотрудники, 
														КомментироватьРасчет);
	
	// Удаляем движения
	НаборОсновныеНачисления.Очистить();
	НаборОсновныеНачисления.Записать();
	
	НаборЗаписейРабочееВремя.Очистить();
	НаборЗаписейРабочееВремя.Записать();
	
	Записать();
	
КонецПроцедуры  // РассчитатьНачисления


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ПЕРЕРАСЧЕТА

Функция ЗаполнитьПоПерерассчитываемомуДокументу(ИсходныйДокумент, Знач Сотрудники = Неопределено) Экспорт
	
	// проверять необходимость перерасчета не будем,
	// если процедура запущена, то будем заполнять и пересчитывать
	
	//////////////////////////////////////////////////////////////////////////////////
	// Выполним заполнение данных из перерасчитываемого документа
 	ЗаполнитьЗначенияСвойств(ЭтотОбъект,ИсходныйДокумент,,
		"Проведен, Номер, Дата, ПометкаУдаления, ПерерассчитываемыйДокумент, ПериодРегистрации, Комментарий, Ответственный"); // кроме указанных
	ПерерассчитываемыйДокумент = ИсходныйДокумент.Ссылка;
	
	// ТЧ Отпуска
	Для Каждого СтрокаОтпуска Из ИсходныйДокумент.Отпуска Цикл
		НоваяСтрока = Отпуска.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаОтпуска);
		
	КонецЦикла;
	
	// ТЧ Начисления
	Если Истина Тогда
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ПерерассчитываемыйДокумент", ПерерассчитываемыйДокумент);
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	Начисления.Сотрудник,
		|	Начисления.Назначение,
		|	Начисления.ВидРасчета,
		|	Начисления.ДатаНачала,
		|	Начисления.ДатаОкончания,
		|	Начисления.БазовыйПериодНачало,
		|	Начисления.БазовыйПериодКонец,
		|	Начисления.Показатель1,
		|	Начисления.Показатель2,
		|	Начисления.Показатель3,
		|	Начисления.Показатель4,
		|	Начисления.Показатель5,
		|	Начисления.Показатель6,
		|	Начисления.ГрафикРаботы,
		|	Начисления.ПодразделениеОрганизации,
		|	Начисления.ВидУчетаВремениДляСредней,
		|	Начисления.НормаДнейЗаМесяц,
		|	Начисления.НормаЧасовЗаМесяц,
		|	-Начисления.Результат КАК Результат,
		|	-Начисления.ОплаченоДнейЧасов КАК ОплаченоДнейЧасов,
		|	-Начисления.ОтработаноДней КАК ОтработаноДней,
		|	-Начисления.ОтработаноЧасов КАК ОтработаноЧасов,
		|	-Начисления.КалендарныеДни КАК КалендарныеДни,
		|	-Начисления.НормаДней КАК НормаДней,
		|	-Начисления.НормаЧасов КАК НормаЧасов,
		|	ИСТИНА КАК Сторно,
		|	Начисления.Ссылка КАК СторнируемыйДокумент,
		|	Начисления.Авторасчет КАК Авторасчет
		|ИЗ
		|	Документ."+Метаданные().Имя+".Начисления КАК Начисления
		|ГДЕ
		|	Начисления.Ссылка = &ПерерассчитываемыйДокумент
		|	И (НЕ Начисления.Сторно)
		|	И Начисления.Ссылка.Проведен";
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			// сторно-строка
			ЗаполнитьЗначенияСвойств(Начисления.Добавить(), Выборка);
		КонецЦикла;
		
	КонецЕсли;
	
	// Сторнировать расчет средней не надо, так как при их расчете все расчитывается заново
	
	//////////////////////////////////////////////////////////////////////////////////
	// Выполним заполнение новых данных, по ПериодуРегистрации указанному 
	// в перерасчитываемом документе, по всем табличным частям
	Записать();
	АвтозаполнениеРасчетСреднего();
	РассчитатьРасчетСреднего();

	АвтозаполнениеНачисления();
	
	Возврат Истина;
	
КонецФункции // ЗаполнитьПоПерерассчитываемомуДокументу()

Процедура Перерассчитать( Сотрудники = Неопределено) Экспорт
	
	// проверим можно ли что-то делать с документом
	ОписаниеПричиныОтказа = "";
	Если ПроведениеРасчетов.ДокументНельзяИзменятьЗаднимЧислом(Ссылка, ОписаниеПричиныОтказа) Тогда
		ВызватьИсключение(ОписаниеПричиныОтказа);
	КонецЕсли;
	
	Если Не ПроведениеРасчетов.НеобходимостьПерерасчета(Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	// получим список перерассчитываемых сорудников
	СотрудникиСписок = Новый СписокЗначений;
	Если Сотрудники = Неопределено Тогда
		
		СотрудникиСписок.Добавить(Сотрудник);
		
	Иначе
		
		СотрудникиСписок = Сотрудники;
		
	КонецЕсли;
	
	// если не перерассчитываем никого - возврат
	Если СотрудникиСписок.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	
	// читаем все данные 
	НачатьТранзакцию();
	Прочитать();
	
	Движения.ОсновныеНачисленияРаботниковОрганизаций.Прочитать();
	Движения.РасчетСреднегоЗаработка.Прочитать();
	ЗафиксироватьТранзакцию();
	
	////////////////////////////////////////////////////////////////////////
	// Расчет средней
	Если Истина Тогда
		// перерассчитываем записи средней
		
		ОсновнойВидРасчета = ПланыВидовРасчета.СреднийЗаработок.ПоЗаработку;
		тКоэффициентПовышенияОкладов = 1;
		
		Выборка = ПроведениеРасчетов.РассчитатьСреднююПорядок100(Ссылка).Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Если Выборка.ВидРасчета = ОсновнойВидРасчета Тогда
				тКоэффициентПовышенияОкладов = Выборка.КоэффициентПовышенияОкладов * тКоэффициентПовышенияОкладов;
			КонецЕсли;
			
			СтрокаТабличнойЧасти = РасчетСреднего.Получить( Выборка.НомерСтроки - 1 );
			
			Если НЕ СтрокаТабличнойЧасти.Авторасчет Тогда
				Продолжить;
			КонецЕсли;
			
			СтрокаТабличнойЧасти.Результат				= Выборка.Результат * КоэффициентПовышенияОкладов;
			СтрокаТабличнойЧасти.РезультатПолный		= Выборка.РезультатПолный;
			
			СтрокаТабличнойЧасти.ОтработаноДней			= Выборка.ОтработаноДней;
			СтрокаТабличнойЧасти.ОтработаноЧасов		= Выборка.ОтработаноЧасов;
			Если ВидУчетаВремениДляСредней = Перечисления.ВидыУчетаВремениДляСредней.ПоКалендарнымДнямСПраздниками
				И СтрокаТабличнойЧасти.ВидРасчета = ПланыВидовРасчета.СреднийЗаработок.ПоЗаработкуДляОтпуска Тогда
				СтрокаТабличнойЧасти.КалендарныеДни		= 1 + День(Выборка.БазовыйПериодКонец) - День(Выборка.БазовыйПериодНачало);
			Иначе
				СтрокаТабличнойЧасти.КалендарныеДни		= Выборка.КалендарныеДни;
			КонецЕсли;
			СтрокаТабличнойЧасти.НормаДней				= Выборка.НормаДней;
			СтрокаТабличнойЧасти.НормаЧасов				= Выборка.НормаЧасов;
			
		КонецЦикла;
		
		Движения.РасчетСреднегоЗаработка.Записать(Истина, Истина);
		
		РасчитатьСреднюю();
	КонецЕсли;
	Записать();
	
	////////////////////////////////////////////////////////////////////////
	// Основные начисления
	
	Если Истина Тогда
		Для Каждого Строка из Движения.ОсновныеНачисленияРаботниковОрганизаций Цикл
			Если Строка.Сторно или НЕ Строка.Авторасчет Тогда
				Продолжить;
			КонецЕсли;
			СпособРасчета = Строка.ВидРасчета.СпособРасчета;
			Если СпособРасчета = Перечисления.СпособыРасчетаОплатыТруда.ПоСреднемуЗаработкуДляОтпуска
				Или СпособРасчета = Перечисления.СпособыРасчетаОплатыТруда.ПоСреднемуЗаработку
				Или СпособРасчета = Перечисления.СпособыРасчетаОплатыТруда.ДоплатаДоСреднегоЗаработка
				Или СпособРасчета = Перечисления.СпособыРасчетаОплатыТруда.ПоСреднемуЗаработкуКомпенсацияОтпуска
				Или СпособРасчета = Перечисления.СпособыРасчетаОплатыТруда.ПоСреднемуЗаработкуФСС
				Или СпособРасчета = Перечисления.СпособыРасчетаОплатыТруда.ВыходноеПособие Тогда
				Строка.Показатель1 = СуммаСредней;
			КонецЕсли;	
		КонецЦикла;
		Для Каждого Строка из Начисления Цикл
			Если Строка.Сторно или НЕ Строка.Авторасчет Тогда
				Продолжить;
			КонецЕсли;
			СпособРасчета = Строка.ВидРасчета.СпособРасчета;
			Если СпособРасчета = Перечисления.СпособыРасчетаОплатыТруда.ПоСреднемуЗаработкуДляОтпуска
				Или СпособРасчета = Перечисления.СпособыРасчетаОплатыТруда.ПоСреднемуЗаработку
				Или СпособРасчета = Перечисления.СпособыРасчетаОплатыТруда.ДоплатаДоСреднегоЗаработка
				Или СпособРасчета = Перечисления.СпособыРасчетаОплатыТруда.ПоСреднемуЗаработкуКомпенсацияОтпуска
				Или СпособРасчета = Перечисления.СпособыРасчетаОплатыТруда.ПоСреднемуЗаработкуФСС
				Или СпособРасчета = Перечисления.СпособыРасчетаОплатыТруда.ВыходноеПособие Тогда
				Строка.Показатель1 = СуммаСредней;
			КонецЕсли;	
		КонецЦикла;
		
		// перерассчитываем записи начислений
		ПроведениеРасчетов.РассчитатьЗаписиРегистраРасчета("ОсновныеНачисленияРаботниковОрганизаций", 
											Движения.ОсновныеНачисленияРаботниковОрганизаций, 
											Начисления, 
											, 
											СотрудникиСписок);
		Движения.ОсновныеНачисленияРаботниковОрганизаций.Записать(Истина, Истина);
	КонецЕсли;
	Записать();
	
	////////////////////////////////////////////////////////////////////////
	// Взаиморасчеты с работниками
	Если Истина Тогда
		// сначала удалим сведения о взаиморасчетах с работниками
		ВзаиморасчетыСРаботникамиОрганизаций = Движения.ВзаиморасчетыСРаботникамиОрганизаций;
		ВзаиморасчетыСРаботникамиОрганизаций.Очистить();
		
		ВыборкаПоНачислениям = СформироватьЗапросПоНачислениям().Выбрать();
		Пока ВыборкаПоНачислениям.Следующий() Цикл 
			ДобавитьСтрокуВДвиженияПоРегистрамНакопления(ВыборкаПоНачислениям)
		КонецЦикла;
		
	КонецЕсли;
	Записать();
	
	////////////////////////////////////////////////////////////////////////
	// Удалим записи перерасчета по которым выполнен перерасчет
	НаборЗаписей = РегистрыРасчета.ОсновныеНачисленияРаботниковОрганизаций.Перерасчеты.ПерерасчетОсновныхНачислений.СоздатьНаборЗаписей();
	ПроведениеРасчетов.УдалитьЗаписиПерерасчетаПоРегиструРасчета(Ссылка, НаборЗаписей, СотрудникиСписок);
	НаборЗаписей.Записать();
	
	НаборЗаписей = РегистрыРасчета.РасчетСреднегоЗаработка.Перерасчеты.ПерерасчетСреднегоЗаработка.СоздатьНаборЗаписей();
	ПроведениеРасчетов.УдалитьЗаписиПерерасчетаПоРегиструРасчета(Ссылка, НаборЗаписей, СотрудникиСписок);
	НаборЗаписей.Записать();
	
КонецПроцедуры // Перерассчитать()


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

Функция СформироватьЗапросПоШапке()

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументСсылка" , Ссылка);
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ОплатаПоСреднемуЗаработку.Дата													КАК Дата,
	|	ОплатаПоСреднемуЗаработку.ПериодРегистрации										КАК ПериодРегистрации,
	|	ОплатаПоСреднемуЗаработку.Организация											КАК Организация,
	|	ОплатаПоСреднемуЗаработку.Ссылка												КАК Ссылка,
	|	ОплатаПоСреднемуЗаработку.Сотрудник												КАК Сотрудник,
	//|	ОплатаПоСреднемуЗаработку.ДатаНачала											КАК ДатаНачала,
	//|	ОплатаПоСреднемуЗаработку.ДатаОкончания											КАК ДатаОкончания,
	|	
	|   ОплатаПоСреднемуЗаработку.ПериодРасчетаСреднегоНачало	КАК ПериодРасчетаСреднегоЗаработкаНачало,
	|   ОплатаПоСреднемуЗаработку.ПериодРасчетаСреднегоКонец	КАК ПериодРасчетаСреднегоЗаработкаКонец
	|
	|ИЗ Документ."+Ссылка.Метаданные().Имя+" КАК ОплатаПоСреднемуЗаработку
	|
	|ГДЕ ОплатаПоСреднемуЗаработку.Ссылка = &ДокументСсылка
	|";
	
	Запрос.Текст = ТекстЗапроса;

	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросПоШапке()

Функция СформироватьЗапросПоНачислениям()

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументСсылка" , Ссылка);
	Запрос.УстановитьПараметр("парамСписокПочасовыхОтклонений", ПроведениеРасчетов.ПолучитьСписокНачисленийПочасовыхОтклонений());
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтрокиНачисления.Ссылка.Организация,
	|	СтрокиНачисления.Ссылка.ПериодРегистрации,
	|	СтрокиНачисления.НомерСтроки,  
	|	СтрокиНачисления.Авторасчет,
	|   ЗНАЧЕНИЕ(Перечисление.КодыОперацийВзаиморасчетыСРаботникамиОрганизаций.Начисления) КАК КодОперации,
	|
	|	СтрокиНачисления.Сотрудник,
	|	СтрокиНачисления.Назначение,
	|	СтрокиНачисления.ВидРасчета,
	|	СтрокиНачисления.ВидРасчета.СпособРасчета		КАК СпособРасчета,
    |
	|	КОНЕЦПЕРИОДА(СтрокиНачисления.Ссылка.ПериодРегистрации, МЕСЯЦ)	КАК Период,
	|	СтрокиНачисления.Ссылка.ПериодРегистрации						КАК ПериодВзаиморасчетов,
	|	СтрокиНачисления.ВидРасчета.СчетУчета			КАК СчетУчета,
	|	СтрокиНачисления.Результат						КАК СуммаВзаиморасчетов,
	|
	|	СтрокиНачисления.ДатаНачала,
	|	СтрокиНачисления.ДатаОкончания,
	|	СтрокиНачисления.БазовыйПериодНачало,
	|	СтрокиНачисления.БазовыйПериодКонец,
	|	СтрокиНачисления.ДатаНачала			КАК ПериодДействияНачало,
	|	СтрокиНачисления.ДатаОкончания		КАК ПериодДействияКонец,
	|
	|	СтрокиНачисления.Результат,
	|	СтрокиНачисления.ОплаченоДнейЧасов,
	|	СтрокиНачисления.ОтработаноДней,
	|	СтрокиНачисления.ОтработаноЧасов,
	|	СтрокиНачисления.НормаДней,
	|	СтрокиНачисления.НормаЧасов,
	|	СтрокиНачисления.КалендарныеДни,
	|
	|	СтрокиНачисления.ПодразделениеОрганизации,
	|	СтрокиНачисления.ГрафикРаботы,
	|   ВЫБОР	КОГДА СтрокиНачисления.ГрафикРаботы ССЫЛКА Справочник.ГрафикиРаботы
	|			ТОГДА NULL
	|			ИНАЧЕ СтрокиНачисления.Сотрудник
	|	КОНЕЦ						КАК ГрафикРаботыСотрудник,
	|	СтрокиНачисления.ГрафикРаботы.СуммированныйУчетРабочегоВремени КАК СуммированныйУчетРабочегоВремени,
	|	ВЫБОР	КОГДА СтрокиНачисления.ВидРасчета.ВидВремени В(&парамСписокПочасовыхОтклонений) 
	|				ТОГДА	ИСТИНА 
	|			ИНАЧЕ		ЛОЖЬ 
	|	КОНЕЦ														КАК ЯвляетсяПочасовымОтклонением,
	|	СтрокиНачисления.ВидРасчета.ОбозначениеВТабелеУчетаРабочегоВремени КАК ВидИспользованияРабочегоВремени, 
	|	СтрокиНачисления.Показатель1,
	|	СтрокиНачисления.Показатель2,
	|	СтрокиНачисления.Показатель3,
	|	СтрокиНачисления.Показатель4,
	|	СтрокиНачисления.Показатель5,
	|	СтрокиНачисления.Показатель6,
	|	СтрокиНачисления.НормаЧасовЗаМесяц,
	|	СтрокиНачисления.НормаДнейЗаМесяц,
	|	СтрокиНачисления.ВидУчетаВремениДляСредней,
	|	СтрокиНачисления.ОплачиватьЧасов,
	|	СтрокиНачисления.СторнируемыйДокумент,
	|	СтрокиНачисления.Сторно
	|
	|ИЗ Документ."+Ссылка.Метаданные().Имя+".Начисления КАК СтрокиНачисления
	|
	|ГДЕ СтрокиНачисления.Ссылка = &ДокументСсылка
	|";

	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросПоНачислениям()

Функция СформироватьЗапросПоРасчетСреднего() Экспорт

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументСсылка", Ссылка);

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтрокаРасчетСреднего.Ссылка.ПериодРегистрации КАК ПериодРегистрации,
	|	СтрокаРасчетСреднего.Ссылка.Сотрудник КАК Сотрудник,
	|	СтрокаРасчетСреднего.Ссылка.Организация КАК Организация,
	|
	|	СтрокаРасчетСреднего.НомерСтроки,
	|	СтрокаРасчетСреднего.Авторасчет,
	|	СтрокаРасчетСреднего.ВидРасчета,
	|	СтрокаРасчетСреднего.БазовыйПериодНачало,
	|	СтрокаРасчетСреднего.БазовыйПериодКонец,
	|	СтрокаРасчетСреднего.КоэффициентПовышенияОкладов,
	|
	|	СтрокаРасчетСреднего.ОтработаноДней,
	|	СтрокаРасчетСреднего.ОтработаноЧасов,
	|	СтрокаРасчетСреднего.НормаДней,
	|	СтрокаРасчетСреднего.НормаЧасов,
	|	СтрокаРасчетСреднего.КалендарныеДни,
	|
	|	СтрокаРасчетСреднего.Результат,
	|	СтрокаРасчетСреднего.РезультатПолный
	|ИЗ Документ."+Ссылка.Метаданные().Имя+".РасчетСреднего КАК СтрокаРасчетСреднего
	|
	|ГДЕ СтрокаРасчетСреднего.Ссылка = &ДокументСсылка
	|";
	
	Возврат Запрос.Выполнить();
	
КонецФункции // СформироватьЗапросПоРасчетСреднего()


Процедура ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ) Экспорт

	//  Организация
	Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.Организация) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указана организация, по которой выполняется начисление!", Отказ);
	КонецЕсли;

	// Сотрудник
	Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.Сотрудник) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не выбрано физическое лицо!", Отказ);
	ИначеЕсли ВыборкаПоШапкеДокумента.Сотрудник.Организация <> ВыборкаПоШапкеДокумента.Организация Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Сотрудник не соответствует организации!", Отказ);
	КонецЕсли;

	// ПериодРегистрации
	Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.ПериодРегистрации) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указан период регистрации!", Отказ);
	КонецЕсли;
	
КонецПроцедуры // ПроверитьЗаполнениеШапки()

Процедура ПроверитьЗаполнениеСтрокиНачисления(ВыборкаПоШапкеДокумента, ВыборкаПоСтрокамДокумента, Отказ)

	СтрокаНачалаСообщенияОбОшибке = "В строке номер """+ СокрЛП(ВыборкаПоСтрокамДокумента.НомерСтроки) +
									""" табл. части ""Начисления"": ";
	
	// ВидРасчета
	Если НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ВидРасчета) Тогда
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не указан вид расчета!", Отказ);
	КонецЕсли;
	
	// Дата начала 
	Если НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ДатаНачала) Тогда
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не указана дата начала начисления!", Отказ);
	Иначе
		// Дата окончания 
		Если НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ДатаОкончания) Тогда
			ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не указана дата окончания начисления!", Отказ);
		Иначе
			Если ВыборкаПоСтрокамДокумента.ДатаОкончания < ВыборкаПоСтрокамДокумента.ДатаНачала Тогда
				ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "дата окончания начисления не должна быть меньше даты начала!", Отказ);
			Иначе	
				Если НачалоМесяца(ВыборкаПоСтрокамДокумента.ДатаОкончания) <> НачалоМесяца(ВыборкаПоСтрокамДокумента.ДатаНачала) Тогда
					ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "даты начала и окончания должны принадлежать одному месяцу!", Отказ);
				КонецЕсли;
			КонецЕсли; 
		КонецЕсли;
	КонецЕсли;
	
	// Подразделение
	Если НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ПодразделениеОрганизации) Тогда
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не указано подразделение организации!", Отказ);
	КонецЕсли;
	
КонецПроцедуры // ПроверитьЗаполнениеСтрокиНачисления()

Процедура ПроверитьЗаполнениеСтрокиРасчетСреднего(ВыборкаПоСтрокамДокумента, Отказ) Экспорт

	СтрокаНачалаСообщенияОбОшибке = "В строке номер """+ СокрЛП(ВыборкаПоСтрокамДокумента.НомерСтроки) +
									""" табл. части ""Расчет среднего"": ";
	
	// Вид расчета
	Если НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ВидРасчета) Тогда
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не указан вид расчета среднего заработка!", Отказ);
	КонецЕсли;
	
	// Дата начала базового периода
	Если НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.БазовыйПериодНачало) Тогда
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не указана дата начала базового периода!", Отказ);
		// Дата окончания базового периода
	ИначеЕсли НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.БазовыйПериодКонец) Тогда
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не указана дата окончания базового периода!", Отказ);
	ИначеЕсли ВыборкаПоСтрокамДокумента.БазовыйПериодКонец < ВыборкаПоСтрокамДокумента.БазовыйПериодНачало Тогда
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "дата начала базового периода не может быть больше даты окончания базового периода!", Отказ);
	КонецЕсли; 
	
КонецПроцедуры // ПроверитьЗаполнениеСтрокиРасчетСреднего()


Процедура ДобавитьСтрокуОсновныхНачислений(ВыборкаПоСтрокамДокумента, НаборЗаписей, Проведение = Истина)
	
	Движение = НаборЗаписей.Добавить();
	ЗаполнитьЗначенияСвойств(Движение, ВыборкаПоСтрокамДокумента);
	
 	Движение.ВидУчетаВремени = ПроведениеРасчетов.ПолучитьВидУчетаВремени(ВыборкаПоСтрокамДокумента.СпособРасчета, ВыборкаПоСтрокамДокумента.ВидРасчета.ВидВремени, ВыборкаПоСтрокамДокумента.СуммированныйУчетРабочегоВремени);
	
	Если ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ВидРасчета.ВидОтпуска) Тогда
		Если ВыборкаПоСтрокамДокумента.ВидРасчета.ВидОтпуска.ВключатьПраздники Тогда
			Движение.ВидУчетаВремениДляСредней = Перечисления.ВидыУчетаВремениДляСредней.ПоКалендарнымДнямСПраздниками;
		Иначе
			Движение.ВидУчетаВремениДляСредней = Перечисления.ВидыУчетаВремениДляСредней.ПоКалендарнымДням;
		Конецесли;	
	КонецЕсли;
	
	Если НЕ Проведение И ТипЗнч(ВыборкаПоСтрокамДокумента.ГрафикРаботы) = Тип("СправочникСсылка.ГрафикиРаботы") Тогда
		Если  ВыборкаПоСтрокамДокумента.ГрафикРаботы.СокращатьКалендарныеДни Тогда
			Движение.ГрафикРаботы = ВыборкаПоСтрокамДокумента.ГрафикРаботы.ГрафикПолногоРабочегоВремени;	
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры // ДобавитьСтрокуОсновныхНачислений

Процедура ДобавитьСтрокуРабочегоВремени(ВыборкаПоНачислениям, НаборЗаписей)
	
	Если ВыборкаПоНачислениям.ЯвляетсяПочасовымОтклонением Тогда
		Движение = НаборЗаписей.Добавить();
		
		// Свойства
		Движение.Период = ВыборкаПоНачислениям.ПериодДействияНачало;
		
		// Измерения
		Движение.Сотрудник							= ВыборкаПоНачислениям.Сотрудник;
		Движение.Организация 						= ВыборкаПоНачислениям.Организация;
		Движение.ВидИспользованияРабочегоВремени 	= ВыборкаПоНачислениям.ВидИспользованияРабочегоВремени;
		
		// Ресурсы
		Движение.Часов			= ВыборкаПоНачислениям.ОплачиватьЧасов;
		
	КонецЕсли;
	
КонецПроцедуры // ДобавитьСтрокуРабочегоВремени()

Процедура ДобавитьСтрокуРасчетаСреднего(ВыборкаПоСтрокамДокумента, НаборЗаписей) Экспорт
	
	Движение = НаборЗаписей.Добавить();
	ЗаполнитьЗначенияСвойств(Движение,ВыборкаПоСтрокамДокумента);
	
КонецПроцедуры 

Процедура ДобавитьСтрокуВДвиженияПоРегистрамНакопления(ВыборкаПоСтрокамДокумента)
	
	// ВзаиморасчетыСРаботникамиОрганизаций
	Движение = Движения.ВзаиморасчетыСРаботникамиОрганизаций.Добавить();
	ЗаполнитьЗначенияСвойств(Движение, ВыборкаПоСтрокамДокумента);
	
	Движение.ВидДвижения			= ВидДвиженияНакопления.Приход;

КонецПроцедуры // ДобавитьСтрокуВДвиженияПоРегистрамНакопления()


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПроведения(Отказ, Режим)

	ВыборкаПоШапкеДокумента = СформироватьЗапросПоШапке().Выбрать();
	ВыборкаПоШапкеДокумента.Следующий();
	ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ);

	// Движения стоит добавлять, если в проведении еще не отказано (отказ =ложь)
	Если НЕ Отказ Тогда
		
		////////////////////////////////////////////////////////////////////////
		// основные Начисления
		
		ВыборкаПоНачислениям = СформироватьЗапросПоНачислениям().Выбрать();
		Пока ВыборкаПоНачислениям.Следующий() Цикл 
			
			ПроверитьЗаполнениеСтрокиНачисления(ВыборкаПоШапкеДокумента, ВыборкаПоНачислениям, Отказ);
			Если НЕ Отказ Тогда
				ДобавитьСтрокуОсновныхНачислений(ВыборкаПоНачислениям, Движения.ОсновныеНачисленияРаботниковОрганизаций);
				ДобавитьСтрокуВДвиженияПоРегистрамНакопления(ВыборкаПоНачислениям);
			КонецЕсли;
			
		КонецЦикла;
		
		
		////////////////////////////////////////////////////////////////////////
		// Расчет средней
		
		ВыборкаПоСредним = СформироватьЗапросПоРасчетСреднего().Выбрать();
		Пока ВыборкаПоСредним.Следующий() Цикл
			
			ПроверитьЗаполнениеСтрокиРасчетСреднего(ВыборкаПоСредним, Отказ);
			Если НЕ Отказ Тогда
				ДобавитьСтрокуРасчетаСреднего(ВыборкаПоСредним, Движения.РасчетСреднегоЗаработка);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли; 

КонецПроцедуры

Процедура ОбработкаЗаполнения(Основание)
	мНеСоздаватьДокумент = Ложь;
	
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.ОтпускаОрганизаций") Тогда
		
		Если Основание.ВидОперации = Перечисления.ВидыОперацийОтпускаОрганизаций.Отпуск Тогда
		
			Запрос = Новый Запрос;
			
			ТекстЗапроса = "ВЫБРАТЬ
		   |	ВидыОтпусков.ссылка КАК ВидОтпуска,
		   |	ОсновныеНачисленияОрганизаций.Ссылка КАК ВидРасчета
		   |ИЗ
		   |	Справочник.ВидыОтпусков КАК ВидыОтпусков
		   |	ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.ОсновныеНачисленияОрганизаций КАК ОсновныеНачисленияОрганизаций
		   |	ПО ВидыОтпусков.Ссылка = ОсновныеНачисленияОрганизаций.ВидОтпуска
		   |
		   |УПОРЯДОЧИТЬ ПО
		   |    ОсновныеНачисленияОрганизаций.Ссылка.Предопределенный
		   |";
			
			Запрос.Текст = ТекстЗапроса;
			ВидыРасчетов = Запрос.Выполнить().Выгрузить();
			
			// Заполнение шапки
			Запрос = Новый Запрос;
			
			ТекстЗапроса = "
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	КадровыеОтпуска.НомерСтроки					КАК КомерСтроки,
			|   КадровыеОтпуска.Сотрудник					КАК Сотрудник,
			|   КадровыеОтпуска.ДатаНачала					КАК ДатаНачала,
			|   КадровыеОтпуска.ДатаОкончания				КАК ДатаОкончания,
			|   КадровыеОтпуска.ВидОтпуска					КАК ВидОтпуска,
			|   Отпуска.КадровыйПриказ						КАК КадровыйПриказ
			|
			|ИЗ	Документ.ОтпускаОрганизаций.РаботникиОрганизации КАК КадровыеОтпуска
			|
			|// физлица, по которым уже рассчитаны отпускные, нам не нужны
			|ЛЕВОЕ СОЕДИНЕНИЕ(
			|	ВЫБРАТЬ
			|		ОтпускОрг.Ссылка.Сотрудник	КАК Сотрудник,
			|		ОтпускОрг.ДатаНачала	КАК ДатаНачала,
			|		ОтпускОрг.Ссылка.ПриказОтпускОрганизации	КАК КадровыйПриказ
			|   ИЗ Документ.НачислениеОтпускаРаботникамОрганизаций.Отпуска  КАК ОтпускОрг
			|   ГДЕ ОтпускОрг.Ссылка.ПриказОтпускОрганизации = &парамРегистратор 
			|	И	ОтпускОрг.Ссылка.Проведен
			|) КАК Отпуска
			|ПО Отпуска.КадровыйПриказ = КадровыеОтпуска.Ссылка
			|И Отпуска.Сотрудник = КадровыеОтпуска.Сотрудник
			|И Отпуска.ДатаНачала = КадровыеОтпуска.ДатаНачала
			|
			|ГДЕ	КадровыеОтпуска.Ссылка = &парамРегистратор
			|	И	Отпуска.КадровыйПриказ ЕСТЬ NULL
			|";
			
			
			Запрос.УстановитьПараметр("парамРегистратор",	Основание);
			Запрос.Текст = ТекстЗапроса;
			ВсеОтпуска = Запрос.Выполнить().Выгрузить();
			Если ВсеОтпуска.Количество() = 0 тогда
				мНеСоздаватьДокумент = Истина;
				Сообщить("По всем физ.лицам уже рассчитаны отпуска");
				Возврат;
			КонецЕсли;
			
			ВсеРаботники = ВсеОтпуска.Скопировать();
			ВсеРаботники.Свернуть("Сотрудник");
			Если ВсеРаботники.Количество() = 1 Тогда
				ТекущаяСтрока = ВсеРаботники[0];
			ИначеЕсли НЕ ЗначениеЗаполнено(Сотрудник) Тогда	
				ТекущаяСтрока = ВсеРаботники.ВыбратьСтроку("Выберите работника:");
			Иначе
				ТекущаяСтрока = ВсеРаботники.Найти(Сотрудник,"Сотрудник");
			КонецЕсли;	
			
			Если ТекущаяСтрока <> Неопределено Тогда
				
				Отбор = Новый Структура();
				Отбор.Вставить("Сотрудник",ТекущаяСтрока.Сотрудник);
				Строки = ВсеОтпуска.НайтиСтроки(Отбор);
				Если Строки.Количество() = 0 Тогда
				    Возврат;
				КонецЕсли;
				Отпуска.Очистить();
				Для Каждого СтрокаОтпуск из Строки Цикл 
			
					ВидРасчета = ВидыРасчетов.Найти(СтрокаОтпуск.ВидОтпуска,"ВидОтпуска");
					Если ВидРасчета = Неопределено Тогда 
						Продолжить;
					КонецЕсли;
					Если ВидРасчета.ВидРасчета = NULL Тогда 
						Продолжить;
					КонецЕсли;
					Если ВидРасчета.ВидРасчета.СпособРасчета = Перечисления.СпособыРасчетаОплатыТруда.НулеваяСумма Тогда 
						Продолжить;
					КонецЕсли;
	                				
					Отпуск = Отпуска.Добавить();
					Отпуск.ДатаНачала = СтрокаОтпуск.ДатаНачала;
					Отпуск.ДатаОкончания = СтрокаОтпуск.ДатаОкончания;
					ВидРасчета = ВидыРасчетов.Найти(СтрокаОтпуск.ВидОтпуска,"ВидОтпуска");
					Отпуск.ВидРасчета = ВидРасчета.ВидРасчета;
				КонецЦикла;	
				
				ВидДокументаОтпуск = Перечисления.ВидыДокументаОтпуск.Отпускные;
				Сотрудник = ТекущаяСтрока.Сотрудник;
				ПриказОтпускОрганизации = Основание;
				Организация = Основание.Организация;
			Иначе
				мНеСоздаватьДокумент = Истина;
				Возврат;
			КонецЕсли;
			
		ИначеЕсли Основание.ВидОперации = Перечисления.ВидыОперацийОтпускаОрганизаций.КомпенсацияОтпуска Тогда
			
			// Заполнение шапки
			Запрос = Новый Запрос;
			
			ТекстЗапроса = "
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|   КадровыеОтпуска.Сотрудник					КАК Сотрудник,
			|   КадровыеОтпуска.Дней						КАК Дней,
			|   Отпуска.КадровыйПриказ						КАК КадровыйПриказ
			|
			|ИЗ	
			|  (ВЫБРАТЬ
			|    КадровыеОтпуска.Сотрудник					КАК Сотрудник,
			|    СУММА(КадровыеОтпуска.ИспользоватьДней)	КАК Дней,
			|    КадровыеОтпуска.Ссылка						КАК Ссылка
            |   ИЗ
			|	 Документ.ОтпускаОрганизаций.ИспользованиеЕжегодногоОтпуска КАК КадровыеОтпуска
			|	ГДЕ	КадровыеОтпуска.Ссылка = &парамРегистратор
			|   СГРУППИРОВАТЬ ПО
			|    КадровыеОтпуска.Сотрудник,КадровыеОтпуска.Ссылка
			|  ) КАК КадровыеОтпуска
			|
			|// физлица, по которым уже выплачена компенсация, нам не нужны
			|ЛЕВОЕ СОЕДИНЕНИЕ(
			|	ВЫБРАТЬ
			|		ОтпускОрг.Сотрудник	КАК Сотрудник,
			|		ОтпускОрг.ПриказОтпускОрганизации	КАК КадровыйПриказ
			|   ИЗ Документ.НачислениеОтпускаРаботникамОрганизаций  КАК ОтпускОрг
			|   ГДЕ ОтпускОрг.Ссылка.ПриказОтпускОрганизации = &парамРегистратор 
			|	И	ОтпускОрг.Ссылка.Проведен
			|) КАК Отпуска
			|ПО Отпуска.КадровыйПриказ = КадровыеОтпуска.Ссылка
			|И Отпуска.Сотрудник = КадровыеОтпуска.Сотрудник
			|
			|ГДЕ	КадровыеОтпуска.Ссылка = &парамРегистратор
			|	И	Отпуска.КадровыйПриказ ЕСТЬ NULL
			|";
			
			
			Запрос.УстановитьПараметр("парамРегистратор",	Основание);
			Запрос.Текст = ТекстЗапроса;
			ВсеОтпуска = Запрос.Выполнить().Выгрузить();
			Если ВсеОтпуска.Количество() = 0 тогда
				мНеСоздаватьДокумент = Истина;
				Сообщить("По всем физ.лицам уже выплачена компенсация");
				Возврат;
			КонецЕсли;
			
			ВсеРаботники = ВсеОтпуска.Скопировать();
			ВсеРаботники.Свернуть("Сотрудник");
			Если ВсеРаботники.Количество() = 1 Тогда
				ТекущаяСтрока = ВсеРаботники[0];
			ИначеЕсли НЕ ЗначениеЗаполнено(Сотрудник) Тогда	
				ТекущаяСтрока = ВсеРаботники.ВыбратьСтроку("Выберите работника:");
			Иначе
				ТекущаяСтрока = ВсеРаботники.Найти(Сотрудник,"Сотрудник");
	        КонецЕсли;
			
			Если ТекущаяСтрока <> Неопределено Тогда
				
				Отбор = Новый Структура();
				Отбор.Вставить("Сотрудник",ТекущаяСтрока.Сотрудник);
				Строки = ВсеОтпуска.НайтиСтроки(Отбор);
				Если Строки.Количество() = 0 Тогда
				    Возврат;
				КонецЕсли;
				
				Отпуска.Очистить();
				ВидДокументаОтпуск = Перечисления.ВидыДокументаОтпуск.КомпенсацияОтпуска;
				ДнейЧасовКомпенсацииОтпуска = Строки[0].Дней;	
				Сотрудник = ТекущаяСтрока.Сотрудник;
				ПриказОтпускОрганизации = Основание;
				Организация = Основание.Организация;
			Иначе
				мНеСоздаватьДокумент = Истина;
				Возврат;
			КонецЕсли;

			
		ИначеЕсли Основание.ВидОперации = Перечисления.ВидыОперацийОтпускаОрганизаций.КорректировкаОтпуска Тогда	
			Сообщить("Ввод на основании для корректировки не предусмотрен");
			мНеСоздаватьДокумент = Истина;
			Возврат;
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ПроцедурыУправленияПерсоналом.ЗаполнитьФизЛицо(ЭтотОбъект);
	
	Если ПерерассчитываемыйДокумент = Ссылка И НЕ Ссылка.Пустая() Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Документ не может быть исправлением самого себя!",Отказ);
	КонецЕсли;	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ИНИЦИАЛИЗАЦИЯ ОБЪЕКТА 
