////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура Перерассчитать( Сотрудники = Неопределено) Экспорт
	
	// проверим можно ли что-то делать с документом
	ОписаниеПричиныОтказа = "";
	Если ПроведениеРасчетов.ДокументНельзяИзменятьЗаднимЧислом(Ссылка, ОписаниеПричиныОтказа) Тогда
		ВызватьИсключение(ОписаниеПричиныОтказа);
	КонецЕсли;
	
	Если Не ПроведениеРасчетов.НеобходимостьПерерасчета(Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	// получим список перерассчитываемых сорудников
	СотрудникиСписок = Новый СписокЗначений;
	Если Сотрудники = Неопределено Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("парамРегистратор", Ссылка);
		
		ТекстЗапроса = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		Перерасчет.Сотрудник
		|ИЗ РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций.ПерерасчетОсновныхНачислений КАК Перерасчет
		|ГДЕ Перерасчет.ОбъектПерерасчета = &парамРегистратор
		|";
		
		Запрос.Текст = ТекстЗапроса;
		СотрудникиСписок.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Сотрудник"));
		
	Иначе
		
		СотрудникиСписок = Сотрудники;
		
	КонецЕсли;
	
	// если не перерассчитываем никого - возврат
	Если СотрудникиСписок.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	
	// читаем все данные 
	НачатьТранзакцию();
	Прочитать();
	
	Движения.ОсновныеНачисленияРаботниковОрганизаций.Прочитать();
	Движения.ВзаиморасчетыСРаботникамиОрганизаций.Прочитать();
	ЗафиксироватьТранзакцию();
	
	////////////////////////////////////////////////////////////////////////
	// Основные начисления
	Если Истина Тогда
		// перерассчитываем записи начислений
		ПроведениеРасчетов.РассчитатьЗаписиРегистраРасчета("ОсновныеНачисленияРаботниковОрганизаций", 
											Движения.ОсновныеНачисленияРаботниковОрганизаций, 
											Начисления, 
											, 
											СотрудникиСписок);
		Движения.ОсновныеНачисленияРаботниковОрганизаций.Записать(Истина, Истина);
	КонецЕсли;
	
	// записываем документ перед вызовом СформироватьВзаиморасчетыСРаботниками
	Записать();
	
	////////////////////////////////////////////////////////////////////////
	// Взаиморасчеты с работниками
	Если Истина Тогда
		// сначала удалим сведения о взаиморасчетах с работниками
		ВзаиморасчетыСРаботникамиОрганизаций = Движения.ВзаиморасчетыСРаботникамиОрганизаций;
		ПоследнееДвижение = ВзаиморасчетыСРаботникамиОрганизаций.Количество()-1;
		// удалим записи по всем сотрудника полученного списка
		// обходим в обратном порядке
		Для Сч = 0 По ПоследнееДвижение Цикл
			Если СотрудникиСписок.НайтиПоЗначению(ВзаиморасчетыСРаботникамиОрганизаций[ПоследнееДвижение - Сч].Сотрудник) <> Неопределено Тогда
				ВзаиморасчетыСРаботникамиОрганизаций.Удалить(ПоследнееДвижение - Сч);
			КонецЕсли;
		КонецЦикла;
		
		// теперь сформируем начисления к выплате по начислениям документа для перерассчитываемых работников
		////СформироватьВзаиморасчетыСРаботниками( СотрудникиСписок );
		
	КонецЕсли;
	
	Записать();
	
	////////////////////////////////////////////////////////////////////////
	// Удалим записи перерасчета по которым выполнен перерасчет
	НаборЗаписей = РегистрыРасчета.ОсновныеНачисленияРаботниковОрганизаций.Перерасчеты.ПерерасчетОсновныхНачислений.СоздатьНаборЗаписей();
	ПроведениеРасчетов.УдалитьЗаписиПерерасчетаПоКоторымВыполненПерерасчет(Ссылка, НаборЗаписей, СотрудникиСписок);
	НаборЗаписей.Записать();
	
КонецПроцедуры // Перерассчитать()

Функция ЗаполнитьПоПерерассчитываемомуДокументу(ИсходныйДокумент, Знач Сотрудники = Неопределено) Экспорт
	
	ПерерассчитываемыйДокумент = ИсходныйДокумент.Ссылка;
 	ЗаполнитьЗначенияСвойств(ЭтотОбъект,ИсходныйДокумент,,
		"Проведен, Номер, Дата, ПометкаУдаления, ПерерассчитываемыйДокумент, ПериодРегистрации, Комментарий, Ответственный"); // кроме указанных
	

	Если Сотрудники = "ПоДаннымПерерасчета" Тогда
		// получим список Сотрудников для перерасчета
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ПерерассчитываемыйДокумент", ПерерассчитываемыйДокумент);
		Запрос.Текст = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Перерасчет.Сотрудник
		|ИЗ РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций.ПерерасчетОсновныхНачислений КАК Перерасчет
		|ГДЕ Перерасчет.ОбъектПерерасчета = &ПерерассчитываемыйДокумент
		|";
		
		Результат = Запрос.Выполнить().Выгрузить();
		
		СотрудникиПерерасчета = Результат.ВыгрузитьКолонку("Сотрудник");
		
	ИначеЕсли Сотрудники <> Неопределено Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Сотрудники", Сотрудники);
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СотрудникиОрганизаций.Ссылка КАК Сотрудник
		|ИЗ
		|	Справочник.СотрудникиОрганизаций КАК СотрудникиОрганизаций
		|ГДЕ
		|	СотрудникиОрганизаций.Ссылка В(&Сотрудники)";
		
		Результат = Запрос.Выполнить().Выгрузить();
		
		СотрудникиПерерасчета = Результат.ВыгрузитьКолонку("Сотрудник");
		
	Иначе
		// получим список Сотрудников для перерасчета
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ПерерассчитываемыйДокумент", ПерерассчитываемыйДокумент);
		Запрос.Текст = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Перерасчет.Сотрудник
		|ИЗ Документ.ОплатаСверхурочныхЧасов.Начисления КАК Перерасчет
		|ГДЕ Перерасчет.Ссылка = &ПерерассчитываемыйДокумент
		|";
		
		Результат = Запрос.Выполнить().Выгрузить();
		
		СотрудникиПерерасчета = Результат.ВыгрузитьКолонку("Сотрудник");
		
	КонецЕсли;	
	
	Если СотрудникиПерерасчета.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ПерерассчитываемыйДокумент", ПерерассчитываемыйДокумент);
	Запрос.УстановитьПараметр("СотрудникиПерерасчета", СотрудникиПерерасчета);
		// ТЧ Начисления
	Если Истина Тогда
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	Начисления.Сотрудник,
		|	Начисления.Физлицо,
		|	Начисления.ПодразделениеОрганизации,
		|	Начисления.ДатаВыхода,
		|	Начисления.Размер,
		|	-Начисления.Результат КАК Результат,
		|	-Начисления.ЧасовДвойных КАК ЧасовДвойных,
		|	-Начисления.ЧасовОдинарных КАК ЧасовОдинарных,
		|	ИСТИНА КАК Сторно,
		|	Начисления.Ссылка КАК СторнируемыйДокумент,
		|	Начисления.НомерСтроки КАК НомерСтроки
		|ИЗ
		|	Документ.ОплатаСверхурочныхЧасов.Начисления КАК Начисления
		|ГДЕ
		|	Начисления.Ссылка = &ПерерассчитываемыйДокумент
		|	И (НЕ Начисления.Сторно)
		|	И Начисления.Сотрудник В(&СотрудникиПерерасчета)
		|	И Начисления.Ссылка.Проведен
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Начисления.Сотрудник,
		|	Начисления.Физлицо,
		|	Начисления.ПодразделениеОрганизации,
		|	Начисления.ДатаВыхода,
		|	Начисления.Размер,
		|	0,
		|	0,
		|	0,
		|	ЛОЖЬ,
		|	NULL,
		|	Начисления.НомерСтроки
		|ИЗ
		|	Документ.ОплатаСверхурочныхЧасов.Начисления КАК Начисления
		|ГДЕ
		|	Начисления.Ссылка = &ПерерассчитываемыйДокумент
		|	И (НЕ Начисления.Сторно)
		|	И Начисления.Сотрудник В(&СотрудникиПерерасчета)
		|	И Начисления.Ссылка.Проведен
		|УПОРЯДОЧИТЬ ПО
		|	Сторно УБЫВ,
		|	НомерСтроки";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(Начисления.Добавить(), Выборка);
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Истина;
	
	Записать();
	
КонецФункции // ЗаполнитьПоПерерассчитываемомуДокументу()

Функция Автозаполнение(ПостроительЗапроса,Реквизиты) Экспорт
	
	ИсходныйТекстПостроителя = ПостроительЗапроса.Текст;
	ТекстЗапросаПоСпискуРаботников = СтрЗаменить(ИсходныйТекстПостроителя,"РАЗРЕШЕННЫЕ","");
	ТекстЗапросаПоСпискуРаботников = Лев(ТекстЗапросаПоСпискуРаботников, Найти(ТекстЗапросаПоСпискуРаботников,"УПОРЯДОЧИТЬ") - 1);
	
	ПостроительЗапроса.Параметры.Вставить("РегламентВалюта", 		глЗначениеПеременной("ВалютаРегламентированногоУчета"));
	ПостроительЗапроса.Параметры.Вставить("ПустаяВалюта", 		Справочники.Валюты.ПустаяСсылка());
	ПостроительЗапроса.Параметры.Вставить("ДатаВалютногоУчета", 	ПериодРегистрации);
	ПостроительЗапроса.Параметры.Вставить("ДатаНачала", 	ПериодРегистрации);
	ПостроительЗапроса.Параметры.Вставить("ГоловнаяОрганизация", 	ОбщегоНазначения.ГоловнаяОрганизация(Организация));
	ПостроительЗапроса.Параметры.Вставить("РабочийДень",			Перечисления.ВидыДнейПроизводственногоКалендаря.Рабочий);
	ПостроительЗапроса.Параметры.Вставить("ПредпраздничныйДень",	Перечисления.ВидыДнейПроизводственногоКалендаря.Предпраздничный);
	ПостроительЗапроса.Параметры.Вставить("НачальнаяДата", '00010101');
	ПостроительЗапроса.Параметры.Вставить("Год",Год(Реквизиты.ДатаВыхода));
	ПостроительЗапроса.Параметры.Вставить("ДатаВыхода",Реквизиты.ДатаВыхода);
	ПостроительЗапроса.Параметры.Вставить("ЧасовОдинарных",Реквизиты.ЧасовОдинарных);
	ПостроительЗапроса.Параметры.Вставить("ЧасовДвойных",Реквизиты.ЧасовДвойных);
	РасчетыПоЧасам = Новый Массив(2);
	РасчетыПоЧасам[0] = Перечисления.СпособыРасчетаОплатыТруда.ПоЧасовойТарифнойСтавке;
	РасчетыПоЧасам[1] = Перечисления.СпособыРасчетаОплатыТруда.СдельныйЗаработок;
	ПостроительЗапроса.Параметры.Вставить("РасчетыПоЧасам",РасчетыПоЧасам);
	МесячныеСтавки = Новый Массив(2);
	МесячныеСтавки[0] = Перечисления.СпособыРасчетаОплатыТруда.ПоМесячнойТарифнойСтавкеПоДням;
	МесячныеСтавки[1] = Перечисления.СпособыРасчетаОплатыТруда.ПоМесячнойТарифнойСтавкеПоЧасам;
	ПостроительЗапроса.Параметры.Вставить("МесячныеСтавки",МесячныеСтавки);
	ПостроительЗапроса.Параметры.Вставить("ПоДневнойСтавке",Перечисления.СпособыРасчетаОплатыТруда.ПоДневнойТарифнойСтавке);
	
	ОсновнойТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	РаботникиИНачисления.Сотрудник,
	|	РаботникиИНачисления.Сотрудник.Физлицо КАК Физлицо,
	|	РаботникиИНачисления.ПодразделениеОрганизации,
	|	&ДатаВыхода КАК ДатаВыхода,
	|	&ЧасовОдинарных КАК ЧасовОдинарных,
	|	&ЧасовДвойных КАК ЧасовДвойных,
	|	ВЫБОР
	|		КОГДА РаботникиИНачисления.СпособРасчета В (&РасчетыПоЧасам)
	|			ТОГДА РаботникиИНачисления.ОкладТариф
	|		КОГДА РаботникиИНачисления.СпособРасчета В (&МесячныеСтавки)
	|			ТОГДА ВЫБОР
	|					КОГДА ДанныеКалендаря.НормаЧасовЗаМесяц = 0
	|						ТОГДА 0
	|					ИНАЧЕ РаботникиИНачисления.ОкладТариф / ДанныеКалендаря.НормаЧасовЗаМесяц
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Размер,
	|	(&ЧасовДвойных * 2 + &ЧасовОдинарных) * ВЫБОР
	|		КОГДА РаботникиИНачисления.СпособРасчета В (&РасчетыПоЧасам)
	|			ТОГДА РаботникиИНачисления.ОкладТариф
	|		КОГДА РаботникиИНачисления.СпособРасчета В (&МесячныеСтавки)
	|			ТОГДА ВЫБОР
	|					КОГДА ДанныеКалендаря.НормаЧасовЗаМесяц = 0
	|						ТОГДА 0
	|					ИНАЧЕ РаботникиИНачисления.ОкладТариф / ДанныеКалендаря.НормаЧасовЗаМесяц
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Результат
	|ИЗ
	|	(ВЫБРАТЬ
	|		СписокРаботников.Сотрудник,
	|		СписокРаботников.Подразделение КАК ПодразделениеОрганизации,
	|		СписокРаботников.ГрафикРаботы,
	|		ВЫБОР
	|			КОГДА ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.ВидРасчета.ТребуетВводаТарифногоРазряда
	|				ТОГДА РазмерТарифныхСтавокСрезПоследних.Размер
	|				ИНАЧЕ ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.Показатель1
	|		КОНЕЦ * ВЫБОР
	|			КОГДА ВЫБОР
	|						КОГДА ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.ВидРасчета.ТребуетВводаТарифногоРазряда
	|								ТОГДА РазмерТарифныхСтавокСрезПоследних.Валюта
	|						ИНАЧЕ ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.Валюта1
	|				КОНЕЦ В (&РегламентВалюта, &ПустаяВалюта)
	|				ТОГДА 1
	|			ИНАЧЕ ЕСТЬNULL(Валюты.Курс / Валюты.Кратность, 0)
	|		КОНЕЦ КАК ОкладТариф,
	|		ЕСТЬNULL(СписокРаботников.ГрафикРаботы.ДлительностьРабочейНедели, 0) КАК ДлительностьРабочейНедели,
	|		СписокРаботников.ГрафикРаботы.ВидГрафика КАК ВидГрафика,
	|		ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.ВидРасчета.СпособРасчета КАК СпособРасчета,
	|		ЕСТЬNULL(ДанныеПроизводственногоКалендаря.ЧислоРабочихДней, 0) КАК ЧислоРабочихДней,
	|		ЕСТЬNULL(ДанныеПроизводственногоКалендаря.ЧислоПредпраздничныхДней, 0) КАК ЧислоПредпраздничныхДней
	|	ИЗ
	|		(" + ТекстЗапросаПоСпискуРаботников + ") КАК СписокРаботников
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПлановыеНачисленияРаботниковОрганизаций.СрезПоследних(&ДатаВыхода, ) КАК ПлановыеНачисленияРаботниковОрганизацийСрезПоследних
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РазмерТарифныхСтавок.СрезПоследних(&ДатаВыхода, ) КАК РазмерТарифныхСтавокСрезПоследних
	|				ПО (ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.ТарифныйРазряд1 = РазмерТарифныхСтавокСрезПоследних.ТарифныйРазряд)
	|			ПО СписокРаботников.Сотрудник = ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.Сотрудник
	|				И (ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.ВидРасчетаИзмерение.Ссылка ЕСТЬ NULL )
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалютДляРасчетовСПерсоналом КАК Валюты
	|			ПО (Валюты.Период = &ДатаВалютногоУчета)
	|				И (Валюты.Валюта = ВЫБОР
	|							КОГДА ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.ВидРасчета.ТребуетВводаТарифногоРазряда
	|								ТОГДА РазмерТарифныхСтавокСрезПоследних.Валюта
	|							ИНАЧЕ ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.Валюта1
	|				КОНЕЦ)
	|			ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|				СУММА(ВЫБОР
	|						КОГДА РегламентированныйПроизводственныйКалендарь.ВидДня = &РабочийДень
	|							ТОГДА 1
	|						ИНАЧЕ 0
	|					КОНЕЦ) КАК ЧислоРабочихДней,
	|				СУММА(ВЫБОР
	|						КОГДА РегламентированныйПроизводственныйКалендарь.ВидДня = &ПредпраздничныйДень
	|							ТОГДА 1
	|						ИНАЧЕ 0
	|					КОНЕЦ) КАК ЧислоПредпраздничныхДней
	|			ИЗ
	|				РегистрСведений.РегламентированныйПроизводственныйКалендарь КАК РегламентированныйПроизводственныйКалендарь
	|			ГДЕ
	|				РегламентированныйПроизводственныйКалендарь.Год = &Год) КАК ДанныеПроизводственногоКалендаря
	|			ПО (ИСТИНА)) КАК РаботникиИНачисления
	|
	|//-----------------------
	|// данные нормы календаря
	|ЛЕВОЕ СОЕДИНЕНИЕ (
	|	ВЫБРАТЬ
	|		ГрафикиРаботы.ГрафикРаботы	КАК ГрафикРаботы,
	|		ГрафикиРаботы.Сотрудник		КАК Сотрудник,
	|		ГрафикиРаботы.Документ		КАК Документ,
	|		СУММА( ГрафикиРаботы.ОсновноеЗначение  )		КАК НормаДнейЗаМесяц,
	|		СУММА( ГрафикиРаботы.ДополнительноеЗначение )	КАК НормаЧасовЗаМесяц
	|	ИЗ	РегистрСведений.ГрафикиРаботыПоВидамВремени КАК ГрафикиРаботы
	|	ГДЕ		НАЧАЛОПЕРИОДА( ГрафикиРаботы.Дата, МЕСЯЦ ) = НАЧАЛОПЕРИОДА(&ДатаНачала, МЕСЯЦ) 
	|		И	ГрафикиРаботы.ВидУчетаВремени = ЗНАЧЕНИЕ(Перечисление.ВидыУчетаВремени.ПоДням) 
	|		И НЕ ГрафикиРаботы.Документ ССЫЛКА Документ.ТабельУчетаРабочегоВремениОрганизации 
	|		И НЕ ГрафикиРаботы.Документ ССЫЛКА Документ.ВводИндивидуальныхГрафиковРаботыОрганизации 
	|	СГРУППИРОВАТЬ ПО
	|		ГрафикиРаботы.ГрафикРаботы,
	|		ГрафикиРаботы.Сотрудник,
	|		ГрафикиРаботы.Документ
	|) КАК ДанныеКалендаря
	|ПО		ВЫБОР	КОГДА РаботникиИНачисления.ГрафикРаботы.СокращенноеРабочееВремя	
	|		     		ТОГДА ДанныеКалендаря.ГрафикРаботы = РаботникиИНачисления.ГрафикРаботы.ГрафикПолногоРабочегоВремени
	|				ИНАЧЕ ДанныеКалендаря.ГрафикРаботы = РаботникиИНачисления.ГрафикРаботы
	|		КОНЕЦ 
	|
	|
	|";
	
	//Основной текст запроса
	ПостроительЗапроса.Текст = ОсновнойТекстЗапроса;
	ПостроительЗапроса.Выполнить();
	Начисления.Загрузить(ПостроительЗапроса.Результат.Выгрузить());
	
КонецФункции // Автозаполнение()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Рассчитывает часовую тарифную ставку по строке выборки
// Формирует запрос для заполнения ТЧ документа по данным перерасчетов
Функция СформироватьЗапросДляПерерасчета(ДокументСсылка, ПериодРегистрацииДокумента, ФизЛица = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ДокументСсылка",			ДокументСсылка);
	Запрос.УстановитьПараметр("РегламентВалюта",		глЗначениеПеременной("ВалютаРегламентированногоУчета"));
	Запрос.УстановитьПараметр("ДатаВалютногоУчета",		ПериодРегистрацииДокумента);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация",	ОбщегоНазначения.ГоловнаяОрганизация(Организация));
	Запрос.УстановитьПараметр("РабочийДень",			Перечисления.ВидыДнейПроизводственногоКалендаря.Рабочий);
	Запрос.УстановитьПараметр("ПредпраздничныйДень",	Перечисления.ВидыДнейПроизводственногоКалендаря.Предпраздничный);
	Запрос.УстановитьПараметр("НачальнаяДата",			'00010101');
	
	Если ФизЛица = Неопределено Тогда
		// получим список физлиц для перерасчета
		Запрос.Текст = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Перерасчет.ФизЛицо
		|ИЗ
		|	РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций.ПерерасчетОсновныхНачислений КАК Перерасчет
		|ГДЕ
		|	Перерасчет.ОбъектПерерасчета = &ДокументСсылка
		|";
		
		Запрос.УстановитьПараметр("ФизЛицаПерерасчета", Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ФизЛицо"));
		
	Иначе
		Запрос.УстановитьПараметр("ФизЛицаПерерасчета", ФизЛица);
		
	КонецЕсли;
	
	// выполним запрос к Начисления
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Начисления.НомерСтроки,
	|	Начисления.Сотрудник,
	|	Начисления.Сотрудник.Физлицо КАК Физлицо,
	|	Начисления.ДатаВыхода,
	|	Начисления.Размер,
	|	Начисления.Результат,
	|	Начисления.ПодразделениеОрганизации,
	|	Начисления.ЧасовОдинарных,
	|	Начисления.ЧасовДвойных,
	|	ОсновноеНачисление.Показатель1 * ВЫБОР
	|		КОГДА ОсновноеНачисление.Валюта1 = &РегламентВалюта
	|			ТОГДА 1
	|		ИНАЧЕ ЕСТЬNULL(Валюты.Курс / Валюты.Кратность, 0)
	|	КОНЕЦ КАК ОкладТариф,
	|	ОсновноеНачисление.ВидРасчета.СпособРасчета КАК СпособРасчета,
	|	РаботникиОрганизации.ПодразделениеОрганизации КАК НовоеПодразделениеОрганизации,
	|	РаботникиОрганизации.ГрафикРаботы КАК ГрафикРаботы,
 	|	РаботникиОрганизации.ГрафикРаботы.ДлительностьРабочейНедели КАК ДлительностьРабочейНедели,
 	|	РаботникиОрганизации.ГрафикРаботы.ВидГрафика КАК ВидГрафика,
	|	ДанныеПроизводственногоКалендаря.ЧислоРабочихДней,
	|	ДанныеПроизводственногоКалендаря.ЧислоПредпраздничныхДней
	|ИЗ
	|	Документ.ОплатаСверхурочныхЧасов.Начисления КАК Начисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ОплатаСверхурочныхЧасов.НомерСтроки КАК НомерСтроки,
	|			РаботникиОрганизации.Сотрудник КАК Сотрудник,
	|			МАКСИМУМ(РаботникиОрганизации.Период) КАК Период
	|		ИЗ
	|			Документ.ОплатаСверхурочныхЧасов.Начисления КАК ОплатаСверхурочныхЧасов
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизации
	|				ПО ОплатаСверхурочныхЧасов.Сотрудник = РаботникиОрганизации.Сотрудник
	|					И РаботникиОрганизации.Период <= ОплатаСверхурочныхЧасов.ДатаВыхода
	|		ГДЕ
	|			ОплатаСверхурочныхЧасов.Ссылка = &ДокументСсылка
	|			И (НЕ ОплатаСверхурочныхЧасов.Сторно)
	|			И ОплатаСверхурочныхЧасов.Сотрудник.Физлицо В(&ФизЛицаПерерасчета)
	|			И ОплатаСверхурочныхЧасов.Ссылка.Проведен
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ОплатаСверхурочныхЧасов.НомерСтроки,
	|			РаботникиОрганизации.Сотрудник) КАК ДатыПоследнихНазначений
	|		ПО Начисления.НомерСтроки = ДатыПоследнихНазначений.НомерСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизации
	|		ПО ДатыПоследнихНазначений.Сотрудник = РаботникиОрганизации.Сотрудник
	|			И ДатыПоследнихНазначений.Период = РаботникиОрганизации.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ОплатаСверхурочныхЧасов.НомерСтроки КАК НомерСтроки,
	|			ПлановыеНачисления.Сотрудник КАК Сотрудник,
	|			МАКСИМУМ(ПлановыеНачисления.Период) КАК Период
	|		ИЗ
	|			Документ.ОплатаСверхурочныхЧасов.Начисления КАК ОплатаСверхурочныхЧасов
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПлановыеНачисленияРаботниковОрганизаций КАК ПлановыеНачисления
	|				ПО ОплатаСверхурочныхЧасов.Сотрудник = ПлановыеНачисления.Сотрудник
	|					И (ПлановыеНачисления.ВидРасчетаИзмерение.Ссылка ЕСТЬ NULL )
	|					И ПлановыеНачисления.Период <= ОплатаСверхурочныхЧасов.ДатаВыхода
	|		ГДЕ
	|			ОплатаСверхурочныхЧасов.Ссылка = &ДокументСсылка
	|			И (НЕ ОплатаСверхурочныхЧасов.Сторно)
	|			И ОплатаСверхурочныхЧасов.Сотрудник.Физлицо В(&ФизЛицаПерерасчета)
	|			И ОплатаСверхурочныхЧасов.Ссылка.Проведен
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ОплатаСверхурочныхЧасов.НомерСтроки,
	|			ПлановыеНачисления.Сотрудник) КАК ДатыПоследнихПлановыхНачислений
	|		ПО Начисления.НомерСтроки = ДатыПоследнихПлановыхНачислений.НомерСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПлановыеНачисленияРаботниковОрганизаций КАК ОсновноеНачисление
	|		ПО ДатыПоследнихПлановыхНачислений.Сотрудник = ОсновноеНачисление.Сотрудник
	|			И (ОсновноеНачисление.ВидРасчетаИзмерение.Ссылка ЕСТЬ NULL )
	|			И ДатыПоследнихПлановыхНачислений.Период = ОсновноеНачисление.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалютДляРасчетовСПерсоналом КАК Валюты
	|		ПО Валюты.Валюта = ОсновноеНачисление.Валюта1
	|			И (Валюты.Период = &ДатаВалютногоУчета)
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ГодаОплаты.ГодВыхода КАК ГодВыхода,
	|			СУММА(ВЫБОР
	|					КОГДА РегламентированныйПроизводственныйКалендарь.ВидДня = &РабочийДень
	|						ТОГДА 1
	|					ИНАЧЕ 0
	|				КОНЕЦ) КАК ЧислоРабочихДней,
	|			СУММА(ВЫБОР
	|					КОГДА РегламентированныйПроизводственныйКалендарь.ВидДня = &ПредпраздничныйДень
	|						ТОГДА 1
	|					ИНАЧЕ 0
	|				КОНЕЦ) КАК ЧислоПредпраздничныхДней
	|		ИЗ
	|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				ГОД(ОплатаСверхурочныхЧасов.ДатаВыхода) КАК ГодВыхода
	|			ИЗ
	|				Документ.ОплатаСверхурочныхЧасов.Начисления КАК ОплатаСверхурочныхЧасов
	|			ГДЕ
	|				ОплатаСверхурочныхЧасов.Ссылка = &ДокументСсылка) КАК ГодаОплаты
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РегламентированныйПроизводственныйКалендарь КАК РегламентированныйПроизводственныйКалендарь
	|				ПО РегламентированныйПроизводственныйКалендарь.Год = ГодаОплаты.ГодВыхода
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ГодаОплаты.ГодВыхода) КАК ДанныеПроизводственногоКалендаря
	|		ПО (ГОД(Начисления.ДатаВыхода) = ДанныеПроизводственногоКалендаря.ГодВыхода)
	|ГДЕ
	|	Начисления.Ссылка = &ДокументСсылка
	|	И (НЕ Начисления.Сторно)
	|	И Начисления.Сотрудник.Физлицо В(&ФизЛицаПерерасчета)
	|	И Начисления.Ссылка.Проведен";
	
	Возврат Запрос.Выполнить();
	
КонецФункции  // СформироватьЗапросДляПерерасчета()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

// Формирует запрос по шапке документа
//
// Параметры: 
//	Режим - режим проведения
//
// Возвращаемое значение:
//	Результат запроса
//
Функция СформироватьЗапросПоШапке()

	Запрос = Новый Запрос;

	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка" , Ссылка);
	Запрос.УстановитьПараметр("парамПустаяОрганизация", Справочники.Организации.ПустаяСсылка());

	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОплатаСверхурочныхЧасов.Дата,
	|	ОплатаСверхурочныхЧасов.Организация,
	|	ОплатаСверхурочныхЧасов.Организация КАК ГоловнаяОрганизация,
	|	ОплатаСверхурочныхЧасов.Организация КАК ОбособленноеПодразделение,
	|	ОплатаСверхурочныхЧасов.ПерерассчитываемыйДокумент КАК ПерерассчитываемыйДокумент,
	|	ОплатаСверхурочныхЧасов.ПерерассчитываемыйДокумент.ПериодРегистрации КАК ПериодПерерасчета,
	|	ОплатаСверхурочныхЧасов.ПерерассчитываемыйДокумент.Организация КАК ОрганизацияПерерасчета,
	|	ОплатаСверхурочныхЧасов.ПериодРегистрации,
	|	ОплатаСверхурочныхЧасов.Ответственный,
	|	ОплатаСверхурочныхЧасов.Ссылка
	|ИЗ
	|	Документ.ОплатаСверхурочныхЧасов КАК ОплатаСверхурочныхЧасов
	|ГДЕ
	|	ОплатаСверхурочныхЧасов.Ссылка = &ДокументСсылка";

	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросПоШапке()

// Формирует запрос по таблице "Начисления" документа
//
// Параметры: 
//	Режим		- режим проведения.
//
// Возвращаемое значение:
//	Результат запроса.
//
Функция СформироватьЗапросПоНачисления(ВыборкаПоШапкеДокумента)

	Запрос = Новый Запрос;

	ГоловнаяОрганизация = ВыборкаПоШапкеДокумента.ГоловнаяОрганизация;
	
	СписокСтруктурныхПодразделений = ОбщегоНазначения.ПолучитьСписокОбособленныхПодразделенийОрганизации(ГоловнаяОрганизация);
	СписокСтруктурныхПодразделений.Добавить(ГоловнаяОрганизация);
	Запрос.УстановитьПараметр("СписокСтруктурныхПодразделений",	СписокСтруктурныхПодразделений);
	
	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка",					Ссылка);
	Запрос.УстановитьПараметр("ПустаяДата",						'00010101');
	Запрос.УстановитьПараметр("ГоловнаяОрганизация",			ГоловнаяОрганизация);
	Запрос.УстановитьПараметр("ВидРасчета",						ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.ОплатаСверхурочных);
	
	// Описание текста запроса:
	// 1. Выборка "ТЧНачисления":
	//		Выбираются строки документа.
	// 2. Выборка "ПересекающиесяСтроки":
	//		Среди остальных строк документа ищем строки, имеющие пересекающийся период действия
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТЧНачисления.НомерСтроки КАК НомерСтроки,
	|	ТЧНачисления.Сотрудник КАК Назначение,
	|	ТЧНачисления.Сотрудник.Наименование,
	|	ТЧНачисления.Сотрудник.Физлицо КАК Физлицо,
	|	ВЫБОР КОГДА ТЧНачисления.Сотрудник.ВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятостиВОрганизации.ВнутреннееСовместительство) 
	|         ТОГДА ТЧНачисления.Сотрудник.ОсновноеНазначение
	|         ИНАЧЕ ТЧНачисления.Сотрудник
	|	КОНЕЦ КАК Сотрудник,
	|	ТЧНачисления.ДатаВыхода КАК ДатаВыхода,
	|	ТЧНачисления.Сторно КАК Сторно,
	|	ТЧНачисления.ЧасовДвойных + ТЧНачисления.ЧасовОдинарных КАК ОтработаноЧасов,
	|	ОсновныеНачисленияОрганизаций.Ссылка КАК ВидРасчета,
	|	ОсновныеНачисленияОрганизаций.СпособРасчета КАК СпособРасчета,
	|	ТЧНачисления.Размер,
	|	ТЧНачисления.Результат,
	|	ТЧНачисления.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	ВЫБОР
	|		КОГДА ТЧНачисления.Сотрудник.Организация = &ГоловнаяОрганизация
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ОшибкаНеСоответствиеСотрудникаИОрганизации,
	|	ВЫБОР
	|		КОГДА (НЕ ТЧНачисления.ПодразделениеОрганизации.Владелец В (&СписокСтруктурныхПодразделений))
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОшибкаПодразделениеНеПринадлежитОрганизации,
	|	ДатыПоследнихДвиженийРаботников.Период КАК ДатаПоследнегоДвиженияПоРаботнику,
	|	ВЫБОР
	|		КОГДА ГрафикРаботыПоСотруднику.НомерСтроки ЕСТЬ NULL
	|			ТОГДА 
	|				ДанныеПоРаботникуДоНазначения.ГрафикРаботы	
	|		ИНАЧЕ ТЧНачисления.Сотрудник
	|	КОНЕЦ КАК ГрафикРаботы,
	|	ДанныеПоРаботникуДоНазначения.ЗанимаемыхСтавок КАК ЗанимаемыхСтавок,
	|	ПересекающиесяСтроки.КонфликтнаяСтрокаНомер
	|ИЗ
	|	Документ.ОплатаСверхурочныхЧасов.Начисления КАК ТЧНачисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ТЧНачисления.НомерСтроки КАК НомерСтроки,
	|			МАКСИМУМ(РаботникиОрганизации.Период) КАК Период
	|		ИЗ
	|			Документ.ОплатаСверхурочныхЧасов.Начисления КАК ТЧНачисления
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизации
	|				ПО РаботникиОрганизации.Период <= ТЧНачисления.ДатаВыхода
	|					И ТЧНачисления.Сотрудник = РаботникиОрганизации.Сотрудник
	|		ГДЕ
	|			ТЧНачисления.Ссылка = &ДокументСсылка
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ТЧНачисления.НомерСтроки) КАК ДатыПоследнихДвиженийРаботников
	|		ПО ТЧНачисления.НомерСтроки = ДатыПоследнихДвиженийРаботников.НомерСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК ДанныеПоРаботникуДоНазначения
	|		ПО ДанныеПоРаботникуДоНазначения.Период = ДатыПоследнихДвиженийРаботников.Период
	|			И ТЧНачисления.Сотрудник = ДанныеПоРаботникуДоНазначения.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ТЧНачисления.НомерСтроки КАК НомерСтроки,
	|			МИНИМУМ(ТЧНачисления2.НомерСтроки) КАК КонфликтнаяСтрокаНомер
	|		ИЗ
	|			Документ.ОплатаСверхурочныхЧасов.Начисления КАК ТЧНачисления
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОплатаСверхурочныхЧасов.Начисления КАК ТЧНачисления2
	|				ПО (ТЧНачисления2.Ссылка = &ДокументСсылка)
	|					И ТЧНачисления.НомерСтроки > ТЧНачисления2.НомерСтроки
	|					И ТЧНачисления.ДатаВыхода = ТЧНачисления2.ДатаВыхода
	|					И ТЧНачисления.Сотрудник = ТЧНачисления2.Сотрудник
	|		ГДЕ
	|			ТЧНачисления.Ссылка = &ДокументСсылка
	|			И (НЕ ТЧНачисления.Сторно)
	|			И (НЕ ТЧНачисления2.Сторно)
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ТЧНачисления.НомерСтроки) КАК ПересекающиесяСтроки
	|		ПО ТЧНачисления.НомерСтроки = ПересекающиесяСтроки.НомерСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.ОсновныеНачисленияОрганизаций КАК ОсновныеНачисленияОрганизаций
	|		ПО (ОсновныеНачисленияОрганизаций.Ссылка = &ВидРасчета)
	|		
	|       ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			СтрокиНачисления.НомерСтроки Как НомерСтроки
	|       ИЗ
	|			Документ.ОплатаСверхурочныхЧасов.Начисления КАК СтрокиНачисления
	|       	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикиРаботыПоВидамВремени КАК ГрафикиРаботыПоВидамВремени
	|       	ПО ГрафикиРаботыПоВидамВремени.ГрафикРаботы = СтрокиНачисления.Сотрудник
	|				И ГрафикиРаботыПоВидамВремени.Месяц = НАЧАЛОПЕРИОДА(СтрокиНачисления.ДатаВыхода, МЕСЯЦ)
	|       ГДЕ
	|       	СтрокиНачисления.Ссылка = &ДокументСсылка) КАК ГрафикРаботыПоСотруднику
	|		ПО ТЧНачисления.НомерСтроки = ГрафикРаботыПоСотруднику.НомерСтроки
	|		
	|		
	|ГДЕ
	|	ТЧНачисления.Ссылка = &ДокументСсылка";

	Запрос.Текст = ТекстЗапроса;

	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросПоНачисления()

// Проверяет правильность заполнения шапки документа.
// Если какой-то из реквизтов шапки, влияющий на проведение не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса по шапке,
// все проверяемые реквизиты должны быть включены в выборку по шапке.
//
// Параметры: 
//	ВыборкаПоШапкеДокумента	- выборка из результата запроса по шапке документа,
//	Отказ					- флаг отказа в проведении,
//	Заголовок				- Заголовок для сообщений об ошибках проведения.
//
Процедура ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ, Заголовок)

	Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.Организация) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указана организация!", Отказ, Заголовок);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.ПериодРегистрации) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указан период регистрации (месяц)!", Отказ, Заголовок);
	КонецЕсли;

	// соответствие периодов документа и перерассчитываемого документа
	Если ВыборкаПоШапкеДокумента.ПериодПерерасчета <> null 
		и ВыборкаПоШапкеДокумента.ПериодРегистрации <= ВыборкаПоШапкеДокумента.ПериодПерерасчета Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Период документа должен быть больше периода перерассчитываемого документа!", Отказ, Заголовок);
	КонецЕсли;
	
	// соответствие организаций документа и перерассчитываемого документа
	Если ВыборкаПоШапкеДокумента.ОрганизацияПерерасчета <> null 
		и ВыборкаПоШапкеДокумента.ОбособленноеПодразделение <> ВыборкаПоШапкеДокумента.ОрганизацияПерерасчета Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Организация, заданная для документа, должна совпадать с организацией перерассчитываемого документа!", Отказ, Заголовок);
	КонецЕсли;

КонецПроцедуры // ПроверитьЗаполнениеШапки()

// Проверяет правильность заполнения реквизитов в строке ТЧ "РаботникиОрганизации" документа.
// Если какой-то из реквизтов, влияющий на проведение не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса по строке ТЧ документа,
// все проверяемые реквизиты должны быть включены в выборку.
//
// Параметры: 
//	ВыборкаПоШапкеДокумента		- выборка из результата запроса по шапке документа,
//	ВыборкаПоСтрокамДокумента	- спозиционированная на определеной строке выборка 
//								  из результата запроса по товарам документа, 
//	Отказ						- флаг отказа в проведении,
//	Заголовок					- Заголовок для сообщений об ошибках проведения.
//
Процедура ПроверитьЗаполнениеСтрокиРаботникаОрганизации(ВыборкаПоШапкеДокумента, ВыборкаПоСтрокамДокумента, Отказ, Заголовок)
	
	СтрокаНачалаСообщенияОбОшибке = "В строке номер """+ СокрЛП(ВыборкаПоСтрокамДокумента.НомерСтроки) +
	""" табл. части ""Начисления"": ";

	// Сотрудник
	НетСотрудника = НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.Сотрудник);
	Если НетСотрудника Тогда
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не выбран сотрудник!", Отказ, Заголовок);
	КонецЕсли;
	
	// Подразделение
	Если НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ПодразделениеОрганизации) Тогда
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не указано подразделение организации!", Отказ, Заголовок);
	ИначеЕсли ВыборкаПоСтрокамДокумента.ОшибкаПодразделениеНеПринадлежитОрганизации Тогда
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "указано подразделение, принадлежащее другой организации!", Отказ, Заголовок);
	КонецЕсли;
	
	// ДатаВыхода
	НетДатаВыхода = Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ДатаВыхода);
	Если НетДатаВыхода Тогда
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не указана дата!", Отказ, Заголовок);
	КонецЕсли;
	
	Если НетСотрудника ИЛИ НетДатаВыхода Тогда
		Возврат; // Дальше не проверяем
	КонецЕсли;
	
	// График работы
	Если НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ГрафикРаботы) Тогда
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не указан рабочий график!", Отказ, Заголовок);
	КонецЕсли;
	
	// Организация сотрудника должна совпадать с организацией документа
	Если ВыборкаПоСтрокамДокумента.ОшибкаНеСоответствиеСотрудникаИОрганизации Тогда
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "указанный сотрудник оформлен на другую организацию!", Отказ, Заголовок);
	КонецЕсли;
	
	// Проверка: ранее работник должен быть принят на работу
	Если ВыборкаПоСтрокамДокумента.ЗанимаемыхСтавок = NULL Тогда
		СтрокаПродолжениеСообщенияОбОшибке = "на " + Формат(ВыборкаПоСтрокамДокумента.ДатаВыхода, "ДЛФ=DD") + " работник " + ВыборкаПоСтрокамДокумента.СотрудникНаименование + " еще не принят на работу!";
		Сообщить(СтрокаНачалаСообщенияОбОшибке + СтрокаПродолжениеСообщенияОбОшибке);
		
	ИначеЕсли ВыборкаПоСтрокамДокумента.ЗанимаемыхСтавок = 0 Тогда
		СтрокаПродолжениеСообщенияОбОшибке = "на " + Формат(ВыборкаПоСтрокамДокумента.ДатаВыхода, "ДЛФ=DD") + " работник " + ВыборкаПоСтрокамДокумента.СотрудникНаименование + " уже уволен (с " + Формат(ВыборкаПоСтрокамДокумента.ДатаПоследнегоДвиженияПоРаботнику, "ДЛФ=DD") + ")!";
		Сообщить(СтрокаНачалаСообщенияОбОшибке + СтрокаПродолжениеСообщенияОбОшибке);
		
	КонецЕсли;
	
	// Проверка: противоречие другой строке документа
	Если ВыборкаПоСтрокамДокумента.КонфликтнаяСтрокаНомер <> NULL Тогда
		СтрокаСообщениеОбОшибке = "указана повторяющаяся строка (см. строку  № " + ВыборкаПоСтрокамДокумента.КонфликтнаяСтрокаНомер + ")!"; 
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + СтрокаСообщениеОбОшибке, Отказ, Заголовок);
	КонецЕсли;

КонецПроцедуры // ПроверитьЗаполнениеСтрокиРаботникаОрганизации()

// По строке выборок из результатов запроса по документу формируем движения по регистру
//
// Параметры: 
//	ВыборкаПоСтрокамДокумента				- спозиционированная на определеной строке выборка 
//											  из результата запроса к ТЧ документа, 
// Возвращаемое значение:
//	Нет.
//
Процедура ДобавитьСтрокуОсновныхНачислений(ВыборкаПоШапкеДокумента, ВыборкаПоСтрокамДокумента)
	
	Движение = Движения.ОсновныеНачисленияРаботниковОрганизаций.Добавить();
	
	// Свойства

	Движение.ПериодРегистрации			= ВыборкаПоШапкеДокумента.ПериодРегистрации;
	Движение.ПериодДействияНачало		= ВыборкаПоСтрокамДокумента.ДатаВыхода;
	Движение.ПериодДействияКонец		= КонецДня(ВыборкаПоСтрокамДокумента.ДатаВыхода);
	Движение.ВидРасчета					= ВыборкаПоСтрокамДокумента.ВидРасчета;
	Движение.Сторно						= ВыборкаПоСтрокамДокумента.Сторно;
	
	// Измерения
	Движение.Сотрудник					= ВыборкаПоСтрокамДокумента.Сотрудник;
	Движение.Назначение					= ВыборкаПоСтрокамДокумента.Назначение;
	Движение.Организация				= ВыборкаПоШапкеДокумента.ГоловнаяОрганизация;
	
	// Ресурсы
	Движение.Результат					= ВыборкаПоСтрокамДокумента.Результат;
	
	// ресурсы по отработанному времени (только часы)
	Движение.ОтработаноЧасов			= ВыборкаПоСтрокамДокумента.ОтработаноЧасов;
	
	Движение.ОплаченоДнейЧасов			= ВыборкаПоСтрокамДокумента.ОтработаноЧасов;
	
	// Реквизиты
	Движение.Показатель1				= ВыборкаПоСтрокамДокумента.Размер;
	Движение.ПодразделениеОрганизации	= ВыборкаПоСтрокамДокумента.ПодразделениеОрганизации;
	Движение.ГрафикРаботы				= ВыборкаПоСтрокамДокумента.ГрафикРаботы;
	Движение.ВидУчетаВремени			= Перечисления.ВидыУчетаВремени.ПоЧасам;
	
КонецПроцедуры // ДобавитьСтрокуОсновныхНачислений

// По строке выборки результата запроса по документу формируем движения по регистрам
//
// Параметры: 
//	ВыборкаПоШапкеДокумента						- выборка из результата запроса по шапке документа
//
// Возвращаемое значение:
//	Нет.
//
Процедура ДобавитьСтрокуВДвиженияПоРегистрамНакопления(ВыборкаПоШапкеДокумента, ВыборкаПоТЧ)
		
	Движение = Движения.ВзаиморасчетыСРаботникамиОрганизаций.Добавить();
	
	// Свойства
	Движение.Период					= КонецМесяца(ПериодРегистрации);
	Движение.ВидДвижения			= ВидДвиженияНакопления.Приход;
	
	
	// Измерения
	Движение.Организация			= ВыборкаПоШапкеДокумента.Организация;
	Движение.Сотрудник				= ВыборкаПоТЧ.Сотрудник;
	Движение.ПериодВзаиморасчетов	= ПериодРегистрации;
	Движение.СчетУчета				= ВыборкаПоТЧ.ВидРасчета.СчетУчета;
	
	
	// Ресурсы
	Движение.СуммаВзаиморасчетов	= ВыборкаПоТЧ.Результат;
	
	Движение.КодОперации			= Перечисления.КодыОперацийВзаиморасчетыСРаботникамиОрганизаций.Начисления;
	
КонецПроцедуры // ДобавитьСтрокуВДвиженияПоРегистрамНакопления()


///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПроведения(Отказ, Режим)

	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка);
	
	РезультатЗапросаПоШапке = СформироватьЗапросПоШапке();

	// Получим реквизиты шапки из запроса
	ВыборкаПоШапкеДокумента = РезультатЗапросаПоШапке.Выбрать();

	Если ВыборкаПоШапкеДокумента.Следующий() Тогда

		//Надо позвать проверку заполнения реквизитов шапки
		ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ, Заголовок);

		// Движения стоит добавлять, если в проведении еще не отказано (отказ =ложь)
		Если НЕ Отказ Тогда

			// получим реквизиты табличной части
			РезультатЗапросаПоНачислениям = СформироватьЗапросПоНачисления(ВыборкаПоШапкеДокумента);
			ВыборкаПоНачислениям = РезультатЗапросаПоНачислениям.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаПоНачислениям.Следующий() Цикл

				// проверим очередную строку табличной части
				ПроверитьЗаполнениеСтрокиРаботникаОрганизации(ВыборкаПоШапкеДокумента, ВыборкаПоНачислениям, Отказ, Заголовок);
				Если НЕ Отказ Тогда

					// Заполним записи в наборах записей регистров
					ДобавитьСтрокуОсновныхНачислений(ВыборкаПоШапкеДокумента, ВыборкаПоНачислениям);
					ДобавитьСтрокуВДвиженияПоРегистрамНакопления(ВыборкаПоШапкеДокумента, ВыборкаПоНачислениям);
						
				КонецЕсли;

			КонецЦикла;
			
			Если НЕ ПерерассчитываемыйДокумент.Пустая() Тогда
				// выполним удаление перерасчетов перерассчитываемого документа
				НаборЗаписейПерерасчета = РегистрыРасчета.ОсновныеНачисленияРаботниковОрганизаций.Перерасчеты.ПерерасчетОсновныхНачислений.СоздатьНаборЗаписей();
				НаборЗаписейПерерасчета.Отбор.ОбъектПерерасчета.Значение = ПерерассчитываемыйДокумент;
				НаборЗаписейПерерасчета.Записать();
			КонецЕсли;

		КонецЕсли;

	КонецЕсли;

КонецПроцедуры // ОбработкаПроведения()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	КраткийСоставДокумента = ПроцедурыУправленияПерсоналом.ЗаполнитьКраткийСоставДокумента(Начисления);
	ПроцедурыУправленияПерсоналом.ЗаполнитьФизЛицоПоТЧ(Начисления);
	
	Если ПерерассчитываемыйДокумент = Ссылка И НЕ Ссылка.Пустая() Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Документ не может быть исправлением самого себя!",Отказ);
	КонецЕсли;	
	
КонецПроцедуры // ПередЗаписью()

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
