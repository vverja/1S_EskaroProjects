// Функция формирования печатной формы документа "УслугиСобственнымПодразделениям"
//
Функция ПечатьУслугиСобственнымПодразделениям(МассивОбъектов, ОбъектыПечати)
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_УслугиСобственнымПодразделениям_УслугиСобственнымПодразделениям";
	
	Макет  = ПолучитьМакет("УслугиСобственнымПодразделениям");
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	УслугиСобственнымПодразделениям.Ссылка,
	|	УслугиСобственнымПодразделениям.Представление,
	|	УслугиСобственнымПодразделениям.Организация,
	|	УслугиСобственнымПодразделениям.Организация.Представление КАК ПечОрганизация,
	|	УслугиСобственнымПодразделениям.Номенклатура.Представление КАК ПечУслуга,
	|	ВЫБОР
	|		КОГДА &ТипУчета = ""Упр""
	|			ТОГДА УслугиСобственнымПодразделениям.Подразделение
	|		ИНАЧЕ УслугиСобственнымПодразделениям.ПодразделениеОрганизации
	|	КОНЕЦ КАК Подразделение,
	|	ВЫБОР
	|		КОГДА &ТипУчета = ""Упр""
	|			ТОГДА ПРЕДСТАВЛЕНИЕ(УслугиСобственнымПодразделениям.Подразделение)
	|		ИНАЧЕ ПРЕДСТАВЛЕНИЕ(УслугиСобственнымПодразделениям.ПодразделениеОрганизации)
	|	КОНЕЦ КАК ПечПодразделение,
	|	УслугиСобственнымПодразделениям.НоменклатурнаяГруппа КАК НомГруппа,
	|	ПРЕДСТАВЛЕНИЕ(УслугиСобственнымПодразделениям.НоменклатурнаяГруппа) КАК ПечНомГруппа,
	|	ПРЕДСТАВЛЕНИЕ(УслугиСобственнымПодразделениям.Номенклатура.БазоваяЕдиницаИзмерения) КАК ЕдиницаИзмерения,
	|	УслугиСобственнымПодразделениям.Затраты.(
	|		НомерСтроки КАК НомерСтроки,
	|		ВЫБОР
	|			КОГДА &ТипУчета = ""Упр""
	|				ТОГДА УслугиСобственнымПодразделениям.Затраты.ПодразделениеПолучатель
	|			ИНАЧЕ УслугиСобственнымПодразделениям.Затраты.ПодразделениеОрганизацииПолучатель
	|		КОНЕЦ КАК ПодразделениеПолучатель,
	|		ВЫБОР
	|			КОГДА &ТипУчета = ""Упр""
	|				ТОГДА ПРЕДСТАВЛЕНИЕ(УслугиСобственнымПодразделениям.Затраты.ПодразделениеПолучатель)
	|			ИНАЧЕ ПРЕДСТАВЛЕНИЕ(УслугиСобственнымПодразделениям.Затраты.ПодразделениеОрганизацииПолучатель)
	|		КОНЕЦ КАК ПечПодразделениеПолучатель,
	|		Количество КАК ПечКоличество
	|	)
	|ИЗ
	|	Документ.УслугиСобственнымПодразделениям КАК УслугиСобственнымПодразделениям
	|ГДЕ
	|	УслугиСобственнымПодразделениям.Ссылка = &ТекДок
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	ПервыйДокумент = Истина;
	
	Для Каждого Ссылка Из МассивОбъектов Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабДок.ВысотаТаблицы + 1;
		
		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапроса;
	
		ТипУчета = ?(Ссылка.ОтражатьВУправленческомУчете, "Упр","Бух");
		Запрос.УстановитьПараметр("ТекДок",   Ссылка);
		Запрос.УстановитьПараметр("ТипУчета", ТипУчета);
	
		РезультатЗапроса = Запрос.Выполнить();
		Шапка = РезультатЗапроса.Выбрать();
		Шапка.Следующий();
		
		// Вывод заголовка
		Область = Макет.ПолучитьОбласть("Заголовок");
		Область.Параметры.Заполнить(Шапка);
		Область.Параметры.Заголовок 				= ОбщегоНазначения.СформироватьЗаголовокДокумента(Ссылка.ПолучитьОбъект());
		ТабДок.Вывести(Область);
		
		Область 		= Макет.ПолучитьОбласть("Шапка");
		ТабДок.Вывести(Область);
		Область 		= Макет.ПолучитьОбласть("Строка");
		ТабЧасть 		= Шапка.Затраты.Выбрать();
		КоличествоИтого = 0;
		
		Пока ТабЧасть.Следующий() Цикл
			
			Область.Параметры.Заполнить(ТабЧасть);
			Область.Параметры.ПечНомер = ТабЧасть.НомерСтроки;
			
			ТабДок.Вывести(Область);
			КоличествоИтого = КоличествоИтого + ТабЧасть.ПечКоличество;
			
		КонецЦикла;
		
		Область = Макет.ПолучитьОбласть("Подвал");
		Область.Параметры.ПечКоличество    = КоличествоИтого;
		Область.Параметры.ЕдиницаИзмерения = ?(ЗначениеЗаполнено(Шапка.ЕдиницаИзмерения),", "+Шапка.ЕдиницаИзмерения,"");
		ТабДок.Вывести(Область);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДок, НомерСтрокиНачало, ОбъектыПечати, Ссылка);
		
	КонецЦикла; 
	
	Возврат ТабДок;
	
КонецФункции // ПечатьУслугиСобственнымПодразделениям()
	
// Сформировать печатные формы объектов
//
// ВХОДЯЩИЕ:
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
//   ОшибкиПечати          - Список значений  - Ошибки печати  (значение - ссылка на объект, представление - текст ошибки)
//   ОбъектыПечати         - Список значений  - Объекты печати (значение - ссылка на объект, представление - имя области в которой был выведен объект)
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "УслугиСобственнымПодразделениям") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "УслугиСобственнымПодразделениям", "Услуги собственным подразделениям", ПечатьУслугиСобственнымПодразделениям(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
	
КонецПроцедуры

// В функции описано, какие данные следует сохранять в шаблоне
Функция СтруктураДополнительныхДанныхФормы() Экспорт
	
	Возврат ХранилищаНастроек.ДанныеФорм.СформироватьСтруктуруДополнительныхДанных("Затраты");
	
КонецФункции
