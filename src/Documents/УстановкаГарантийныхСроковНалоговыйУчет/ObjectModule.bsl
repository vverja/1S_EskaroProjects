// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

// Проверяет правильность заполнения шапки документа.
// Если какой-то из реквизтов шапки, влияющий на проведение не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
// Проверяется также правильность заполнения реквизитов ссылочных полей документа.
// Проверка выполняется по объекту и по выборке из результата запроса по шапке.
//
// Параметры: 
//  СтруктураШапкиДокумента - выборка из результата запроса по шапке документа,
//  Отказ                   - флаг отказа в проведении,
//  Заголовок               - строка, заголовок сообщения об ошибке проведения.
//
Процедура ПроверитьЗаполнениеШапки(СтруктураШапкиДокумента, Отказ, Заголовок)

	// Укажем, что надо проверить:
	СтруктураОбязательныхПолей = Новый Структура("Организация");

	// Вызовем общую процедуру проверки.
	ЗаполнениеДокументов.ПроверитьЗаполнениеШапкиДокумента(ЭтотОбъект, СтруктураОбязательныхПолей, Отказ, Заголовок);

КонецПроцедуры // ПроверитьЗаполнениеШапки()

// Выполняет движения по регистрам 
//
Процедура ДвиженияПоРегистрам(СтруктураШапкиДокумента, Отказ, Заголовок)
	
	ГарантийныеСрокиНУ = Движения.ГарантийныеСрокиНалоговыйУчет;

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка",      Ссылка);
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	УстановкаГарантийныхСроков.НоменклатурнаяГруппа,
	|	УстановкаГарантийныхСроков.Срок
	|ИЗ
	|	Документ.УстановкаГарантийныхСроковНалоговыйУчет.ГарантийныеСроки КАК УстановкаГарантийныхСроков
	|ГДЕ
	|	УстановкаГарантийныхСроков.Ссылка = &Ссылка
	|";
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();

	Для Каждого Строка Из РезультатЗапроса Цикл

		НоваяСтрока = ГарантийныеСрокиНУ.Добавить();

		НоваяСтрока.Период        			= СтруктураШапкиДокумента.Дата;
		НоваяСтрока.Организация				= СтруктураШапкиДокумента.Организация;
		НоваяСтрока.НоменклатурнаяГруппа	= Строка.НоменклатурнаяГруппа;
		НоваяСтрока.Срок 					= Строка.Срок;

	КонецЦикла;

КонецПроцедуры // ДвиженияПоРегистрам()

// Процедура - обработчик события "ОбработкаПроведения"
//
Процедура ОбработкаПроведения(Отказ, Режим)

 	// Сформируем структуру реквизитов шапки документа
	СтруктураШапкиДокумента = ОбщегоНазначения.СформироватьСтруктуруШапкиДокумента(ЭтотОбъект);
	
	Заголовок = ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка);
	ПроверитьЗаполнениеШапки(СтруктураШапкиДокумента, Отказ, Заголовок);
	
	Если НЕ Отказ Тогда
	
		ДвиженияПоРегистрам(СтруктураШапкиДокумента, Отказ, Заголовок);
	
	КонецЕсли; 
                                                                              	
КонецПроцедуры // ОбработкаПроведения()


