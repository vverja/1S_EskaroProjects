
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА

// Процедура выводит на печать текущий документ
//
Процедура ПечатьЗадания() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТекущийДокумент", ЭтотОбъект.Ссылка);

	Запрос.Текст =
	"ВЫБРАТЬ
	|	кпкЗаданиеАгента.Номер,
	|	кпкЗаданиеАгента.Дата,
	|	кпкЗаданиеАгента.Организация,
	|	кпкЗаданиеАгента.Контрагент,
	|	кпкЗаданиеАгента.кпкТорговаяТочка,
	|	кпкЗаданиеАгента.Комментарий,
	|	кпкЗаданиеАгента.Ответственный,
	|	кпкЗаданиеАгента.Агент,
	|	кпкЗаданиеАгента.ПереченьЗаданий.(
	|		Ссылка,
	|		НомерСтроки,
	|		Родитель,
	|		Задание,
	|		Комментарий,
	|		Обязательная,
	|		Результат
	|	)
	|ИЗ
	|	Документ.кпкЗаданиеАгента КАК кпкЗаданиеАгента
	|ГДЕ
	|	кпкЗаданиеАгента.Ссылка = &ТекущийДокумент";

	Шапка = Запрос.Выполнить().Выбрать();
	Шапка.Следующий();
	
	ТабДокумент   = Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ЗаданиеАгента";
	Макет         = ПолучитьМакет("Макет");

	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	
	НомерДок = ОбщегоНазначения.ПолучитьНомерНаПечать(Шапка);
	
	ОбластьМакета.Параметры.ТекстЗаголовка = "Задание агента №" + НомерДок + " от " + Формат(Шапка.Дата, "ДЛФ=DD");
	ТабДокумент.Вывести(ОбластьМакета);

	ОбластьМакета = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьМакета.Параметры.Заполнить(Шапка);
		
	ТабДокумент.Вывести(ОбластьМакета);
	
	ТабДокумент.НачатьАвтогруппировкуСтрок();
 
	ВыборкаЗаданий = Шапка.ПереченьЗаданий.Выбрать();
	
	ОбластьСтроки 	    = Макет.ПолучитьОбласть("СтрокаТаблицы");	
	ОбластьСтрокиГруппа = Макет.ПолучитьОбласть("СтрокаТаблицыГруппа");	
	
	ТекГруппа = "";
	Пока ВыборкаЗаданий.Следующий() Цикл
		
		Если ВыборкаЗаданий.Родитель <> ТекГруппа Тогда
			ОбластьСтрокиГруппа.Параметры.Заполнить(ВыборкаЗаданий);
			ТабДокумент.Вывести(ОбластьСтрокиГруппа, 1);
			
			ТекГруппа  = ВыборкаЗаданий.Задание;							
		Иначе
			ОбластьСтроки.Параметры.Заполнить(ВыборкаЗаданий);
			ТабДокумент.Вывести(ОбластьСтроки, 2);
		КонецЕсли;
		
	КонецЦикла;

	ТабДокумент.ЗакончитьАвтогруппировкуСтрок();

	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	ОбластьМакета.Параметры.Заполнить(Шапка);
	ТабДокумент.Вывести(ОбластьМакета);
	
	ТабДокумент.ТолькоПросмотр = Истина;
	ТабДокумент.АвтоМасштаб = Истина;	
	ТабДокумент.Показать();    

КонецПроцедуры //ПечатьЗадания()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Процедура - обработчик события "ОбработкаЗаполнения".
//
Процедура ОбработкаЗаполнения(Основание)
	
	Если ТипЗнч(Основание) = Тип("Структура") Тогда
		ДокОснование = Основание.Док;
		
		ЗаполнениеДокументов.ЗаполнитьШапкуДокументаПоОснованию(ЭтотОбъект, ДокОснование);
		
		Организация      = ДокОснование.Организация;
		Агент 			 = ДокОснование.Агент;	
		Дата 			 = Основание.День;
		Контрагент 		 = Основание.Контрагент;
		кпкТорговаяТочка = Основание.ТорговаяТочка;	 
		ТипПлана 	     = Основание.Док.ТипПлана;     
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.кпкМаршрут") или
			  ТипЗнч(Основание) = Тип("ДокументСсылка.кпкПланПосещений") Тогда                            
                            
		ДокОснование = Основание.Ссылка;
		
		Организация      = Основание.Организация;
		Агент 			 = Основание.Агент;	
		Дата 			 = Основание.Дата;	
		ТипПлана 	     = ДокОснование.ТипПлана;     
	КонецЕсли;
	
КонецПроцедуры //ОбработкаЗаполнения()

// Процедура - обработчик события "ПередЗаписью".
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Запрос = Новый Запрос( "ВЫБРАТЬ
	|	кпкЗаданиеАгента.Ссылка
	|ИЗ
	|	Документ.кпкЗаданиеАгента КАК кпкЗаданиеАгента
	|ГДЕ
	|	кпкЗаданиеАгента.Ссылка <> &Ссылка
	|	И НАЧАЛОПЕРИОДА(кпкЗаданиеАгента.Дата, ДЕНЬ) = &Дата
	|	И кпкЗаданиеАгента.Агент = &Агент
	|	И кпкЗаданиеАгента.Контрагент = &Контрагент
	|	И кпкЗаданиеАгента.кпкТорговаяТочка = &кпкТорговаяТочка
	|	И кпкЗаданиеАгента.ПометкаУдаления = ЛОЖЬ");
	
	Запрос.УстановитьПараметр("Ссылка", 		  ЭтотОбъект.Ссылка);
	Запрос.УстановитьПараметр("Дата",   		  НачалоДня(Дата));
	Запрос.УстановитьПараметр("Агент",   		  Агент);
	Запрос.УстановитьПараметр("Контрагент", 	  Контрагент);
	Запрос.УстановитьПараметр("кпкТорговаяТочка", кпкТорговаяТочка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ОбщегоНазначения.СообщитьОбОшибке("На текущий день уже существует введенный документ с такими же " + 
										  "параметрами! Документ: """ + Выборка.Ссылка + """");
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры //ПередЗаписью()

