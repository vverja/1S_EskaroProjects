
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Обработчик события ПередЗаписью набора записей регистра сведений
Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	Если  ЭтотОбъект.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;	
	// Расчет ресурса ОсновноеЗначение.
	Если (Отбор.План.Значение = Ложь) 
		И (Отбор.План.Использование)
		И (Отбор.ГрафикРаботы.Использование) 
		И (Отбор.Сотрудник.Использование) 
		И (Отбор.Месяц.Использование) Тогда
		
		ЗапросРасчетаОсновногоЗначения = Новый Запрос;
		ЗапросРасчетаОсновногоЗначения.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		ЗапросРасчетаОсновногоЗначения.УстановитьПараметр("Месяц", Отбор.Месяц.Значение);
		ЗапросРасчетаОсновногоЗначения.УстановитьПараметр("ГрафикРаботы", Отбор.ГрафикРаботы.Значение);
		ЗапросРасчетаОсновногоЗначения.УстановитьПараметр("Сотрудник", Отбор.Сотрудник.Значение);
		Сотрудник = Отбор.ГрафикРаботы.Значение;
		
		ПоРеглУчету = ТипЗнч(Отбор.ГрафикРаботы.Значение) = Тип("СправочникСсылка.СотрудникиОрганизаций");
		ИмяТаблицы = "Работники" + ?(ПоРеглУчету, "Организаций", "");
		ИмяСправочника = ?(ПоРеглУчету, "СотрудникиОрганизаций", "ФизическиеЛица");
		ИмяПоля = ?(ПоРеглУчету, "Сотрудник", "ФизЛицо");
		ВнешнийНабор = ЭтотОбъект.Выгрузить();
		ЗапросРасчетаОсновногоЗначения.УстановитьПараметр("ИсточникДанных",ВнешнийНабор);
		
		ЗапросРасчетаОсновногоЗначения.Текст = 
		"ВЫБРАТЬ
		|       ЗаписьНабора.ГрафикРаботы,
		|       ЗаписьНабора.Сотрудник,
		|		ЗаписьНабора.ВидУчетаВремени,
		|		ЗаписьНабора.Дата,
		|		ЗаписьНабора.ОсновноеЗначение,
		|		ЗаписьНабора.ДополнительноеЗначение,
		|		ЗаписьНабора.ПроизводственныйКалендарьПятидневкаПолное,
		|		ЗаписьНабора.ПроизводственныйКалендарьПятидневкаЧасыПолное,
		|		ЗаписьНабора.ПроизводственныйКалендарьШестидневкаПолное,
		|		ЗаписьНабора.ПроизводственныйКалендарьКалендарныеДниПолное,
		|		ЗаписьНабора.ОсновноеЗначениеНормаПолное,
		|		ЗаписьНабора.ДополнительноеЗначениеНормаПолное,
		|		ЗаписьНабора.ПроизводственныйКалендарьПятидневка,
		|		ЗаписьНабора.ПроизводственныйКалендарьПятидневкаЧасы,
		|		ЗаписьНабора.ПроизводственныйКалендарьШестидневка,
		|		ЗаписьНабора.ПроизводственныйКалендарьКалендарныеДни,
		|		ЗаписьНабора.ОсновноеЗначениеНорма,
		|		ЗаписьНабора.ДополнительноеЗначениеНорма,
		|       ЗаписьНабора.Документ,
		|		ЗаписьНабора.ОсновноеЗначениеПолное,
		|		ЗаписьНабора.ДополнительноеЗначениеПолное,
		|		ЗаписьНабора.Месяц,
		|		ЗаписьНабора.План
		|ПОМЕСТИТЬ ВТЗаписиРегистра
		|ИЗ &ИсточникДанных КАК ЗаписьНабора
		|";
		ЗапросРасчетаОсновногоЗначения.Выполнить();
		
		ЗапросРасчетаОсновногоЗначения.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Графики.ГрафикРаботы
		|ИЗ
		|	РегистрСведений.ГрафикиРаботыПоВидамВремени КАК Графики
		|ГДЕ
		|	Графики.ГрафикРаботы = &ГрафикРаботы
		|	И Графики.Сотрудник = &Сотрудник
		|	И Графики.Месяц = &Месяц
		|	И Графики.План = ИСТИНА";
		
		Если ЗапросРасчетаОсновногоЗначения.Выполнить().Выбрать().Количество() = 1 Тогда
			// применяем индивидуальный график
			
			ЗапросРасчетаОсновногоЗначения.Текст = 
			"ВЫБРАТЬ
			|	ЗаписьНабора.ГрафикРаботы,
			|	ЗаписьНабора.Сотрудник,
			|	ЗаписьНабора.ВидУчетаВремени,
			|	ЗаписьНабора.Дата,
			|	ЗаписьНабора.ОсновноеЗначениеПолное - РасчетОсновногоЗначения.ОсновноеЗначение КАК ОсновноеЗначение,
			|	ЗаписьНабора.ДополнительноеЗначениеПолное - РасчетОсновногоЗначения.ДополнительноеЗначение КАК ДополнительноеЗначение,
			|	РасчетОсновногоЗначения.ПроизводственныйКалендарьПятидневка КАК ПроизводственныйКалендарьПятидневкаПолное,
			|	РасчетОсновногоЗначения.ПроизводственныйКалендарьПятидневкаЧасы КАК ПроизводственныйКалендарьПятидневкаЧасыПолное,
			|	РасчетОсновногоЗначения.ПроизводственныйКалендарьШестидневка КАК ПроизводственныйКалендарьШестидневкаПолное,
			|	РасчетОсновногоЗначения.ПроизводственныйКалендарьКалендарныеДни КАК ПроизводственныйКалендарьКалендарныеДниПолное,
			|	РасчетОсновногоЗначения.ОсновноеЗначениеНорма КАК ОсновноеЗначениеНормаПолное,
			|	РасчетОсновногоЗначения.ДополнительноеЗначениеНорма КАК ДополнительноеЗначениеНормаПолное,
			|	0 КАК ПроизводственныйКалендарьПятидневка,
			|	0 КАК ПроизводственныйКалендарьПятидневкаЧасы,
			|	0 КАК ПроизводственныйКалендарьШестидневка,
			|	0 КАК ПроизводственныйКалендарьКалендарныеДни,
			|	0 КАК ОсновноеЗначениеНорма,
			|	0 КАК ДополнительноеЗначениеНорма,
			|	ЗаписьНабора.Документ,
			|	ЗаписьНабора.ОсновноеЗначениеПолное,
			|	ЗаписьНабора.ДополнительноеЗначениеПолное,
			|	ЗаписьНабора.Месяц,
			|	ЗаписьНабора.План
			|ИЗ
			|	ВТЗаписиРегистра КАК ЗаписьНабора
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикиРаботыПоВидамВремени КАК РасчетОсновногоЗначения
			|		ПО ЗаписьНабора.ГрафикРаботы = РасчетОсновногоЗначения.ГрафикРаботы
			|			И ЗаписьНабора.Сотрудник = РасчетОсновногоЗначения.Сотрудник
			|			И (РасчетОсновногоЗначения.Месяц = &Месяц)
			|			И (РасчетОсновногоЗначения.План)
			|			И ЗаписьНабора.Дата = РасчетОсновногоЗначения.Дата
			|			И ЗаписьНабора.ВидУчетаВремени = РасчетОсновногоЗначения.ВидУчетаВремени";
			
		Иначе
			// применяем общие графики
			// формируем временную таблицу с общими графиками работы
		
			Месяц = Отбор.Месяц.Значение;
			ГодМесяц = Формат(Месяц, "ДФ=""гггг,ММ,""");
			
			ТекстЗапроса = "ВЫБРАТЬ ДАТАВРЕМЯ("+ ГодМесяц +  "1) КАК Дата ПОМЕСТИТЬ ВТДаты";
			Для Число = 2 По День(КонецМесяца(Месяц)) Цикл
				ТекстЗапроса = ТекстЗапроса + "
				|ОБЪЕДИНИТЬ ВСЕ 
				|ВЫБРАТЬ ДАТАВРЕМЯ("+ ГодМесяц + Число + ")";
			КонецЦикла;
			
			ЗапросРасчетаОсновногоЗначения.Текст = ТекстЗапроса;
			ЗапросРасчетаОсновногоЗначения.Выполнить();
			
			ЗапросРасчетаОсновногоЗначения.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
            |   Даты.Дата КАК Дата,
            |   РаботникиОрганизаций.ГрафикРаботы КАК ГрафикРаботы
            |ПОМЕСТИТЬ ВТДатыГрафики
            |ИЗ
            |   (ВЫБРАТЬ
            |       Даты.Дата КАК Дата,
            |       ЕСТЬNULL(ОпределениеГрафикаСрезПоследних.ДатаОпределенияГрафика, ОпределениеГрафикаСрезПервых.ДатаОпределенияГрафика) КАК ДатаОпределенияГрафика
            |   ИЗ
            |       ВТДаты КАК Даты
            |           ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
            |               МАКСИМУМ(РаботникиОрганизаций.Период) КАК ДатаОпределенияГрафика,
            |               Даты.Дата КАК Дата
            |           ИЗ
            |               ВТДаты КАК Даты
            |                   ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизаций
            |                   ПО (РаботникиОрганизаций.Сотрудник = &Сотрудник)
            |                       И Даты.Дата >= РаботникиОрганизаций.Период
            |           
            |           СГРУППИРОВАТЬ ПО
            |               Даты.Дата) КАК ОпределениеГрафикаСрезПоследних
            |           ПО (ОпределениеГрафикаСрезПоследних.Дата = Даты.Дата)
            |           ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
            |               МИНИМУМ(РаботникиОрганизаций.Период) КАК ДатаОпределенияГрафика,
            |               Даты.Дата КАК Дата
            |           ИЗ
            |               ВТДаты КАК Даты
            |                   ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизаций
            |                   ПО (РаботникиОрганизаций.Сотрудник = &Сотрудник)
            |                       И Даты.Дата <= РаботникиОрганизаций.Период
            |           
            |           СГРУППИРОВАТЬ ПО
            |               Даты.Дата) КАК ОпределениеГрафикаСрезПервых
            |           ПО (ОпределениеГрафикаСрезПервых.Дата = Даты.Дата)) КАК Даты
            |       ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизаций
            |       ПО (РаботникиОрганизаций.Сотрудник = &Сотрудник)
            |           И Даты.ДатаОпределенияГрафика = РаботникиОрганизаций.Период
            |
            |ИНДЕКСИРОВАТЬ ПО
            |   Дата,
            |   ГрафикРаботы";
			ЗапросРасчетаОсновногоЗначения.Выполнить();
			
			ЗапросРасчетаОсновногоЗначения.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
            |   ЗаписьНабора.ГрафикРаботы,
            |   ЗаписьНабора.Сотрудник,
            |   ЗаписьНабора.ВидУчетаВремени,
            |   ЗаписьНабора.Дата,
            |   ЗаписьНабора.ОсновноеЗначениеПолное КАК ОсновноеЗначение,
            |   ЗаписьНабора.ДополнительноеЗначениеПолное КАК ДополнительноеЗначение,
            |   РасчетОсновногоЗначения.ПроизводственныйКалендарьПятидневка КАК ПроизводственныйКалендарьПятидневкаПолное,
            |   РасчетОсновногоЗначения.ПроизводственныйКалендарьПятидневкаЧасы КАК ПроизводственныйКалендарьПятидневкаЧасыПолное,
            |   РасчетОсновногоЗначения.ПроизводственныйКалендарьШестидневка КАК ПроизводственныйКалендарьШестидневкаПолное,
            |   РасчетОсновногоЗначения.ПроизводственныйКалендарьКалендарныеДни КАК ПроизводственныйКалендарьКалендарныеДниПолное,
            |   РасчетОсновногоЗначения.ОсновноеЗначениеНорма КАК ОсновноеЗначениеНормаПолное,
            |   РасчетОсновногоЗначения.ДополнительноеЗначениеНорма КАК ДополнительноеЗначениеНормаПолное,
            |   РасчетОсновногоЗначения.ПроизводственныйКалендарьПятидневка КАК ПроизводственныйКалендарьПятидневка,
            |   РасчетОсновногоЗначения.ПроизводственныйКалендарьПятидневкаЧасы КАК ПроизводственныйКалендарьПятидневкаЧасы,
            |   РасчетОсновногоЗначения.ПроизводственныйКалендарьШестидневка КАК ПроизводственныйКалендарьШестидневка,
            |   РасчетОсновногоЗначения.ПроизводственныйКалендарьКалендарныеДни КАК ПроизводственныйКалендарьКалендарныеДни,
            |   РасчетОсновногоЗначения.ОсновноеЗначениеНорма КАК ОсновноеЗначениеНорма,
            |   РасчетОсновногоЗначения.ДополнительноеЗначениеНорма КАК ДополнительноеЗначениеНорма,
            |   ЗаписьНабора.Документ,
            |   ЗаписьНабора.ОсновноеЗначениеПолное,
            |   ЗаписьНабора.ДополнительноеЗначениеПолное,
            |   ЗаписьНабора.Месяц,
            |   ЗаписьНабора.План
            |ИЗ
            |   ВТЗаписиРегистра КАК ЗаписьНабора
            |       ЛЕВОЕ СОЕДИНЕНИЕ ВТДатыГрафики КАК ДатыГрафики
            |       ПО (ДатыГрафики.Дата = ЗаписьНабора.Дата)
            |       ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикиРаботыПоВидамВремени КАК РасчетОсновногоЗначения
            |       ПО (ДатыГрафики.ГрафикРаботы = РасчетОсновногоЗначения.ГрафикРаботы)
            |           И (РасчетОсновногоЗначения.Месяц = &Месяц)
            |           И (РасчетОсновногоЗначения.План)
            |           И ЗаписьНабора.Дата = РасчетОсновногоЗначения.Дата
            |           И ЗаписьНабора.ВидУчетаВремени = РасчетОсновногоЗначения.ВидУчетаВремени";
			
		КонецЕсли;
		ЭтотОбъект.Загрузить(ЗапросРасчетаОсновногоЗначения.Выполнить().Выгрузить());
		
	
	ИначеЕсли ТипЗнч(Отбор.ГрафикРаботы.Значение) = Тип("СправочникСсылка.ГрафикиРаботы") Тогда
		
		ГрафикРаботы = Отбор.ГрафикРаботы.Значение;
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Ссылка", ГрафикРаботы);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ГрафикиРаботы.ГрафикПолногоРабочегоВремени,
		|	ГрафикиРаботы.СокращенноеРабочееВремя,
		|	ГрафикиРаботы.СокращатьКалендарныеДни
		|ИЗ
		|	Справочник.ГрафикиРаботы КАК ГрафикиРаботы
		|ГДЕ
		|	ГрафикиРаботы.Ссылка = &Ссылка";
		Результат = Запрос.Выполнить();
		Если Результат.Пустой() Тогда
			Возврат
		КонецЕсли;
		
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ГрафикПолногоРабочегоВремени = Выборка.ГрафикПолногоРабочегоВремени;
		ТребуетсяНормаДругогоГрафика = Выборка.СокращенноеРабочееВремя И Не ГрафикПолногоРабочегоВремени.Пустая();
		СокращатьКалендарныеДни = Выборка.СокращатьКалендарныеДни И Не ГрафикПолногоРабочегоВремени.Пустая();
		
		СоответствиеМесяцев = Новый Соответствие;
		Для каждого ЗаписьНабора Из ЭтотОбъект Цикл
			ЗаписьНабора.Месяц = НачалоМесяца(ЗаписьНабора.Дата);
			Если Не ТребуетсяНормаДругогоГрафика Тогда
				Если ЗаписьНабора.ВидУчетаВремени = Перечисления.ВидыУчетаВремени.ПоДням Или ЗаписьНабора.ВидУчетаВремени = Перечисления.ВидыУчетаВремени.ПоЧасам Тогда
					ЗаписьНабора.ОсновноеЗначениеНорма = ЗаписьНабора.ОсновноеЗначение;
					ЗаписьНабора.ДополнительноеЗначениеНорма = ЗаписьНабора.ДополнительноеЗначение;
				КонецЕсли;
			Иначе 	
				СоответствиеМесяцев.Вставить(ЗаписьНабора.Месяц);
			КонецЕсли;
		КонецЦикла;
		
		Если ТребуетсяНормаДругогоГрафика Тогда
			
			МассивМесяцев = Новый Массив;
			Для каждого Элемент Из СоответствиеМесяцев Цикл
				МассивМесяцев.Добавить(Элемент.Ключ)
			КонецЦикла; 
			
			Запрос.УстановитьПараметр("МассивМесяцев", МассивМесяцев);
			Запрос.УстановитьПараметр("парамГрафикПолногоРабочегоВремени", ГрафикПолногоРабочегоВремени);
			
			Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ГрафикиРаботыПоВидамВремени.Дата,
			|	ГрафикиРаботыПоВидамВремени.ОсновноеЗначениеНорма КАК НормаДней,
			|	ГрафикиРаботыПоВидамВремени.ДополнительноеЗначениеНорма КАК НормаЧасов
			|ИЗ
			|	РегистрСведений.ГрафикиРаботыПоВидамВремени КАК ГрафикиРаботыПоВидамВремени
			|ГДЕ
			|	ГрафикиРаботыПоВидамВремени.ГрафикРаботы = &парамГрафикПолногоРабочегоВремени
			|	И ГрафикиРаботыПоВидамВремени.ВидУчетаВремени = ЗНАЧЕНИЕ(Перечисление.ВидыУчетаВремени.ПоДням)
			|	И НАЧАЛОПЕРИОДА(ГрафикиРаботыПоВидамВремени.Дата, МЕСЯЦ) В (&МассивМесяцев)";
			
			ДанныеНормыВремени = Запрос.Выполнить().Выгрузить();
			ДанныеНормыВремени.Индексы.Добавить("Дата");
			Для каждого ЗаписьНабора Из ЭтотОбъект Цикл
				НормаДней = 0;
				НормаЧасов = 0;	
				НайденнаяСтрока = ДанныеНормыВремени.Найти(ЗаписьНабора.Дата, "Дата");
				Если НайденнаяСтрока <> Неопределено Тогда
					НормаДней = НайденнаяСтрока.НормаДней;
					НормаЧасов = НайденнаяСтрока.НормаЧасов;
				КонецЕсли;
				Если ЗаписьНабора.ВидУчетаВремени = Перечисления.ВидыУчетаВремени.ПоДням Тогда 
					ЗаписьНабора.ОсновноеЗначениеНорма = НормаДней;
					ЗаписьНабора.ДополнительноеЗначениеНорма = НормаЧасов;
				ИначеЕсли ЗаписьНабора.ВидУчетаВремени = Перечисления.ВидыУчетаВремени.ПоЧасам Тогда	
					ЗаписьНабора.ОсновноеЗначениеНорма = НормаЧасов;      
					ЗаписьНабора.ДополнительноеЗначениеНорма = НормаДней;
				КонецЕсли;
				
				Если СокращатьКалендарныеДни Тогда
					Если ЗаписьНабора.ОсновноеЗначениеНорма > 0 И ЗаписьНабора.ОсновноеЗначение = 0 Тогда
						ЗаписьНабора.ПроизводственныйКалендарьКалендарныеДни = 0;
					Конецесли;	
					
				Конецесли;	
					
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Для каждого ЗаписьНабора Из ЭтотОбъект Цикл
		ЗаписьНабора.ПроизводственныйКалендарьКалендарныеДниСПраздниками = 1;
	КонецЦикла;

	
КонецПроцедуры

// Обработчик события ПриЗаписи набора записей регистра сведений
Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если (Отбор.План.Значение = Истина) И (Отбор.План.Использование)
		И (Отбор.ГрафикРаботы.Использование) И (Отбор.Месяц.Использование) Тогда
		// Перезапись набора записей с отбором по План = Ложь
		НаборЗаписейФакта = РегистрыСведений.ГрафикиРаботыПоВидамВремени.СоздатьНаборЗаписей();
		НаборЗаписейФакта.Отбор.План.Установить(Ложь);
		НаборЗаписейФакта.Отбор.ГрафикРаботы.Установить(Отбор.ГрафикРаботы.Значение);
		НаборЗаписейФакта.Отбор.Сотрудник.Установить(Отбор.Сотрудник.Значение);
		НаборЗаписейФакта.Отбор.Месяц.Установить(Отбор.Месяц.Значение);
		НаборЗаписейФакта.Прочитать();
		Если НаборЗаписейФакта.Количество() <> 0 Тогда
			НаборЗаписейФакта.Записать();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры
