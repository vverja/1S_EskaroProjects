////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

Перем РежимЗаписиРегистратора Экспорт;
Перем ПустойНаборЗаписей;

Перем мСотрудники;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

Процедура ВыполнитьПроверку(СообщатьОбОшибкахПериодов, УчитыватьРегистратор)
	
	Если СообщатьОбОшибкахПериодов Тогда
		СообщенияОбОшибках = Новый Массив;
	Иначе
		СообщенияОбОшибках = НеОпределено;
	КонецЕсли;
			
	ОшибкиПериодов						= Новый Соответствие;
	
	Регистратор = Отбор.Регистратор.Значение;
	
	ПроцедурыУправленияПерсоналом.ПолучитьОшибкиПериодовСостоянияСотрудника(Регистратор, УчитыватьРегистратор, мСотрудники, СообщенияОбОшибках, ОшибкиПериодов);
	
	// запишем новое состояние ошибок
	НаборЗаписей = РегистрыСведений.ПериодыСостоянийРаботниковОрганизацийОшибки.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Сотрудник.Использование = Истина;
	Для Каждого Элемент Из мСотрудники Цикл
		НаборЗаписей.Очистить();
		НаборЗаписей.Отбор.Сотрудник.Значение = Элемент;
		// если ошибка периода - пишем непустой набор записей
		Если ОшибкиПериодов[Элемент] = 0 Тогда
			Строка = НаборЗаписей.Добавить();
			Строка.Сотрудник = Элемент;
		КонецЕсли;
		НаборЗаписей.Записать();
	КонецЦикла;
	
	// сообщим об ошибках
	Если СообщатьОбОшибкахПериодов Тогда
		Для Каждого Сообщение Из СообщенияОбОшибках Цикл
			СтрокаСообщениеОбОшибке = Сообщение.Сотрудник + ": Противоречие в периодах по состоянию" + Символы.ПС+ "(" + 
			Формат(Сообщение.ДатаНачала, "ДФ='дд МММ гг ""г.""'") + " - " + 
			Формат(Сообщение.ДатаОкончания, "ДФ='дд МММ гг ""г.""'") + " и " + 
			Формат(Сообщение.ДатаНачалаПред, "ДФ='дд МММ гг ""г.""'") + " - " + 
			Формат(Сообщение.ДатаОкончанияПред, "ДФ='дд МММ гг ""г.""'") + ")";
			СтрокаСообщениеОбОшибке = СтрокаСообщениеОбОшибке + Символы.ПС + "Документы, которые противоречат друг другу:";
			Если не ПустаяСтрока(Сообщение.Регистратор1) Тогда
				СтрокаСообщениеОбОшибке = СтрокаСообщениеОбОшибке + Символы.ПС + "   " + Сообщение.Регистратор1;
			КонецЕсли;
			Если не ПустаяСтрока(Сообщение.Регистратор2) Тогда
				СтрокаСообщениеОбОшибке = СтрокаСообщениеОбОшибке + Символы.ПС + "   " + Сообщение.Регистратор2;
			КонецЕсли;
			Если не ПустаяСтрока(Сообщение.ПослРегистратор1) Тогда
				СтрокаСообщениеОбОшибке = СтрокаСообщениеОбОшибке + Символы.ПС + "   " + Сообщение.ПослРегистратор1;
			КонецЕсли;
			Если не ПустаяСтрока(Сообщение.ПослРегистратор2) Тогда
				СтрокаСообщениеОбОшибке = СтрокаСообщениеОбОшибке + Символы.ПС + "   " + Сообщение.ПослРегистратор2;
			КонецЕсли;
			ОбщегоНазначения.СообщитьОбОшибке(СтрокаСообщениеОбОшибке);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Регистратор",	Отбор.Регистратор.Значение);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Периоды.Сотрудник
	|ИЗ
	|	РегистрСведений.ПериодыСостоянийРаботниковОрганизаций КАК Периоды
	|ГДЕ
	|	Периоды.Регистратор = &Регистратор";
	
	мСотрудники = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Сотрудник");
	
	ВыполнитьПроверку(РежимЗаписиРегистратора = РежимЗаписиДокумента.ОтменаПроведения, Ложь);
	
	ПустойНаборЗаписей = ЭтотОбъект.Количество() = 0;
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	мСотрудники.Очистить();
	
	Для Каждого Запись Из ЭтотОбъект Цикл
		мСотрудники.Добавить(Запись.Сотрудник);
	КонецЦикла;

	Если Не ПустойНаборЗаписей Тогда
		ВыполнитьПроверку(РежимЗаписиРегистратора = РежимЗаписиДокумента.Проведение, Истина);
	КонецЕсли;
	
КонецПроцедуры // ПриЗаписи()

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
