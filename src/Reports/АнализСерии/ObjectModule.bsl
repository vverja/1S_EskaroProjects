#Если Клиент Тогда
//Печать наименования раздела
Процедура НапечататьРаздел(Макет, ДокументРезультат, Раздел)
	Секция = Макет.ПолучитьОбласть("Раздел");
	Если Раздел = 1 Тогда
		Секция.Параметры.Раздел = "Движение по сериям";
	ИначеЕсли Раздел = 2 Тогда
		Секция.Параметры.Раздел = "Внутренняя сертификация";
	ИначеЕсли Раздел = 3 Тогда
		Секция.Параметры.Раздел = "Внешняя сертификация";
	Иначе
		Секция.Параметры.Раздел = "Рекламации";

	КонецЕсли;
	ДокументРезультат.Вывести(Секция);
КонецПроцедуры//НапечататьРаздел()

//Относит документ, переданный в качестве параметра, к конкретному разделу
Функция ПолучитьРаздел(Ссылка)
	
	Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя") Тогда
		Возврат 4;
	ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.ЗаявкаНаСертификациюНоменклатуры") Тогда
		Если Ссылка.ВидОперации = Перечисления.ВидыОперацийЗаявкаНаСертификациюНоменклатуры.внутренняя Тогда
			Возврат 2;
		Иначе
			Возврат 3;
		КонецЕсли;
	 ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.АктОтбораПробНоменклатуры") Тогда
		Если Ссылка.ВидОперации = Перечисления.ВидыОперацийАктОтбораПробНоменклатуры.ДляВнутреннейСертификации Тогда
			Возврат 2;
		Иначе
			Возврат 3;
		КонецЕсли;
	ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.СертификацияНоменклатуры") Тогда
		Если Ссылка.ВидОперации = Перечисления.ВидыОперацийСертификацияНоменклатуры.внутренняя Тогда
			Возврат 2;
		Иначе
			Возврат 3;
		КонецЕсли;
	Иначе
		Возврат 1;
	КонецЕсли;

КонецФункции//ПолучитьРаздел()

// Процедура заполняет данными поле табличного документа отчета
//
// Параметры
//  ДокументРезультат - поле табличного документа
//
// Возвращаемое значение
//  НЕТ
//
Процедура СформироватьОтчет(ДокументРезультат) Экспорт
	
	Если НЕ ЗначениеЗаполнено(СерияНоменклатуры) Тогда
		Предупреждение("Не выбрана серия номенклатуры!");
		Возврат;
	КонецЕсли;
	
	Макет = ЭтотОбъект.ПолучитьМакет("АнализСерии");
	ДокументРезультат.Очистить();
	
	Секция = Макет.ПолучитьОбласть("Шапка");
	Секция.Параметры.ДатаОтч = Формат(ДатаОтчета,"ДЛФ = Д");
	Секция.Параметры.Серия = СерияНоменклатуры;
	Секция.Параметры.Наименование = СерияНоменклатуры.Владелец;
	
	ДокументРезультат.Вывести(Секция);
	
	Мас = Новый Массив(1);
	Мас[0] = СерияНоменклатуры;
	ТаблицаСсылок = НайтиПоСсылкам(Мас);
	ТаблицаДокументов = Новый ТаблицаЗначений;
	ТаблицаДокументов.Колонки.Добавить("Документ");
	ТаблицаДокументов.Колонки.Добавить("Дата");
	ТаблицаДокументов.Колонки.Добавить("Раздел");
	Для каждого стр Из ТаблицаСсылок Цикл
		Если Лев(ТипЗнч(стр.Данные),16) = "Документ ссылка:" Тогда
			Если ((стр.Данные.Дата <= КонецДня(ДатаОтчета)) или (ДатаОтчета = '00010101')) и (стр.Данные.Проведен) Тогда
				НовСтр = ТаблицаДокументов.Добавить();
				Новстр.Документ = стр.Данные;
				Новстр.Дата = стр.Данные.Дата;
				Если ПоРазделам Тогда
					Новстр.Раздел = ПолучитьРаздел(стр.Данные)
				Иначе
					Новстр.Раздел = "";
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	ТаблицаДокументов.Сортировать("Раздел, Дата");
	Ном = 0;
	ТекущийРаздел = "";
	Для каждого стр Из ТаблицаДокументов Цикл
		
		Если стр.Раздел <> ТекущийРаздел Тогда
			ТекущийРаздел = стр.Раздел;
			НапечататьРаздел(Макет, ДокументРезультат, ТекущийРаздел);
			Ном = 0;
		КонецЕсли;
		Ном = Ном + 1;
		Секция = Макет.ПолучитьОбласть("Строка");
		Секция.Параметры.Ном = Ном;
		Секция.Параметры.Документ = стр.Документ.Метаданные();
		Секция.Параметры.НомерДата = Формат(стр.Документ.Дата,"ДЛФ = Д")+Символы.ПС+Строка(стр.Документ.Номер);
		Секция.Параметры.Документ1 = стр.Документ;
		Секция.Параметры.Примечание = стр.Документ.Комментарий;
		
		ДокументРезультат.Вывести(Секция);
	
	КонецЦикла;
	
	Если ПоказатьОстатки Тогда
		
		Ном = 0;
		
		ЗапросСклады = Новый Запрос("
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Склад, КоличествоОстаток
		|ИЗ РегистрНакопления.ТоварыНаСкладах.Остатки(&Дата)
		|ГДЕ СерияНоменклатуры = &Серия
		|");
		ЗапросСклады.УстановитьПараметр("Серия",СерияНоменклатуры);
		ЗапросСклады.УстановитьПараметр("Дата",КонецДня(ДатаОтчета));
		ОстаткиСклады = ЗапросСклады.Выполнить().Выгрузить();
		Секция = Макет.ПолучитьОбласть("Шапка1");
		ДокументРезультат.Вывести(Секция);
		Секция = Макет.ПолучитьОбласть("Строка1");
		Для Каждого стр Из ОстаткиСклады Цикл
			Ном = Ном + 1;
			Секция.Параметры.Ном = Ном;
			Секция.Параметры.Склад = стр.Склад;
			Секция.Параметры.Остаток = Формат(стр.КоличествоОстаток,"ЧДЦ = 3");
			ДокументРезультат.Вывести(Секция);
		КонецЦикла;	
		ЗапросВсегоРеализация = Новый Запрос("
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СерияНоменклатуры, Сумма(Количество) КАК КоличВсего
		|ИЗ 
		|	(ВЫБРАТЬ СерияНоменклатуры, Регистратор КАК Ссылка, Количество
		|	ИЗ РегистрНакопления.ТоварыНаСкладах
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|	(ВЫБРАТЬ Ссылка ИЗ Документ.РеализацияТоваровУслуг) Док
		|	ПО Регистратор = Док.Ссылка
		|	ГДЕ (СерияНоменклатуры = &Серия) И ((Регистратор.Дата <= &Дата) ИЛИ (&Дата = &пустаяДата))) Таб1
		|СГРУППИРОВАТЬ ПО СерияНоменклатуры");
		ЗапросВсегоРеализация.УстановитьПараметр("Серия",СерияНоменклатуры);
		ЗапросВсегоРеализация.УстановитьПараметр("Дата",КонецДня(ДатаОтчета));
		ЗапросВсегоРеализация.УстановитьПараметр("пустаяДата",'00010101');
		ТаблицаВсегоРеализации = ЗапросВсегоРеализация.Выполнить().Выгрузить();
		Секция = Макет.ПолучитьОбласть("Отгрузка");
		Для Каждого СтрокаРеализации Из ТаблицаВсегоРеализации Цикл
			Секция.Параметры.ОтгруженоВсего = Формат(СтрокаРеализации.КоличВсего,"ЧДЦ = 3");
			ДокументРезультат.Вывести(Секция);		
		КонецЦикла;	
	КонецЕсли;	
	
	ДокументРезультат.ТолькоПросмотр = истина;
		
КонецПроцедуры //СформироватьОтчет()
#КонецЕсли
