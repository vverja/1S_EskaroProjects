#Если Клиент Тогда

Процедура ЗаполнитьНачальныеНастройки() Экспорт
	
	ПостроительОтчета = ОбщийОтчет.ПостроительОтчета;
	
	СтруктураПредставлениеПолей = Новый Структура;
	МассивОтбора = Новый Массив;
	ОбщийОтчет.мСоответствиеНазначений = Новый Соответствие;
	ОбщийОтчет.мСтруктураДляОтбораПоКатегориям = Новый Структура;
	
	
	Текст = "
	| ВЫБРАТЬ 
	|	СУММА(КнигаПриобретений.БазаНДС)               	КАК БазаНДС,
	|	СУММА(КнигаПриобретений.НДС)             		КАК НДС,
	|	КнигаПриобретений.ДоговорКонтрагента			КАК ДоговорКонтрагента,
	|	КнигаПриобретений.СтавкаНДС 					КАК СтавкаНДС
	|{ВЫБРАТЬ 
	|	КнигаПриобретений.Организация.* 				КАК Организация,
	|	КнигаПриобретений.КодОперации.* 				КАК КодОперации,
	|	КнигаПриобретений.Регистратор.* 				КАК Регистратор,
	|	КнигаПриобретений.ДоговорКонтрагента.*			КАК ДоговорКонтрагента,
	|	КнигаПриобретений.СтавкаНДС.* 					КАК СтавкаНДС
	|	//СВОЙСТВА
	|}
	|ИЗ 
	|	РегистрНакопления.КнигаПриобретений КАК КнигаПриобретений
	|	//СОЕДИНЕНИЯ
	|ГДЕ
	|	КнигаПриобретений.Активность = Истина
	|	И КнигаПриобретений.СтатьяКнигиПриобретения = &СтатьяДекларацииКорректировки
    |	И КнигаПриобретений.Период >= &ДатаНач
    |	И ((&ДатаКон = &ПустаяДата) ИЛИ (КнигаПриобретений.Период <= &ДатаКон))
	|{ГДЕ
	|	КнигаПриобретений.Организация.* 				КАК Организация,
	|	КнигаПриобретений.КодОперации.* 				КАК КодОперации,
	|	КнигаПриобретений.Регистратор.* 				КАК Регистратор,
	|	КнигаПриобретений.ДоговорКонтрагента.*			КАК ДоговорКонтрагента,
	|	КнигаПриобретений.СтавкаНДС.* 					КАК СтавкаНДС
	|	//СВОЙСТВА
	|	//КАТЕГОРИИ
	|}
	|
	|СГРУППИРОВАТЬ ПО
	|	КнигаПриобретений.Организация,
	|	КнигаПриобретений.КодОперации,
	|	КнигаПриобретений.ДоговорКонтрагента,
	|	КнигаПриобретений.СтавкаНДС
	|	//СГРУППИРОВАТЬПО
	|
	|{УПОРЯДОЧИТЬ ПО
	|	КнигаПриобретений.Организация.*,
	|	КнигаПриобретений.КодОперации.*,
	|	КнигаПриобретений.Регистратор.*,
	|	КнигаПриобретений.ДоговорКонтрагента.*,
	|	КнигаПриобретений.СтавкаНДС.*
	|	//СВОЙСТВА
	|}
	|
	|ИТОГИ 
	|	СУММА(БазаНДС),
	|	СУММА(НДС)
	|	
	|ПО ОБЩИЕ,
	|	Организация,
	|	КодОперации
	|
	|{ИТОГИ ПО
	|	КнигаПриобретений.Организация.*,
	|	КнигаПриобретений.КодОперации.*,
	|	КнигаПриобретений.Регистратор.*,
	|	КнигаПриобретений.ДоговорКонтрагента.*,
	|	КнигаПриобретений.СтавкаНДС.*
	|	//СВОЙСТВА
	|}
	|";
	
	СтруктураПредставлениеПолей = Новый Структура(
	"Организация,
	|КодОперации,
	|Регистратор,
	|ДоговорКонтрагента,
	|СтавкаНДС",
	"Организация",
	"Тип корректировки",
	"Регистратор (документ движения)",
	"Договор контрагента",
	"Ставка НДС");
	
	МассивОтбора = Новый Массив;
	
	ПостроительОтчета.Текст = Текст;
	
	Если ОбщийОтчет.Показатели.Найти("БазаНДС", "Имя") = Неопределено Тогда
		ОбщийОтчет.ЗаполнитьПоказатели("БазаНДС", "База НДС", Истина, "ЧЦ=15; ЧДЦ=2");
	КонецЕсли;
	Если ОбщийОтчет.Показатели.Найти("НДС", "Имя") = Неопределено Тогда
		ОбщийОтчет.ЗаполнитьПоказатели("НДС", "Сумма н/к", Истина, "ЧЦ=15; ЧДЦ=2");
	КонецЕсли;
	
	ОбщийОтчет.мНазваниеОтчета = "Корректировки налогового кредита по НДС";
	
	УправлениеОтчетами.ЗаполнитьПредставленияПолей(СтруктураПредставлениеПолей, ПостроительОтчета);
	УправлениеОтчетами.ОчиститьДополнительныеПоляПостроителя(ПостроительОтчета);
	УправлениеОтчетами.ЗаполнитьОтбор(МассивОтбора, ПостроительОтчета);
	ОбщийОтчет.мВыбиратьИмяРегистра = Ложь;
	
	ОбщийОтчет.ВыводитьПоказателиВСтроку = Истина;
	ОбщийОтчет.РаскрашиватьИзмерения = Истина;
	
КонецПроцедуры

Процедура СформироватьОтчет(ДокументРезультат, ПоказыватьЗаголовок, ВысотаЗаголовка, ТолькоЗаголовок = Ложь) Экспорт
	
	
	ОбщийОтчет.ПостроительОтчета.Параметры.Вставить("СтатьяДекларацииКорректировки", Справочники.СтатьиНалоговыхДеклараций.НДС_НККорректировкаПрочее);
	ОбщийОтчет.ПостроительОтчета.Параметры.Вставить("ПустаяДата",      		ОбщегоНазначения.ПустоеЗначениеТипа(Тип("Дата")));
	
	ОбщийОтчет.СформироватьОтчет(ДокументРезультат, ПоказыватьЗаголовок, ВысотаЗаголовка, ТолькоЗаголовок);

КонецПРоцедуры

// Читает свойство Построитель отчета
//
// Параметры
//	Нет
//
Функция ПолучитьПостроительОтчета() Экспорт

	Возврат ОбщийОтчет.ПолучитьПостроительОтчета();

КонецФункции // ПолучитьПостроительОтчета()

// Настраивает отчет по переданной структуре параметров
//
// Параметры:
//	Нет.
//
Процедура Настроить(Параметры) Экспорт

	ОбщийОтчет.Настроить(Параметры, ЭтотОбъект);

КонецПроцедуры

// Возвращает основную форму отчета, связанную с данным экземпляром отчета
//
// Параметры
//	Нет
//
Функция ПолучитьОсновнуюФорму() Экспорт
	
	ОснФорма = ПолучитьФорму();
	ОснФорма.ОбщийОтчет = ОбщийОтчет;
	ОснФорма.ЭтотОтчет = ЭтотОбъект;
	Возврат ОснФорма;
	
КонецФункции // ПолучитьОсновнуюФорму()

// Формирует структуру, в которую складываются настройки
//
Функция СформироватьСтруктуруДляСохраненияНастроек(ПоказыватьЗаголовок) Экспорт
	
	СтруктураНастроек = Новый Структура;
	
	ОбщийОтчет.СформироватьСтруктуруДляСохраненияНастроек(СтруктураНастроек, ПоказыватьЗаголовок);
	
	Возврат СтруктураНастроек;
	
КонецФункции

// Заполняет настройки из структуры - кроме состояния панели "Отбор"
//
Процедура ВосстановитьНастройкиИзСтруктуры(СохраненныеНастройки, ПоказыватьЗаголовок, Отчет=Неопределено) Экспорт
	
	// Если отчет, вызвавший порцедуру, не передан, то считаем, что ее вызвал этот отчет
	Если Отчет = Неопределено Тогда
		Отчет = ЭтотОбъект;
	КонецЕсли;

	ОбщийОтчет.ВосстановитьНастройкиИзСтруктуры(СохраненныеНастройки, ПоказыватьЗаголовок, Отчет);
	
КонецПроцедуры

#КонецЕсли