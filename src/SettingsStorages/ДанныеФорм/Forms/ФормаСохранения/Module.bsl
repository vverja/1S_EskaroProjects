// Заполняется список настроек
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Заполним список настроек
	Пользователь = глЗначениеПеременной("глТекущийПользователь");
	КлючОбъекта  = ХранилищаНастроек.ДанныеФорм.СократитьКлючОбъекта(Параметры.КлючОбъекта);
	СписокНастроек.Загрузить(ХранилищаНастроек.ДанныеФорм.НастройкиПользователя(Пользователь, КлючОбъекта));
	
КонецПроцедуры

// Создает новую пустую настройку (без данных)
&НаСервереБезКонтекста
Функция СоздатьНовуюНастройку(Объект, ИмяНастройки, Пользователь)
	
	// Настройки доступны роли Пользователь
	
	Настройка = Справочники.НастройкиЗаполненияФорм.СоздатьЭлемент();
	Настройка.Объект                               = Объект;
	Настройка.Наименование                         = ИмяНастройки;
	ПользовательНастройки = Настройка.Пользователи.Добавить();
	ПользовательНастройки.Пользователь   = Пользователь;
	Попытка
		Настройка.Записать();
	Исключение
		Возврат Неопределено;
	КонецПопытки;
	
	Возврат Настройка.Ссылка;
	
КонецФункции

// Проверяет, нет ли настроек данных такого же объекта с таким же наименованием
&НаСервереБезКонтекста
Функция ЕстьОдноименныеНастройки(КлючОбъекта, Наименование)
	
	// Настройки доступны роли Пользователь
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	НастройкиЗаполненияФорм.Ссылка
	|ИЗ
	|	Справочник.НастройкиЗаполненияФорм КАК НастройкиЗаполненияФорм
	|ГДЕ
	|	НастройкиЗаполненияФорм.Наименование = &Наименование
	|	И НастройкиЗаполненияФорм.Объект = &Объект"
	);
	
	Запрос.УстановитьПараметр("Объект",       КлючОбъекта);
	Запрос.УстановитьПараметр("Наименование", Наименование);
	
	Возврат НЕ Запрос.Выполнить().Пустой();
	
КонецФункции

&НаКлиенте
Процедура СохранитьПоверхСуществующей(ДанныеВыбраннойСтроки)
	
	// Спросим пользователя, действительно ли он хочет заменить эту настройку, или просто очень хочет похожее название
	ТекстВопроса = НСтр("ru='Заменить настройку ""%ИмяНастройки%""?'");
	ТекстВопроса = СтрЗаменить(ТекстВопроса, "%ИмяНастройки%", ДанныеВыбраннойСтроки.Наименование);
	Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		// Оповестим о выборе пользователем настройки
		Закрыть(Новый ВыборНастроек(ДанныеВыбраннойСтроки.Ссылка));
	КонецЕсли;
	
КонецПроцедуры

// Обработчик нажатия кнопки сохранить
// Новая настройка записывается под указанным именем
&НаКлиенте
Процедура Сохранить()
	
	Если ПустаяСтрока(Наименование) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не задано наименование настройки.'"),, "Наименование");
		Возврат;
	КонецЕсли;
	
	ДанныеВыбраннойСтроки = Элементы.СписокНастроек.ТекущиеДанные;
	Если ДанныеВыбраннойСтроки <> Неопределено И Наименование = ДанныеВыбраннойСтроки.Наименование Тогда
		// Если название настройки совпадает с текущей строкой, то, вероятно, пользователь
		// выбрал строку и нажал "сохранить".
		// В этом случае запишем настройку поверх выбранной.
		СохранитьПоверхСуществующей(ДанныеВыбраннойСтроки);
		Возврат;
	КонецЕсли;
	
	Если ЕстьОдноименныеНастройки(КлючОбъекта, Наименование) Тогда
		// Спросим у пользователя, не ошибся ли он с именем настройки
		ТекстВопроса = НСтр("ru='Настройка ""%ИмяНастройки%"" уже есть. 
		|Записать новую настройку с таким же наименованием?'");
		ТекстВопроса = СтрЗаменить(ТекстВопроса, "%ИмяНастройки%", Наименование);
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Если Ответ <> КодВозвратаДиалога.Да Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	// Создаем новую настройку
	КлючСохраненнойНастройки = СоздатьНовуюНастройку(КлючОбъекта, Наименование, Пользователь);
	Если КлючСохраненнойНастройки = Неопределено Тогда
		// Не удалось записать
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не удалось сохранить настройку.'"));
		Возврат;
	КонецЕсли;
	
	Закрыть(Новый ВыборНастроек(КлючСохраненнойНастройки));
	
КонецПроцедуры

// При выборе строки в списке настройка записывается поверх выбранной
&НаКлиенте
Процедура СписокНастроекВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ДанныеВыбраннойСтроки = СписокНастроек[ВыбраннаяСтрока];
	
	СохранитьПоверхСуществующей(ДанныеВыбраннойСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокНастроекПриАктивизацииСтроки(Элемент)
	
	ДанныеВыбраннойСтроки = Элементы.СписокНастроек.ТекущиеДанные;
	Если ДанныеВыбраннойСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Наименование = ДанныеВыбраннойСтроки.Наименование;
	
КонецПроцедуры
