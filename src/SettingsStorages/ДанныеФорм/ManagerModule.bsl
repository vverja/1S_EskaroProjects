// ОБЩЕГО НАЗНАЧЕНИЯ

// Приводит ключ объекта к виду, не содержащему упоминание формы, например:
// Документ.РасходныйКассовыйОрдер
Функция СократитьКлючОбъекта(КлючОбъекта) Экспорт
	
	// Приведем ключ объекта к виду, не содержащему упоминание формы, например:
	ПозицияПервойТочки = Найти(КлючОбъекта, ".");
	Если ПозицияПервойТочки = 0 Тогда
		Возврат КлючОбъекта;
	Иначе
		ПозицияВторойТочки = Найти(Сред(КлючОбъекта, ПозицияПервойТочки + 1),".");
		Если ПозицияВторойТочки = 0 Тогда
			Возврат КлючОбъекта;
		Иначе
			Возврат Лев(КлючОбъекта, ПозицияПервойТочки + ПозицияВторойТочки - 1);
		КонецЕсли;
	КонецЕсли;

КонецФункции

// РАБОТА СО СПИСКОМ НАСТРОЕК

// Возвращает список настроек указанного пользователя (если необходимо - к указанному объекту)
//
Функция НастройкиПользователя(Пользователь, Объект = Неопределено) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ГруппыПользователейПользователиГруппы.Ссылка
	|ПОМЕСТИТЬ ГруппыПользователя
	|ИЗ
	|	Справочник.ГруппыПользователей.ПользователиГруппы КАК ГруппыПользователейПользователиГруппы
	|ГДЕ
	|	ГруппыПользователейПользователиГруппы.Пользователь = &Пользователь
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Справочник.ГруппыПользователей.ВсеПользователи)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Настройки.Ссылка,
	|	Настройки.Ссылка.Объект КАК Объект,
	|	Настройки.Ссылка.Наименование КАК Наименование
	|ИЗ
	|	(ВЫБРАТЬ
	|		НастройкиЗаполненияФормПользователи.Ссылка КАК Ссылка
	|	ИЗ
	|		Справочник.НастройкиЗаполненияФорм.Пользователи КАК НастройкиЗаполненияФормПользователи
	|	ГДЕ
	|		НастройкиЗаполненияФормПользователи.Пользователь = &Пользователь
	|		И (НЕ НастройкиЗаполненияФормПользователи.Ссылка.ПометкаУдаления)
	|		И (НастройкиЗаполненияФормПользователи.Ссылка.Объект = &Объект
	|				ИЛИ &Объект = НЕОПРЕДЕЛЕНО)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НастройкиЗаполненияФормПользователи.Ссылка
	|	ИЗ
	|		Справочник.НастройкиЗаполненияФорм.Пользователи КАК НастройкиЗаполненияФормПользователи
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ГруппыПользователя КАК ГруппыПользователя
	|			ПО НастройкиЗаполненияФормПользователи.Пользователь = ГруппыПользователя.Ссылка
	|	ГДЕ
	|		(НЕ НастройкиЗаполненияФормПользователи.Ссылка.ПометкаУдаления)
	|		И (НастройкиЗаполненияФормПользователи.Ссылка.Объект = &Объект
	|				ИЛИ &Объект = НЕОПРЕДЕЛЕНО)) КАК Настройки
	|
	|УПОРЯДОЧИТЬ ПО
	|	Наименование"
	);
	Запрос.УстановитьПараметр("Пользователь", 	Пользователь);
	Запрос.УстановитьПараметр("Объект", 		Объект);
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// ОБРАБОТЧИКИ СОБЫТИЙ СОХРАНЕНИЯ И ВОССТАНОВЛЕНИЯ НАСТРОЕК

// Выполняет запись данных формы в уже существующий элемент справочника НастройкиЗаполненияФорм
//
Процедура ОбработкаСохранения(КлючОбъекта, КлючНастроек, Настройки, ОписаниеНастроек, Пользователь)
	
	Если ЗначениеЗаполнено(КлючНастроек) Тогда
		
		ОбъектХранилища = КлючНастроек.ПолучитьОбъект();
		ОбъектХранилища.Данные = Новый ХранилищеЗначения(Настройки);
		
		ОбъектХранилища.Заблокировать();
		ОбъектХранилища.Записать();
		
	КонецЕсли;
	
КонецПроцедуры

// Выполняет чтение данных формы из выбранного элемента справочника НастройкиЗаполненияФорм
//
Процедура ОбработкаЗагрузки(КлючОбъекта, КлючНастроек, Настройки, ОписаниеНастроек, Пользователь)
	
	Если ЗначениеЗаполнено(КлючНастроек) Тогда
		Настройки = КлючНастроек.Данные.Получить();
	КонецЕсли;
	
КонецПроцедуры

// ПОДГОТОВКА ДАННЫХ ФОРМЫ К СОХРАНЕНИЮ В НАСТРОЙКУ

// Подготавливает структуру, описывающую реквизиты объекта, которые нужно сохранять в настройку
// (упаковывает переданные данные в структуру)
Функция СформироватьСтруктуруДополнительныхДанных(ТабличныеЧасти) Экспорт
	
	Возврат Новый Структура("ТабличныеЧасти", Новый Структура(ТабличныеЧасти));
	
КонецФункции

// Дополняет настройки данными табличных частей
//
Процедура ДобавитьДополнительныеДанныеВНастройку(Объект, Настройки, СтруктураДополнительныхДанных) Экспорт
	
	Для Каждого ОписаниеТабличнойЧасти Из СтруктураДополнительныхДанных.ТабличныеЧасти Цикл
		Настройки.Вставить(ОписаниеТабличнойЧасти.Ключ, Объект[ОписаниеТабличнойЧасти.Ключ].Выгрузить());
	КонецЦикла;
	
КонецПроцедуры

// ЗАПОЛНЕНИЕ ОБЪЕКТА ПО СОХРАНЕННОЙ НАСТРОЙКЕ

// Заполняет табличные части объекта данными настройки
//
Процедура ЗаполнитьОбъектДополнительнымиДанными(Объект, Настройки, СтруктураДополнительныхДанных) Экспорт
	
	Если ТипЗнч(Настройки) <> Тип("Соответствие") Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ОписаниеТабличнойЧасти Из СтруктураДополнительныхДанных.ТабличныеЧасти Цикл
		ДанныеТабличнойЧасти = Настройки[ОписаниеТабличнойЧасти.Ключ];
		Если ТипЗнч(ДанныеТабличнойЧасти) = Тип("ТаблицаЗначений") Тогда
			Объект[ОписаниеТабличнойЧасти.Ключ].Загрузить(ДанныеТабличнойЧасти);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры	

// Заполняет объект данными настройки
//
Процедура ЗаполнитьОбъектПоНастройке(Объект, ДанныеЗаполнения, СтруктураДополнительныхДанных) Экспорт
		
	Настройки = ДанныеЗаполнения.Данные.Получить();
	
	Если ТипЗнч(Настройки) <> Тип("Соответствие") Тогда
		Возврат;
	КонецЕсли;
	
	// Заполним реквизиты документа
	// В соответствии в качестве ключей используются строки вида Объект.ИмяРеквизита
	// Получим структуру, у которой в качестве ключей будет ИмяРеквизита
	ЗначенияРеквизитов = Новый Структура();
	Для Каждого ЭлементСоответствия Из Настройки Цикл
		Если Лев(ЭлементСоответствия.Ключ, 7) = "Объект." Тогда
			ЗначенияРеквизитов.Вставить(Сред(ЭлементСоответствия.Ключ, 8), ЭлементСоответствия.Значение);
		КонецЕсли;
	КонецЦикла;
	ЗаполнитьЗначенияСвойств(Объект, ЗначенияРеквизитов);
		
	// Заполним данные табличных частей
	ЗаполнитьОбъектДополнительнымиДанными(Объект, Настройки, СтруктураДополнительныхДанных);

КонецПроцедуры

