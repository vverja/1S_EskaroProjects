Функция ПолучитьСсылкуПоКлючуТекущейНастройки(КлючТекущейНастройки) Экспорт
	Если СтроковыеФункцииКлиентСервер.ЭтоУникальныйИдентификатор(КлючТекущейНастройки) тогда
		Возврат Справочники.НастройкиФормированияДокументовПоОрдерам.ПолучитьСсылку(Новый УникальныйИдентификатор(КлючТекущейНастройки));
	Иначе
		Возврат Справочники.НастройкиФормированияДокументовПоОрдерам.ПустаяСсылка();
	КонецЕсли;
КонецФункции

Процедура ОбработкаЗагрузки(КлючОбъекта, КлючНастроек, Настройки, ОписаниеНастроек, Пользователь)

	Ссылка = ПолучитьСсылкуПоКлючуТекущейНастройки(КлючНастроек);
	
	Если Не Ссылка.Пустая() Тогда
		Настройки = Новый Соответствие();
		Настройки.Вставить("НастройкиКомпоновщика",Ссылка.НастройкиКомпоновщика.Получить());
		Настройки.Вставить("КлючТекущейНастройки", КлючНастроек);
		Настройки.Вставить("ТекущаяНастройка", Ссылка.Наименование);
		ОписаниеНастроек = Новый ОписаниеНастроек();
		ОписаниеНастроек.Представление = Ссылка.Наименование;
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаСохранения(КлючОбъекта, КлючНастроек, Настройки, ОписаниеНастроек, Пользователь)
	
	Ссылка = ПолучитьСсылкуПоКлючуТекущейНастройки(КлючНастроек);

	Если Не Ссылка.Пустая() Тогда
		НастройкаОбъект = Ссылка.ПолучитьОбъект();
		НастройкаОбъект.Заблокировать();
		НастройкаОбъект.НастройкиКомпоновщика = Новый ХранилищеЗначения(Настройки["НастройкиКомпоновщика"], Новый СжатиеДанных());
		Если ОписаниеНастроек <> Неопределено Тогда
			НастройкаОбъект.Наименование = ОписаниеНастроек.Представление;
		КонецЕсли;
		НастройкаОбъект.Записать();
	КонецЕсли;
	
	
КонецПроцедуры

