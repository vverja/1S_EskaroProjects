
Функция ВыполнитьОбмен()
	Обмен = Обработки.УниверсальныйОбменДаннымиXML.Создать();
	Обмен.РежимОбмена = "Выгрузка";
	ИмяВремФайла = ПолучитьИмяВременногоФайла("xml");
	Обмен.ИмяФайлаОбмена = ИмяВремФайла;
	
	
	ХранилищеФайла = Справочники.ХранилищеДополнительнойИнформации.НайтиПоНаименованию("ПравилаОбменаДляТА");
	Если ХранилищеФайла = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли; 
	
    ИмяФайлаПравилОбмена = ПолучитьИмяВременногоФайла("xml");
	ЗаписьТекста = Новый ЗаписьТекста(ИмяФайлаПравилОбмена);
	ЗаписьТекста.Записать(ХранилищеФайла.Хранилище.Получить());
	ЗаписьТекста.Закрыть();
	
	Обмен.ИмяФайлаПравилОбмена = ИмяФайлаПравилОбмена;
	Обмен.ЗагрузитьПравилаОбмена();
	
	
	Обмен.ВыполнитьВыгрузку();	
	
	// Конец
	ЧтениеТекста = Новый ЧтениеТекста;
	ЧтениеТекста.Открыть(ИмяВремФайла, КодировкаТекста.UTF8);
	
	Результат = ЧтениеТекста.Прочитать();
	
	ЧтениеТекста.Закрыть();
	
	УдалитьФайлы(ИмяВремФайла);
	УдалитьФайлы(ИмяФайлаПравилОбмена);
	
	ХранилищеДанных = Новый ХранилищеЗначения(Результат, Новый СжатиеДанных(9));
	
	Возврат ХранилищеДанных;
	
	//Возврат Обмен.СформироватьДанныеДляОтчета();
КонецФункции


Функция ПолучитьОтчет(ДатаНачала, ДатаОкончания, СтрокаИнформацииРасшифровки)
	Отчет = Отчеты.ВедомостьТоварыНаСкладах.Создать();
	ПараметрыВывода = Отчет.КомпоновщикНастроек.Настройки.ПараметрыВывода;
	ПараметрыВывода.УстановитьЗначениеПараметра("ВертикальноеРасположениеОбщихИтогов",
	РасположениеИтоговКомпоновкиДанных.Начало);
	ПараметрыВывода.УстановитьЗначениеПараметра("ВыводитьЗаголовок",
	ТипВыводаТекстаКомпоновкиДанных.НеВыводить);
	ПараметрыВывода.УстановитьЗначениеПараметра("ВыводитьПараметрыДанных",
	ТипВыводаТекстаКомпоновкиДанных.НеВыводить);
	ПараметрДанныхНачалоПериода =
	Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы[0];
	ПараметрДанныхНачалоПериода.Значение = ДатаНачала;
	ПараметрДанныхНачалоПериода.Использование = Истина;
	ПараметрДанныхКонецПериода =
	Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы[1];
	ПараметрДанныхКонецПериода.Значение = ДатаОкончания;
	ПараметрДанныхКонецПериода.Использование = Истина;
	
	ТабличныйДокумент = Новый ТабличныйДокумент();
	ДанныеРасшифровки = Неопределено;
	Отчет.СкомпоноватьРезультат(ТабличныйДокумент, ДанныеРасшифровки);
	ИнформацияРасшифровки = Новый Соответствие;
	Для Каждого Элемент из ДанныеРасшифровки.Элементы Цикл
		Если ТипЗнч(Элемент) = Тип("ЭлементРасшифровкиКомпоновкиДанныхПоля") Тогда
			Поля = Элемент.ПолучитьПоля();
			Если Поля.Количество() > 0 Тогда
				ИнформацияРасшифровки.Вставить(Элемент.Идентификатор, Поля[0].Значение);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	СтрокаИнформацииРасшифровки = СериализаторXDTO.ЗаписатьXDTO(ИнформацияРасшифровки);
	Возврат СериализаторXDTO.ЗаписатьXDTO(ТабличныйДокумент);
КонецФункции