
Функция НачатьОбмен(КодУзла, НомерПринятого, НомерОтправленного)
	УзелОбмена = ПланыОбмена.Мобильный.НайтиПоКоду(КодУзла);
	Если УзелОбмена.Пустая() Тогда
		Возврат "";
	КонецЕсли;
	
	Если УзелОбмена.НомерОтправленного <> НомерОтправленного 
		ИЛИ УзелОбмена.НомерПринятого <> НомерПринятого Тогда
		Узел = УзелОбмена.ПолучитьОбъект();
		Узел.НомерОтправленного = НомерОтправленного;
		Узел.НомерПринятого = НомерПринятого;
		Узел.Записать();
		Обмен.ЗарегистрироватьИзмененияДанных(УзелОбмена);
	КонецЕсли;
	Возврат УзелОбмена.Код;
КонецФункции

Функция ПолучитьДанные(КодУзла)
	УзелОбмена = ПланыОбмена.Мобильный.НайтиПоКоду(КодУзла);
	Если УзелОбмена.Пустая() Тогда
		ВызватьИсключение("Неизвестное устройство " + КодУзла);
	КонецЕсли;
	Возврат Обмен.СформироватьПакетОбмена(УзелОбмена);	
КонецФункции

Функция ЗаписатьДанные(КодУзла, ДанныеМобильногоПриложения)
	УзелОбмена = ПланыОбмена.Мобильный.НайтиПоКоду(КодУзла);
	Если УзелОбмена.Пустая() Тогда
		ВызватьИсключение("Неизвестное устройство " + КодУзла);
	КонецЕсли;
	
	Возврат Обмен.ПринятьПакетОбмена(УзелОбмена, ДанныеМобильногоПриложения);
КонецФункции

